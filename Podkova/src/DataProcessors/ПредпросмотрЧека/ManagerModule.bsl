
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("ДокументСсылка") Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ВыбраннаяФорма = "Форма";
	
	Если Не Параметры.Свойство("ПодключенноеОборудование") Тогда
		
		ПодключенноеОборудование = ПодключаемоеОборудованиеУТВызовСервера.ОборудованиеПодключенноеПоОрганизации(Параметры.Организация);
		Параметры.Вставить(
			"ПодключенноеОборудование",
			ПодключенноеОборудование);
		
		Если ПодключенноеОборудование.ККТ.Количество() = 0 Тогда
			ВыбраннаяФорма = "ОшибкаПодключенияККТ";
		КонецЕсли;
		
	КонецЕсли;
	
	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		
		ВалютаРегл = Константы.ВалютаРегламентированногоУчета.Получить();
		
		Если ТипЗнч(Параметры.ДокументСсылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
			Или ТипЗнч(Параметры.ДокументСсылка) = Тип("ДокументСсылка.РеализацияУслугПрочихАктивов")
			Или ТипЗнч(Параметры.ДокументСсылка) = Тип("ДокументСсылка.АктВыполненныхРабот")
			Или ТипЗнч(Параметры.ДокументСсылка) = Тип("ДокументСсылка.ВыкупВозвратнойТарыКлиентом") Тогда
			
			Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.ДокументСсылка, "Валюта, ВалютаВзаиморасчетов");
			
			Если Реквизиты.Валюта <> ВалютаРегл
				Или Реквизиты.ВалютаВзаиморасчетов <> ВалютаРегл Тогда
				ВыбраннаяФорма = "ОшибкаИспользуемойВалюты";
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(Параметры.ДокументСсылка) = Тип("ДокументСсылка.ВзаимозачетЗадолженности") Тогда
			
			// ОК
			
		Иначе
			
			Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.ДокументСсылка, "Валюта");
			
			Если Реквизиты.Валюта <> ВалютаРегл Тогда
				ВыбраннаяФорма = "ОшибкаИспользуемойВалюты";
			КонецЕсли;
			
		КонецЕсли;
		
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныеПроцедурыИФункции
	
#Область Печать

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СогласиеНаОбработкуПерсональныхДанных") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"СогласиеНаОбработкуПерсональныхДанных",
			НСтр("ru = 'Согласие на обработку персональных данных'"),
			СформироватьПечатнуюФормуСогласиеНаОбработкуПерсональныхДанных(МассивОбъектов, ПараметрыПечати, ПараметрыПечати),
			,
			"Обработка.ПредпросмотрЧека.СогласиеНаОбработкуПерсональныхДанных");
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуСогласиеНаОбработкуПерсональныхДанных(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПредпросмотрЧека.ПФ_MXL_СогласиеНаОбработкуПерсональныхДанных");
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПредпросмотрЧека_СогласиеНаОбработкуПерсональныхДанных";
	
	Область = Макет.ПолучитьОбласть("Шапка");
	ТабличныйДокумент.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("ОписаниеСогласия");
	ТабличныйДокумент.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("ПостояннаяЧастьПерсональныхДанных");
	ТабличныйДокумент.Вывести(Область);
	
	Если ПараметрыПечати.ТипПерсональныхДанных = "ИНН" Тогда
		Область = Макет.ПолучитьОбласть("ПерсональныеДанныеИНН");
		ТабличныйДокумент.Вывести(Область);
	Иначе
		Область = Макет.ПолучитьОбласть("ПерсональныеДанныеПаспортныеДанные");
		ТабличныйДокумент.Вывести(Область);
	КонецЕсли;
	
	Область = Макет.ПолучитьОбласть("Подвал");
	ТабличныйДокумент.Вывести(Область);
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Если ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли