#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ПолучениеДанныхИнформационнойБазы

// Формирует таблицу способов обеспечения, используемых для обеспечения потребностей в номенклатуре.
//
// Параметры:
//  Параметры - Структура - структура с полями:
//     * Настройки - НастройкиКомпоновкиДанных - настройки для отбора номенклатуры и способов обеспечения:
//                   Склад, ПодразделениеПолучатель, Номенклатура, Характеристика,
//                   СпособОбеспечения, ИсточникОбеспечения, ТипОбеспечения, Назначение
//  АдресРезультата - УникальныйИдентификатор, Строка - адрес во временном хранилище, по которому надо поместить таблицу:
//     * СпособОбеспечения - СправочникСсылка.СпособыОбеспеченияПотребностей - способ обеспечения,
//     * СрокИсполненияЗаказа - Число - реквизит способа обеспечения,
//     * ОбеспечиваемыйПериод - Число - реквизит способа обеспечения,
//     * ФормироватьПлановыеЗаказы - Булево - реквизит способа обеспечения,
//     * ДатаСледующейПоставки - Дата - реквизит способа обеспечения,
//     * ДатаПоставки - Дата - расчетная дата поставки по способу обеспечения,
//     * ПлановаяДатаЗаказа - реквизит способа обеспечения,
//     * НаступилаДатаОчередногоЗаказа - Булево - реквизит способа обеспечения.
//     * Отметка - Булево - признак отображения флага в строке таблицы,
//     * СпособОбеспеченияПредставление - Строка - визуальное отображение способа обеспечения,
//     * ДнейДоПлановогоЗаказа - Число - используется для отображения надписи в строке с обратным отсчетом
//     * НетПлановойДатыЗаказаПоКалендарю - Булево - служебный признак для отображения ошибки заполнения графика работы,
//     * НетДатыПоставкиПоКалендарю - Булево - служебный признак для отображения ошибки заполнения графика работы,
//     * Порядок - Число - 0, если способ обеспечения не пуст, 1 - если способ обеспечения пуст, для упорядочивания в таблице.
//
Процедура ТаблицаСпособовОбеспечения(Параметры, АдресРезультата) Экспорт
	
	СхемаКомпоновкиДанных = Обработки.ОбеспечениеПотребностей.ПолучитьМакет("МакетКомпоновкиДляСерверныхОтборов");
	СхемаКомпоновкиДанных.НаборыДанных.Набор.Запрос = ВременнаяТаблицаСпособовОбеспечения();
	АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных);
	
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных);
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных();
	КомпоновщикНастроек.Инициализировать(ИсточникНастроек);
	
	КомпоновщикНастроек.ЗагрузитьНастройки(Параметры.Настройки);
	КомпоновщикНастроек.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);
	
	Запрос = ПолучитьЗапросСОтборамиКомпоновкиДанных(АдресСхемыКомпоновкиДанных, КомпоновщикНастроек, "Набор");
	УдалитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
	Запрос.УстановитьПараметр("НачалоТекущегоДня", НачалоДня(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("ДоступныеТипыОбеспечения", ДоступныеТипыОбеспечения());
	Запрос.УстановитьПараметр("ФОЗаказыНаПеремещенияВключена",
		ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыНаПеремещение"));
	Запрос.УстановитьПараметр("ФОПроизводство2_2Включена",
		ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеПроизводством2_2"));
	Запрос.УстановитьПараметр("ОптимизироватьЗапасыРаспределительногоЦентра",
		Константы.ОптимизироватьЗапасыРаспределительногоЦентра.Получить());
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.Выполнить();
	
	// Актуализируем график заказов для используемых способов, записываем изменения в базу.
	
	НачалоДня = НачалоДня(ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня);
	Запрос.Текст = Обработки.ОбеспечениеПотребностей.СформироватьТекстЗапросаАктуализацииСпособовОбеспечения();
	
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Истина);
	Пока Выборка.Следующий() Цикл
		
		СпособОбъект = Выборка.СпособОбеспечения.ПолучитьОбъект();
		Справочники.СпособыОбеспеченияПотребностей.АктуализироватьГрафикЗаказовНаСервере(СпособОбъект, НачалоДня);
		СпособОбъект.Записать();
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("КалендарьПредприятия", Константы.ОсновнойКалендарьПредприятия.Получить());
	Запрос.Текст = Обработки.ОбеспечениеПотребностей.СформироватьТекстЗапросаРеквизитовСпособаОбеспечения(Неопределено);
	
	ПоместитьВоВременноеХранилище(Запрос.Выполнить().Выгрузить(), АдресРезультата);
	
КонецПроцедуры

// Рассчитывает минимальный и максимальный запас для товара.
//
// Параметры:
//  Товар - ДанныеФормыКоллекция - Содержит параметры расчета потребности в товаре при поддержании запаса:
//   МетодОбеспечения - ПеречислениеСсылка.МетодыОбеспеченияПотребностей,
//   ФормироватьПлановыеЗаказы - Булево,
//   СрокПоставки - Число,
//   СрокДоПлановойПоставки - Число,
//   СреднедневноеПотребление - Число
//   СтраховойЗапас - Число,
//   ОбеспечиваемыйПериод - Число,
//   МинимальныйЗапас - Число - минимальный запас, назначеннй пользователем (для метода мин-макс),
//   МаксимальныйЗапас - Число - минимальный запас, назначеннй пользователем (для метода мин-макс).
//
// Возвращаемое значение:
//  Структура - Структура с полями:
//   МинимальныйЗапас - Число - Рассчитанный минимальный запас,
//   МаксимальныйЗапас - Число - Рассчитанный максимальный запас.
//
Функция МинимальныйИМаксимальныйЗапасы(Товар) Экспорт
	
	МетодПоСтатистике = ПредопределенноеЗначение("Перечисление.МетодыОбеспеченияПотребностей.ПоддержаниеЗапасаНаСрокПоСтатистике");
	МетодПоНорме      = ПредопределенноеЗначение("Перечисление.МетодыОбеспеченияПотребностей.ПоддержаниеЗапасаНаСрокПоНорме");
	
	Если Товар.МетодОбеспечения = МетодПоСтатистике Или Товар.МетодОбеспечения = МетодПоНорме Тогда
		
		Если Товар.ФормироватьПлановыеЗаказы Тогда
			СрокПоставки = Мин(Товар.СрокПоставки, Товар.СрокДоПлановойПоставки);
		Иначе
			СрокПоставки = Товар.СрокПоставки;
		КонецЕсли;
		
		МинимальныйЗапас  = Товар.СреднедневноеПотребление * СрокПоставки;
		МаксимальныйЗапас = Товар.СреднедневноеПотребление * Товар.ОбеспечиваемыйПериод;
		
		Если Товар.ТипЕдиницыИзмерения = Перечисления.ТипыИзмеряемыхВеличин.КоличествоШтук Тогда
			
			Если Цел(МинимальныйЗапас) <> МинимальныйЗапас Тогда
				МинимальныйЗапас = 1 + Цел(МинимальныйЗапас);
			КонецЕсли;
			
			Если Цел(МаксимальныйЗапас) <> МаксимальныйЗапас Тогда
				МаксимальныйЗапас = 1 + Цел(МаксимальныйЗапас);
			КонецЕсли;
		
		КонецЕсли;
		
		МинимальныйЗапас  = МинимальныйЗапас  + Товар.СтраховойЗапас;
		МаксимальныйЗапас = МаксимальныйЗапас + Товар.СтраховойЗапас;
		
	Иначе
		МинимальныйЗапас  = Товар.МинимальныйЗапас;
		МаксимальныйЗапас = Товар.МаксимальныйЗапас;
	КонецЕсли;
	
	Возврат Новый Структура("МинимальныйЗапас, МаксимальныйЗапас", МинимальныйЗапас, МаксимальныйЗапас);
	
КонецФункции

// Рассчитывает уровень запаса по текущим складским запасам и установленным для товара параметрам поддержания запаса.
//
// Параметры:
//  Товар - ДанныеФормыКоллекция - Содержит параметры расчета потребности в товаре при поддержании запаса:
//   МетодОбеспечения - ПеречислениеСсылка.МетодыОбеспеченияПотребностей,
//   МинимальныйЗапас - Число,
//   МаксимальныйЗапас - Число,
//   ЗаказыКПоступлению - Число,
//   Остаток - Число,
//   ПотреблениеСтабильно - Булево.
//
// Возвращаемое значение:
//  Структура - Структура с полями:
//   УровеньЗапаса - Число,
//   УровеньЗапасаМаксимум - Число,
//   КартинкаСменитьМетод - Число,
//   ТочкаЗаказаКартинка - Число,
//   КОтменеЗаказаКартинка - Число.
//
Функция УровеньЗапасаИКартинкаСменитьМетод(Товар) Экспорт

	МетодПоСтатистике =
		Товар.МетодОбеспечения = ПредопределенноеЗначение("Перечисление.МетодыОбеспеченияПотребностей.ПоддержаниеЗапасаНаСрокПоСтатистике");

	УровеньЗапаса =
		?(Товар.Остаток - Товар.МинимальныйЗапас < 0,     1,      // критический
		?(Товар.Остаток - 2 * Товар.МинимальныйЗапас < 0, 2,      // близкий к критическому
		?(Товар.Остаток < Товар.МаксимальныйЗапас / 2,    4,      // меньше половины
		                                                  7)));

	УровеньЗапасаМаксимум =
		?(Товар.Остаток > Товар.МаксимальныйЗапас,      6,      // избыток
		?(Товар.Остаток >= Товар.МаксимальныйЗапас / 2, 5,      // больше половины
		                                                7));

	ТочкаЗаказаКартинка   = Товар.Остаток - Товар.МинимальныйЗапас < 0;
	КОтменеЗаказаКартинка = Товар.ЗаказыКпоступлению + Товар.Остаток - Товар.МинимальныйЗапас > Товар.МаксимальныйЗапас
		И Товар.ЗаказыКпоступлению > 0;

	КартинкаСменитьМетод =
		?(Товар.ПотреблениеСтабильно И Не МетодПоСтатистике,     0, // рекомендуется использовать статистику
		?(Не Товар.ПотреблениеСтабильно И МетодПоСтатистике,     1, // рекомендуется использовать мин-макс
		?(Товар.ПотреблениеСтабильно И МетодПоСтатистике,       -1, // метод соответствует рекомендации
		?(Не Товар.ПотреблениеСтабильно И Не МетодПоСтатистике, -1, // метод соответствует рекомендации
		                                                                                         -2)))); // нет рекомендации

	Результат = Новый Структура();
	Результат.Вставить("УровеньЗапаса", УровеньЗапаса);
	Результат.Вставить("УровеньЗапасаМаксимум", УровеньЗапасаМаксимум);
	Результат.Вставить("КартинкаСменитьМетод", КартинкаСменитьМетод);
	Результат.Вставить("ТочкаЗаказаКартинка", ТочкаЗаказаКартинка);
	Результат.Вставить("КОтменеЗаказаКартинка", КОтменеЗаказаКартинка);
	Возврат Результат;

КонецФункции

// Рассчитывает количество к заказу для поддержания запаса.
//
// Параметры:
//  Товар - ДанныеФормыКоллекция - Содержит параметры расчета потребности в товаре при поддержании запаса:
//   Остаток - Число,
//   ЗаказыКПоступлению - Число,
//   МинимальныйЗапас - Число,
//   МаксимальныйЗапас - Число,
//   ФормироватьПлановыеЗаказы - Булево,
//   ДатаЗаказа - Число.
//
// Возвращаемое значение:
//  Структура - Структура с полями:
//   Отметка - Булево,
//   КЗаказу - Число,
//   ДатаЗаказаНаступила - Булево,
//   ТочкаЗаказаНаступила - Булево.
//
Функция КоличествоКЗаказуДляПоддержанияЗапаса(Товар) Экспорт

	ДоТочкиЗаказа = Товар.Остаток - Товар.МинимальныйЗапас;
	
	ВосполнениеСтраховогоЗапаса = Мин(Товар.СтраховойЗапас, Макс(0, -ДоТочкиЗаказа));
	КЗаказу = Макс(0, Товар.МаксимальныйЗапас - Товар.СтраховойЗапас - Товар.ЗаказыКпоступлению - Макс(0, ДоТочкиЗаказа) + ВосполнениеСтраховогоЗапаса)
		+ Макс(0, Мин(Товар.РезервыНаСкладе, -Товар.Остаток));
	
	ДатаЗаказаНаступила = Товар.ФормироватьПлановыеЗаказы 
		И Товар.ДатаЗаказа <= НачалоДня(ТекущаяДатаСеанса())
		И ЗначениеЗаполнено(Товар.ДатаЗаказа);
	ТочкаЗаказаНаступила = ДоТочкиЗаказа <= 0;
	Отметка = (ДатаЗаказаНаступила Или ТочкаЗаказаНаступила И Не Товар.ФормироватьПлановыеЗаказы) И КЗаказу > 0;

	Результат = Новый Структура();
	Результат.Вставить("КЗаказу",              КЗаказу);
	Результат.Вставить("Отметка",              Отметка);
	Результат.Вставить("ДатаЗаказаНаступила",  ДатаЗаказаНаступила);
	Результат.Вставить("ТочкаЗаказаНаступила", ТочкаЗаказаНаступила);
	Результат.Вставить("ДоТочкиЗаказа",        ДоТочкиЗаказа);

	Возврат Результат;

КонецФункции

// Рассчитывает количество к заказу для обеспечения заказов.
//
// Параметры:
//  Товар - ДанныеФормыКоллекция - Содержит параметры расчета потребности в товаре/работе для обеспечении заказов:
//   Требуется - Число,
//   КРезервированию - Число,
//   ДатаОтгрузки - Дата,
//   ФормироватьПлановыеЗаказы - Булево,
//   ДатаЗаказа - Дата,
//   ДатаСледующейПоставкиПоГрафику - Дата,
//   ОбеспечиваемыйПериод - Число,
//   ГраницаОбеспечиваемогоПериода - Дата.
//
// Возвращаемое значение:
//  Структура - Структура с полями:
//   Отметка - Булево,
//   КЗаказу - Число,
//   ДатаЗаказаНаступила - Булево,
//   ОтгрузкаВнутриПериода - Булево.
//
Функция КоличествоКЗаказуДляОбеспеченияЗаказов(Товар) Экспорт

	КЗаказу = Товар.Требуется - Товар.КРезервированию;
	ДатаЗаказаНаступила = Товар.ФормироватьПлановыеЗаказы И Товар.ДатаЗаказа <= НачалоДня(ТекущаяДатаСеанса());

	Если ДатаЗаказаНаступила Тогда

		ОтгрузкаВнутриПериода = Товар.ДатаОтгрузки < Товар.ДатаСледующейПоставкиПоГрафику;

	ИначеЕсли Товар.ФормироватьПлановыеЗаказы Тогда

		ОтгрузкаВнутриПериода = Ложь;

	Иначе

		Если Товар.ОбеспечиваемыйПериод > 0 Тогда

			ОтгрузкаВнутриПериода = Товар.ДатаОтгрузки < Товар.ГраницаОбеспечиваемогоПериода;

		Иначе

			ОтгрузкаВнутриПериода = Истина;

		КонецЕсли;


	КонецЕсли;

	Отметка = КЗаказу > 0 И ОтгрузкаВнутриПериода;

	Результат = Новый Структура();
	Результат.Вставить("КЗаказу",               КЗаказу);
	Результат.Вставить("Отметка",               Отметка);
	Результат.Вставить("ДатаЗаказаНаступила",   ДатаЗаказаНаступила);
	Результат.Вставить("ОтгрузкаВнутриПериода", ОтгрузкаВнутриПериода);

	Возврат Результат;

КонецФункции

#КонецОбласти

// Заполняет обеспечение в заказах вариантом "Из заказа".
//
// Параметры:
//   ТаблицаКОбеспечению - ТаблицаЗначений - таблица содержащая поля, идентифицирующие строки заказов,
//                                           в которых необходимо заполнить вариант обеспечения, содержит поля
//                                           "Заказ", "КодСтроки", "Номенклатура", "Характеристика", "Назначение".
// 
// Возвращаемое значение:
//   Булево - Истина, если есть сообщения об ошибках.
//
Функция ОбеспечитьИзЗаказа(ТаблицаКОбеспечению) Экспорт

	ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Заказ КАК Заказ,
		|	Т.ДатаОтгрузки   КАК ДатаОтгрузки,
		|
		|	Т.Склад          КАК Склад,
		|	Т.Номенклатура   КАК Номенклатура,
		|	Т.Характеристика КАК Характеристика,
		|	Т.Назначение     КАК Назначение
		|
		|ПОМЕСТИТЬ ВтТовары
		|ИЗ
		|	&Таблица КАК Т
		|;
		|
		|/////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Заказ            КАК Ссылка,
		|	Товары.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	ВтТовары КАК Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.Товары КАК Товары
		|		ПО Т.Заказ = Товары.Ссылка
		|		 И Т.Номенклатура   = Товары.Номенклатура
		|		 И Т.Характеристика = Товары.Характеристика
		|		 И Т.Склад          = Товары.Склад
		|		 И(Т.Назначение     = Товары.Ссылка.Назначение
		|			ИЛИ Товары.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Требуется))
		|		 И Т.ДатаОтгрузки   = Товары.ДатаОтгрузки
		|		 И Товары.ВариантОбеспечения В(
		|			ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Обособленно),
		|			ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Требуется))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Т.Заказ            КАК Ссылка,
		|	Товары.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	ВтТовары КАК Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаВозвратТоваровОтКлиента.ЗаменяющиеТовары КАК Товары
		|		ПО Т.Заказ = Товары.Ссылка
		|		 И Т.Номенклатура   = Товары.Номенклатура
		|		 И Т.Характеристика = Товары.Характеристика
		|		 И Т.Склад          = Товары.Ссылка.Склад
		|		 И(Т.Назначение     = Товары.Ссылка.Назначение
		|			ИЛИ Товары.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Требуется))
		|		 И Т.ДатаОтгрузки   = Товары.ДатаОтгрузки
		|		 И Товары.ВариантОбеспечения В(
		|			ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Обособленно),
		|			ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Требуется))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Т.Заказ            КАК Ссылка,
		|	Товары.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	ВтТовары КАК Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПеремещение.Товары КАК Товары
		|		ПО Т.Заказ = Товары.Ссылка
		|		 И Т.Номенклатура   = Товары.Номенклатура
		|		 И Т.Характеристика = Товары.Характеристика
		|		 И Т.Склад          = Товары.Ссылка.СкладОтправитель
		|		 И( Т.Назначение = ВЫБОР КОГДА Товары.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) ТОГДА
		|		                                       Товары.Ссылка.Назначение
		|		                                  ИНАЧЕ
		|		                                       Товары.Назначение
		|		                            КОНЕЦ
		|			ИЛИ Товары.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Требуется))
		|		 И Т.ДатаОтгрузки   = Товары.НачалоОтгрузки
		|		 И Товары.ВариантОбеспечения В(
		|			ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Обособленно),
		|			ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Требуется))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Т.Заказ            КАК Ссылка,
		|	Товары.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	ВтТовары КАК Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаСборку.Товары КАК Товары
		|		ПО Т.Заказ = Товары.Ссылка
		|		 И Т.Номенклатура   = Товары.Номенклатура
		|		 И Т.Характеристика = Товары.Характеристика
		|		 И Т.Склад          = Товары.Ссылка.Склад
		|		 И( Т.Назначение = ВЫБОР КОГДА Товары.Ссылка.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) ТОГДА
		|		                                       Товары.Ссылка.НазначениеТовары
		|		                                  ИНАЧЕ
		|		                                       Товары.Ссылка.Назначение
		|		                            КОНЕЦ
		|			ИЛИ Товары.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Требуется))
		|		 И Товары.ВариантОбеспечения В(
		|			ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Обособленно),
		|			ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Требуется))
		|		 И Товары.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Т.Заказ            КАК Ссылка,
		|	Товары.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	ВтТовары КАК Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаВнутреннееПотребление.Товары КАК Товары
		|		ПО Т.Заказ = Товары.Ссылка
		|		 И Т.Номенклатура   = Товары.Номенклатура
		|		 И Т.Характеристика = Товары.Характеристика
		|		 И Т.Склад          = Товары.Ссылка.Склад
		|		 И (Т.Назначение     = Товары.Ссылка.Назначение
		|			ИЛИ Товары.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Требуется))
		|		 И Т.ДатаОтгрузки   = Товары.ДатаОтгрузки
		|		 И Товары.ВариантОбеспечения В(
		|			ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Обособленно),
		|			ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Требуется))
		|
		|ИТОГИ ПО
		|	Ссылка";

	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Таблица", ТаблицаКОбеспечению);

	УстановитьПривилегированныйРежим(Истина);
	ВыборкаЗаказов = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	ПереченьВариантов = Новый Массив();
	ПереченьВариантов.Добавить(Перечисления.ВариантыОбеспечения.ИзЗаказов);

	ИндексыСтрок = Новый Массив();
	ЕстьОшибки = Ложь;
	Пока ВыборкаЗаказов.Следующий() Цикл

		ИндексыСтрок.Очистить();
		Выборка = ВыборкаЗаказов.Выбрать();
		Пока Выборка.Следующий() Цикл
			ИндексыСтрок.Добавить(Выборка.НомерСтроки - 1);
		КонецЦикла;

		ЕстьОшибки = ЕстьОшибки Или ВыполнитьОбеспечениеЗаказа(ВыборкаЗаказов.Ссылка, ИндексыСтрок, ПереченьВариантов);

	КонецЦикла;

	Возврат ЕстьОшибки;
	
КонецФункции

// Возвращает таблицу, содержащие данные, идентифицирующие строки заказа для последующего заполнения в них обеспечения.
//
// Параметры:
//  Документ - ДокументСсылка.ЗаказКлиента, ДокументСсылка.ЗаявкаНаВозвратТоваровОтКлиента,
//             ДокументСсылка.ЗаказНаПреремещение, ДокументСсылка.ЗаказНаСборку, ДокументСсылка.ЗаказНаРемонт,
//             ДокументСсылка.ЗаказДавальца, ДокументСсылка.ЗаказПереработчику, ДокументСсылка.ЗаказНаПроизводство,
//             ДокументСсылка.МаршрутныйЛистПроизводства - Заказ,
//  ИндексыСтрок - Массив - содержит индексы строк табличной части документа,  для последующего заполнения в них обеспечения.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица с полями согласно функции ОбеспечениеСервер.ТаблицаПотребностей.
//
Функция ТаблицаПотребностей(Документ, ИндексыСтрок) Экспорт

	Тип = ТипЗнч(Документ.Ссылка);
	
	ИмяТабличнойЧасти = "Товары";

	Если Тип = Тип("ДокументСсылка.ЗаявкаНаВозвратТоваровОтКлиента") Тогда
		
		ИмяТабличнойЧасти = "ЗаменяющиеТовары";
		

	КонецЕсли;

	ТаблицаПотребностей = ОбеспечениеСервер.ТаблицаПотребностей();
	Если Тип = Тип("ДокументСсылка.ЗаказНаСборку") И
		Документ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РазборкаТоваров Тогда
		
	Иначе


		// Таблица с полями: "НомерСтроки, Количество, ДатаОтгрузки, ВариантОбеспечения, Упаковка, Номенклатура,
		// Характеристика, Склад".

		Для каждого ИндексСтроки Из ИндексыСтрок Цикл

			СтрокаПотребности = ТаблицаПотребностей.Добавить();

			СтрокаТовары = Документ[ИмяТабличнойЧасти][ИндексСтроки];
			ЗаполнитьЗначенияСвойств(СтрокаПотребности, СтрокаТовары);
			СтрокаПотребности.Назначение = Документ.Назначение;
			СтрокаПотребности.Подразделение = Документ.Подразделение;
			СтрокаПотребности.Идентификатор = ИндексСтроки;

			Если Тип = Тип("ДокументСсылка.ЗаявкаНаВозвратТоваровОтКлиента") Тогда
				СтрокаПотребности.Склад = Документ.Склад;
			ИначеЕсли Тип = Тип("ДокументСсылка.ЗаказНаВнутреннееПотребление") Тогда
				СтрокаПотребности.Склад = Документ.Склад;
			ИначеЕсли Тип = Тип("ДокументСсылка.ЗаказНаСборку") Тогда
				СтрокаПотребности.Склад = Документ.Склад;
				СтрокаПотребности.ДатаОтгрузки = Документ.НачалоСборкиРазборки;
			ИначеЕсли Тип = Тип("ДокументСсылка.ЗаказНаПеремещение") Тогда
				СтрокаПотребности.Склад = Документ.СкладОтправитель;
				СтрокаПотребности.ДатаОтгрузки = СтрокаТовары.НачалоОтгрузки;
			КонецЕсли;
		
		КонецЦикла;
	
	КонецЕсли;
	
	Возврат ТаблицаПотребностей;
	
КонецФункции

// Возвращает две ближайшие даты заказа по календарю работы.
//
// Параметры:
//   КалендарьЗаказа - СправочникСсылка.Календари - Календарь по кторому необходимо рассчитать даты заказа.
//   НачалоПериода   - Дата - Дата, с которой начинается поиск дат заказа.
//
// Возвращаемое значение:
//   Структура - структура со следующими полями:
//     * ДатаЗаказа          - Дата - ближайшая рабочая дата по календарю.
//     *ДатаСледующегоЗаказа - Дата - следующая рабочая дата за ближайшей.
//
Функция ПолучитьДатыЗаказаПоКалендарю(КалендарьЗаказа, НачалоПериода) Экспорт
	
	КлючевыеДаты = Новый Структура("ДатаЗаказа, ДатаСледующегоЗаказа", Дата(1, 1, 1), Дата(1, 1, 1));
	Если КалендарьЗаказа.Пустая() Тогда
		КлючевыеДаты.ДатаЗаказа           = НачалоПериода;
		КлючевыеДаты.ДатаСледующегоЗаказа = НачалоПериода + 86400; //86400 - длительность суток в секундах.
	Иначе
		
		Запрос = Новый Запрос();
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
			|	КалендарныйГрафик.ДатаГрафика КАК ДатаЗаказа
			|ИЗ
			|	РегистрСведений.КалендарныеГрафики КАК КалендарныйГрафик
			|ГДЕ
			|	КалендарныйГрафик.Календарь     =  &КалендарьЗаказа
			|	И КалендарныйГрафик.ДатаГрафика >= &НачалоПериода
			|	И КалендарныйГрафик.ДеньВключенВГрафик
			|
			|УПОРЯДОЧИТЬ ПО
			|	КалендарныйГрафик.ДатаГрафика";
		Запрос.УстановитьПараметр("КалендарьЗаказа", КалендарьЗаказа);
		Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			КлючевыеДаты.ДатаЗаказа = Выборка.ДатаЗаказа;
		КонецЕсли;
		
		Если Выборка.Следующий() Тогда
			КлючевыеДаты.ДатаСледующегоЗаказа = Выборка.ДатаЗаказа;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат КлючевыеДаты;
		
КонецФункции

// Формирует текст запроса для получения реквизитов способа обеспечения.
// Для верной отработки запроса способы обеспечения должны быть обновлены (при необходимости произведен сдвиг плановых поставок).
//
//	Параметры:
//		ИмяВременнойТаблицы - Строка - Имя временной таблицы в тексте запроса, в которую будет помещен результат.
//			Если не задано, результат не помещается во временну таблицу.
//		ИмяИсходнойТаблицы  - Строка - Имя исходной временной таблицы, содержащей способы обеспечения,
//			для которых необходимо получить значения реквизитов.
//
//	Возвращаемое значение:
//		Строка - Текст запроса.
//
Функция СформироватьТекстЗапросаРеквизитовСпособаОбеспечения(ИмяВременнойТаблицы = Неопределено, ИмяИсходнойТаблицы = Неопределено) Экспорт
	
	// План выполнения запроса.
	// 1. Исходные данные размещены во временной таблице "СпособыОбеспечения", каждая строка таблицы содержит ссылку на
	//	способ обеспечения, для которого необходимо получить расчетные параметры поставки.
	// 2. Дополняем данные реквизитами справочника способов обеспечения потребностей "СпрСпособ"
	//		- "ФормироватьПлановыеЗаказы",
	//		- "ПлановаяДатаПоставки",
	//		- "ДатаСледующейПоставки"
	//		- "СрокИсполненияЗаказа",
	//		- "ОбеспечиваемыйПериод".
	// 3. Рассчитываем дату поставки по способу обеспечения согласно календарю предприятия и сроку исполнения заказа
	//		- "ДатаПоставки.ДатаГрафика"
	//		- "ДатаПоставкиПлюсГод.ДатаГрафика" (если дата поставки в следующем году).
	//			3.а. Получаем количество рабочих дней с начала года для текущей даты, заданной параметром "НачалоПериода".
	//				- "ТекущаяДата.КоличествоДнейВГрафикеСНачалаГода".
	//			3.б. Получаем рабочий день в году текущей даты, отстоящий от текущей даты на "СрокИсполненияЗаказа"
	//				- "ДатаПоставки.ДатаГрафика".
	//			3.в. Получаем количество рабочих дней в году текущей даты
	//			(в случае, если дата поставки в году текущей даты не обнаружена).
	//				- "ВсегоРабочихДнейВГоду.КоличествоДнейВГрафикеСНачалаГода".
	//			3.г. Получаем рабочий день в следующем от текущей даты году, отстоящий от текущей даты на "СрокИсполненияЗаказа".
	//			(в случае, если дата поставки в году текущей даты не обнаружена).
	//				- "ДатаПоставкиПлюсГод.ДатаГрафика".
	// 4. Рассчитываем дату заказа на ближайшую поставку по способу обеспечения согласно календарю предприятия и сроку
	// исполнения заказа
	//		- "ДатаПоставки.ДатаГрафика"
	//			4.а. Получаем количество рабочих дней до плановой поставки по графику.
	//				- "ПлановаяДатаПоставкиГрафик.КоличествоДнейВГрафикеСНачалаГода"
	//			4.б. Получаем рабочий день в году плановой поставки по способу упреждающий дату поставки на срок исполнения заказа.
	//				- "ПлановаяДатаЗаказаГрафик.ДатаГрафика".
	//			4.в. Получаем количество рабочих дней в году, предшествующем году плановой поставки.
	//				- "ВсегоРабочихДнейВГодуПлановогоЗаказа.КоличествоДнейВГрафикеСНачалаГода".
	//			(в случае, если дата планового заказа в году плановой поставки не обнаружена).
	//				- "ВсегоРабочихДнейВГоду.КоличествоДнейВГрафикеСНачалаГода".
	//			4.г. Получаем рабочий день в году предшествующем плановой поставки по способу,
	//			упреждающий дату поставки на срок исполнения заказа.
	//			(в случае, если дата планового заказа в году плановой поставки не обнаружена).
	//				- "ДатаЗаказаМинусГод.ДатаГрафика".
	
	Возврат
	"ВЫБРАТЬ
	|	ЕСТЬNULL(СпрСпособ.Ссылка, ЗНАЧЕНИЕ(Справочник.СпособыОбеспеченияПотребностей.ПустаяСсылка)) КАК СпособОбеспечения,
	|	ЕСТЬNULL(СпрСпособ.СрокИсполненияЗаказа, 0)                   КАК СрокИсполненияЗаказа,
	|	ЕСТЬNULL(СпрСпособ.ОбеспечиваемыйПериод, 0)                   КАК ОбеспечиваемыйПериод,
	|	ЕСТЬNULL(СпрСпособ.ФормироватьПлановыеЗаказы, ЛОЖЬ)           КАК ФормироватьПлановыеЗаказы,
	|	ЕСТЬNULL(СпрСпособ.ДатаСледующейПоставки, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаСледующейПоставки,
	
	|	ВЫБОР
	|		КОГДА СпрСпособ.ФормироватьПлановыеЗаказы И СпрСпособ.ПлановаяДатаПоставки > ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|			СпрСпособ.ПлановаяДатаПоставки //есть актуальная дата поставки по графику
	|		КОГДА &КалендарьПредприятия = ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка) ТОГДА //календари не используются
	|			ДОБАВИТЬКДАТЕ(&НачалоПериода, ДЕНЬ, ЕСТЬNULL(СпрСпособ.СрокИсполненияЗаказа, 0))
	|		ИНАЧЕ
	|			ЕСТЬNULL(ДатаПоставки.ДатаГрафика, ЕСТЬNULL(ДатаПоставкиПлюсГод.ДатаГрафика, ДАТАВРЕМЯ(1, 1, 1)))
	|	КОНЕЦ                                      КАК ДатаПоставки,
	|	ВЫБОР
	|		КОГДА СпрСпособ.ФормироватьПлановыеЗаказы И СпрСпособ.ПлановаяДатаЗаказа = &НачалоПериода ТОГДА
	|			&НачалоПериода
	|		КОГДА &КалендарьПредприятия = ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка)
	|			И СпрСпособ.ФормироватьПлановыеЗаказы
	|			И СпрСпособ.ПлановаяДатаПоставки > ДАТАВРЕМЯ(1, 1, 1) ТОГДА //календари не используются
	|			ДОБАВИТЬКДАТЕ(СпрСпособ.ПлановаяДатаПоставки, ДЕНЬ, - СпрСпособ.СрокИсполненияЗаказа)
	|		ИНАЧЕ
	|			ЕСТЬNULL(ПлановаяДатаЗаказаГрафик.ДатаГрафика, ЕСТЬNULL(ДатаЗаказаМинусГод.ДатаГрафика, ДАТАВРЕМЯ(1, 1, 1)))
	|	КОНЕЦ                                      КАК ПлановаяДатаЗаказа,
	|	ВЫБОР
	|		КОГДА СпрСпособ.ФормироватьПлановыеЗаказы И СпрСпособ.ПлановаяДатаЗаказа = &НачалоПериода ТОГДА
	|			ИСТИНА
	|		КОГДА СпрСпособ.ФормироватьПлановыеЗаказы И СпрСпособ.ПлановаяДатаПоставки > ДАТАВРЕМЯ(1, 1, 1)
	|			И &КалендарьПредприятия = ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка)
	|			И ВЫБОР КОГДА СпрСпособ.ФормироватьПлановыеЗаказы И СпрСпособ.ПлановаяДатаПоставки > ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|						ДОБАВИТЬКДАТЕ(СпрСпособ.ПлановаяДатаПоставки, ДЕНЬ, - СпрСпособ.СрокИсполненияЗаказа) <= &НачалоПериода
	|					ИНАЧЕ
	|						ЛОЖЬ
	|				КОНЕЦ
	|		ТОГДА
	|			ИСТИНА
	|		КОГДА СпрСпособ.ФормироватьПлановыеЗаказы И СпрСпособ.ПлановаяДатаПоставки > ДАТАВРЕМЯ(1, 1, 1)
	|			И ЕСТЬNULL(ПлановаяДатаЗаказаГрафик.ДатаГрафика, ДатаЗаказаМинусГод.ДатаГрафика) <= &НачалоПериода
	|		ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ                                      КАК НаступилаДатаОчередногоЗаказа" + 
	?(ИмяВременнойТаблицы = Неопределено, ",
	// Следующие поля являются служебными и добавляются в выборку в запросе только на втором шаге
	// Обеспечение потребностей, при формировании таблицы основных способов обеспечения.
	|	ИСТИНА                                                         КАК Отметка,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(СпособыОбеспечения.СпособОбеспечения)      КАК СпособОбеспеченияПредставление,
	|	ВЫБОР
	|		КОГДА СпрСпособ.ФормироватьПлановыеЗаказы И СпрСпособ.ПлановаяДатаЗаказа = &НачалоПериода ТОГДА
	|			0
	|		КОГДА &КалендарьПредприятия = ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка)
	|			И СпрСпособ.ФормироватьПлановыеЗаказы
	|			И СпрСпособ.ПлановаяДатаПоставки > ДАТАВРЕМЯ(1, 1, 1) ТОГДА //календари не используются
	|			РАЗНОСТЬДАТ(&НачалоПериода, СпрСпособ.ПлановаяДатаПоставки, ДЕНЬ) - СпрСпособ.СрокИсполненияЗаказа
	|		ИНАЧЕ
	|			РАЗНОСТЬДАТ(&НачалоПериода, ЕСТЬNULL(ПлановаяДатаЗаказаГрафик.ДатаГрафика,
	|				ДатаЗаказаМинусГод.ДатаГрафика), ДЕНЬ)
	|	КОНЕЦ                                                          КАК ДнейДоПлановогоЗаказа,
	|	ВЫБОР
	|		КОГДА СпрСпособ.ФормироватьПлановыеЗаказы И СпрСпособ.ПлановаяДатаПоставки = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ                                                          КАК НетГрафикаПоставок,
	|	ВЫБОР
	|		КОГДА &КалендарьПредприятия <> ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка)
	|			И СпрСпособ.ФормироватьПлановыеЗаказы
	|			И СпрСпособ.ПлановаяДатаПоставки > ДАТАВРЕМЯ(1, 1, 1)
	|			И ЕСТЬNULL(ПлановаяДатаЗаказаГрафик.ДатаГрафика, ДатаЗаказаМинусГод.ДатаГрафика) ЕСТЬ NULL ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ                                                          КАК НетПлановойДатыЗаказаПоКалендарю,
	|	ВЫБОР
	|		КОГДА СпрСпособ.ФормироватьПлановыеЗаказы И СпрСпособ.ПлановаяДатаПоставки > ДАТАВРЕМЯ(1, 1, 1)
	|			ИЛИ &КалендарьПредприятия = ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка)
	|			ИЛИ НЕ ЕСТЬNULL(ДатаПоставки.ДатаГрафика, ДатаПоставкиПлюсГод.ДатаГрафика) ЕСТЬ NULL ТОГДА
	|			ЛОЖЬ
	|		ИНАЧЕ
	|			ИСТИНА
	|	КОНЕЦ                                                          КАК НетДатыПоставкиПоКалендарю,
	|	ВЫБОР
	|		КОГДА СпрСпособ.Ссылка ЕСТЬ NULL ТОГДА
	|			1
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ                                                          КАК Порядок", "
	|ПОМЕСТИТЬ " + ИмяВременнойТаблицы) + "
	|ИЗ" +
	?(ИмяИсходнойТаблицы = Неопределено, "
	|	СпособыОбеспечения КАК СпособыОбеспечения", "
	|	" + ИмяИсходнойТаблицы + " КАК СпособыОбеспечения") + "
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОбеспеченияПотребностей КАК СпрСпособ
	|		ПО СпособыОбеспечения.СпособОбеспечения = СпрСпособ.Ссылка
	
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КалендарныеГрафики КАК ТекущаяДата
	|		ПО &КалендарьПредприятия = ТекущаяДата.Календарь
	|			И ТекущаяДата.ДатаГрафика = &НачалоПериода
	|			И ТекущаяДата.Год = ГОД(&НачалоПериода)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КалендарныеГрафики КАК ДатаПоставки
	|		ПО &КалендарьПредприятия = ДатаПоставки.Календарь
	|			И ДатаПоставки.Год = ГОД(&НачалоПериода)
	|			И ДатаПоставки.КоличествоДнейВГрафикеСНачалаГода =
	|				ТекущаяДата.КоличествоДнейВГрафикеСНачалаГода
	|				+ ЕСТЬNULL(СпрСпособ.СрокИсполненияЗаказа, 0)
	|				+ ВЫБОР
	|					КОГДА ТекущаяДата.ДеньВключенВГрафик ТОГДА
	|						0
	|					ИНАЧЕ
	|						1
	|				КОНЕЦ
	|			И ДатаПоставки.ДеньВключенВГрафик
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КалендарныеГрафики КАК ВсегоРабочихДнейВГоду
	|		ПО &КалендарьПредприятия = ВсегоРабочихДнейВГоду.Календарь
	|			И ДатаПоставки.ДатаГрафика ЕСТЬ NULL
	|			И ВсегоРабочихДнейВГоду.ДатаГрафика = НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(&НачалоПериода, ГОД), ДЕНЬ)
	|			И ВсегоРабочихДнейВГоду.Год = ГОД(&НачалоПериода)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КалендарныеГрафики КАК ДатаПоставкиПлюсГод
	|		ПО &КалендарьПредприятия = ДатаПоставкиПлюсГод.Календарь
	|			И ДатаПоставки.ДатаГрафика ЕСТЬ NULL
	|			И ДатаПоставкиПлюсГод.КоличествоДнейВГрафикеСНачалаГода =
	|				ЕСТЬNULL(СпрСпособ.СрокИсполненияЗаказа, 0)
	|				- (ВсегоРабочихДнейВГоду.КоличествоДнейВГрафикеСНачалаГода - ТекущаяДата.КоличествоДнейВГрафикеСНачалаГода)
	|				+ ВЫБОР
	|					КОГДА ТекущаяДата.ДеньВключенВГрафик ТОГДА
	|						0
	|					ИНАЧЕ
	|						1
	|				КОНЕЦ
	|			И ДатаПоставкиПлюсГод.Год = ГОД(&НачалоПериода) + 1
	|			И ДатаПоставкиПлюсГод.ДеньВключенВГрафик
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КалендарныеГрафики КАК ПлановаяДатаПоставкиГрафик
	|			ПО &КалендарьПредприятия = ПлановаяДатаПоставкиГрафик.Календарь
	|			И ПлановаяДатаПоставкиГрафик.ДатаГрафика = СпрСпособ.ПлановаяДатаПоставки
	|			И ПлановаяДатаПоставкиГрафик.Год = ГОД(СпрСпособ.ПлановаяДатаПоставки)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КалендарныеГрафики КАК ПлановаяДатаЗаказаГрафик
	|			ПО &КалендарьПредприятия = ПлановаяДатаЗаказаГрафик.Календарь
	|			И СпрСпособ.ФормироватьПлановыеЗаказы
	|			И ПлановаяДатаЗаказаГрафик.Год = ГОД(СпрСпособ.ПлановаяДатаПоставки)
	|			И ПлановаяДатаЗаказаГрафик.КоличествоДнейВГрафикеСНачалаГода =
	|				ПлановаяДатаПоставкиГрафик.КоличествоДнейВГрафикеСНачалаГода
	|				- СпрСпособ.СрокИсполненияЗаказа
	|			И ПлановаяДатаЗаказаГрафик.ДеньВключенВГрафик
	
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КалендарныеГрафики КАК ВсегоРабочихДнейВГодуПлановогоЗаказа
	|		ПО &КалендарьПредприятия = ВсегоРабочихДнейВГодуПлановогоЗаказа.Календарь
	|			И СпрСпособ.ФормироватьПлановыеЗаказы
	|			И ПлановаяДатаЗаказаГрафик.ДатаГрафика ЕСТЬ NULL
	|			И ВсегоРабочихДнейВГодуПлановогоЗаказа.ДатаГрафика =
	|					ВЫБОР КОГДА СпрСпособ.ФормироватьПлановыеЗаказы И СпрСпособ.ПлановаяДатаПоставки > ДАТАВРЕМЯ(1,1,1) ТОГДА
	|									ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(СпрСпособ.ПлановаяДатаПоставки, ГОД), ДЕНЬ, -1)
	|								ИНАЧЕ
	|									NULL
	|							КОНЕЦ
	|			И ВсегоРабочихДнейВГодуПлановогоЗаказа.Год = ГОД(СпрСпособ.ПлановаяДатаПоставки) - 1
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КалендарныеГрафики КАК ДатаЗаказаМинусГод
	|		ПО &КалендарьПредприятия = ДатаЗаказаМинусГод.Календарь
	|			И СпрСпособ.ФормироватьПлановыеЗаказы
	|			И ПлановаяДатаЗаказаГрафик.ДатаГрафика ЕСТЬ NULL
	|			И ДатаЗаказаМинусГод.КоличествоДнейВГрафикеСНачалаГода =
	|				ВсегоРабочихДнейВГодуПлановогоЗаказа.КоличествоДнейВГрафикеСНачалаГода
	|				- СпрСпособ.СрокИсполненияЗаказа
	|				+ ПлановаяДатаПоставкиГрафик.КоличествоДнейВГрафикеСНачалаГода
	|			И ДатаЗаказаМинусГод.Год = ГОД(СпрСпособ.ПлановаяДатаПоставки) - 1
	|			И ДатаЗаказаМинусГод.ДеньВключенВГрафик" + 
	?(ИмяВременнойТаблицы = Неопределено, "
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	СпрСпособ.Наименование", "
	|ИНДЕКСИРОВАТЬ ПО
	|	СпособОбеспечения
	|;
	|
	|/////////////////////////////////////////////////////////////////////////
	|");

КонецФункции

// Предназначена для получения текста запроса ссылок на элементы справочника "Способы обеспечения потребностей",
// для которых требуется актуализация реквизитов "Плановая дата поставки" и "Дата следующей поставки", вследствие
// изменения текущей даты.
//
// Возвращаемое значение:
//   Строка - Текст запроса способов обеспечения, требующих акутуализацию на дату, заданную параметром в тексте запроса
//            "НачалоПериода".
//
Функция СформироватьТекстЗапросаАктуализацииСпособовОбеспечения() Экспорт
	
	// Актуализации требуют реквизиты ПлановаяДатаПоставки и ДатаСледующейПоставки, если для способа обеспечения
	// предусмотрено плановое формирование заказа и ПлановаяДатаЗаказа (дата заказа на плановую дату поставки) пройдена
	// (меньше текущей).
	Возврат
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СпособыОбеспечения.СпособОбеспечения            КАК СпособОбеспечения
	|ИЗ
	|	СпособыОбеспечения КАК СпособыОбеспечения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СпособыОбеспеченияПотребностей КАК СпрСпособ
	|		ПО СпрСпособ.Ссылка = СпособыОбеспечения.СпособОбеспечения
	|ГДЕ
	|	СпрСпособ.ФормироватьПлановыеЗаказы
	|	И СпрСпособ.ПлановаяДатаЗаказа < &НачалоПериода
	|	И (СпрСпособ.ПлановаяДатаПоставки <> ДАТАВРЕМЯ(1, 1, 1)
	|		ИЛИ СпрСпособ.ДатаСледующейПоставки <> ДАТАВРЕМЯ(1, 1, 1))";
	
КонецФункции

//Процедура ЗаполнитьРеквизитыСпособаОбеспечения
//	Параметры:
//		"СтруктураСпособаОбеспечения" - Структура, содержит ссылку на способ обеспечения
//		и поля, в которые будут помещены реквизиты способа обеспечения.
//		"НачалоПериода"  - дата, на которую получаются значения реквизитов.
//
Процедура ЗаполнитьРеквизитыСпособаОбеспечения(ПараметрыЦиклаПоставки, НачалоПериода, АктуализироватьДаты) Экспорт
	
	СпособОбеспечения = ПараметрыЦиклаПоставки.СпособОбеспечения;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("КалендарьПредприятия", Константы.ОсновнойКалендарьПредприятия.Получить());
	Запрос.УстановитьПараметр("СпособОбеспечения",    СпособОбеспечения);
	Запрос.УстановитьПараметр("НачалоПериода",        НачалоПериода);
	
	Если АктуализироватьДаты И ЗначениеЗаполнено(СпособОбеспечения) Тогда
		
		Запрос.Текст = "ВЫБРАТЬ
		|	&СпособОбеспечения КАК СпособОбеспечения
		|ПОМЕСТИТЬ СпособыОбеспечения
		|;
		|
		|/////////////////////////////////////////////////////////////////////
		|" + Обработки.ОбеспечениеПотребностей.СформироватьТекстЗапросаАктуализацииСпособовОбеспечения();
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			
			СпособОбъект = Выборка.СпособОбеспечения.ПолучитьОбъект();
			Справочники.СпособыОбеспеченияПотребностей.АктуализироватьГрафикЗаказовНаСервере(СпособОбъект, НачалоПериода);
			СпособОбъект.Записать();
			
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&СпособОбеспечения КАК СпособОбеспечения,
	|	NULL КАК Частота
	|ПОМЕСТИТЬ СпособыОбеспечения
	|;
	|
	|/////////////////////////////////////////////////////////////////////
	|" + СформироватьТекстЗапросаРеквизитовСпособаОбеспечения();
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	ЗаполнитьЗначенияСвойств(ПараметрыЦиклаПоставки, Выборка);
	
КонецПроцедуры

// Предназначена для расчета статистики потребления товара на складе.
//
// Параметры:
//  Параметры - Структура - структура с полями:
//     * КлючПотребности - Структура - структура с полями:
//        ** Номенклатура   - СправочникСсылка.Номенклатура               - номенклатура для расчета статистики
//        ** Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика для расчета статистики
//        ** Склад          - СправочникСсылка.Склады                     - склад для расчета статистики.
//  АдресРезультата - УникальныйИдентификатор, Строка - адрес во временном хранилище.
//
// Возвращаемое значение:
//  Структура - Структура, содержащая данные о среднедневном потреблении. Состав структуры:
//    * СреднедневноеПотребление - Число  - среднедневное потребление в единицах хранения номенклатуры
//    * СреднееОтклонение        - Число  - СКО среднедневного потребления, рассчитанное в процентах
//    * ПотреблениеСтабильно     - Булево - истина, если вариация менее 5%, ложь - в противном случае
//    * ДанныеЗаПериод           - СтандартныйПериод - период за который расчитана статистика.
//
Функция СтатистикаПотребления(Параметры, АдресРезультата) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	&Номенклатура   КАК Номенклатура,
		|	&Характеристика КАК Характеристика,
		|	&Склад          КАК Склад
		|ПОМЕСТИТЬ ВтТовары";
	Для Каждого Свойство Из Параметры.КлючПотребности Цикл
		Запрос.УстановитьПараметр(Свойство.Ключ, Свойство.Значение);
	КонецЦикла;
	Запрос.Выполнить();
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос.УстановитьПараметр("НачалоТекущегоДня", НачалоДня(ТекущаяДатаСеанса()));
	ОбеспечениеСервер.УстановитьПараметрыЗапросаРасчетаСтатистики(Запрос);
	ОбеспечениеСервер.ДобавитьВременнуюТаблицуСтатистикиПотребления(Запрос, "ВтТовары");
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Статистика.СреднедневноеПотребление КАК СреднедневноеПотребление,
		|	Статистика.ДисперсияПотребления     КАК ДисперсияПотребления,
		|	Статистика.ПотреблениеСтабильно     КАК ПотреблениеСтабильно,
		|
		|	Статистика.Потребление          КАК Потребление,
		|	Статистика.ДатаНачалаПериода    КАК ДатаНачалаПериода,
		|	Статистика.ДатаОкончанияПериода КАК ДатаОкончанияПериода
		|ИЗ
		|	ВтСтатистикаПотребления КАК Статистика";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Результат = Новый Структура("СреднедневноеПотребление, СреднееОтклонение, ПотреблениеСтабильно, ДанныеЗаПериод");
	Если Выборка.Следующий() Тогда
		Результат.СреднедневноеПотребление = Выборка.СреднедневноеПотребление;
		Если ЗначениеЗаполнено(Выборка.ДисперсияПотребления) И Выборка.ДисперсияПотребления > 0 Тогда
			Результат.СреднееОтклонение    = Pow(Выборка.ДисперсияПотребления, 0.5);
		КонецЕсли;
		Результат.ПотреблениеСтабильно     = Выборка.ПотреблениеСтабильно;
		
		Результат.ДанныеЗаПериод = Новый СтандартныйПериод(Выборка.ДатаНачалаПериода, Выборка.ДатаОкончанияПериода);
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
	
КонецФункции

#Область СтруктурыДанных

// Возвращает структуру с описанием полей таблицы потребностей по заказам.
//
// Параметры:
//  Секции - Строка - Определяет блоки полей, возможные значения: "СвойстваПотребности", "РеквизитыЗаказаКОбеспечению"
//                    "РеквизитыОбеспечивающегоЗаказа".
//
// Возвращаемое значение:
//  Структура - свойство структуры - имя поля, значение свойства - тип поля.
//
Функция ПоляТаблицыЗаказы(Секции) Экспорт

	Поля = Новый Структура();

	Если СтрНайти(Секции, "СвойстваПотребности") Тогда

		Поля.Вставить("Номенклатура",             Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		Поля.Вставить("Характеристика",           Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		Поля.Вставить("Склад",                    Новый ОписаниеТипов("СправочникСсылка.Склады"));
		Поля.Вставить("ПодразделениеПолучатель",  Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия"));
		Поля.Вставить("Назначение",               Новый ОписаниеТипов("СправочникСсылка.Назначения"));

	КонецЕсли;

	Если СтрНайти(Секции, "РеквизитыЗаказаКОбеспечению") Тогда

		Поля.Вставить("Заказ",        ОбеспечениеКлиентСервер.ОписаниеТиповЗаказыКОбеспечению());
		Поля.Вставить("ДатаОтгрузки", ОбщегоНазначенияУТ.ПолучитьОписаниеТиповДаты(ЧастиДаты.Дата));

	КонецЕсли;

	Если СтрНайти(Секции, "РеквизитыОбеспечивающегоЗаказа") Тогда

		Поля.Вставить("ТипОбеспечения",      Новый ОписаниеТипов("ПеречислениеСсылка.ТипыОбеспечения"));
		Поля.Вставить("ИсточникОбеспечения", Новый ОписаниеТипов("СправочникСсылка.Склады, СправочникСсылка.Партнеры, СправочникСсылка.СтруктураПредприятия"));
		Поля.Вставить("Соглашение",          Новый ОписаниеТипов("СправочникСсылка.СоглашенияСПоставщиками"));
		Поля.Вставить("СпособОбеспечения",   Новый ОписаниеТипов("СправочникСсылка.СпособыОбеспеченияПотребностей"));
		Поля.Вставить("ВидЦены",             Новый ОписаниеТипов("СправочникСсылка.ВидыЦенПоставщиков"));
		Поля.Вставить("ДатаПоставки",        ОбщегоНазначенияУТ.ПолучитьОписаниеТиповДаты(ЧастиДаты.Дата));
		Поля.Вставить("КЗаказу",             ОбщегоНазначенияУТ.ПолучитьОписаниеТиповЧисла(15, 3));

	КонецЕсли;

	Возврат Поля;

КонецФункции

// Возвращает структуру с описанием полей таблицы потребностей для поддержания запасов.
//
// Параметры:
//  Секции - Строка - Определяет блоки полей, возможные значения: "СвойстваПотребности", "РеквизитыОбеспечивающегоЗаказа".
//
// Возвращаемое значение:
//  Структура - свойство структуры - имя поля, значение свойства - тип поля.
//
Функция ПоляТаблицыЗапасы(Секции) Экспорт

	Поля = Новый Структура();

	Если СтрНайти(Секции, "СвойстваПотребности") Тогда

		Поля.Вставить("Номенклатура",   Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		Поля.Вставить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		Поля.Вставить("Склад",          Новый ОписаниеТипов("СправочникСсылка.Склады"));

	КонецЕсли;

	Если СтрНайти(Секции, "РеквизитыОбеспечивающегоЗаказа") Тогда

		Поля.Вставить("ТипОбеспечения",      Новый ОписаниеТипов("ПеречислениеСсылка.ТипыОбеспечения"));
		Поля.Вставить("ИсточникОбеспечения", Новый ОписаниеТипов("СправочникСсылка.Склады, СправочникСсылка.Партнеры, СправочникСсылка.СтруктураПредприятия"));
		Поля.Вставить("Соглашение",          Новый ОписаниеТипов("СправочникСсылка.СоглашенияСПоставщиками"));
		Поля.Вставить("СпособОбеспечения",   Новый ОписаниеТипов("СправочникСсылка.СпособыОбеспеченияПотребностей"));
		Поля.Вставить("ВидЦены",             Новый ОписаниеТипов("СправочникСсылка.ВидыЦенПоставщиков"));
		Поля.Вставить("ДатаПоставки",        ОбщегоНазначенияУТ.ПолучитьОписаниеТиповДаты(ЧастиДаты.Дата));
		Поля.Вставить("КЗаказу",             ОбщегоНазначенияУТ.ПолучитьОписаниеТиповЧисла(15, 3));

	КонецЕсли;

	Возврат Поля;

КонецФункции

// Заполняет и проводит документ созданный обработкой.
//
// Параметры:
//  МенеджерДокументов - ДокументМенеджер - программный объект, менеджер документа,
//  ДанныеЗаполнения - Структура - Структура содержащая значения реквизитов и табличные части для заполнения документа,
//  Статус - ПеречислениеСсылка.СтатусыВнутреннмихЗаказов, ПеречислениеСсылка.СтатусыЗаказовНаПеремещение,
//           ПеречислениеСсылка.СтатусыЗаказовПоставщикам - статус, в котором необходимо провести документ
//  СообщенияОбОшибках - Структура - содержит возникшие сообщенияоб ошибка записи документа.
//
// Возвращаемое значение:
//   ДокументОбъект.ЗаказПоставщику     - Проведенный заказ, созданный обработкой.
//   ДокументОбъект.ЗаказНаСборку       - Проведенный заказ, созданный обработкой.
//   ДокументОбъект.ЗаказНаПеремещение  - Проведенный заказ, созданный обработкой.
//   ДокументОбъект.ЗаказНаПроизводство - Проведенный заказ, созданный обработкой.
//
Функция ЗаполнитьИПровестиДокумент(МенеджерДокументов, ДанныеЗаполнения, Статус, СообщенияОбОшибках) Экспорт

	ДокументОбъект = МенеджерДокументов.СоздатьДокумент();
	ДанныеЗаполнения.Вставить("Дата", ТекущаяДатаСеанса());

	ДокументОбъект.Заполнить(ДанныеЗаполнения);

	Если Не ЗначениеЗаполнено(ДокументОбъект.Статус) Тогда
		ДокументОбъект.Статус = Статус;
	КонецЕсли;

	ЗаписатьДокумент(ДокументОбъект, СообщенияОбОшибках);

	Возврат ДокументОбъект;

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныйПрограммныйИнтерфейс

Процедура ТаблицаЗапасы(Параметры, АдресРезультата) Экспорт
	
	Запрос = Новый Запрос();
	ИспользоватьАссортимент = Ложь;
	
	Если Параметры.ТипОтбора = "ОтборПоЗначениям" Тогда
		
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
		Запрос.УстановитьПараметр("Номенклатура",      Параметры.Отбор.Номенклатура);
		Запрос.УстановитьПараметр("Характеристика",    Параметры.Отбор.Характеристика);
		Запрос.УстановитьПараметр("Склад",             Параметры.Отбор.Склад);
		Запрос.УстановитьПараметр("СпособОбеспечения", Параметры.Отбор.СпособОбеспечения);
		Запрос.Текст =
			"ВЫБРАТЬ
			|	&Номенклатура      КАК Номенклатура,
			|	&Характеристика    КАК Характеристика,
			|	&Склад             КАК Склад,
			|	&СпособОбеспечения КАК СпособОбеспечения
			|ПОМЕСТИТЬ ВтТовары
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Способы.СпособОбеспечения                           КАК СпособОбеспечения,
			|	Способы.СпособОбеспечения.ФормироватьПлановыеЗаказы КАК ФормироватьПлановыеЗаказы,
			|	NULL                                                КАК ДатаПоставки
			|ПОМЕСТИТЬ ВтСпособыОбеспеченияПереопределенный
			|ИЗ
			|	ВтТовары КАК Способы";
		Запрос.Выполнить();
		
	Иначе
		
		СхемаКомпоновкиДанных = Обработки.ОбеспечениеПотребностей.ПолучитьМакет("МакетКомпоновкиДляСерверныхОтборов");
		АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных);
		
		ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных);
		КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных();
		КомпоновщикНастроек.Инициализировать(ИсточникНастроек);
		КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
		КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(Параметры.Отбор);
		
		СхемаКомпоновкиДанных.НаборыДанных.Набор.Запрос = ВременнаяТаблицаТоваровДляПоддержанияЗапаса();
		Запрос = ПолучитьЗапросСОтборамиКомпоновкиДанных(АдресСхемыКомпоновкиДанных, КомпоновщикНастроек, "Набор");
		УдалитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
		Запрос.Выполнить();
		
		Запрос.Текст =
			
			"ВЫБРАТЬ
			|	Способы.СпособОбеспечения         КАК СпособОбеспечения,
			|	Способы.ФормироватьПлановыеЗаказы КАК ФормироватьПлановыеЗаказы,
			|	Способы.ДатаПоставки              КАК ДатаПоставки
			|ПОМЕСТИТЬ ВтСпособыОбеспеченияПереопределенный
			|ИЗ
			|	&ТаблицаСпособовОбеспечения КАК Способы";
		
		Запрос.УстановитьПараметр("ТаблицаСпособовОбеспечения", Параметры.ТаблицаСпособовОбеспечения);
		Запрос.Выполнить();
		ИспользоватьАссортимент = ПолучитьФункциональнуюОпцию("ИспользоватьАссортимент");
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос.УстановитьПараметр("НачалоТекущегоДня", НачалоДня(ТекущаяДатаСеанса()));
	ОбеспечениеСервер.УстановитьПараметрыЗапросаРасчетаСтатистики(Запрос);
	ОбеспечениеСервер.ДобавитьВременнуюТаблицуСтатистикиПотребления(Запрос, "ВтТовары");
	
	УстановитьПривилегированныйРежим(Ложь);
	
	ТекстыЗапросов = Новый Массив();
	ТекстыЗапросов.Добавить(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Товары.СпособОбеспечения КАК Ссылка
		|ПОМЕСТИТЬ ВтСпособыОбеспечения
		|ИЗ
		|	ВтТовары КАК Товары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСпособыОбеспеченияПереопределенный КАК ТаблицаСпособыОбеспеченияПререопределенный
		|		ПО ЕСТЬNULL(Товары.СпособОбеспечения, ЗНАЧЕНИЕ(Справочник.СпособыОбеспеченияПотребностей.ПустаяСсылка))
		|			= ТаблицаСпособыОбеспеченияПререопределенный.СпособОбеспечения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|");
	ТекстыЗапросов.Добавить(Справочники.СпособыОбеспеченияПотребностей.ВременнаяТаблицаДатПоставок());
	ТекстыЗапросов.Добавить("УНИЧТОЖИТЬ ВтСпособыОбеспечения");
	ТекстыЗапросов.Добавить(ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
	ТекстыЗапросов.Добавить(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Товары.Склад             КАК Склад,
		|	Товары.СпособОбеспечения КАК СпособОбеспечения
		|
		|ПОМЕСТИТЬ ВтСкладыИСпособыОбеспечения
		|ИЗ
		|	ВтТовары КАК Товары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|");
	
	ТекстыЗапросов.Добавить(Справочники.СпособыОбеспеченияПотребностей.ВременнаяТаблицаИнтерваловРаботыСкладов());
	
	ТекстыЗапросов.Добавить(ВременнаяТаблицаРазличнаяНоменклатура("ВтТовары"));
	ТекстыЗапросов.Добавить(ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	ТекстыЗапросов.Добавить(РегистрыСведений.СхемыОбеспечения.ВременнаяТаблицаТопологияСкладов("РазличнаяНоменклатура"));
	ТекстыЗапросов.Добавить(ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	ТекстыЗапросов.Добавить(РегистрыСведений.СхемыОбеспечения.ВременнаяТаблицаРазличныеТоварыИСклады("ВтТовары", Ложь));
	ТекстыЗапросов.Добавить(ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	ТекстыЗапросов.Добавить(ВременнаяТаблицаДанныеЗаполненияЗаказаПоставщику());
	ТекстыЗапросов.Добавить(ВременнаяТаблицаСвободныеОстатки(Ложь));
	ТекстыЗапросов.Добавить(ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	ТекстыЗапросов.Добавить(ВременнаяТаблицаЗаказыКПоступлениюИОтгрузке(Ложь));
	ТекстыЗапросов.Добавить(ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
	Текст =
		"ВЫБРАТЬ
		|	Т.Номенклатура                 КАК Номенклатура,
		|	Т.Характеристика               КАК Характеристика,
		|	Т.Склад                        КАК Склад,
		|	Т.Номенклатура.Наименование    КАК НоменклатураНаименование,
		|	Т.Характеристика.Наименование  КАК ХарактеристикаНаименование,
		|	Т.Склад.Наименование           КАК СкладНаименование,
		|
		|	ВЫБОР КОГДА ТоварныеОграничения.ОбеспечениеЗаказовПриПоддержанииЗапаса
		|				= ЗНАЧЕНИЕ(Перечисление.ОбеспечениеЗаказовПриПоддержанииЗапаса.НезависимоОтЗапасов) ТОГДА
		|				
		|				ВЫБОР КОГДА ЕСТЬNULL(Заказы.ПоступитМинусРезерв, 0) < 0 ТОГДА
		|							
		|							ЕСТЬNULL(Остатки.ВНаличииМинусРезерв, 0)
		|								+ ЕСТЬNULL(Заказы.ПоступитМинусРезерв, 0)
		|								- ЕСТЬNULL(ЗаказыВнутриСети.КРезервированию, 0)
		|							
		|						ИНАЧЕ
		|							
		|							ЕСТЬNULL(Остатки.ВНаличииМинусРезерв, 0)
		|								- ВЫБОР КОГДА ЕСТЬNULL(ЗаказыВнутриСети.КРезервированию, 0) < ЕСТЬNULL(Заказы.ПоступитМинусРезерв, 0) ТОГДА
		|											0
		|										ИНАЧЕ
		|											ЕСТЬNULL(ЗаказыВнутриСети.КРезервированию, 0) - ЕСТЬNULL(Заказы.ПоступитМинусРезерв, 0)
		|									КОНЕЦ
		|							
		|					КОНЕЦ
		|				
		|			ИНАЧЕ
		|				
		|				ЕСТЬNULL(Остатки.ВНаличии, 0)
		|					- ВЫБОР КОГДА ЕСТЬNULL(ЗаказыВнутриСети.КОтгрузке, 0) < ЕСТЬNULL(Заказы.Поступит, 0) ТОГДА
		|								0
		|							ИНАЧЕ
		|								ЕСТЬNULL(ЗаказыВнутриСети.КОтгрузке, 0) - ЕСТЬNULL(Заказы.Поступит, 0)
		|						КОНЕЦ
		|					
		|		КОНЕЦ КАК Остаток,
		|	
		|	ВЫБОР КОГДА ТоварныеОграничения.ОбеспечениеЗаказовПриПоддержанииЗапаса
		|				= ЗНАЧЕНИЕ(Перечисление.ОбеспечениеЗаказовПриПоддержанииЗапаса.НезависимоОтЗапасов) ТОГДА
		|			
		|				ВЫБОР КОГДА ЕСТЬNULL(Заказы.ПоступитМинусРезерв, 0) > ЕСТЬNULL(ЗаказыВнутриСети.КРезервированию, 0) ТОГДА
		|						0
		|					ИНАЧЕ
		|						ЕСТЬNULL(ЗаказыВнутриСети.КРезервированию, 0) - ЕСТЬNULL(Заказы.ПоступитМинусРезерв, 0)
		|				КОНЕЦ
		|					+ ЕСТЬNULL(Остатки.ВНаличии, 0) - ЕСТЬNULL(Остатки.ВНаличииМинусРезерв, 0)
		|				
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК РезервыНаСкладе,
		|		
		|	ВЫБОР КОГДА ТоварныеОграничения.ОбеспечениеЗаказовПриПоддержанииЗапаса
		|					= ЗНАЧЕНИЕ(Перечисление.ОбеспечениеЗаказовПриПоддержанииЗапаса.НезависимоОтЗапасов) ТОГДА
		|				
		|				ВЫБОР КОГДА ЕСТЬNULL(Заказы.ПоступитМинусРезерв, 0) < 0 ТОГДА
		|								0
		|						ИНАЧЕ
		|							
		|							ЕСТЬNULL(Заказы.ПоступитМинусРезерв, 0)
		|								- ВЫБОР КОГДА ЕСТЬNULL(ЗаказыВнутриСети.КРезервированию, 0) < ЕСТЬNULL(Заказы.ПоступитМинусРезерв, 0) ТОГДА
		|											ЕСТЬNULL(ЗаказыВнутриСети.КРезервированию, 0)
		|										ИНАЧЕ
		|											ЕСТЬNULL(Заказы.ПоступитМинусРезерв, 0)
		|									КОНЕЦ
		|					КОНЕЦ
		|				
		|			ИНАЧЕ
		|				
		|				ЕСТЬNULL(Заказы.Поступит, 0)
		|					- ВЫБОР КОГДА ЕСТЬNULL(ЗаказыВнутриСети.КОтгрузке, 0) < ЕСТЬNULL(Заказы.Поступит, 0) ТОГДА
		|								ЕСТЬNULL(ЗаказыВнутриСети.КОтгрузке, 0)
		|							ИНАЧЕ
		|								ЕСТЬNULL(Заказы.Поступит, 0)
		|						КОНЕЦ
		|				
		|		КОНЕЦ КАК ЗаказыКПоступлению,
		|
		// Параметры номенклатуры
		|	&ТекстЗапросаВесНоменклатуры                        КАК Вес,
		|	Т.Номенклатура.ЕдиницаИзмерения                     КАК ЕдиницаИзмерения,
		|	Т.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины КАК ТипЕдиницыИзмерения,
		|	Т.Номенклатура.Код                                  КАК Код,
		|	Т.Номенклатура.Артикул                              КАК Артикул,
		|	Т.Номенклатура.ИспользоватьУпаковки                 КАК ИспользоватьУпаковки,
		|
		// Товарные ограничения
		|	ЕСТЬNULL(ТоварныеОграничения.МетодОбеспеченияПотребностей,
		|		ЗНАЧЕНИЕ(Перечисление.МетодыОбеспеченияПотребностей.ЗаказПодЗаказ)) КАК МетодОбеспечения,
		|	ТоварныеОграничения.МинимальноеКоличествоЗапаса      КАК МинимальныйЗапас,
		|	ТоварныеОграничения.МаксимальноеКоличествоЗапаса     КАК МаксимальныйЗапас,
		|	ТоварныеОграничения.СтраховоеКоличествоЗапаса        КАК СтраховойЗапас,
		|
		// Упаковки заказа
		|	СпрУпаковки.Ссылка                                   КАК УпаковкаЗаказа,
		|	ЕСТЬNULL(СпрУпаковки.Числитель, 1)                   КАК ЧислительУпаковки,
		|	ЕСТЬNULL(СпрУпаковки.Знаменатель, 1)                 КАК ЗнаменательУпаковки,
		|
		// Способ обеспечения
		|	Т.СпособОбеспечения                                  КАК СпособОбеспечения,
		|	ЕСТЬNULL(Т.СпособОбеспечения.ТипОбеспечения,
		|		ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Покупка))  КАК ТипОбеспечения,
		|	ТаблицаСпособовОбеспеченияПереопределенный.ФормироватьПлановыеЗаказы КАК ФормироватьПлановыеЗаказы,
		|	ВЫБОР КОГДА ТаблицаСпособовОбеспеченияПереопределенный.ФормироватьПлановыеЗаказы ТОГДА
		|				ТаблицаСпособовОбеспечения.ПлановаяДатаПоставки
		|			ИНАЧЕ
		|				ЕСТЬNULL(ТаблицаСпособовОбеспеченияПереопределенный.ДатаПоставки,
		|					ТаблицаСпособовОбеспечения.ДатаВозможнойПоставки)
		|		КОНЕЦ                                            КАК ДатаПоставки,
		|	ТаблицаСпособовОбеспечения.ПлановаяДатаЗаказа        КАК ДатаЗаказа,
		|	ТаблицаСпособовОбеспечения.ПлановаяДатаПоставки      КАК ДатаБлижайшейПоставкиПоГрафику,
		|	ТаблицаСпособовОбеспечения.ДатаСледующейПоставки     КАК ДатаСледующейПоставкиПоГрафику,
		|	ЕСТЬNULL(&ФОЗаказыНаПеремещенияВключена И НЕ Т.Склад.ЦеховаяКладовая
		|		ИЛИ &ФОПроизводство2_2Включена И Т.Склад.ЦеховаяКладовая, ЛОЖЬ) КАК ПеремещениеРазрешено,
		|
		// Данные для расчета запасов прогнозным методом
		|	ВЫБОР КОГДА НЕ ТаблицаСпособовОбеспеченияПереопределенный.ФормироватьПлановыеЗаказы ТОГДА
		|				ТаблицаСпособовОбеспечения.ОбеспечиваемыйПериод
		|			ИНАЧЕ
		|				ИнтервалМеждуПоставками.КоличествоДней
		|		КОНЕЦ                                            КАК ОбеспечиваемыйПериод,
		|	ВозможнаяПоставка.КоличествоДней                     КАК СрокПоставки,
		|	ПлановаяПоставка.КоличествоДней                      КАК СрокДоПлановойПоставки,
		|
		|	ВЫБОР КОГДА ТоварныеОграничения.МетодОбеспеченияПотребностей
		|			= ЗНАЧЕНИЕ(Перечисление.МетодыОбеспеченияПотребностей.ПоддержаниеЗапасаНаСрокПоСтатистике) ТОГДА
		|				ЕСТЬNULL(ТаблицаСтатистики.СреднедневноеПотребление, 0)
		|			ИНАЧЕ
		|				ТоварныеОграничения.НормаПотребления
		|		КОНЕЦ                                            КАК СреднедневноеПотребление,
		|
		// Диагностика проблем
		|	СпрКалендарьСклада.Ссылка                                       КАК КалендарьСклада,
		|	ОсновнойКалендарь.Значение                                      КАК КалендарьПредприятия,
		|
		|	ТаблицаСпособовОбеспечения.ОшибкаНеЗаполненкалендарьПредприятия КАК ОшибкаНеЗаполненКалендарьПредприятия,
		|	ВЫБОР КОГДА ВозможнаяПоставка.КоличествоДней ЕСТЬ NULL
		|			ИЛИ ПлановаяПоставка.КоличествоДней ЕСТЬ NULL
		|			ИЛИ ИнтервалМеждуПоставками.КоличествоДней ЕСТЬ NULL ТОГДА
		|				ИСТИНА
		|			ИНАЧЕ
		|				ЛОЖЬ
		|		КОНЕЦ                                                       КАК ОшибкаНеЗаполненКалендарьСклада,
		|	ЕСТЬNULL(ТаблицаСтатистики.ПотреблениеСтабильно, ЛОЖЬ)          КАК ПотреблениеСтабильно,
		|
		// Условия закупок
		|	//%УсловияЗакупок
		|	ЕСТЬNULL(ДанныеЗаполнения.Партнер,
		|		Т.СпособОбеспечения.ИсточникОбеспеченияПотребностей)        КАК ИсточникОбеспечения,
		|	ВЫБОР КОГДА Т.СпособОбеспечения.Соглашение <> ЗНАЧЕНИЕ(Справочник.СоглашенияСПоставщиками.ПустаяСсылка) ТОГДА
		|				Т.СпособОбеспечения.Соглашение
		|			ИНАЧЕ
		|				ДанныеЗаполнения.Соглашение
		|		КОНЕЦ                                                       КАК Соглашение
		|ИЗ
		|	ВтТовары КАК Т
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСпособыОбеспеченияПереопределенный КАК ТаблицаСпособовОбеспеченияПереопределенный
		|		ПО ЕСТЬNULL(Т.СпособОбеспечения, ЗНАЧЕНИЕ(Справочник.СпособыОбеспеченияПотребностей.ПустаяСсылка))
		|			= ТаблицаСпособовОбеспеченияПереопределенный.СпособОбеспечения
		|		 И ВЫБОР КОГДА Т.СпособОбеспечения.ТипОбеспечения = ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Перемещение) ТОГДА
		|					ЕСТЬNULL(&ФОЗаказыНаПеремещенияВключена И НЕ Т.Склад.ЦеховаяКладовая
		|						ИЛИ &ФОПроизводство2_2Включена И Т.Склад.ЦеховаяКладовая, ЛОЖЬ)
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТоварныеОграничения КАК ТоварныеОграничения
		|		ПО &ПодстановкаТоварногоОграничения
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ОсновнойКалендарьПредприятия КАК ОсновнойКалендарь
		|		ПО ИСТИНА
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Календари КАК СпрКалендарьСклада
		|		ПО ВЫБОР КОГДА Т.Склад.Календарь <> ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка) ТОГДА
		|					Т.Склад.Календарь
		|				ИНАЧЕ
		|					ОсновнойКалендарь.Значение
		|			КОНЕЦ = СпрКалендарьСклада.Ссылка
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСпособыОбеспеченияДатыПоставок КАК ТаблицаСпособовОбеспечения
		|		ПО Т.СпособОбеспечения = ТаблицаСпособовОбеспечения.Ссылка
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтИнтервалКалендарногоГрафика КАК ВозможнаяПоставка
		|		ПО СпрКалендарьСклада.Ссылка = ВозможнаяПоставка.Календарь
		|		 И &НачалоТекущегоДня = ВозможнаяПоставка.Дата1
		|		 И ТаблицаСпособовОбеспечения.ДатаВозможнойПоставки = ВозможнаяПоставка.Дата2
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтИнтервалКалендарногоГрафика КАК ПлановаяПоставка
		|		ПО СпрКалендарьСклада.Ссылка = ПлановаяПоставка.Календарь
		|		 И &НачалоТекущегоДня = ПлановаяПоставка.Дата1
		|		 И ТаблицаСпособовОбеспечения.ПлановаяДатаПоставки = ПлановаяПоставка.Дата2
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтИнтервалКалендарногоГрафика КАК ИнтервалМеждуПоставками
		|		ПО СпрКалендарьСклада.Ссылка = ИнтервалМеждуПоставками.Календарь
		|		 И ТаблицаСпособовОбеспечения.ПлановаяДатаПоставки = ИнтервалМеждуПоставками.Дата1
		|		 И ТаблицаСпособовОбеспечения.ДатаСледующейПоставки = ИнтервалМеждуПоставками.Дата2
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСвободныеОстатки КАК Остатки
		|		ПО Т.Номенклатура   = Остатки.Номенклатура
		|		 И Т.Характеристика = Остатки.Характеристика
		|		 И Т.Склад          = Остатки.Склад
		|		 И Остатки.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтЗаказы КАК Заказы
		|		ПО Т.Номенклатура   = Заказы.Номенклатура
		|		 И Т.Характеристика = Заказы.Характеристика
		|		 И Т.Склад          = Заказы.Склад
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтЗаказыРаспределительнойСетиНеОтгруженные КАК ЗаказыВнутриСети
		|		ПО Т.Номенклатура   = ЗаказыВнутриСети.Номенклатура
		|		 И Т.Характеристика = ЗаказыВнутриСети.Характеристика
		|		 И Т.Склад          = ЗаказыВнутриСети.ЦентрСети
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтатистикаПотребления КАК ТаблицаСтатистики
		|		ПО Т.Номенклатура   = ТаблицаСтатистики.Номенклатура
		|		 И Т.Характеристика = ТаблицаСтатистики.Характеристика
		|		 И Т.Склад          = ТаблицаСтатистики.Склад
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДанныеЗаполненияЗаказаПоставщику КАК ДанныеЗаполнения
		|		ПО Т.Номенклатура    = ДанныеЗаполнения.Номенклатура
		|		 И Т.Характеристика  = ДанныеЗаполнения.Характеристика
		|		 И Т.Склад           = ДанныеЗаполнения.Склад
		|		 И ЕСТЬNULL(Т.СпособОбеспечения.ТипОбеспечения = ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Покупка), ИСТИНА)
		|		 И Т.СпособОбеспечения.ИсточникОбеспеченияПотребностей.Ссылка ЕСТЬ NULL
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныПоставщиков КАК ЦеныПоставщиков
		|		ПО Т.Номенклатура       = ЦеныПоставщиков.Номенклатура
		|			И Т.Характеристика  = ЦеныПоставщиков.Характеристика
		|			И ЕСТЬNULL(ДанныеЗаполнения.ВидЦеныПоставщика, Т.СпособОбеспечения.ВидЦеныПоставщика) = ЦеныПоставщиков.ВидЦеныПоставщика
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
		|		ПО ЦеныПоставщиков.Валюта = КурсыВалют.Валюта
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсВалютаСоглашения
		|		ПО ЕСТЬNULL(ДанныеЗаполнения.Валюта, Т.СпособОбеспечения.Соглашение.Валюта) = КурсВалютаСоглашения.Валюта
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТоварныеОграничения КАК УпаковкиЗаказа
		|		ПО &ПодстановкаУпаковкиЗаказа
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК СпрУпаковки
		|		ПО УпаковкиЗаказа.УпаковкаЗаказа = СпрУпаковки.Ссылка
		|
		|//%Ассортимент
		|
		|УПОРЯДОЧИТЬ ПО
		|	НоменклатураНаименование, ХарактеристикаНаименование";
	
	Текст = РегистрыСведений.ТоварныеОграничения.ПодставитьСоединение(Текст,
		"ПодстановкаТоварногоОграничения");
	Текст = РегистрыСведений.ТоварныеОграничения.ПодставитьСоединениеДляУпаковкиЗаказа(Текст,
		"ПодстановкаУпаковкиЗаказа");
	Текст = СтрЗаменить(Текст, "&ТекстЗапросаВесНоменклатуры",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"Т.Номенклатура.ЕдиницаИзмерения",
			"Т.Номенклатура"));
	
	Текст = СтрЗаменить(Текст, "//%УсловияЗакупок", ТекстПолейУсловияЗакупок());
	
	Если ИспользоватьАссортимент Тогда
		Текст = СтрЗаменить(Текст, "//%Ассортимент", ТекстУсловияАссортимент());
	Иначе
		Текст = СтрЗаменить(Текст, "//%Ассортимент", "");
	КонецЕсли;
	
	ТекстыЗапросов.Добавить(Текст);
	
	Запрос.Текст = СтрСоединить(ТекстыЗапросов);
	
	Запрос.УстановитьПараметр("ИспользоватьХарактеристикиНоменклатуры",
		ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры"));
	Запрос.УстановитьПараметр("ФОЗаказыНаПеремещенияВключена",
		ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыНаПеремещение"));
	Запрос.УстановитьПараметр("ФОПроизводство2_2Включена",
		ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеПроизводством2_2"));
	
	Запрос.УстановитьПараметр("НачалоТекущегоДня", НачалоДня(ТекущаяДатаСеанса()));
	
	КоэффициентВалютыУпрУчета = РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыУправленческогоУчета(НачалоДня(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("КоэффициентВалютыУпрУчета", КоэффициентВалютыУпрУчета);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(ТекущаяДатаСеанса()));
	
	УстановитьПривилегированныйРежим(Истина);
	ПоместитьВоВременноеХранилище(Запрос.Выполнить().Выгрузить(), АдресРезультата);
	
КонецПроцедуры

Процедура ТаблицаПотребностейПоЗаказамИОстатков(Параметры, АдресРезультата) Экспорт
	
	ТекстыЗапросов = Новый Массив;
	
	Если Параметры.ТипОтбора = "ОтборПоЗначениям" Тогда
	
		Запрос = Новый Запрос();
		ТекстыЗапросов.Добавить(
			"ВЫБРАТЬ
			|	&Номенклатура   КАК Номенклатура,
			|	&Характеристика КАК Характеристика,
			|	&Склад          КАК Склад,
			|	&ДатаОтгрузкиЗаказа      КАК ДатаОтгрузкиЗаказа,
			|	&СкладОтгрузкиЗаказа     КАК СкладОтгрузкиЗаказа,
			|	&ПодразделениеПолучатель КАК ПодразделениеПолучатель,
			|	ВЫБОР КОГДА &СпособОбеспечения = ЗНАЧЕНИЕ(Справочник.СпособыОбеспеченияПотребностей.ПустаяСсылка) ТОГДА
			|			NULL
			|		ИНАЧЕ
			|			&СпособОбеспечения
			|	КОНЕЦ           КАК СпособОбеспечения,
			|	
			|	&Назначение     КАК Назначение,
			|	&Заказ          КАК Заказ,
			|	&ДатаОтгрузки   КАК ДатаОтгрузки,
			|	&Требуется      КАК Количество
			|ПОМЕСТИТЬ ВтТовары
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Способы.СпособОбеспечения                                           КАК СпособОбеспечения,
			|	ЕСТЬNULL(Способы.СпособОбеспечения.ФормироватьПлановыеЗаказы, ЛОЖЬ) КАК ФормироватьПлановыеЗаказы,
			|	NULL                                                                КАК ДатаПоставки
			|ПОМЕСТИТЬ ВтСпособыОбеспеченияПереопределенный
			|ИЗ
			|	ВтТовары КАК Способы
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|");
		
		Для Каждого Свойство Из Параметры.Отбор Цикл
			Запрос.УстановитьПараметр(Свойство.Ключ, Свойство.Значение);
		КонецЦикла;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
		
	Иначе
		
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
			
		СхемаКомпоновкиДанных = Обработки.ОбеспечениеПотребностей.ПолучитьМакет("МакетКомпоновкиДляСерверныхОтборов");
		АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных);
		
		ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных);
		КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных();
		КомпоновщикНастроек.Инициализировать(ИсточникНастроек);
		КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
		КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(Параметры.Отбор);
		
		// Основной запрос
		Тексты = Новый Массив();
		Тексты.Добавить(ВременнаяТаблицаОтборПоСегментам());
		Тексты.Добавить(ВременнаяТаблицаОтборПоЗаказам());
		Тексты.Добавить(ВременнаяТаблицаПотребностиВсехСкладовПоЗаказам());
		Тексты.Добавить(ВременнаяТаблицаРазличнаяНоменклатура("ПотребностиВсехСкладовПоЗаказам"));
		Тексты.Добавить(РегистрыСведений.СхемыОбеспечения.ВременнаяТаблицаТопологияСкладов("РазличнаяНоменклатура"));
		Тексты.Добавить(ВременнаяТаблицаПотребностиВыбранныхСкладов());
		Тексты.Добавить(ВыборкаПервойЗаписиПотребностейПоЗаказамДляКомпоновкиЗапроса());
		Текст = СтрСоединить(Тексты, ОбщегоНазначения.РазделительПакетаЗапросов());

		СхемаКомпоновкиДанных.НаборыДанных.Набор.Запрос = Текст;
		Запрос = ПолучитьЗапросСОтборамиКомпоновкиДанных(АдресСхемыКомпоновкиДанных, КомпоновщикНастроек, "Набор");
		УдалитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
		
		// Отделим часть запроса, которую необходимо выполнить привилегированно
		Позиция = СтрНайти(Запрос.Текст, "УНИЧТОЖИТЬ ВтОстаткиОбособленныхТоваров");
		Позиция = СтрНайти(Запрос.Текст, "ВЫБРАТЬ", НаправлениеПоиска.СНачала, Позиция);
		ТекстЗапросаПривилегированно = Сред(Запрос.Текст, Позиция);
		Запрос.Текст = Лев(Запрос.Текст, Позиция - 1);
		Запрос.УстановитьПараметр("ОптимизироватьЗапасыРаспределительногоЦентра",
			Константы.ОптимизироватьЗапасыРаспределительногоЦентра.Получить());
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
		УстановитьПривилегированныйРежим(Истина);
		Запрос.Текст = ТекстЗапросаПривилегированно;
		Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
		
		ТекстыЗапросов.Добавить(
			"ВЫБРАТЬ
			|	Способы.СпособОбеспечения         КАК СпособОбеспечения,
			|	Способы.ФормироватьПлановыеЗаказы КАК ФормироватьПлановыеЗаказы,
			|	Способы.ДатаПоставки              КАК ДатаПоставки
			|ПОМЕСТИТЬ ВтСпособыОбеспеченияПереопределенный
			|ИЗ
			|	&ТаблицаСпособовОбеспечения КАК Способы
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|");
		
		Запрос.УстановитьПараметр("ТаблицаСпособовОбеспечения", Параметры.ТаблицаСпособовОбеспечения);
		
		ТекстыЗапросов.Добавить(
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Товары.Номенклатура            КАК Номенклатура,
			|	Товары.Характеристика          КАК Характеристика,
			|	Товары.Склад                   КАК Склад,
			|	Товары.Назначение              КАК Назначение,
			|	Товары.ПодразделениеПолучатель КАК Подразделение
			|ПОМЕСТИТЬ ТоварыИСкладыПопавшиеВОбработку
			|ИЗ
			|	ВтТовары КАК Товары
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура, Характеристика, Склад, Назначение
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|");
			
	КонецЕсли;
	
	ТекстыЗапросов.Добавить(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Товары.СпособОбеспечения КАК Ссылка
		|ПОМЕСТИТЬ ВтСпособыОбеспечения
		|ИЗ
		|	ВтТовары КАК Товары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСпособыОбеспеченияПереопределенный КАК ТаблицаСпособыОбеспеченияПререопределенный
		|		ПО ЕСТЬNULL(Товары.СпособОбеспечения, ЗНАЧЕНИЕ(Справочник.СпособыОбеспеченияПотребностей.ПустаяСсылка))
		|			= ТаблицаСпособыОбеспеченияПререопределенный.СпособОбеспечения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|");
	ТекстыЗапросов.Добавить(Справочники.СпособыОбеспеченияПотребностей.ВременнаяТаблицаДатПоставок());
	
	ТекстыЗапросов.Добавить(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Товары.Склад             КАК Склад,
		|	Товары.СпособОбеспечения КАК СпособОбеспечения
		|
		|ПОМЕСТИТЬ ВтСкладыИСпособыОбеспечения
		|ИЗ
		|	ВтТовары КАК Товары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|");
	ТекстыЗапросов.Добавить(Справочники.СпособыОбеспеченияПотребностей.ВременнаяТаблицаИнтерваловРаботыСкладов());
	
	ТекстыЗапросов.Добавить(ВременнаяТаблицаГраницыОбеспечиваемогоПериода());
	ТекстыЗапросов.Добавить(ВременнаяТаблицаДанныеЗаполненияЗаказаПоставщику());
	
	Текст =
		"ВЫБРАТЬ
		|	Т.Номенклатура                                   КАК Номенклатура,
		|	Т.Характеристика                                 КАК Характеристика,
		|	Т.Склад                                          КАК Склад," +
	?(Параметры.ТипОтбора = "ОтборПоЗначениям", "",
		"
		|	ЕСТЬNULL(ТопологияСкладов.Центр,
		|		ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))    КАК РаспределительныйЦентр,
		|	НЕ ТоварыИСкладыПопавшиеВОбработку.Номенклатура ЕСТЬ NULL КАК РаспределительныйЦентрЕстьВТаблице,") + "
		|	Т.СкладОтгрузкиЗаказа                            КАК СкладОтгрузкиЗаказа,
		|	Т.ДатаОтгрузкиЗаказа                             КАК ДатаОтгрузкиЗаказа,
		|	Т.Номенклатура.Наименование                      КАК НоменклатураНаименование,
		|	Т.Характеристика.Наименование                    КАК ХарактеристикаНаименование,
		|	Т.Склад.Наименование                             КАК СкладНаименование,
		|
		|	Т.ПодразделениеПолучатель                        КАК ПодразделениеПолучатель,
		|	Т.Назначение                                     КАК Назначение,
		|	Т.Заказ                                          КАК Заказ,
		|	СпрПриоритет.Ссылка                              КАК Приоритет,
		|	ЕСТЬNULL(СпрПриоритет.РеквизитДопУпорядочивания,
		|		0)                                           КАК ПриоритетРеквизитДопУпорядочивания,
		|	Т.ДатаОтгрузки                                   КАК ДатаОтгрузки,
		|	Т.Количество                                     КАК Требуется,
		|
		|	ВЫБОР КОГДА Т.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		 И ТоварныеОграничения.МетодОбеспеченияПотребностей <> ЗНАЧЕНИЕ(Перечисление.МетодыОбеспеченияПотребностей.ЗаказПодЗаказ)
		|		 И ТоварныеОграничения.ОбеспечениеЗаказовПриПоддержанииЗапаса = ЗНАЧЕНИЕ(Перечисление.ОбеспечениеЗаказовПриПоддержанииЗапаса.НезависимоОтЗапасов) ТОГДА
		|				ИСТИНА
		|			ИНАЧЕ
		|				ЛОЖЬ
		|		КОНЕЦ КАК ОбеспечиватьНезависимоОтЗапасов,
		|	
		|	(Т.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		И ТоварныеОграничения.МетодОбеспеченияПотребностей <> ЗНАЧЕНИЕ(Перечисление.МетодыОбеспеченияПотребностей.ЗаказПодЗаказ)
		|		И ТоварныеОграничения.ОбеспечениеЗаказовПриПоддержанииЗапаса = ЗНАЧЕНИЕ(Перечисление.ОбеспечениеЗаказовПриПоддержанииЗапаса.НезависимоОтЗапасов))
		|	 ИЛИ Т.СкладОтгрузкиЗаказа = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|	 ИЛИ Т.Склад = Т.СкладОтгрузкиЗаказа                КАК РазмещатьЗаказыНаОтгрузку,
		// Параметры номенклатуры
		|	&ТекстЗапросаВесНоменклатуры                        КАК Вес,
		|	Т.Номенклатура.ЕдиницаИзмерения                     КАК ЕдиницаИзмерения,
		|	Т.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины КАК ТипЕдиницыИзмерения,
		|	Т.Номенклатура.Код                                  КАК Код,
		|	Т.Номенклатура.Артикул                              КАК Артикул,
		|	Т.Номенклатура.ИспользоватьУпаковки                 КАК ИспользоватьУпаковки,
		|
		|	ВЫБОР КОГДА Т.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа) ТОГДА
		|				ИСТИНА
		|			ИНАЧЕ
		|				ЛОЖЬ
		|		КОНЕЦ КАК ЭтоРабота,
		|
		// Способ обеспечения
		|	Т.СпособОбеспечения                                  КАК СпособОбеспечения,
		|	ЕСТЬNULL(Т.СпособОбеспечения.ТипОбеспечения,
		|		ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Покупка))  КАК ТипОбеспечения,
		|	ЕСТЬNULL(ТаблицаСпособовОбеспеченияПереопределенный.ФормироватьПлановыеЗаказы, ЛОЖЬ) КАК ФормироватьПлановыеЗаказы,
		|	ВЫБОР КОГДА ТаблицаСпособовОбеспеченияПереопределенный.ФормироватьПлановыеЗаказы ТОГДА
		|			ТаблицаСпособовОбеспечения.ПлановаяДатаПоставки
		|		ИНАЧЕ
		// При методе Заказ под заказ и правиле Заказ при достижении точки заказа, источником является ДатаОтгрузки заказа,
		// если она больше даты возможной поставки по способу обеспечения.
		|			ВЫБОР КОГДА (НЕ ТаблицаСпособовОбеспеченияПереопределенный.ФормироватьПлановыеЗаказы)
		|				И (ТоварныеОграничения.МетодОбеспеченияПотребностей = ЗНАЧЕНИЕ(Перечисление.МетодыОбеспеченияПотребностей.ЗаказПодЗаказ)
		|					ИЛИ ТоварныеОграничения.МетодОбеспеченияПотребностей ЕСТЬ NULL) ТОГДА
		|				
		|				ВЫБОР КОГДА ЕСТЬNULL(ТаблицаСпособовОбеспеченияПереопределенный.ДатаПоставки, ТаблицаСпособовОбеспечения.ДатаВозможнойПоставки) >
		|					
		|					ВЫБОР КОГДА Т.ДатаОтгрузки > &НачалоТекущегоДня ТОГДА
		|								Т.ДатаОтгрузки
		|							ИНАЧЕ
		|								&НачалоТекущегоДня
		|						КОНЕЦ
		|					
		|					ТОГДА
		|							ЕСТЬNULL(ТаблицаСпособовОбеспеченияПереопределенный.ДатаПоставки, ТаблицаСпособовОбеспечения.ДатаВозможнойПоставки)
		|					ИНАЧЕ
		|						
		|						ВЫБОР КОГДА Т.ДатаОтгрузки > &НачалоТекущегоДня ТОГДА
		|									Т.ДатаОтгрузки
		|								ИНАЧЕ
		|									&НачалоТекущегоДня
		|							КОНЕЦ
		|						
		|				КОНЕЦ
		|					
		|			ИНАЧЕ
		|				ЕСТЬNULL(ТаблицаСпособовОбеспеченияПереопределенный.ДатаПоставки,
		|					ТаблицаСпособовОбеспечения.ДатаВозможнойПоставки)
		|			КОНЕЦ
		|		КОНЕЦ                                            КАК ДатаПоставки,
		|	
		|	ЕСТЬNULL(ТаблицаСпособовОбеспеченияПереопределенный.ДатаПоставки,
		|		ТаблицаСпособовОбеспечения.ДатаВозможнойПоставки) КАК ДатаВозможнойПоставки,
		|	
		|	НЕ ТаблицаСпособовОбеспеченияПереопределенный.ФормироватьПлановыеЗаказы
		|		И (ТоварныеОграничения.МетодОбеспеченияПотребностей = ЗНАЧЕНИЕ(Перечисление.МетодыОбеспеченияПотребностей.ЗаказПодЗаказ)
		|					ИЛИ ТоварныеОграничения.МетодОбеспеченияПотребностей ЕСТЬ NULL) КАК ДатаПоставкиРассчитанаПоДатеОтгрузки,
		|	
		|	ТаблицаСпособовОбеспечения.ПлановаяДатаЗаказа        КАК ДатаЗаказа,
		|	ТаблицаСпособовОбеспечения.ПлановаяДатаПоставки      КАК ДатаБлижайшейПоставкиПоГрафику,
		|	ЕСТЬNULL(ТаблицаСпособовОбеспечения.ДатаСледующейПоставки, ДАТАВРЕМЯ(1,1,1)) КАК ДатаСледующейПоставкиПоГрафику,
		|	ЕСТЬNULL(ТаблицаСпособовОбеспечения.ОбеспечиваемыйПериод, 0)      КАК ОбеспечиваемыйПериод,
		|	ВозможнаяПоставка.КоличествоДней                     КАК СрокПоставки,
		|	ЕСТЬNULL(ВЫБОР КОГДА СпрКалендарьСклада.Ссылка ЕСТЬ NULL ТОГДА
		|				ДОБАВИТЬКДАТЕ(ТаблицаСпособовОбеспечения.ДатаВозможнойПоставки,
		|					ДЕНЬ, ТаблицаСпособовОбеспечения.ОбеспечиваемыйПериод)
		|			ИНАЧЕ
		|				ГраницаПериода.Дата
		|		КОНЕЦ, ДАТАВРЕМЯ(1,1,1)) КАК ГраницаОбеспечиваемогоПериода,
		|	ЕСТЬNULL(&ФОЗаказыНаПеремещенияВключена И НЕ Т.Склад.ЦеховаяКладовая
		|		ИЛИ &ФОПроизводство2_2Включена И Т.Склад.ЦеховаяКладовая, ЛОЖЬ) КАК ПеремещениеРазрешено,
		|
		// Диагностика проблем
		|	СпрКалендарьСклада.Ссылка                                       КАК КалендарьСклада,
		|	ОсновнойКалендарь.Значение                                      КАК КалендарьПредприятия,
		|
		|	ТаблицаСпособовОбеспечения.ОшибкаНеЗаполненкалендарьПредприятия КАК ОшибкаНеЗаполненКалендарьПредприятия,
		|	ВЫБОР КОГДА ВозможнаяПоставка.КоличествоДней ЕСТЬ NULL
		|			ИЛИ ГраницаПериода.Дата ЕСТЬ NULL ТОГДА
		|				ИСТИНА
		|			ИНАЧЕ
		|				ЛОЖЬ
		|		КОНЕЦ                                                       КАК ОшибкаНеЗаполненКалендарьСклада,
		|
		// Условия закупок
		|	//%УсловияЗакупок
		|	ЕСТЬNULL(ДанныеЗаполнения.Партнер,
		|		Т.СпособОбеспечения.ИсточникОбеспеченияПотребностей)        КАК ИсточникОбеспечения,
		|	ВЫБОР КОГДА Т.СпособОбеспечения.Соглашение <> ЗНАЧЕНИЕ(Справочник.СоглашенияСПоставщиками.ПустаяСсылка) ТОГДА
		|				Т.СпособОбеспечения.Соглашение
		|			ИНАЧЕ
		|				ДанныеЗаполнения.Соглашение
		|		КОНЕЦ                                                       КАК Соглашение,
		|	СпрУпаковки.Ссылка                                              КАК УпаковкаЗаказа,
		|	ЕСТЬNULL(СпрУпаковки.Числитель, 1)                              КАК ЧислительУпаковки,
		|	ЕСТЬNULL(СпрУпаковки.Знаменатель, 1)                            КАК ЗнаменательУпаковки
		|ИЗ
		|	ВтТовары КАК Т
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ОсновнойКалендарьПредприятия КАК ОсновнойКалендарь
		|		ПО ИСТИНА
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСпособыОбеспеченияПереопределенный КАК ТаблицаСпособовОбеспеченияПереопределенный
		|		ПО ЕСТЬNULL(Т.СпособОбеспечения, ЗНАЧЕНИЕ(Справочник.СпособыОбеспеченияПотребностей.ПустаяСсылка))
		|			= ТаблицаСпособовОбеспеченияПереопределенный.СпособОбеспечения
		|		 И ВЫБОР КОГДА Т.СпособОбеспечения.ТипОбеспечения = ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Перемещение) ТОГДА
		|					ЕСТЬNULL(&ФОЗаказыНаПеремещенияВключена И НЕ Т.Склад.ЦеховаяКладовая
		|						ИЛИ &ФОПроизводство2_2Включена И Т.Склад.ЦеховаяКладовая, ЛОЖЬ)
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Календари КАК СпрКалендарьСклада
		|		ПО ВЫБОР КОГДА Т.Склад.Календарь <> ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка) ТОГДА
		|					Т.Склад.Календарь
		|				ИНАЧЕ
		|					ОсновнойКалендарь.Значение
		|			КОНЕЦ = СпрКалендарьСклада.Ссылка
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСпособыОбеспеченияДатыПоставок КАК ТаблицаСпособовОбеспечения
		|		ПО Т.СпособОбеспечения = ТаблицаСпособовОбеспечения.Ссылка
		|" +
	?(Параметры.ТипОтбора = "ОтборПоЗначениям", "",
		"
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТопологияСкладов КАК ТопологияСкладов
		|		ПО ТопологияСкладов.Номенклатура   = Т.Номенклатура
		|		 И ТопологияСкладов.Характеристика = Т.Характеристика
		|		 И ТопологияСкладов.Склад          = Т.Склад
		|		 И ТопологияСкладов.Центр         <> Т.Склад
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыИСкладыПопавшиеВОбработку КАК ТоварыИСкладыПопавшиеВОбработку
		|		ПО ТоварыИСкладыПопавшиеВОбработку.Номенклатура   = Т.Номенклатура
		|		 И ТоварыИСкладыПопавшиеВОбработку.Характеристика = Т.Характеристика
		|		 И ТоварыИСкладыПопавшиеВОбработку.Назначение     = Т.Назначение
		|		 И ТоварыИСкладыПопавшиеВОбработку.Склад          = ТопологияСкладов.Центр") + "
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОбеспеченияПотребностей КАК РеквизитыСпособаОбеспечения
		|		ПО РеквизитыСпособаОбеспечения.Ссылка = Т.СпособОбеспечения
		|		 И РеквизитыСпособаОбеспечения.ДлительностьВДнях > 0
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтИнтервалКалендарногоГрафика КАК ВозможнаяПоставка
		|		ПО СпрКалендарьСклада.Ссылка = ВозможнаяПоставка.Календарь
		|		 И &НачалоТекущегоДня = ВозможнаяПоставка.Дата1
		|		 И ТаблицаСпособовОбеспечения.ДатаВозможнойПоставки = ВозможнаяПоставка.Дата2
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДатыГрафика КАК ГраницаПериода
		|		ПО СпрКалендарьСклада.Ссылка = ГраницаПериода.Календарь
		|		 И ТаблицаСпособовОбеспечения.ОбеспечиваемыйПериод  = ГраницаПериода.ЧислоДней
		|		 И ТаблицаСпособовОбеспечения.ДатаВозможнойПоставки = ГраницаПериода.ДатаОтсчета
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДанныеЗаполненияЗаказаПоставщику КАК ДанныеЗаполнения
		|		ПО Т.Номенклатура    = ДанныеЗаполнения.Номенклатура
		|		 И Т.Характеристика  = ДанныеЗаполнения.Характеристика
		|		 И Т.Склад           = ДанныеЗаполнения.Склад
		|		 И ЕСТЬNULL(Т.СпособОбеспечения.ТипОбеспечения = ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Покупка), ИСТИНА)
		|		 И Т.СпособОбеспечения.ИсточникОбеспеченияПотребностей.Ссылка ЕСТЬ NULL
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныПоставщиков КАК ЦеныПоставщиков
		|		ПО Т.Номенклатура       = ЦеныПоставщиков.Номенклатура
		|			И Т.Характеристика  = ЦеныПоставщиков.Характеристика
		|			И ЕСТЬNULL(ДанныеЗаполнения.ВидЦеныПоставщика, Т.СпособОбеспечения.ВидЦеныПоставщика) = ЦеныПоставщиков.ВидЦеныПоставщика
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
		|		ПО ЦеныПоставщиков.Валюта = КурсыВалют.Валюта
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсВалютаСоглашения
		|		ПО ЕСТЬNULL(ДанныеЗаполнения.Валюта, Т.СпособОбеспечения.Соглашение.Валюта) = КурсВалютаСоглашения.Валюта
		|
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Приоритеты КАК СпрПриоритет
		|		ПО &ПриоритетУсловие = СпрПриоритет.Ссылка
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТоварныеОграничения КАК ТоварныеОграничения
		|		ПО &ПодстановкаТоварногоОграничения
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТоварныеОграничения КАК УпаковкиЗаказа
		|		ПО &ПодстановкаУпаковкиЗаказа
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК СпрУпаковки
		|		ПО УпаковкиЗаказа.УпаковкаЗаказа = СпрУпаковки.Ссылка
		|
		|//%Ассортимент
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПриоритетРеквизитДопУпорядочивания,
		|	ДатаОтгрузки,
		|	Заказ,
		|	НоменклатураНаименование, ХарактеристикаНаименование, СкладНаименование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВтСпособыОбеспечения;";
	
	Текст = РегистрыСведений.ТоварныеОграничения.ПодставитьСоединение(Текст,
		"ПодстановкаТоварногоОграничения");
	Текст = РегистрыСведений.ТоварныеОграничения.ПодставитьСоединениеДляУпаковкиЗаказа(Текст,
		"ПодстановкаУпаковкиЗаказа");
	Текст = СтрЗаменить(Текст, "&ТекстЗапросаВесНоменклатуры",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"Т.Номенклатура.ЕдиницаИзмерения",
			"Т.Номенклатура"));
	
	ТекстПриоритет = "Т.Заказ.Приоритет";
	Если Параметры.ТипОтбора = "ОтборПоЗначениям" И Параметры.Отбор.Заказ.Метаданные().Реквизиты.Найти("Приоритет") = Неопределено Тогда
		ТекстПриоритет = "NULL";
	КонецЕсли;
	Текст = СтрЗаменить(Текст, "&ПриоритетУсловие", ТекстПриоритет);
	
	Текст = СтрЗаменить(Текст, "//%УсловияЗакупок", ТекстПолейУсловияЗакупок());
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьАссортимент") Тогда
		Текст = СтрЗаменить(Текст, "//%Ассортимент", ТекстУсловияАссортимент());
	Иначе
		Текст = СтрЗаменить(Текст, "//%Ассортимент", "");
	КонецЕсли;
	
	ТекстыЗапросов.Добавить(Текст);
	Запрос.Текст = СтрСоединить(ТекстыЗапросов);
	
	Запрос.УстановитьПараметр("ФОЗаказыНаПеремещенияВключена",
		ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыНаПеремещение"));
	Запрос.УстановитьПараметр("ФОПроизводство2_2Включена",
		ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеПроизводством2_2"));
	
	НачалоДня = НачалоДня(ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("НачалоТекущегоДня", НачалоДня);
	Запрос.УстановитьПараметр("НачалоПериода",     НачалоДня);

	КоэффициентВалютыУпрУчета = РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыУправленческогоУчета(НачалоДня);
	Запрос.УстановитьПараметр("КоэффициентВалютыУпрУчета", КоэффициентВалютыУпрУчета);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	Таблица = РезультатЗапроса.Выгрузить();
	
	Если Параметры.ТипОтбора <> "ОтборПоЗначениям" Тогда
		
		Поля = "Номенклатура,Характеристика,Склад,КЗаказу";
		ТаблицаЗапасы = Параметры.ТаблицаЗапасы.Скопировать(Новый Структура("Отметка", Истина), Поля);
		Запрос.УстановитьПараметр("ТаблицаЗапасы", ТаблицаЗапасы);
	
		ТекстыЗапросов = Новый Массив();
		
		Текст =
			"ВЫБРАТЬ
			|	Запасы.Номенклатура   КАК Номенклатура,
			|	Запасы.Характеристика КАК Характеристика,
			|	Запасы.Склад          КАК Склад,
			|	Запасы.КЗаказу        КАК КЗаказу
			|ПОМЕСТИТЬ ВтЗапасы
			|ИЗ
			|	&ТаблицаЗапасы КАК Запасы
			|ГДЕ
			|	Запасы.КЗаказу > 0";
			
		ТекстыЗапросов.Добавить(Текст);
		
		ТекстыЗапросов.Добавить(РегистрыСведений.СхемыОбеспечения.ВременнаяТаблицаРазличныеТоварыИСклады("ТоварыИСкладыПопавшиеВОбработку", Истина));
		ТекстыЗапросов.Добавить(ВременнаяТаблицаСвободныеОстатки(Истина));
		ТекстыЗапросов.Добавить(ВременнаяТаблицаЗаказыКПоступлениюИОтгрузке(Истина));
		
		Текст =
			"ВЫБРАТЬ
			|	Товары.Номенклатура                                    КАК Номенклатура,
			|	Товары.Характеристика                                  КАК Характеристика,
			|	Товары.Склад                                           КАК Склад,
			|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеПолучатель,
			|	Товары.Назначение                                      КАК Назначение,
			|	
			|	ВЫБОР КОГДА Товары.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			|			ИЛИ НастройкаПоддержанияЗапаса.ОбеспечениеЗаказовПриПоддержанииЗапаса ЕСТЬ NULL
			|			ИЛИ НастройкаПоддержанияЗапаса.ОбеспечениеЗаказовПриПоддержанииЗапаса
			|				= ЗНАЧЕНИЕ(Перечисление.ОбеспечениеЗаказовПриПоддержанииЗапаса.ЗаСчетЗапасов) ТОГДА
			|			
			|				ЕСТЬNULL(Запасы.КЗаказу, 0)
			|					+ ЕСТЬNULL(Остатки.ВНаличииМинусРезерв, 0)
			|					+ ЕСТЬNULL(Заказы.ПоступитМинусРезерв, 0)
			|					- ЕСТЬNULL(ЗаказыВнутриСети.КРезервированию, 0)
			|			
			|			ИНАЧЕ // в режиме Обеспечивать независимо к резервированию берутся только излишки (остаток > максимального запаса и т.п.)
			|				
			|				ВЫБОР КОГДА ЕСТЬNULL(Заказы.ПоступитМинусРезерв, 0)
			|								- ЕСТЬNULL(ЗаказыВнутриСети.КРезервированию, 0)
			|								- ЕСТЬNULL(НастройкаПоддержанияЗапаса.МинимальноеКоличествоЗапаса, 0) <= 0 ТОГДА
			|							
			|							ЕСТЬNULL(Остатки.ВНаличииМинусРезерв, 0)
			|								- ВЫБОР КОГДА ЕСТЬNULL(Заказы.ПоступитМинусРезерв, 0) - ЕСТЬNULL(ЗаказыВнутриСети.КРезервированию, 0) > 0 ТОГДА
			|											0
			|										ИНАЧЕ
			|											ЕСТЬNULL(ЗаказыВнутриСети.КРезервированию, 0) - ЕСТЬNULL(Заказы.ПоступитМинусРезерв, 0)
			|									КОНЕЦ
			|									- ЕСТЬNULL(НастройкаПоддержанияЗапаса.МаксимальноеКоличествоЗапаса, 0)
			|									- ЕСТЬNULL(НастройкаПоддержанияЗапаса.СтраховоеКоличествоЗапаса, 0)
			|						
			|						ИНАЧЕ
			|							
			|							ЕСТЬNULL(Остатки.ВНаличииМинусРезерв, 0)
			|								+ ЕСТЬNULL(Заказы.ПоступитМинусРезерв, 0)
			|								- ЕСТЬNULL(ЗаказыВнутриСети.КРезервированию, 0)
			|								- ЕСТЬNULL(НастройкаПоддержанияЗапаса.МаксимальноеКоличествоЗапаса, 0)
			|								- ЕСТЬNULL(НастройкаПоддержанияЗапаса.МинимальноеКоличествоЗапаса, 0)
			|								- ЕСТЬNULL(НастройкаПоддержанияЗапаса.СтраховоеКоличествоЗапаса, 0)
			|						
			|					КОНЕЦ
			|					
			|		КОНЕЦ КАК КРезервированию
			|ИЗ
			|	ТоварыИСкладыПопавшиеВОбработку КАК Товары
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСвободныеОстатки КАК Остатки
			|		ПО Остатки.Номенклатура   = Товары.Номенклатура
			|		 И Остатки.Характеристика = Товары.Характеристика
			|		 И Остатки.Склад          = Товары.Склад
			|		 И Остатки.Назначение     = Товары.Назначение
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВтЗаказы КАК Заказы
			|		ПО Заказы.Номенклатура   = Товары.Номенклатура
			|		 И Заказы.Характеристика = Товары.Характеристика
			|		 И Заказы.Склад          = Товары.Склад
			|		 И Заказы.Назначение     = Товары.Назначение
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВтЗаказыРаспределительнойСетиНеОтгруженные КАК ЗаказыВнутриСети
			|		ПО ЗаказыВнутриСети.Номенклатура   = Товары.Номенклатура
			|		 И ЗаказыВнутриСети.Характеристика = Товары.Характеристика
			|		 И ЗаказыВнутриСети.ЦентрСети      = Товары.Склад
			|		 И ЗаказыВнутриСети.Назначение     = Товары.Назначение
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВтЗапасы КАК Запасы
			|		ПО Запасы.Номенклатура = Товары.Номенклатура
			|		 И Запасы.Характеристика = Товары.Характеристика
			|		 И Запасы.Склад          = Товары.Склад
			|		 И Товары.Назначение     = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТоварныеОграничения КАК НастройкаПоддержанияЗапаса
			|		ПО &ПодстановкаТоварногоОграничения
			|ГДЕ
			|	Товары.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	РаботыНабор.Номенклатура                 КАК Номенклатура,
			|	РаботыНабор.Характеристика               КАК Характеристика,
			|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
			|	РаботыНабор.Подразделение                КАК ПодразделениеПолучатель,
			|	РаботыНабор.Назначение                   КАК Назначение,
			|	СУММА(РаботыНабор.КОбеспечениюРасход)    КАК КРезервированию
			|ИЗ(
			|	ВЫБРАТЬ
			|		Работы.Номенклатура       КАК Номенклатура,
			|		Работы.Характеристика     КАК Характеристика,
			|		Работы.Подразделение      КАК Подразделение,
			|		Работы.Назначение         КАК Назначение,
			|		Работы.КОбеспечениюРасход КАК КОбеспечениюРасход
			|	ИЗ
			|		РегистрНакопления.ОбеспечениеЗаказовРаботами.Обороты(,,,
			|			(Номенклатура, Характеристика, Назначение, Подразделение) В(
			|				ВЫБРАТЬ
			|					Фильтр.Номенклатура   КАК Номенклатура,
			|					Фильтр.Характеристика КАК Характеристика,
			|					Фильтр.Назначение     КАК Назначение,
			|					Фильтр.Подразделение  КАК Подразделение
			|				ИЗ
			|					ТоварыИСкладыПопавшиеВОбработку КАК Фильтр)) КАК Работы
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		Товары.Номенклатура   КАК Номенклатура,
			|		Товары.Характеристика КАК Характеристика,
			|		Товары.Подразделение  КАК Подразделение,
			|		Товары.Назначение     КАК Назначение,
			|		Товары.Количество     КАК КОбеспечениюРасход
			|	ИЗ
			|		Документ.ЗаказПоставщику.Товары КАК Товары
			|			
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТоварыИСкладыПопавшиеВОбработку КАК Фильтр
			|			ПО Товары.Номенклатура   = Фильтр.Номенклатура
			|			 И Товары.Характеристика = Фильтр.Характеристика
			|			 И Товары.Подразделение  = Фильтр.Подразделение
			|			 И Товары.Назначение     = Фильтр.Назначение
			|	ГДЕ
			|		Товары.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.Согласован)
			|			И Товары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
			|			И НЕ Товары.Отменено
			|			И Товары.Ссылка.Проведен) КАК РаботыНабор
			|	СГРУППИРОВАТЬ ПО
			|		РаботыНабор.Номенклатура,
			|		РаботыНабор.Характеристика,
			|		РаботыНабор.Подразделение,
			|		РаботыНабор.Назначение";
		ТекстыЗапросов.Добавить(Текст);
		
		Запрос.Текст = СтрСоединить(ТекстыЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
		Запрос.Текст = РегистрыСведений.ТоварныеОграничения.ПодставитьСоединение(Запрос.Текст, "ПодстановкаТоварногоОграничения", 
			"Товары.Номенклатура,Товары.Характеристика,Товары.Склад", "НастройкаПоддержанияЗапаса");
		РезультатЗапроса = Запрос.Выполнить();
		ТаблицаОстатки = РезультатЗапроса.Выгрузить();
		ТаблицаОстатки.Индексы.Добавить("Номенклатура, Характеристика, Склад, ПодразделениеПолучатель, Назначение");
		
		ТаблицаОстатковНаВсехСкладах = Новый ТаблицаЗначений();
		ТаблицаОстатковНаВсехСкладах.Колонки.Добавить("Номенклатура");
		ТаблицаОстатковНаВсехСкладах.Колонки.Добавить("Характеристика");
		ТаблицаОстатковНаВсехСкладах.Колонки.Добавить("Назначение");
		ТаблицаОстатковНаВсехСкладах.Колонки.Добавить("КРезервированию");
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеПеремещениемОбособленныхТоваров") Тогда
			
			// Получаем все, что заказано по всем складам (Потребность, всего - КЗаказу).
			// Вычитаем разницу (Потребность, всего - Потребность, попавшая в обработку).
			Запрос.Текст =
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	Набор.Номенклатура           КАК Номенклатура,
				|	Набор.Характеристика         КАК Характеристика,
				|	Набор.Назначение             КАК Назначение,
				|	СУММА(Набор.КРезервированию) КАК КРезервированию
				|ИЗ(
				|	ВЫБРАТЬ
				|		ОстаткиОбеспечениеЗаказов.Номенклатура         КАК Номенклатура,
				|		ОстаткиОбеспечениеЗаказов.Характеристика       КАК Характеристика,
				|		ОстаткиОбеспечениеЗаказов.Назначение           КАК Назначение,
				|		ОстаткиОбеспечениеЗаказов.Склад                КАК Склад,
				|		- ОстаткиОбеспечениеЗаказов.КЗаказуОстаток     КАК КРезервированию
				|	ИЗ
				|		РегистрНакопления.ОбеспечениеЗаказов.Остатки(,
				|			(Номенклатура, Характеристика, Назначение) В(
				|				ВЫБРАТЬ РАЗЛИЧНЫЕ
				|					Товары.Номенклатура   КАК Номенклатура,
				|					Товары.Характеристика КАК Характеристика,
				|					Товары.Назначение     КАК Назначение
				|				ИЗ
				|					РазличныеТоварыИСклады КАК Товары
				|				ГДЕ
				|					Товары.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))) КАК ОстаткиОбеспечениеЗаказов
				|	
				|	ОБЪЕДИНИТЬ ВСЕ
				|	
				|	ВЫБРАТЬ
				|		ОстаткиОбеспечениеЗаказов.Номенклатура         КАК Номенклатура,
				|		ОстаткиОбеспечениеЗаказов.Характеристика       КАК Характеристика,
				|		ОстаткиОбеспечениеЗаказов.Назначение           КАК Назначение,
				|		ОстаткиОбеспечениеЗаказов.Склад                КАК Склад,
				|		ОстаткиОбеспечениеЗаказов.ПотребностьОстаток   КАК КРезервированию
				|	ИЗ
				|		РегистрНакопления.ОбеспечениеЗаказов.Остатки(,
				|			(Номенклатура, Характеристика, Назначение, Склад) В(
				|				ВЫБРАТЬ РАЗЛИЧНЫЕ
				|					Товары.Номенклатура   КАК Номенклатура,
				|					Товары.Характеристика КАК Характеристика,
				|					Товары.Назначение     КАК Назначение,
				|					Товары.Склад          КАК Склад
				|				ИЗ
				|					РазличныеТоварыИСклады КАК Товары
				|				ГДЕ
				|					Товары.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))) КАК ОстаткиОбеспечениеЗаказов) КАК Набор
				|СГРУППИРОВАТЬ ПО
				|	Набор.Номенклатура, Набор.Характеристика, Набор.Назначение
				|ИМЕЮЩИЕ
				|	СУММА(Набор.КРезервированию) > 0";
				
			ТаблицаОстатковНаВсехСкладах = Запрос.Выполнить().Выгрузить();
			ТаблицаОстатковНаВсехСкладах.Индексы.Добавить("Номенклатура, Характеристика, Назначение");
			
		КонецЕсли;
		
		
		ЕстьОбособленныеТоварыНаДругихСкладах = ЗначениеЗаполнено(ТаблицаОстатковНаВсехСкладах);
		
		// Расчет количества к резервированию с текущего склада.
		Отбор = Новый Структура("Номенклатура, Характеристика, Склад, Назначение, ПодразделениеПолучатель");
		ОтборОстатковВсехСкладов = Новый Структура("Номенклатура, Характеристика, Назначение");
		КвалифкаторКРезервированию = Новый КвалификаторыЧисла(15, 3, ДопустимыйЗнак.Неотрицательный);
		Таблица.Колонки.Добавить("КРезервированию", Новый ОписаниеТипов("Число", , , КвалифкаторКРезервированию));
		Для Каждого СтрокаЗаказы Из Таблица Цикл
			
			ЗаполнитьЗначенияСвойств(Отбор, СтрокаЗаказы);
			ЗаполнитьЗначенияСвойств(ОтборОстатковВсехСкладов, СтрокаЗаказы);
			СтрокиОстатков = ТаблицаОстатки.НайтиСтроки(Отбор);
			
			Если СтрокиОстатков.Количество() > 0 Тогда
				
				НайденнаяСтрока = СтрокиОстатков[0];
				КРезервированию = Мин(НайденнаяСтрока.КРезервированию, СтрокаЗаказы.Требуется);
				СтрокаЗаказы.КРезервированию = КРезервированию;
				НайденнаяСтрока.КРезервированию = НайденнаяСтрока.КРезервированию - КРезервированию;
				
			КонецЕсли;
			
			Если ЕстьОбособленныеТоварыНаДругихСкладах
				И ЗначениеЗаполнено(СтрокаЗаказы.Назначение)
				И Не СтрокаЗаказы.ЭтоРабота
				И Не СтрокаЗаказы.РаспределительныйЦентрЕстьВТаблице Тогда
				
				НайденныеСтроки = ТаблицаОстатковНаВсехСкладах.НайтиСтроки(ОтборОстатковВсехСкладов);
				Если НайденныеСтроки.Количество() > 0 Тогда
					КРезервированию = Мин(НайденныеСтроки[0].КРезервированию, СтрокаЗаказы.КРезервированию);
					НайденныеСтроки[0].КРезервированию = НайденныеСтроки[0].КРезервированию - КРезервированию;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		// Дообеспечение товарами с других складов.
		Для Каждого СтрокаЗаказы Из Таблица Цикл
			
			Если ЕстьОбособленныеТоварыНаДругихСкладах
				И ЗначениеЗаполнено(СтрокаЗаказы.Назначение)
				И Не СтрокаЗаказы.ЭтоРабота
				И Не ЗначениеЗаполнено(СтрокаЗаказы.РаспределительныйЦентр) Тогда
				
				ЗаполнитьЗначенияСвойств(ОтборОстатковВсехСкладов, СтрокаЗаказы);
				НайденныеСтроки = ТаблицаОстатковНаВсехСкладах.НайтиСтроки(ОтборОстатковВсехСкладов);
				Если НайденныеСтроки.Количество() > 0 Тогда
					КРезервированию = Мин(НайденныеСтроки[0].КРезервированию, СтрокаЗаказы.Требуется - СтрокаЗаказы.КРезервированию);
					НайденныеСтроки[0].КРезервированию = НайденныеСтроки[0].КРезервированию - КРезервированию;
					СтрокаЗаказы.КРезервированию = СтрокаЗаказы.КРезервированию + КРезервированию;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Таблица.Колонки.Добавить("Отметка");
		Таблица.Колонки.Добавить("КЗаказу");
		Таблица.Колонки.Добавить("ДатаЗаказаНаступила");
		Таблица.Колонки.Добавить("ОтгрузкаВнутриПериода");
		Для Каждого СтрокаЗаказы Из Таблица Цикл
			
			РезультатРасчета = КоличествоКЗаказуДляОбеспеченияЗаказов(СтрокаЗаказы);
			ЗаполнитьЗначенияСвойств(СтрокаЗаказы, РезультатРасчета);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Таблица, АдресРезультата);
	
КонецПроцедуры

Процедура ПоместитьДатыПоГрафикуРаботыВоВременнуюТаблицу(МенеджерВременныхТаблиц, ИмяТаблицыПараметровРасчета, ИмяТаблицыРезультата) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПараметрыРасчета.Календарь                КАК Календарь,
	|	ПараметрыРасчета.ТочкаОтсчета             КАК ТочкаОтсчета,
	|	ПараметрыРасчета.ЧислоДней                КАК ЧислоДней,
	|	ГрафикРаботы.ДатаГрафика                  КАК ПлановаяДата
	|
	|ПОМЕСТИТЬ ИмяТаблицыРезультата
	|
	|ИЗ
	|	ИмяТаблицыПараметровРасчета КАК ПараметрыРасчета
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КалендарныеГрафики КАК ГрафикРаботы
	|		ПО ГрафикРаботы.Календарь = ПараметрыРасчета.Календарь
	|			И ГрафикРаботы.ДатаГрафика        >=        ПараметрыРасчета.ТочкаОтсчета
	|			И ГрафикРаботы.Год                >=        ГОД(ПараметрыРасчета.ТочкаОтсчета)
	|			И ГрафикРаботы.ДеньВключенВГрафик
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КалендарныеГрафики КАК ОтсчитанныеДаты
	|		ПО ОтсчитанныеДаты.Календарь = ПараметрыРасчета.Календарь
	|			И ОтсчитанныеДаты.ДатаГрафика    МЕЖДУ    ПараметрыРасчета.ТочкаОтсчета         И    ГрафикРаботы.ДатаГрафика
	|			И ОтсчитанныеДаты.Год            МЕЖДУ    ГОД(ПараметрыРасчета.ТочкаОтсчета)    И    ГОД(ГрафикРаботы.ДатаГрафика)
	|			И ОтсчитанныеДаты.ДеньВключенВГрафик
	|
	|СГРУППИРОВАТЬ ПО
	|	ПараметрыРасчета.Календарь,
	|	ПараметрыРасчета.ТочкаОтсчета,
	|	ПараметрыРасчета.ЧислоДней,
	|	ГрафикРаботы.ДатаГрафика
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(*) - 1 = ПараметрыРасчета.ЧислоДней
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Календарь,
	|	ТочкаОтсчета,
	|	ЧислоДней
	|;
	|////////////////////////////////////////////////////////////////////////////
	|";
	
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "ИмяТаблицыПараметровРасчета", ИмяТаблицыПараметровРасчета);
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "ИмяТаблицыРезультата",        ИмяТаблицыРезультата);
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеЗаказов

// Создает заказы в информационной базе по имеющейся таблице заказов, зафиксированных пользователем.
//
Процедура СформироватьЗаказы(Параметры, АдресРезультата) Экспорт

	ДокументыОбъекты = Новый Массив();
	ТекстКомментарий = НСтр("ru = 'Сформирован автоматически обработкой ""Формирование заказов по потребностям"".'");

	Таблица = Параметры.ТаблицаПотребностей;
	
	// Округление имеет смысл только когда могут использоваться упаковки номенклатуры
	Если Параметры.ИспользоватьУпаковки Тогда
		ОкруглитьПередСозданиемЗаказов(Таблица);
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ТаблицаЗначений", Таблица);
	Запрос.УстановитьПараметр("НачалоДня", НачалоДня(ТекущаяДатаСеанса()));
	
	ТекстыЗапроса = Новый СписокЗначений();
	ТекстыЗапроса.Добавить(ВременнаяТаблицаИзТаблицыКЗаказу(), "");
	ТекстыЗапроса.Добавить(ВременнаяТаблицаДанныеЗаполнения(), "");
	
	ТекстыЗапроса.Добавить(ТаблицаЗаказыПоставщикуКОформлению(),    "ТаблицаЗаказыПоставщику");
	ТекстыЗапроса.Добавить(ТаблицаЗаказыНаПеремещениеКоформлению(), "ТаблицаЗаказыНаПеремещение");
	ТекстыЗапроса.Добавить(ТаблицаЗаказыНаСборкуКОформлению(),      "ТаблицаЗаказыНаСборку");
	Запрос.УстановитьПараметр("УправлениеПроизводством2_2", ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеПроизводством2_2"));
	
	УстановитьПривилегированныйРежим(Истина);
	Таблицы = ОбщегоНазначенияУТ.ВыгрузитьРезультатыЗапроса(Запрос, ТекстыЗапроса);
	УстановитьПривилегированныйРежим(Ложь);
	
	ПоляКлючаЗаказа = "НаправлениеДеятельности, Соглашение, Партнер, Организация, Подразделение";
	МассивТаблиц = ОбеспечениеСервер.РазбитьТаблицуПоЗначениюКлюча(Таблицы.ТаблицаЗаказыПоставщику, ПоляКлючаЗаказа);
	
	Для Каждого Элемент Из МассивТаблиц Цикл
		
		ПоляКлючаЗаказа = "Склад";
		ДанныеЗаполнения = Элемент.Ключ;
		ТаблицыПоСкладам = ОбеспечениеСервер.РазбитьТаблицуПоЗначениюКлюча(Элемент.Таблица, ПоляКлючаЗаказа, "");
		
		Если ТаблицыПоСкладам.Количество() = 2 //Если заказ с товарами один, то добавляем работы в него.
			И Не ЗначениеЗаполнено(ТаблицыПоСкладам[0].Таблица[0].Склад) Тогда
			
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицыПоСкладам[0].Таблица, ТаблицыПоСкладам[1].Таблица);
			ТаблицыПоСкладам.Удалить(0);
			
		КонецЕсли;
		
		Для Каждого Элемент1 Из ТаблицыПоСкладам Цикл
			
			ДанныеЗаполнения.Вставить("Склад", Элемент1.Ключ.Склад);
			Элемент1.Таблица.Колонки.ПодразделениеПолучатель.Имя = "Подразделение";
			
			ДанныеЗаполнения.Вставить("Комментарий", ТекстКомментарий);
			ДанныеЗаполнения.Вставить("Товары", Элемент1.Таблица);
			Документ = ПолучитьДокумент(Документы.ЗаказПоставщику, ДанныеЗаполнения, Перечисления.СтатусыЗаказовПоставщикам.Подтвержден);
			ДокументыОбъекты.Добавить(Документ);
			
		КонецЦикла;
		
	КонецЦикла;
	
	ПоляКлючаЗаказа = "СкладОтправитель, СкладПолучатель, Организация, Подразделение, НаправлениеДеятельности";
	МассивТаблиц = ОбеспечениеСервер.РазбитьТаблицуПоЗначениюКлюча(Таблицы.ТаблицаЗаказыНаПеремещение, ПоляКлючаЗаказа);
	
	Для Каждого Элемент Из МассивТаблиц Цикл
		
		ДанныеЗаполнения = Элемент.Ключ;
		ДанныеЗаполнения.Вставить("Товары", Элемент.Таблица);
		ДанныеЗаполнения.Вставить("Комментарий", ТекстКомментарий);
		Документ = ПолучитьДокумент(Документы.ЗаказНаПеремещение, ДанныеЗаполнения, Перечисления.СтатусыВнутреннихЗаказов.КОбеспечению);
		ДокументыОбъекты.Добавить(Документ);
		
	КонецЦикла;
	
	ПоляКлючаЗаказа = "Организация, Подразделение, Склад, Номенклатура, Характеристика, Назначение, НаправлениеДеятельности,
		|НачалоСборкиРазборки, ОкончаниеСборкиРазборки, ДлительностьСборкиРазборки, Количество, КоличествоУпаковок";
	МассивТаблиц = ОбеспечениеСервер.РазбитьТаблицуПоЗначениюКлюча(Таблицы.ТаблицаЗаказыНаСборку, ПоляКлючаЗаказа);
	
	Для Каждого Элемент Из МассивТаблиц Цикл
		
		ДанныеЗаполнения = Элемент.Ключ;
		ДанныеЗаполнения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.СборкаТоваров);
		ДанныеЗаполнения.Вставить("Комментарий", ТекстКомментарий);
		Документ = ПолучитьДокумент(Документы.ЗаказНаСборку, ДанныеЗаполнения, Перечисления.СтатусыВнутреннихЗаказов.КОбеспечению);
		ДокументыОбъекты.Добавить(Документ);
		
	КонецЦикла;
	
	
	
	ИспользованиеСтатусов = Новый Соответствие();
	ИспользоватьСтатусы = ПолучитьФункциональнуюОпцию("ИспользоватьСтатусыЗаказовПоставщикам");
	ИспользованиеСтатусов.Вставить(Тип("ДокументСсылка.ЗаказПоставщику"),     ИспользоватьСтатусы);
	ИспользоватьСтатусы = ПолучитьФункциональнуюОпцию("ИспользоватьСтатусыЗаказовНаСборку");
	ИспользованиеСтатусов.Вставить(Тип("ДокументСсылка.ЗаказНаСборку"),       ИспользоватьСтатусы);
	ИспользоватьСтатусы = ПолучитьФункциональнуюОпцию("ИспользоватьСтатусыЗаказовНаПеремещение");
	ИспользованиеСтатусов.Вставить(Тип("ДокументСсылка.ЗаказНаПеремещение"),  ИспользоватьСтатусы);
	ИспользованиеСтатусовИтог = Ложь;
	
	// Помещаем в хранилище результат формирования после записи каждого документа на случай прерывания задания
	Результат = Новый Структура("ТаблицаДокументов,СписокСообщений", Параметры.ТаблицаДокументов, Новый Массив);
	Для Каждого Документ Из ДокументыОбъекты Цикл
		
		Ошибка = Не Документ.ПроверитьЗаполнение();
		
		Если Не Ошибка Тогда
			
			НачатьТранзакцию();
			Попытка
				
				Документ.Записать(РежимЗаписиДокумента.Проведение);
				ДобавитьДокументВРезультат(Документ, Результат, ИспользованиеСтатусов);
				ПоместитьВоВременноеХранилище(Результат, Параметры.АдресРезультатаФормированияЗаказов);
				
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				
				ИмяСобытияЖурнала = НСтр("ru = 'Формирование заказов: создание документов'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
				ЗаписьЖурналаРегистрации(ИмяСобытияЖурнала, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				Ошибка = Истина;
				
			КонецПопытки;
			
		КонецЕсли;
		
		Если Ошибка Тогда
			
			НачатьТранзакцию();
			Попытка
				
				Документ.Записать(РежимЗаписиДокумента.Запись);
				ДобавитьДокументВРезультат(Документ, Результат, ИспользованиеСтатусов);
				ПоместитьВоВременноеХранилище(Результат, Параметры.АдресРезультатаФормированияЗаказов);
				
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				
				ИмяСобытияЖурнала = НСтр("ru = 'Формирование заказов: создание документов'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
				ЗаписьЖурналаРегистрации(ИмяСобытияЖурнала, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				ВызватьИсключение;
				
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьЗаказы(Параметры, АдресРезультата) Экспорт
	
	НачатьТранзакцию(); // На случай отмены задания
	Попытка
	
		СписокОшибок = ОбщегоНазначенияУТ.УдалитьДокументы(Параметры.СсылкиНаУдаление);
		
		ПоместитьВоВременноеХранилище(СписокОшибок, АдресРезультата);
		
		ЗафиксироватьТранзакцию();
	
	Исключение
		ОтменитьТранзакцию();
		
		ИмяСобытияЖурнала = НСтр("ru = 'Формирование заказов: удаление документов'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		ЗаписьЖурналаРегистрации(ИмяСобытияЖурнала, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

Функция ВременнаяТаблицаИзТаблицыКЗаказу()

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Таблица.Номенклатура            КАК Номенклатура,
		|	Таблица.Характеристика          КАК Характеристика,
		|	Таблица.Склад                   КАК Склад,
		|	Таблица.ПодразделениеПолучатель КАК ПодразделениеПолучатель,
		|	Таблица.Назначение              КАК Назначение,
		|	
		|	Таблица.Заказ                   КАК Заказ,
		|	
		|	Таблица.ТипОбеспечения          КАК ТипОбеспечения,
		|	Таблица.ИсточникОбеспечения     КАК ИсточникОбеспечения,
		|	Таблица.Соглашение              КАК Соглашение,
		|	Таблица.СпособОбеспечения       КАК СпособОбеспечения,
		|	Таблица.ВидЦены                 КАК ВидЦены,
		|	Таблица.ДатаПоставки            КАК ДатаПоставки,
		|	Таблица.КЗаказу                 КАК КЗаказу
		|
		|ПОМЕСТИТЬ ВтТаблицаЗначений
		|ИЗ
		|	&ТаблицаЗначений КАК Таблица
		|
		|ГДЕ
		|	Таблица.КЗаказу > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СпособОбеспечения
		|;
		|
		|////////////////////////////////////////////////////////
		|";

	Возврат ТекстЗапроса;

КонецФункции

Функция ВременнаяТаблицаДанныеЗаполнения()
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Т.Номенклатура                               КАК Номенклатура,
		|	Т.Характеристика                             КАК Характеристика,
		|	Т.Склад                                      КАК Склад,
		|	Т.ПодразделениеПолучатель                    КАК ПодразделениеПолучатель,
		|	Т.Назначение                                 КАК Назначение,
		|	
		|	ВЫБОР КОГДА ЛОЖЬ ТОГДА
		|				НЕОПРЕДЕЛЕНО
		|			ИНАЧЕ
		|				НЕОПРЕДЕЛЕНО
		|		КОНЕЦ                                    КАК Заказ, // используется для группировки при формировании заказов на производство
		|	
		|	ВЫБОР КОГДА Т.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) ТОГДА
		|				НЕОПРЕДЕЛЕНО
		|			ИНАЧЕ
		|				НЕОПРЕДЕЛЕНО
		|		КОНЕЦ                                    КАК Давалец, // используется для группировки при формировании заказов на производство
		|	
		|	ВЫБОР КОГДА ЛОЖЬ ТОГДА
		|				НЕОПРЕДЕЛЕНО
		|			ИНАЧЕ
		|				НЕОПРЕДЕЛЕНО
		|		КОНЕЦ                                    КАК Договор, // используется для группировки при формировании заказов на производство
		|	
		|	ВЫБОР КОГДА ЛОЖЬ ТОГДА
		|				НЕОПРЕДЕЛЕНО
		|			ИНАЧЕ
		|				НЕОПРЕДЕЛЕНО
		|		КОНЕЦ                                    КАК ТипПроизводственногоПроцесса, // используется для группировки при формировании заказов на производство
		|
		|	Т.ТипОбеспечения                             КАК ТипОбеспечения,
		|	Т.ИсточникОбеспечения                        КАК ИсточникОбеспечения,
		|	Т.Соглашение                                 КАК Соглашение,
		|	ЕСТЬNULL(Т.Назначение.НаправлениеДеятельности,
		|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
		|
		|	ЕСТЬNULL(Т.СпособОбеспечения.Организация,
		|		ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))          КАК Организация,
		|
		|	ЕСТЬNULL(Т.СпособОбеспечения.Подразделение,
		|		ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Подразделение,
		|
		|	ЕСТЬNULL(Т.СпособОбеспечения.ДлительностьВДнях, 0)          КАК ДлительностьВДнях,
		|
		|	Т.ВидЦены                                    КАК ВидЦены,
		|	Т.ДатаПоставки                               КАК ДатаПоставки,
		|	Т.КЗаказу                                    КАК Количество
		|
		|ПОМЕСТИТЬ ВтДанныеЗаполнения
		|ИЗ
		|	ВтТаблицаЗначений КАК Т
		|;
		|
		|////////////////////////////////////////////////////////
		|";
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТаблицаЗаказыПоставщикуКОформлению()

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВЫБОР КОГДА Т.ИсточникОбеспечения = НЕОПРЕДЕЛЕНО ТОГДА
		|				ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
		|			ИНАЧЕ
		|				Т.ИсточникОбеспечения
		|		КОНЕЦ                                 КАК Партнер,
		|	Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Т.Соглашение КАК Соглашение,
		|	Т.Организация КАК Организация,
		|	Т.Подразделение КАК Подразделение,
		|	Т.Склад КАК Склад,
		|	Т.Номенклатура КАК Номенклатура,
		|	МАКСИМУМ(Т.Номенклатура.СтавкаНДС) КАК СтавкаНДС,
		|	Т.Характеристика КАК Характеристика,
		|	Т.Назначение КАК Назначение,
		|	Т.ПодразделениеПолучатель КАК ПодразделениеПолучатель,
		|	Т.ВидЦены КАК ВидЦеныПоставщика,
		|	Т.ДатаПоставки КАК ДатаПоступления,
		|	СУММА(Т.Количество) КАК КоличествоУпаковок,
		|	СУММА(Т.Количество) КАК Количество
		|ИЗ
		|	ВтДанныеЗаполнения КАК Т
		|ГДЕ
		|	Т.ТипОбеспечения = ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Покупка)
		|
		|СГРУППИРОВАТЬ ПО
		|	Т.НаправлениеДеятельности,
		|	Т.Соглашение,
		|	Т.ИсточникОбеспечения,
		|	Т.Организация,
		|	Т.Подразделение,
		|	Т.Склад,
		|	Т.Номенклатура,
		|	Т.Характеристика,
		|	Т.Назначение,
		|	Т.ДатаПоставки,
		|	Т.ВидЦены,
		|	Т.ПодразделениеПолучатель,
		|	Т.ИсточникОбеспечения
		|
		|УПОРЯДОЧИТЬ ПО
		|	НаправлениеДеятельности,
		|	Соглашение,
		|	Партнер,
		|	Организация,
		|	Подразделение,
		|	Склад,
		|	Назначение,
		|	ДатаПоступления,
		|	Номенклатура,
		|	Характеристика,
		|	ПодразделениеПолучатель
		|;
		|
		|/////////////////////////////////////////////////////////
		|";

	Возврат ТекстЗапроса;

КонецФункции

Функция ТаблицаЗаказыНаПеремещениеКОформлению()

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВЫБОР КОГДА Т.ИсточникОбеспечения = НЕОПРЕДЕЛЕНО ТОГДА
		|				ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|			ИНАЧЕ
		|				Т.ИсточникОбеспечения
		|		КОНЕЦ                                 КАК СкладОтправитель,
		|	Т.Организация                             КАК Организация,
		|	Т.Подразделение                           КАК Подразделение,
		|	Т.Склад                                   КАК СкладПолучатель,
		|	Т.НаправлениеДеятельности                 КАК НаправлениеДеятельности,
		|
		|	Т.Номенклатура                            КАК Номенклатура,
		|	Т.Характеристика                          КАК Характеристика,
		|	Т.Назначение                              КАК Назначение,
		|
		|	МАКСИМУМ(ДОБАВИТЬКДАТЕ(Т.ДатаПоставки, ДЕНЬ, - Т.ДлительностьВДнях)) КАК НачалоОтгрузки,
		|	Т.ДатаПоставки                            КАК ОкончаниеПоступления,
		|
		|	СУММА(Т.Количество)                       КАК КоличествоУпаковок,
		|	СУММА(Т.Количество)                       КАК Количество
		|ИЗ
		|	ВтДанныеЗаполнения КАК Т
		|ГДЕ
		|	Т.ТипОбеспечения = ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Перемещение)
		|СГРУППИРОВАТЬ ПО
		|	Т.ИсточникОбеспечения, Т.Организация, Т.Подразделение, Т.Склад, Т.НаправлениеДеятельности,
		|	Т.Номенклатура, Т.Характеристика, Т.Назначение, Т.ДатаПоставки, Т.ДлительностьВДнях
		|УПОРЯДОЧИТЬ ПО
		|	СкладОтправитель, СкладПолучатель, Организация, Подразделение, НаправлениеДеятельности,                 // порядок обхода заказов
		|	Назначение, НачалоОтгрузки, Номенклатура, Характеристика, ОкончаниеПоступления // порядок строк в заказе
		|;
		|
		|/////////////////////////////////////////////////////////
		|";

	Возврат ТекстЗапроса;

КонецФункции

Функция ТаблицаЗаказыНаСборкуКОформлению()

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Т.Организация                             КАК Организация,
		|	Т.Подразделение                           КАК Подразделение,
		|	Т.Склад                                   КАК Склад,
		|	Т.Номенклатура                            КАК Номенклатура,
		|	Т.Характеристика                          КАК Характеристика,
		|	Т.Назначение                              КАК Назначение,
		|	Т.НаправлениеДеятельности                 КАК НаправлениеДеятельности,
		|
		|	МАКСИМУМ(ДОБАВИТЬКДАТЕ(Т.ДатаПоставки, ДЕНЬ, - Т.ДлительностьВДнях)) КАК НачалоСборкиРазборки,
		|
		|	Т.ДатаПоставки                            КАК ОкончаниеСборкиРазборки,
		|	Т.ДатаПоставки                            КАК ЖелаемаяДатаПоступления,
		|	Т.ДлительностьВДнях                       КАК ДлительностьСборкиРазборки,
		|
		|	СУММА(Т.Количество)                       КАК КоличествоУпаковок,
		|	СУММА(Т.Количество)                       КАК Количество
		|ИЗ
		|	ВтДанныеЗаполнения КАК Т
		|ГДЕ
		|	Т.ТипОбеспечения = ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.СборкаРазборка)
		|СГРУППИРОВАТЬ ПО
		|	Т.Организация, Т.Подразделение, Т.Склад, Т.Номенклатура, Т.Характеристика,
		|	Т.Назначение, Т.НаправлениеДеятельности, Т.ДлительностьВДнях, Т.ДатаПоставки
		|УПОРЯДОЧИТЬ ПО
		|	Организация, Подразделение, Склад, Номенклатура, Характеристика, Назначение, НаправлениеДеятельности, НачалоСборкиРазборки // порядок обхода заказов
		|;
		|
		|/////////////////////////////////////////////////////////
		|";

	Возврат ТекстЗапроса;

КонецФункции



Процедура ОкруглитьПередСозданиемЗаказов(Таблица)
	
	Таблица.Сортировать("Номенклатура, Характеристика, Склад, ДатаПоставки", Новый СравнениеЗначений());
	
	ПоляКлюча = "Номенклатура, Характеристика, Склад";
	Ключ = Новый Структура(ПоляКлюча);
	
	Остаток = 0;
	ОкругленнаяСтрока = Неопределено;
	Для ИндексСтроки = 0 По Таблица.Количество() - 1 Цикл
		
		Строка = Таблица[ИндексСтроки];
		
		Если Не Строка.Отметка 
			Или Строка.КЗаказу = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ИзменилсяКлюч = ОбеспечениеКлиентСервер.ИзменилсяКлюч(Ключ, Строка);
		ЗаполнитьЗначенияСвойств(Ключ, Строка);
		
		Если ИзменилсяКлюч Тогда
			
			Если Остаток > 0 Тогда
				НоваяСтрока = Таблица.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ОкругленнаяСтрока);
				НоваяСтрока.КЗаказу = Остаток;
				НоваяСтрока.Назначение = Справочники.Назначения.ПустаяСсылка();
			КонецЕсли;
			
			Округлено = Округлить(Строка.КЗаказу, ?(Строка.ЧислительУпаковки = 0 Или Строка.ЗнаменательУпаковки = 0, 1, Строка.ЧислительУпаковки / Строка.ЗнаменательУпаковки),
				Строка.ТипЕдиницыИзмерения);
			Остаток = Округлено - Строка.КЗаказу;
			ОкругленнаяСтрока = Строка;
			
		Иначе
			
			Если Остаток >= Строка.КЗаказу Тогда
				Строка.ДатаПоставки = ОкругленнаяСтрока.ДатаПоставки;
				Остаток = Остаток - Строка.КЗаказу;
			Иначе
				
				КЗаказуДляНовойОкругленнойСтроки = Строка.КЗаказу - Остаток;
				
				// Частично относим на дату округленной строки, новой строкой
				НоваяСтрока = Таблица.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
				НоваяСтрока.КЗаказу = Остаток;
				НоваяСтрока.ДатаПоставки = ОкругленнаяСтрока.ДатаПоставки;
				
				// Частично оставляем на дату текущей строки
				Строка.КЗаказу = КЗаказуДляНовойОкругленнойСтроки;
				
				Округлено = Округлить(Строка.КЗаказу, ?(Строка.ЧислительУпаковки = 0 Или Строка.ЗнаменательУпаковки = 0, 1, Строка.ЧислительУпаковки / Строка.ЗнаменательУпаковки),
					Строка.ТипЕдиницыИзмерения);
				Остаток = Округлено - Строка.КЗаказу;
				ОкругленнаяСтрока = Строка;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Остаток > 0 Тогда
		НоваяСтрока = Таблица.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ОкругленнаяСтрока);
		НоваяСтрока.КЗаказу = Остаток;
		НоваяСтрока.Назначение = Справочники.Назначения.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

Функция Округлить(Количество, Кратность, ТипЕдиницыИзмерения)
	
	Если Кратность <> 1
		Или ТипЕдиницыИзмерения = ПредопределенноеЗначение("Перечисление.ТипыИзмеряемыхВеличин.КоличествоШтук") Тогда
		Если Количество % Кратность <> 0 Тогда
			Остаток = Цел(Количество / Кратность) + 1;
			Возврат Остаток * Кратность;
		Иначе
			Возврат Количество;
		КонецЕсли;
	Иначе 
		Возврат Количество;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьЗначенияРеквизитовДокумента(ДокументОбъект)

	ЗначенияРеквизитов = Новый Структура();
	
	ЗначенияРеквизитов.Вставить("Документ",            ДокументОбъект.Ссылка);
	ЗначенияРеквизитов.Вставить("СтандартнаяКартинка", ?(ДокументОбъект.Проведен, 0, ?(ДокументОбъект.ПометкаУдаления, 1, 2)));

	ЗаказНеПодтвержден = ?(ТипЗнч(ДокументОбъект) <> Тип("ДокументОбъект.ЗаказПоставщику")
		Или ДокументОбъект.Статус = Перечисления.СтатусыЗаказовПоставщикам.Подтвержден
		Или ДокументОбъект.Статус = Перечисления.СтатусыЗаказовПоставщикам.Закрыт, ?(ДокументОбъект.Проведен, 0, 1), 1);

	ЗаказНаПроизводствоНеКПроизводству = 0;

	ЗначенияРеквизитов.Вставить("НеПоставленВГрафик",  ЗаказНеПодтвержден);
	ЗначенияРеквизитов.Вставить("НеПоставленВГрафикПроизводства", ЗаказНаПроизводствоНеКПроизводству);

	ЗначенияРеквизитов.Вставить("Статус",              ДокументОбъект.Статус);
	ЗначенияРеквизитов.Вставить("Организация",         ДокументОбъект.Организация);
	
	ДатаПоставки = '00010101';
	ИмяТабЧасти = "";
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ЗаказНаСборку") Тогда
		
		ЗначенияРеквизитов.Вставить("ИсточникОбеспечения", ДокументОбъект.Склад);
		ДатаПоставки = ДокументОбъект.ОкончаниеСборкиРазборки;
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ЗаказНаПеремещение") Тогда
		
		ЗначенияРеквизитов.Вставить("ИсточникОбеспечения", ДокументОбъект.СкладОтправитель);
		ИмяТабЧасти = "Товары";
		ИмяРеквизитаДатыПоставки = "ОкончаниеПоступления";
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ЗаказПоставщику") Тогда
		
		ЗначенияРеквизитов.Вставить("ИсточникОбеспечения", ДокументОбъект.Партнер);
		ЗначенияРеквизитов.Вставить("Сумма",               ДокументОбъект.СуммаДокумента);
		ЗначенияРеквизитов.Вставить("Валюта",              ДокументОбъект.Валюта);
		ИмяТабЧасти = "Товары";
		ИмяРеквизитаДатыПоставки = "ДатаПоступления";

	КонецЕсли;
	
	Если ИмяТабЧасти <> "" Тогда
		
		Для каждого Строка Из ДокументОбъект[ИмяТабЧасти] Цикл
			
			Если ДатаПоставки <> Строка[ИмяРеквизитаДатыПоставки] Тогда
				
				Если ДатаПоставки <> '00010101' Тогда
					
					// Если в документе более одной строки и разные даты поставок, то дату поставки не заполняем.
					ДатаПоставки = '00010101';
					Прервать;
					
				КонецЕсли;
				
				ДатаПоставки = Строка[ИмяРеквизитаДатыПоставки];
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	ЗначенияРеквизитов.Вставить("ДатаПоставки", ДатаПоставки);
	
	Возврат ЗначенияРеквизитов;
	
КонецФункции

Функция ПолучитьДокумент(МенеджерДокументов, ДанныеЗаполнения, Статус)
	
	ДокументОбъект = МенеджерДокументов.СоздатьДокумент();
	ДанныеЗаполнения.Вставить("Дата", ТекущаяДатаСеанса());
	
	ДокументОбъект.Заполнить(ДанныеЗаполнения);
	
	Если Не ЗначениеЗаполнено(ДокументОбъект.Статус) Тогда
		ДокументОбъект.Статус = Статус;
	КонецЕсли;
	
	Возврат ДокументОбъект;
	
КонецФункции

Процедура ДобавитьДокументВРезультат(Документ, Результат, ИспользованиеСтатусов)
	
	НоваяСтрока = Результат.ТаблицаДокументов.Добавить();
	
	НовыеЗначенияРеквизитов = ПолучитьЗначенияРеквизитовДокумента(Документ);
	ЗаполнитьЗначенияСвойств(НоваяСтрока, НовыеЗначенияРеквизитов);
	НоваяСтрока.ИспользованиеСтатусов = ИспользованиеСтатусов[ТипЗнч(НоваяСтрока.Документ)];
	
	Ошибки = ПолучитьСообщенияПользователю(Истина);
	Для Каждого Ошибка Из Ошибки Цикл
		Ошибка.КлючДанных = Документ.Ссылка;
		Результат.СписокСообщений.Добавить(Ошибка);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура УдалитьСтрокиИзТаблицы(Таблица, ТаблицаПолуфабрикатов, ИмяПоляКоличество, КлючПоиска)
	
	Если Таблица = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаПолуфабрикатов.Индексы.Добавить(КлючПоиска);
	Отбор = Новый Структура(КлючПоиска);
	ВсегоСтрок = Таблица.Количество();
	Для Счетчик = 1 По ВсегоСтрок Цикл
		
		СтрокаТаблицы = Таблица[ВсегоСтрок- Счетчик];
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаТаблицы);
		НайденныеСтроки = ТаблицаПолуфабрикатов.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() > 0 Тогда
			
			КоличествоРаспределить = Мин(НайденныеСтроки[0].Количество, СтрокаТаблицы[ИмяПоляКоличество]);
			НайденныеСтроки[0].Количество = НайденныеСтроки[0].Количество - КоличествоРаспределить;
			СтрокаТаблицы[ИмяПоляКоличество] = СтрокаТаблицы[ИмяПоляКоличество] - КоличествоРаспределить;
			Если СтрокаТаблицы[ИмяПоляКоличество] = 0 Тогда
				Таблица.Удалить(СтрокаТаблицы);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьЗапросСОтборамиКомпоновкиДанных(АдресСхемыКомпоновкиДанных, КомпоновщикНастроек, ИмяНабораДанных)
	
	СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
	
	СегментыСервер.ВключитьОтборПоСегментуНоменклатурыВСКД(КомпоновщикНастроек);
	
	ИспользуетсяОтборПоЗаказам = Ложь;
	ОтборКомпоновки = КомпоновщикНастроек.ПолучитьНастройки().Отбор;
	ИспользуетсяОтборПоЗаказам = НайтиОтборРекурсивно(ОтборКомпоновки.Элементы, Новый ПолеКомпоновкиДанных("Заказ"))
		Или НайтиОтборРекурсивно(ОтборКомпоновки.Элементы, Новый ПолеКомпоновкиДанных("НаправлениеДеятельности"));
	
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ИспользуетсяОтборПоЗаказам", Истина, ИспользуетсяОтборПоЗаказам);
	
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных,
		КомпоновщикНастроек.ПолучитьНастройки(),,,, Ложь);

	Запрос = Новый Запрос(МакетКомпоновкиДанных.НаборыДанных[ИмяНабораДанных].Запрос);

	Для каждого ПараметрКомпоновки Из МакетКомпоновкиДанных.ЗначенияПараметров Цикл

		Запрос.УстановитьПараметр(ПараметрКомпоновки.Имя, ПараметрКомпоновки.Значение);

	КонецЦикла;
	ИспользоватьХарактеристикиНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры");
	Запрос.УстановитьПараметр("ИспользоватьХарактеристикиНоменклатуры", ИспользоватьХарактеристикиНоменклатуры);
	Возврат Запрос;

КонецФункции

Функция ВременнаяТаблицаТоваровДляПоддержанияЗапаса()

	ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НоменклатураСегмента.Номенклатура    КАК Номенклатура,
		|	НоменклатураСегмента.Характеристика  КАК Характеристика
		|ПОМЕСТИТЬ ОтборПоСегментам
		|ИЗ
		|	РегистрСведений.НоменклатураСегмента КАК НоменклатураСегмента
		|		{ГДЕ
		|			НоменклатураСегмента.Номенклатура.*    КАК Номенклатура,
		|			НоменклатураСегмента.Характеристика.*  КАК Характеристика,
		|			НоменклатураСегмента.Сегмент.*         КАК СегментНоменклатуры}
		|ГДЕ
		|	&ИспользуетсяОтборПоСегментуНоменклатуры
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|" + Справочники.ФорматыМагазинов.ТекстЗапросаВтФорматыСкладов(Ложь)
		+ РегистрыСведений.ТоварныеОграничения.ВременнаяТаблицаТоварыПоддерживаемогоЗапаса()
		+ "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Т.Номенклатура                       КАК Номенклатура,
		|	Т.Характеристика                     КАК Характеристика,
		|	Т.Склад                              КАК Склад,
		|
		|	СпрСпособ.Ссылка                     КАК СпособОбеспечения
		|
		|ПОМЕСТИТЬ ВтТовары
		|ИЗ
		|	ВтТоварыПоддерживаемогоЗапаса КАК Т
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОбеспеченияПотребностей КАК СпрСпособ
		|		ПО &ПодстановкаОсновногоСпособаОбеспечения
		|
		|//отборы компоновщика
		|{ГДЕ
		|	ВЫРАЗИТЬ(NULL КАК Справочник.Назначения).*              КАК Назначение,
		|	ВЫРАЗИТЬ(NULL КАК Справочник.СтруктураПредприятия).*    КАК ПодразделениеПолучатель,
		|	ВЫРАЗИТЬ(NULL КАК Справочник.НаправленияДеятельности).* КАК НаправлениеДеятельности,
		|	ВЫРАЗИТЬ(NULL КАК Справочник.Назначения).Заказ.*        КАК Заказ}
		|
		|{ГДЕ
		|	(ЕСТЬNULL(СпрСпособ.Ссылка,
		|		ЗНАЧЕНИЕ(Справочник.СпособыОбеспеченияПотребностей.ПустаяСсылка))).*           КАК СпособОбеспечения,
		|	ЕСТЬNULL(СпрСпособ.ТипОбеспечения, ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Покупка)) КАК ТипОбеспечения,
		|	ВЫРАЗИТЬ(
		|		ВЫБОР
		|			КОГДА СпрСпособ.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) ТОГДА
		|				СпрСпособ.Подразделение
		|			ИНАЧЕ
		|				ЕСТЬNULL(Т.Склад.Подразделение,
		|					ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
		|		КОНЕЦ КАК Справочник.СтруктураПредприятия).*                                   КАК Подразделение,
		|	(ЕСТЬNULL(СпрСпособ.ИсточникОбеспеченияПотребностей, НЕОПРЕДЕЛЕНО)).*              КАК ИсточникОбеспечения}
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад,
		|	СпособОбеспечения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВтФорматыСкладов;
		|
		|////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	Т.Номенклатура                       КАК Номенклатура,
		|	Т.Характеристика                     КАК Характеристика,
		|	Т.Склад                              КАК Склад,
		|	Т.СпособОбеспечения                  КАК СпособОбеспечения
		|{ВЫБРАТЬ
		|	Номенклатура.*      КАК Номенклатура,
		|	Характеристика.*    КАК Характеристика,
		|	Склад.*             КАК Склад,
		|	СпособОбеспечения.* КАК СпособОбеспечения}
		|ИЗ
		|	ВтТовары КАК Т";

	ТекстЗапроса = РегистрыСведений.СхемыОбеспечения.ПодставитьСоединениеДляПолученияСпособаОбеспечения(
		ТекстЗапроса, "ПодстановкаОсновногоСпособаОбеспечения");
	
	Возврат ТекстЗапроса;

КонецФункции



Функция ВременнаяТаблицаОтборПоСегментам()
	
	Возврат
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НоменклатураСегмента.Номенклатура    КАК Номенклатура,
		|	НоменклатураСегмента.Характеристика  КАК Характеристика
		|ПОМЕСТИТЬ ОтборПоСегментам
		|ИЗ
		|	РегистрСведений.НоменклатураСегмента КАК НоменклатураСегмента
		|		{ГДЕ
		|			НоменклатураСегмента.Номенклатура.*    КАК Номенклатура,
		|			НоменклатураСегмента.Характеристика.*  КАК Характеристика,
		|			НоменклатураСегмента.Сегмент.*         КАК СегментНоменклатуры}
		|ГДЕ
		|	&ИспользуетсяОтборПоСегментуНоменклатуры
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика";
		
КонецФункции

Функция ВременнаяТаблицаОтборПоЗаказам()
	
	
	Возврат
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РеестрДокументов.Ссылка КАК Заказ
		|ПОМЕСТИТЬ ОтборПоЗаказам
		|ИЗ РегистрСведений.РеестрДокументов КАК РеестрДокументов
		|ГДЕ
		|	&ИспользуетсяОтборПоЗаказам
		|{ГДЕ
		|	РеестрДокументов.Ссылка.* КАК Заказ,
		|	РеестрДокументов.НаправлениеДеятельности.* КАК НаправлениеДеятельности}";

КонецФункции

Функция ВременнаяТаблицаРазличнаяНоменклатура(ИмяТаблицы)
	
	Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Номенклатура   КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика
		|ПОМЕСТИТЬ РазличнаяНоменклатура
		|ИЗ
		|	ИмяТаблицы КАК Таблица
		|ГДЕ
		|	&ОптимизироватьЗапасыРаспределительногоЦентра";
		
	Возврат СтрЗаменить(Текст, "ИмяТаблицы", ИмяТаблицы);
	
КонецФункции

Функция ВременнаяТаблицаПотребностиВсехСкладовПоЗаказам()

	ТекстЗапроса =
		// Необеспеченные работы по заказам на отгрузку
		Справочники.ФорматыМагазинов.ТекстЗапросаВтФорматыСкладов(Ложь) +
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Т.Номенклатура                            КАК Номенклатура,
		|	Т.Характеристика                          КАК Характеристика,
		|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)  КАК Склад,
		|	Т.Назначение                              КАК Назначение,
		|	Т.Подразделение                           КАК ПодразделениеПолучатель
		|
		|ПОМЕСТИТЬ ВтОстаткиРабот
		|ИЗ
		|	РегистрНакопления.ОбеспечениеЗаказовРаботами.Остатки(,
		|		{Номенклатура.* КАК Номенклатура, Характеристика.* КАК Характеристика,
		|			Подразделение.* КАК ПодразделениеПолучатель, Назначение.* КАК Назначение}
		|		{(Номенклатура, Характеристика) В(
		|			ВЫБРАТЬ
		|				ТаблицаОтбора.Номенклатура   КАК Номенклатура,
		|				ТаблицаОтбора.Характеристика КАК Характеристика
		|			ИЗ
		|				ОтборПоСегментам КАК ТаблицаОтбора
		|			ГДЕ
		|			&ИспользуетсяОтборПоСегментуНоменклатуры)}) КАК Т
		|	{ГДЕ ВЫРАЗИТЬ(ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Справочник.Склады).* КАК Склад}
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Назначение, ПодразделениеПолучатель
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// Необеспеченные обособленные товары по заказам на отгрузку
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Т.Номенклатура   КАК Номенклатура,
		|	Т.Характеристика КАК Характеристика,
		|	Т.Склад          КАК Склад,
		|	Т.Назначение     КАК Назначение
		|
		|ПОМЕСТИТЬ ВтОстаткиОбособленныхТоваров
		|ИЗ
		|	РегистрНакопления.ОбеспечениеЗаказов.Остатки(,
		|		{Номенклатура.* КАК Номенклатура, Характеристика.* КАК Характеристика,
		|			Назначение.* КАК Назначение}
		|		{(Номенклатура, Характеристика) В(
		|			ВЫБРАТЬ
		|				ТаблицаОтбора.Номенклатура   КАК Номенклатура,
		|				ТаблицаОтбора.Характеристика КАК Характеристика
		|			ИЗ
		|				ОтборПоСегментам КАК ТаблицаОтбора
		|			ГДЕ
		|			&ИспользуетсяОтборПоСегментуНоменклатуры)}) КАК Т
		|ГДЕ
		|	Т.ПотребностьОстаток > 0
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Назначение, Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// Строки заказов на отгрузку товаров с вариантом обеспечения "Требуется"
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Товары.Номенклатура                       КАК Номенклатура,
		|	Товары.Характеристика                     КАК Характеристика,
		|	Товары.Склад                              КАК Склад,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеПолучатель,
		|	Товары.Назначение                         КАК Назначение,
		|	Товары.Регистратор                        КАК Заказ,
		|	Товары.Период                             КАК ДатаОтгрузки,
		|
		|	Товары.КоличествоНеобеспечено             КАК Количество,
		|
		|	СпрСпособ.Ссылка                          КАК СпособОбеспечения
		|ПОМЕСТИТЬ ПотребностиВсехСкладовПоЗаказам
		|ИЗ
		|	РегистрНакопления.ГрафикОтгрузкиТоваров КАК Товары
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОбеспеченияПотребностей КАК СпрСпособ
		|		ПО &ПодстановкаОсновногоСпособаОбеспечения
		|
		|ГДЕ
		|	Товары.Активность
		|	И Товары.КоличествоНеобеспечено > 0
		|
		|//отборы компоновщика
		|{ГДЕ
		|	Товары.Номенклатура.*                             КАК Номенклатура,
		|	Товары.Характеристика.*                           КАК Характеристика,
		|	Товары.Назначение.*                               КАК Назначение}
		|
		|{ГДЕ Товары.Регистратор В(
		|	ВЫБРАТЬ
		|		ТаблицаОтбора.Заказ   КАК Заказ
		|	ИЗ
		|		ОтборПоЗаказам КАК ТаблицаОтбора
		|	ГДЕ
		|		&ИспользуетсяОтборПоЗаказам)}
		|
		|{ГДЕ (Товары.Номенклатура, Товары.Характеристика) В(
		|	ВЫБРАТЬ
		|		ТаблицаОтбора.Номенклатура   КАК Номенклатура,
		|		ТаблицаОтбора.Характеристика КАК Характеристика
		|	ИЗ
		|		ОтборПоСегментам КАК ТаблицаОтбора
		|	ГДЕ
		|		&ИспользуетсяОтборПоСегментуНоменклатуры)}
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// Необеспеченные строки заказов на отгрузку товаров с вариантом обеспечения "Обособленно".
		|ВЫБРАТЬ
		|	Товары.Номенклатура                       КАК Номенклатура,
		|	Товары.Характеристика                     КАК Характеристика,
		|	Товары.Склад                              КАК Склад,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеПолучатель,
		|	Товары.Назначение                         КАК Назначение,
		|	Товары.Регистратор                        КАК Заказ,
		|	Товары.Период                             КАК ДатаОтгрузки,
		|
		|	Товары.КоличествоПодЗаказ                 КАК Количество,
		|
		|	СпрСпособ.Ссылка                          КАК СпособОбеспечения
		|ИЗ
		|	РегистрНакопления.ГрафикОтгрузкиТоваров КАК Товары
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОстаткиОбособленныхТоваров КАК Остатки
		|		ПО Товары.Номенклатура   = Остатки.Номенклатура
		|		 И Товары.Характеристика = Остатки.Характеристика
		|		 И Товары.Склад          = Остатки.Склад
		|		 И Товары.Назначение     = Остатки.Назначение
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОбеспеченияПотребностей КАК СпрСпособ
		|		ПО &ПодстановкаОсновногоСпособаОбеспечения
		|
		|ГДЕ
		|	Товары.Активность
		|	И Товары.КоличествоПодЗаказ > 0
		|
		|//отборы компоновщика
		|{ГДЕ Товары.Регистратор В(
		|	ВЫБРАТЬ
		|		ТаблицаОтбора.Заказ   КАК Заказ
		|	ИЗ
		|		ОтборПоЗаказам КАК ТаблицаОтбора
		|	ГДЕ
		|		&ИспользуетсяОтборПоЗаказам)}
		|
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// Необеспеченные строки заказов работ на отгрузку
		|ВЫБРАТЬ
		|	Работы.Номенклатура                       КАК Номенклатура,
		|	Работы.Характеристика                     КАК Характеристика,
		|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
		|	Работы.Подразделение                      КАК ПодразделениеПолучатель,
		|	Работы.Назначение                         КАК Назначение,
		|	Работы.Регистратор                        КАК Заказ,
		|	Работы.Период                             КАК ДатаОтгрузки,
		|
		|	Работы.КОбеспечению                       КАК Количество,
		|
		|	СпрСпособ.Ссылка                          КАК СпособОбеспечения
		|ИЗ
		|	РегистрНакопления.ОбеспечениеЗаказовРаботами КАК Работы
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОстаткиРабот КАК Остатки
		|		ПО Работы.Номенклатура   = Остатки.Номенклатура
		|		 И Работы.Характеристика = Остатки.Характеристика
		|		 И Работы.Подразделение  = Остатки.ПодразделениеПолучатель
		|		 И Работы.Назначение     = Остатки.Назначение
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОбеспеченияПотребностей КАК СпрСпособ
		|		ПО &ПодстановкаОсновногоСпособаОбеспеченияРабот
		|
		|ГДЕ
		|	Работы.Активность
		|	И Работы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И Работы.КОбеспечению > 0
		|
		|//отборы компоновщика
		|{ГДЕ Работы.Регистратор В(
		|	ВЫБРАТЬ
		|		ТаблицаОтбора.Заказ   КАК Заказ
		|	ИЗ
		|		ОтборПоЗаказам КАК ТаблицаОтбора
		|	ГДЕ
		|		&ИспользуетсяОтборПоЗаказам)}
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВтФорматыСкладов;
		|УНИЧТОЖИТЬ ВтОстаткиРабот;
		|УНИЧТОЖИТЬ ВтОстаткиОбособленныхТоваров";
	
	ТекстЗапроса = РегистрыСведений.СхемыОбеспечения.ПодставитьСоединениеДляПолученияСпособаОбеспечения(
		ТекстЗапроса,
		"ПодстановкаОсновногоСпособаОбеспеченияРабот",
		"Работы.Номенклатура,Работы.Характеристика,(ВЫРАЗИТЬ(NULL КАК Справочник.Склады))",
		"Работы");
	
	ТекстЗапроса = РегистрыСведений.СхемыОбеспечения.ПодставитьСоединениеДляПолученияСпособаОбеспечения(
		ТекстЗапроса,
		"ПодстановкаОсновногоСпособаОбеспечения",
		"Товары.Номенклатура,Товары.Характеристика,Товары.Склад",
		"Товары");
		
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ВременнаяТаблицаПотребностиВыбранныхСкладов()
	
	Текст =
		"// Обычный склад
		|ВЫБРАТЬ
		|	Товары.Номенклатура                       КАК Номенклатура,
		|	Товары.Характеристика                     КАК Характеристика,
		|	Товары.Склад                              КАК Склад,
		|	Товары.Склад                              КАК СкладОтгрузкиЗаказа,
		|	Товары.ПодразделениеПолучатель            КАК ПодразделениеПолучатель,
		|	Товары.Назначение                         КАК Назначение,
		|	Товары.Заказ                              КАК Заказ,
		|	Товары.ДатаОтгрузки                       КАК ДатаОтгрузки,
		|	Товары.ДатаОтгрузки                       КАК ДатаОтгрузкиЗаказа,
		|	Товары.Количество                         КАК Количество,
		|	Товары.СпособОбеспечения                  КАК СпособОбеспечения
		|ПОМЕСТИТЬ ВтТовары
		|ИЗ
		|	ПотребностиВсехСкладовПоЗаказам КАК Товары
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТопологияСкладов КАК Топология
		|		ПО Товары.Номенклатура   = Топология.Номенклатура
		|		 И Товары.Характеристика = Топология.Характеристика
		|		 И Товары.Склад          = Топология.Склад
		|ГДЕ
		|	Топология.Номенклатура ЕСТЬ NULL
		|
		|{ГДЕ
		|	Товары.Склад.*                                                                                    КАК Склад,
		|	(ЕСТЬNULL(Товары.СпособОбеспечения,
		|		ЗНАЧЕНИЕ(Справочник.СпособыОбеспеченияПотребностей.ПустаяСсылка))).*                          КАК СпособОбеспечения,
		|	ЕСТЬNULL(Товары.СпособОбеспечения.ТипОбеспечения, ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Покупка)) КАК ТипОбеспечения,
		|	ВЫРАЗИТЬ(
		|		ВЫБОР
		|			КОГДА Товары.СпособОбеспечения.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) ТОГДА
		|				Товары.СпособОбеспечения.Подразделение
		|			ИНАЧЕ
		|				ЕСТЬNULL(Товары.Склад.Подразделение,
		|					ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
		|		КОНЕЦ КАК Справочник.СтруктураПредприятия).*                                                  КАК Подразделение,
		|	(ЕСТЬNULL(Товары.СпособОбеспечения.ИсточникОбеспеченияПотребностей, НЕОПРЕДЕЛЕНО)).*              КАК ИсточникОбеспечения}
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|// Удаленный склад распределительной сети
		|ВЫБРАТЬ
		|	ВсеТовары.Номенклатура                       КАК Номенклатура,
		|	ВсеТовары.Характеристика                     КАК Характеристика,
		|	ВсеТовары.Склад                              КАК Склад,
		|	ВсеТовары.Склад                              КАК СкладОтгрузкиЗаказа,
		|	ВсеТовары.ПодразделениеПолучатель            КАК ПодразделениеПолучатель,
		|	ВсеТовары.Назначение                         КАК Назначение,
		|	ВсеТовары.Заказ                              КАК Заказ,
		|	ВсеТовары.ДатаОтгрузки                       КАК ДатаОтгрузки,
		|	ВсеТовары.ДатаОтгрузки                       КАК ДатаОтгрузкиЗаказа,
		|	ВсеТовары.Количество                         КАК Количество,
		|	ВсеТовары.СпособОбеспечения                  КАК СпособОбеспечения
		|ИЗ
		|	ПотребностиВсехСкладовПоЗаказам КАК ВсеТовары
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПеремещение КАК ЗаказыНаПеремещение
		|		ПО ЗаказыНаПеремещение.Ссылка = ВсеТовары.Заказ
		|		
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТопологияСкладов КАК Топология
		|		ПО Топология.Номенклатура   =  ВсеТовары.Номенклатура
		|		 И Топология.Характеристика =  ВсеТовары.Характеристика
		|		 И Топология.Склад          =  ВсеТовары.Склад
		|		 И Топология.Центр          <> ВсеТовары.Склад
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТопологияСкладов КАК СкладыСети
		|		ПО СкладыСети.Номенклатура   =  Топология.Номенклатура
		|		 И СкладыСети.Характеристика =  Топология.Характеристика
		|		 И СкладыСети.Центр          =  Топология.Центр
		|		 И СкладыСети.Склад =
		|									ЗаказыНаПеремещение.СкладПолучатель
		|		 
		|ГДЕ
		|	НЕ Топология.Номенклатура ЕСТЬ NULL
		|		И СкладыСети.Номенклатура ЕСТЬ NULL
		|
		|{ГДЕ
		|	ВсеТовары.Склад.*                                                                                 КАК Склад,
		|	(ЕСТЬNULL(ВсеТовары.СпособОбеспечения,
		|		ЗНАЧЕНИЕ(Справочник.СпособыОбеспеченияПотребностей.ПустаяСсылка))).*                          КАК СпособОбеспечения,
		|	ЕСТЬNULL(ВсеТовары.СпособОбеспечения.ТипОбеспечения, ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Покупка)) КАК ТипОбеспечения,
		|	ВЫРАЗИТЬ(
		|		ВЫБОР
		|			КОГДА ВсеТовары.СпособОбеспечения.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) ТОГДА
		|				ВсеТовары.СпособОбеспечения.Подразделение
		|			ИНАЧЕ
		|				ЕСТЬNULL(ВсеТовары.Склад.Подразделение,
		|					ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
		|		КОНЕЦ КАК Справочник.СтруктураПредприятия).*                                                  КАК Подразделение,
		|	(ЕСТЬNULL(ВсеТовары.СпособОбеспечения.ИсточникОбеспеченияПотребностей, НЕОПРЕДЕЛЕНО)).*           КАК ИсточникОбеспечения}
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|// Распределительный центр распределительной сети
		|ВЫБРАТЬ
		|	ВсеТовары.Номенклатура                                  КАК Номенклатура,
		|	ВсеТовары.Характеристика                                КАК Характеристика,
		|	Топология.Центр                                         КАК Склад,
		|	ВсеТовары.Склад                                         КАК СкладОтгрузкиЗаказа,
		|	ВсеТовары.ПодразделениеПолучатель                       КАК ПодразделениеПолучатель,
		|	ВсеТовары.Назначение                                    КАК Назначение,
		|	ВсеТовары.Заказ                                         КАК Заказ,
		|	ДОБАВИТЬКДАТЕ(ВсеТовары.ДатаОтгрузки, ДЕНЬ,
		|		- ЕСТЬNULL(СпрСпособСкладСети.ДлительностьВДнях, 0))КАК ДатаОтгрузки,
		|	ВсеТовары.ДатаОтгрузки                                  КАК ДатаОтгрузкиЗаказа,
		|	ВсеТовары.Количество                                    КАК Количество,
		|	СпрСпособЦентр.Ссылка                                   КАК СпособОбеспечения
		|ИЗ
		|	ПотребностиВсехСкладовПоЗаказам КАК ВсеТовары
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПеремещение КАК ЗаказыНаПеремещение
		|		ПО ЗаказыНаПеремещение.Ссылка = ВсеТовары.Заказ
		|		
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТопологияСкладов КАК Топология
		|		ПО Топология.Номенклатура   =  ВсеТовары.Номенклатура
		|		 И Топология.Характеристика =  ВсеТовары.Характеристика
		|		 И Топология.Склад          =  ВсеТовары.Склад
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТопологияСкладов КАК СкладыСети
		|		ПО СкладыСети.Номенклатура   =  Топология.Номенклатура
		|		 И СкладыСети.Характеристика =  Топология.Характеристика
		|		 И СкладыСети.Центр          =  Топология.Центр
		|		 И СкладыСети.Склад =
		|									ЗаказыНаПеремещение.СкладПолучатель
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОбеспеченияПотребностей КАК СпрСпособСкладСети
		|		ПО &ПодстановкаОсновногоСпособаОбеспеченияСкладаСети
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОбеспеченияПотребностей КАК СпрСпособЦентр
		|		ПО &ПодстановкаОсновногоСпособаОбеспеченияЦентраСети
		|ГДЕ
		|	НЕ Топология.Номенклатура ЕСТЬ NULL
		|		И СкладыСети.Номенклатура ЕСТЬ NULL
		|
		|{ГДЕ
		|	Топология.Центр.* КАК Склад,
		|	(ЕСТЬNULL(СпрСпособЦентр.Ссылка,
		|		ЗНАЧЕНИЕ(Справочник.СпособыОбеспеченияПотребностей.ПустаяСсылка))).* КАК СпособОбеспечения,
		|	ЕСТЬNULL(СпрСпособЦентр.ТипОбеспечения, ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Покупка)) КАК ТипОбеспечения,
		|	ВЫРАЗИТЬ(
		|		ВЫБОР
		|			КОГДА СпрСпособЦентр.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) ТОГДА
		|				СпрСпособЦентр.Подразделение
		|			ИНАЧЕ
		|				ЕСТЬNULL(Топология.Центр.Подразделение,
		|					ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
		|		КОНЕЦ КАК Справочник.СтруктураПредприятия).* КАК Подразделение,
		|	(ЕСТЬNULL(СпрСпособЦентр.ИсточникОбеспеченияПотребностей, НЕОПРЕДЕЛЕНО)).* КАК ИсточникОбеспечения}";

	Подстановка =
		"ВсеТовары.Номенклатура,
		|ВсеТовары.Характеристика,
		|ВсеТовары.Склад";
		
	Текст = РегистрыСведений.СхемыОбеспечения.ПодставитьСоединениеДляПолученияСпособаОбеспечения(
		Текст, "ПодстановкаОсновногоСпособаОбеспеченияСкладаСети", Подстановка, "СкладСети");
	
	Подстановка =
		"ВсеТовары.Номенклатура,
		|ВсеТовары.Характеристика,
		|Топология.Центр";
		
	Текст = РегистрыСведений.СхемыОбеспечения.ПодставитьСоединениеДляПолученияСпособаОбеспечения(
		Текст, "ПодстановкаОсновногоСпособаОбеспеченияЦентраСети", Подстановка, "ЦентрСети");
	
	ТекстыЗапроса = Новый Массив();
	ТекстыЗапроса.Добавить(Справочники.ФорматыМагазинов.ТекстЗапросаВтФорматыСкладов(Ложь));
	ТекстыЗапроса.Добавить(Текст);
	
	Текст = СтрСоединить(ТекстыЗапроса);
	Возврат Текст;
	
КонецФункции
	
Функция ВыборкаПервойЗаписиПотребностейПоЗаказамДляКомпоновкиЗапроса()
	
	Возврат
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Товары.Номенклатура            КАК Номенклатура,
		|	Товары.Характеристика          КАК Характеристика,
		|	Товары.Склад                   КАК Склад,
		|	Товары.СкладОтгрузкиЗаказа     КАК СкладОтгрузкиЗаказа,
		|	Товары.ПодразделениеПолучатель КАК ПодразделениеПолучатель,
		|	Товары.Назначение              КАК Назначение,
		|	Товары.Заказ                   КАК Заказ,
		|	Товары.ДатаОтгрузки            КАК ДатаОтгрузки,
		|	Товары.ДатаОтгрузкиЗаказа      КАК ДатаОтгрузкиЗаказа,
		|
		|	Товары.Количество              КАК Количество,
		|
		|	Товары.СпособОбеспечения КАК СпособОбеспечения
		|{ВЫБРАТЬ
		|	Номенклатура.*            КАК Номенклатура,
		|	Характеристика.*          КАК Характеристика,
		|	Склад.*                   КАК Склад,
		|	СкладОтгрузкиЗаказа.*     КАК СкладОтгрузкиЗаказа,
		|	ПодразделениеПолучатель.* КАК ПодразделениеПолучатель,
		|	Назначение.*              КАК Назначение,
		|	Заказ.*                   КАК Заказ,
		|	ДатаОтгрузки              КАК ДатаОтгрузки,
		|	ДатаОтгрузкиЗаказа        КАК ДатаОтгрузкиЗаказа,
		|	Количество                КАК Количество,
		|	СпособОбеспечения.*       КАК СпособОбеспечения}
		|ИЗ
		|	ВтТовары КАК Товары";
КонецФункции

Функция ВременнаяТаблицаСпособовОбеспечения()

	ТекстЗапроса =
		// Отбор номенклатуры сегмента.
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НоменклатураСегмента.Номенклатура    КАК Номенклатура,
		|	НоменклатураСегмента.Характеристика  КАК Характеристика
		|ПОМЕСТИТЬ ОтборПоСегментам
		|ИЗ
		|	РегистрСведений.НоменклатураСегмента КАК НоменклатураСегмента
		|		{ГДЕ
		|			НоменклатураСегмента.Номенклатура.*    КАК Номенклатура,
		|			НоменклатураСегмента.Характеристика.*  КАК Характеристика,
		|			НоменклатураСегмента.Сегмент.*         КАК СегментНоменклатуры}
		|ГДЕ
		|	&ИспользуетсяОтборПоСегментуНоменклатуры
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика
		|;
		|"
		+ ВременнаяТаблицаОтборПоЗаказам()
		+ ";
		|////////////////////////////////////////////////////////////////////////////////
		// Работы по заказам на отгрузку
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Т.Номенклатура   КАК Номенклатура,
		|	Т.Характеристика КАК Характеристика,
		|	Т.Назначение     КАК Назначение,
		|	Т.Подразделение  КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО     КАК Склад
		|
		|ПОМЕСТИТЬ ВтОстаткиРабот
		|ИЗ
		|	РегистрНакопления.ОбеспечениеЗаказовРаботами.Остатки(,
		|		{Номенклатура.* КАК Номенклатура, Характеристика.* КАК Характеристика,
		|			Подразделение.* КАК ПодразделениеПолучатель, Назначение.* КАК Назначение}
		|		{(Номенклатура, Характеристика) В(
		|			ВЫБРАТЬ
		|				ТаблицаОтбора.Номенклатура   КАК Номенклатура,
		|				ТаблицаОтбора.Характеристика КАК Характеристика
		|			ИЗ
		|				ОтборПоСегментам КАК ТаблицаОтбора
		|			ГДЕ
		|			&ИспользуетсяОтборПоСегментуНоменклатуры)}) КАК Т
		|ГДЕ
		|	Т.КОбеспечениюОстаток > 0
		|{ГДЕ
		|	ВЫРАЗИТЬ(ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Справочник.Склады).* КАК Склад}
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// Неотгруженные обособленные товары по заказам на отгрузку
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Т.Номенклатура   КАК Номенклатура,
		|	Т.Назначение     КАК Назначение,
		|	Т.Характеристика КАК Характеристика,
		|	Т.Склад          КАК Склад
		|
		|ПОМЕСТИТЬ ВтОстаткиОбособленныхТоваров
		|ИЗ
		|	РегистрНакопления.ОбеспечениеЗаказов.Остатки(,
		|		{Номенклатура.* КАК Номенклатура, Характеристика.* КАК Характеристика,
		|			Назначение.* КАК Назначение}
		|		{(Номенклатура, Характеристика) В(
		|			ВЫБРАТЬ
		|				ТаблицаОтбора.Номенклатура   КАК Номенклатура,
		|				ТаблицаОтбора.Характеристика КАК Характеристика
		|			ИЗ
		|				ОтборПоСегментам КАК ТаблицаОтбора
		|			ГДЕ
		|				&ИспользуетсяОтборПоСегментуНоменклатуры)}) КАК Т
		|ГДЕ
		|	Т.ПотребностьОстаток > 0
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|" + Справочники.ФорматыМагазинов.ТекстЗапросаВтФорматыСкладов(Ложь)
		+ РегистрыСведений.ТоварныеОграничения.ВременнаяТаблицаТоварыПоддерживаемогоЗапаса()
		// Способы обеспечения для товаров из заказов на отгрузку с вариантом обеспечения "Требуется".
		+ "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Т.Номенклатура             КАК Номенклатура,
		|	Т.Характеристика           КАК Характеристика,
		|	Т.Склад                    КАК Склад,
		|	МАКСИМУМ(СпрСпособ.Ссылка) КАК СпособОбеспечения
		|ПОМЕСТИТЬ ВтУдаленныеСкладыСпособыОбеспечения
		|ИЗ
		|	РегистрНакопления.ГрафикОтгрузкиТоваров КАК Т
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОбеспеченияПотребностей КАК СпрСпособ
		|		ПО &ПодстановкаОсновногоСпособаОбеспечения
		|
		|ГДЕ
		|	Т.Активность
		|	И Т.КоличествоНеОбеспечено <> 0
		|СГРУППИРОВАТЬ ПО
		|	Т.Номенклатура, Т.Характеристика, Т.Склад
		|ИМЕЮЩИЕ
		|	СУММА(Т.КоличествоНеОбеспечено) > 0
		|
		|//отборы компоновщика
		|{ГДЕ
		|	Т.Номенклатура.*                             КАК Номенклатура,
		|	Т.Характеристика.*                           КАК Характеристика,
		|	Т.Назначение.*                               КАК Назначение}
		|
		|{ГДЕ Т.Регистратор В(
		|	ВЫБРАТЬ
		|		ТаблицаОтбора.Заказ   КАК Заказ
		|	ИЗ
		|		ОтборПоЗаказам КАК ТаблицаОтбора
		|	ГДЕ
		|		&ИспользуетсяОтборПоЗаказам)}
		|
		|{ГДЕ (Т.Номенклатура, Т.Характеристика) В(
		|	ВЫБРАТЬ
		|		ТаблицаОтбора.Номенклатура   КАК Номенклатура,
		|		ТаблицаОтбора.Характеристика КАК Характеристика
		|	ИЗ
		|		ОтборПоСегментам КАК ТаблицаОтбора
		|	ГДЕ
		|		&ИспользуетсяОтборПоСегментуНоменклатуры)}
		|
		|ОБЪЕДИНИТЬ
		|
		// Способы обеспечения для работ из заказов на отгрузку
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Номенклатура                           КАК Номенклатура,
		|	Т.Характеристика                         КАК Характеристика,
		|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
		|	СпрСпособ.Ссылка                         КАК СпособОбеспечения
		|
		|ИЗ
		|	ВтОстаткиРабот КАК Т
		|		
		|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОбеспечениеЗаказовРаботами КАК ОбеспечениеРаботами
		|		ПО ОбеспечениеРаботами.Номенклатура   = Т.Номенклатура
		|		 И ОбеспечениеРаботами.Характеристика = Т.Характеристика
		|		 И ОбеспечениеРаботами.Назначение     = Т.Назначение
		|		 И ОбеспечениеРаботами.Подразделение  = Т.Подразделение
		|		 И ОбеспечениеРаботами.КОбеспечению > 0
		|		 И ОбеспечениеРаботами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		 И ОбеспечениеРаботами.Активность}
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОбеспеченияПотребностей КАК СпрСпособ
		|		ПО &ПодстановкаОсновногоСпособаОбеспечения
		|
		|//отборы компоновщика
		|{ГДЕ ОбеспечениеРаботами.Регистратор В(
		|	ВЫБРАТЬ
		|		ТаблицаОтбора.Заказ   КАК Заказ
		|	ИЗ
		|		ОтборПоЗаказам КАК ТаблицаОтбора
		|	ГДЕ
		|		&ИспользуетсяОтборПоЗаказам)}
		|
		|ОБЪЕДИНИТЬ
		|
		// Способы обеспечения для товаров из обособленных заказов на отгрузку
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Номенклатура                       КАК Номенклатура,
		|	Т.Характеристика                     КАК Характеристика,
		|	Т.Склад                              КАК Склад,
		|	СпрСпособ.Ссылка                     КАК СпособОбеспечения
		|
		|ИЗ
		|	ВтОстаткиОбособленныхТоваров КАК Т
		|
		|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОбеспечениеЗаказов КАК ОбеспечениеЗаказов
		|		ПО ОбеспечениеЗаказов.Номенклатура   = Т.Номенклатура
		|		 И ОбеспечениеЗаказов.Характеристика = Т.Характеристика
		|		 И ОбеспечениеЗаказов.Назначение     = Т.Назначение
		|		 И ОбеспечениеЗаказов.Склад          = Т.Склад
		|		 И ОбеспечениеЗаказов.Потребность > 0
		|		 И ОбеспечениеЗаказов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		 И ОбеспечениеЗаказов.Активность}
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОбеспеченияПотребностей КАК СпрСпособ
		|		ПО &ПодстановкаОсновногоСпособаОбеспечения
		|
		|//отборы компоновщика
		|{ГДЕ
		|	Т.Номенклатура.*                             КАК Номенклатура,
		|	Т.Характеристика.*                           КАК Характеристика,
		|	Т.Назначение.*                               КАК Назначение}
		|
		|{ГДЕ ОбеспечениеЗаказов.Регистратор В(
		|	ВЫБРАТЬ
		|		ТаблицаОтбора.Заказ   КАК Заказ
		|	ИЗ
		|		ОтборПоЗаказам КАК ТаблицаОтбора
		|	ГДЕ
		|		&ИспользуетсяОтборПоЗаказам)}
		|;
		|
		|/////////////////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Т.Номенклатура                                      КАК Номенклатура,
		|	Т.Характеристика                                    КАК Характеристика,
		|	Т.СпособОбеспечения.ИсточникОбеспеченияПотребностей КАК Склад
		|ПОМЕСТИТЬ ВтРаспредЦентрыСпособыОбеспечения
		|ИЗ
		|	ВтУдаленныеСкладыСпособыОбеспечения КАК Т
		|ГДЕ
		|	&ОптимизироватьЗапасыРаспределительногоЦентра
		|	И Т.СпособОбеспечения.ТипОбеспечения = ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Перемещение)
		|	И Т.СпособОбеспечения.ИсточникОбеспеченияПотребностей ССЫЛКА Справочник.Склады
		|	И Т.СпособОбеспечения.ИсточникОбеспеченияПотребностей <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|;
		|
		|/////////////////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	//%Ассортимент
		|	Т.СпособОбеспечения                  КАК СпособОбеспечения
		|ПОМЕСТИТЬ СпособыОбеспечения
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		Т.Номенклатура                   КАК Номенклатура,
		|		Т.Склад                          КАК Склад,
		|		СпрСпособ.Ссылка                 КАК СпособОбеспечения
		|	ИЗ
		// Способы обеспечения товаров, для которых поддерживается запас.
		|		ВтТоварыПоддерживаемогоЗапаса КАК Т
		|	
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОбеспеченияПотребностей КАК СпрСпособ
		|			ПО &ПодстановкаОсновногоСпособаОбеспечения
		|	ГДЕ
		|		Т.МетодОбеспеченияПотребностей <> ЗНАЧЕНИЕ(Перечисление.МетодыОбеспеченияПотребностей.ЗаказПодЗаказ)
		|		И (&ИспользоватьХарактеристикиНоменклатуры
		|			ИЛИ Т.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
		|	
		|	//отборы компоновщика
		|	{ГДЕ
		|		ВЫРАЗИТЬ(NULL КАК Справочник.Назначения).*              КАК Назначение,
		|		ВЫРАЗИТЬ(NULL КАК Справочник.СтруктураПредприятия).*    КАК ПодразделениеПолучатель,
		|		ВЫРАЗИТЬ(NULL КАК Справочник.НаправленияДеятельности).* КАК НаправлениеДеятельности,
		|		НЕОПРЕДЕЛЕНО                                            КАК Заказ}
		|	
		|	{ГДЕ
		|		Т.Склад.*                                                                                    КАК Склад,
		|		(ЕСТЬNULL(СпрСпособ.Ссылка,
		|			ЗНАЧЕНИЕ(Справочник.СпособыОбеспеченияПотребностей.ПустаяСсылка))).*           КАК СпособОбеспечения,
		|		ЕСТЬNULL(СпрСпособ.ТипОбеспечения, ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Покупка)) КАК ТипОбеспечения,
		|		ВЫРАЗИТЬ(
		|			ВЫБОР
		|				КОГДА СпрСпособ.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) ТОГДА
		|					СпрСпособ.Подразделение
		|				ИНАЧЕ
		|					ЕСТЬNULL(Т.Склад.Подразделение,
		|						ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
		|			КОНЕЦ КАК Справочник.СтруктураПредприятия).*                                   КАК Подразделение,
		|		(ЕСТЬNULL(СпрСпособ.ИсточникОбеспеченияПотребностей, НЕОПРЕДЕЛЕНО)).*              КАК ИсточникОбеспечения}
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Т.Номенклатура                   КАК Номенклатура,
		|		Т.Склад                          КАК Склад,
		|		Т.СпособОбеспечения              КАК СпособОбеспечения
		|	ИЗ
		|		ВтУдаленныеСкладыСпособыОбеспечения КАК Т
		|	{ГДЕ
		|		Т.Склад.*                                                                                    КАК Склад,
		|		(ЕСТЬNULL(Т.СпособОбеспечения,
		|			ЗНАЧЕНИЕ(Справочник.СпособыОбеспеченияПотребностей.ПустаяСсылка))).*                     КАК СпособОбеспечения,
		|		ЕСТЬNULL(Т.СпособОбеспечения.ТипОбеспечения, ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Покупка)) КАК ТипОбеспечения,
		|		ВЫРАЗИТЬ(
		|			ВЫБОР
		|				КОГДА Т.СпособОбеспечения.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) ТОГДА
		|					Т.СпособОбеспечения.Подразделение
		|				ИНАЧЕ
		|					ЕСТЬNULL(Т.Склад.Подразделение,
		|						ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
		|			КОНЕЦ КАК Справочник.СтруктураПредприятия).*                                             КАК Подразделение,
		|		(ЕСТЬNULL(Т.СпособОбеспечения.ИсточникОбеспеченияПотребностей, НЕОПРЕДЕЛЕНО)).*              КАК ИсточникОбеспечения}
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Т.Номенклатура                   КАК Номенклатура,
		|		Т.Склад                          КАК Склад,
		|		СпрСпособ.Ссылка                 КАК СпособОбеспечения
		|	ИЗ
		|		ВтРаспредЦентрыСпособыОбеспечения КАК Т
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОбеспеченияПотребностей КАК СпрСпособ
		|			ПО &ПодстановкаОсновногоСпособаОбеспечения
		|	{ГДЕ
		|		Т.Склад.*                                                                                    КАК Склад,
		|		(ЕСТЬNULL(СпрСпособ.Ссылка,
		|			ЗНАЧЕНИЕ(Справочник.СпособыОбеспеченияПотребностей.ПустаяСсылка))).*           КАК СпособОбеспечения,
		|		ЕСТЬNULL(СпрСпособ.ТипОбеспечения, ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Покупка)) КАК ТипОбеспечения,
		|		ВЫРАЗИТЬ(
		|			ВЫБОР
		|				КОГДА СпрСпособ.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) ТОГДА
		|					СпрСпособ.Подразделение
		|				ИНАЧЕ
		|					ЕСТЬNULL(Т.Склад.Подразделение,
		|						ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
		|			КОНЕЦ КАК Справочник.СтруктураПредприятия).*                                   КАК Подразделение,
		|		(ЕСТЬNULL(СпрСпособ.ИсточникОбеспеченияПотребностей, НЕОПРЕДЕЛЕНО)).*              КАК ИсточникОбеспечения}
		|	) КАК Т
		|ГДЕ
		|	&УсловиеДоступаПоТипуОбеспечения
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СпособОбеспечения
		|;
		|
		|/////////////////////////////////////////////////////////////////////////////////////////////
		|";
		
	ДоступныеТипыОбеспечения = ДоступныеТипыОбеспечения();
	ТекстУсловияДоступаПоТипуОбеспечения = ?(ДоступныеТипыОбеспечения = Неопределено,
		"ИСТИНА",
		"ЕСТЬNULL(Т.СпособОбеспечения.ТипОбеспечения, ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Покупка)) В (&ДоступныеТипыОбеспечения)");
	ТекстУсловияДоступаПоТипуОбеспечения = ТекстУсловияДоступаПоТипуОбеспечения + "
		|И ВЫБОР КОГДА Т.СпособОбеспечения.ТипОбеспечения = ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Перемещение) ТОГДА
		|		ЕСТЬNULL(&ФОЗаказыНаПеремещенияВключена И НЕ Т.Склад.ЦеховаяКладовая
		|			ИЛИ &ФОПроизводство2_2Включена И Т.Склад.ЦеховаяКладовая, ЛОЖЬ)
		|	ИНАЧЕ ИСТИНА КОНЕЦ";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеДоступаПоТипуОбеспечения", ТекстУсловияДоступаПоТипуОбеспечения);
	
	ТекстЗапроса = РегистрыСведений.СхемыОбеспечения.ПодставитьСоединениеДляПолученияСпособаОбеспечения(
		ТекстЗапроса, "ПодстановкаОсновногоСпособаОбеспечения");
	
	ИспользоватьАссортимент = ПолучитьФункциональнуюОпцию("ИспользоватьАссортимент");
	Если ИспользоватьАссортимент Тогда
		// Если используется ассортимент, то дополнительно необходимо получать номенклатуру и склад,
		// для последующей проверки разрешения на закупки.
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//%Ассортимент",
			"	Т.Номенклатура КАК Номенклатура,
			|	Т.Склад КАК Склад,");
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ СпособыОбеспечения", "ПОМЕСТИТЬ ТоварыИСпособыОбеспечения");
		
		ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Т.СпособОбеспечения КАК Ссылка
			|ПОМЕСТИТЬ ВтСпособыОбеспечения
			|ИЗ
			|	ТоварыИСпособыОбеспечения КАК Т
			|;
			|
			|/////////////////////////////////
			|";
		
		ТекстЗапроса = ТекстЗапроса + Справочники.СпособыОбеспеченияПотребностей.ВременнаяТаблицаДатПоставок();
		
		ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Т.СпособОбеспечения КАК СпособОбеспечения
			|ПОМЕСТИТЬ СпособыОбеспечения
			|ИЗ
			|	ТоварыИСпособыОбеспечения КАК Т
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСпособыОбеспеченияДатыПоставок КАК ТаблицаСпособовОбеспечения
			|		ПО Т.СпособОбеспечения = ТаблицаСпособовОбеспечения.Ссылка
			|//%Ассортимент
			|;
			|
			|/////////////////////////////////
			|";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//%Ассортимент", ТекстУсловияАссортимент());
		
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//%Ассортимент", "");
	КонецЕсли;

	ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Т.СпособОбеспечения КАК СпособОбеспечения
		|{ВЫБРАТЬ
		|	СпособОбеспечения.* КАК СпособОбеспечения}
		|ИЗ
		|	СпособыОбеспечения КАК Т";

	Возврат ТекстЗапроса;

КонецФункции


Функция ДоступныеТипыОбеспечения() Экспорт
	
	Результат = Новый Массив();
	ЕстьОграничения = Ложь;
	ЕстьДоступПокупка = ПравоДоступа("Добавление", Метаданные.Документы.ЗаказПоставщику)
		И ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыПоставщикам");
	Если ЕстьДоступПокупка Тогда
		Результат.Добавить(Перечисления.ТипыОбеспечения.Покупка);
	Иначе
		ЕстьОграничения = Истина;
	КонецЕсли;
	
	ЕстьДоступПеремещение = ПравоДоступа("Добавление", Метаданные.Документы.ЗаказНаПеремещение)
		И ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыНаПеремещение");
	Если ЕстьДоступПеремещение Тогда
		Результат.Добавить(Перечисления.ТипыОбеспечения.Перемещение);
	Иначе
		ЕстьОграничения = Истина;
	КонецЕсли;
	
	ЕстьДоступСборка = ПравоДоступа("Добавление", Метаданные.Документы.ЗаказНаСборку)
		И ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыНаСборку");
	Если ЕстьДоступСборка Тогда
		Результат.Добавить(Перечисления.ТипыОбеспечения.СборкаРазборка);
	Иначе
		ЕстьОграничения = Истина;
	КонецЕсли;

	ЕстьОграниченияВременный = Истина;
	ЕстьОграничения = ЕстьОграничения Или ЕстьОграниченияВременный;
	
	ЕстьОграниченияВременный = Истина;
	ЕстьОграничения = ЕстьОграничения Или ЕстьОграниченияВременный;
	
	Если ЕстьОграничения Тогда
		Возврат Результат;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ВременнаяТаблицаГраницыОбеспечиваемогоПериода()

	ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Календарь               КАК Календарь,
		|	Т.ОбеспечиваемыйПериод    КАК ЧислоДней,
		|	Т.ДатаВозможнойПоставки   КАК ДатаОтсчета
		|
		|ПОМЕСТИТЬ ВтПараметрыПоиска
		|ИЗ
		|	ВтКалендариИДаты КАК Т
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Календарь,
		|	ЧислоДней, ДатаОтсчета
		|;
		|
		|/////////////////////////////////////////////////////////
		|";

	ТекстЗапроса = ТекстЗапроса + ОбеспечениеСервер.ТекстЗапросаДатГрафика();

	Возврат ТекстЗапроса;

КонецФункции

Функция ВременнаяТаблицаДанныеЗаполненияЗаказаПоставщику()

	ТекстЗапроса =
		// 1) Дата последнего заказа для позиции, требующей заполнения источника обесепечения.
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Т.Номенклатура   КАК Номенклатура,
		|	Т.Характеристика КАК Характеристика,
		|	Т.Склад          КАК Склад,
		|
		|	МАКСИМУМ(Товары.Ссылка.Дата) КАК ДатаПоследнегоЗаказа
		|
		|ПОМЕСТИТЬ ВтДатаПоследнегоЗаказа
		|ИЗ
		|	ВтТовары КАК Т
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику.Товары КАК Товары
		|		ПО Т.Номенклатура   = Товары.Номенклатура
		|		 И Т.Характеристика = Товары.Характеристика
		|		 И Т.Склад          = Товары.Склад
		|ГДЕ
		|	Т.СпособОбеспечения ЕСТЬ NULL
		|	ИЛИ (Т.СпособОбеспечения.ТипОбеспечения = ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Покупка)
		|			И Т.СпособОбеспечения.ИсточникОбеспеченияПотребностей В(НЕОПРЕДЕЛЕНО,
		|					ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка),
		|					ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
		|					ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)))
		|	И Товары.Ссылка.Проведен
		|СГРУППИРОВАТЬ ПО
		|	Т.Номенклатура, Т.Характеристика, Т.Склад
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// 2 Дата предпоследнего заказа.
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Т.Номенклатура   КАК Номенклатура,
		|	Т.Характеристика КАК Характеристика,
		|	Т.Склад          КАК Склад,
		|	МАКСИМУМ(Т.ДатаПоследнегоЗаказа)  КАК ДатаПоследнегоЗаказа,
		|	МАКСИМУМ(ВЫБОР КОГДА Т.ДатаПоследнегоЗаказа = Товары.Ссылка.Дата ТОГДА
		|				NULL
		|			ИНАЧЕ Товары.Ссылка.Дата
		|		КОНЕЦ)                        КАК ДатаПредпоследнегоЗаказа
		|	
		|ПОМЕСТИТЬ ВтДатаПредпоследнегоЗаказа
		|ИЗ
		|	ВтДатаПоследнегоЗаказа КАК Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику.Товары КАК Товары
		|		ПО Т.Номенклатура   = Товары.Номенклатура
		|		 И Т.Характеристика = Товары.Характеристика
		|		 И Т.Склад         = Товары.Склад
		|ГДЕ
		|	Товары.Ссылка.Проведен
		|СГРУППИРОВАТЬ ПО
		|	Т.Номенклатура, Т.Характеристика, Т.Склад
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// 3) Дата третьего заказа.
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Т.Номенклатура   КАК Номенклатура,
		|	Т.Характеристика КАК Характеристика,
		|	Т.Склад          КАК Склад,
		|	МАКСИМУМ(Т.ДатаПоследнегоЗаказа)     КАК ДатаПоследнегоЗаказа,
		|	МАКСИМУМ(Т.ДатаПредпоследнегоЗаказа) КАК ДатаПредпоследнегоЗаказа,
		|	МАКСИМУМ(ВЫБОР КОГДА Товары.Ссылка.Дата В (Т.ДатаПоследнегоЗаказа, Т.ДатаПредпоследнегоЗаказа) ТОГДА
		|				NULL
		|			ИНАЧЕ Товары.Ссылка.Дата
		|		КОНЕЦ)                           КАК ДатаТретьегоЗаказа
		|	
		|ПОМЕСТИТЬ ВтДатаТретьегоЗаказа
		|ИЗ
		|	ВтДатаПредпоследнегоЗаказа КАК Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику.Товары КАК Товары
		|		ПО Т.Номенклатура   = Товары.Номенклатура
		|		 И Т.Характеристика = Товары.Характеристика
		|		 И Т.Склад         = Товары.Склад
		|ГДЕ
		|	Товары.Ссылка.Проведен
		|СГРУППИРОВАТЬ ПО
		|	Т.Номенклатура, Т.Характеристика, Т.Склад
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// 4) Последние три заказа по позиции.
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Т.Номенклатура   КАК Номенклатура,
		|	Т.Характеристика КАК Характеристика,
		|	Т.Склад          КАК Склад,
		|
		|	МАКСИМУМ(ВЫБОР КОГДА Т.ДатаПоследнегоЗаказа = Товары.Ссылка.Дата ТОГДА
		|				Товары.Ссылка
		|			ИНАЧЕ NULL
		|		КОНЕЦ)                           КАК Заказ1,
		|	МАКСИМУМ(ВЫБОР КОГДА Т.ДатаПредпоследнегоЗаказа = Товары.Ссылка.Дата ТОГДА
		|				Товары.Ссылка
		|			ИНАЧЕ NULL
		|		КОНЕЦ)                           КАК Заказ2,
		|	МАКСИМУМ(ВЫБОР КОГДА Т.ДатаТретьегоЗаказа = Товары.Ссылка.Дата ТОГДА
		|				Товары.Ссылка
		|			ИНАЧЕ NULL
		|		КОНЕЦ)                           КАК Заказ3
		|
		|ПОМЕСТИТЬ ВтТриПоследнихЗаказа
		|ИЗ
		|	ВтДатаТретьегоЗаказа КАК Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику.Товары КАК Товары
		|		ПО Т.Номенклатура   = Товары.Номенклатура
		|		 И Т.Характеристика = Товары.Характеристика
		|		 И Т.Склад         = Товары.Склад
		|		 И Товары.Ссылка.Дата В (Т.ДатаПоследнегоЗаказа, Т.ДатаПредпоследнегоЗаказа, Т.ДатаТретьегоЗаказа)
		|ГДЕ
		|	Товары.Ссылка.Проведен
		|СГРУППИРОВАТЬ ПО
		|	Т.Номенклатура, Т.Характеристика, Т.Склад
		|ИНДЕКСИРОВАТЬ ПО
		|	Заказ1, Заказ2, Заказ3
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// 5) Данные для заполнения источника обеспечения и условий закупки.
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Т.Номенклатура   КАК Номенклатура,
		|	Т.Характеристика КАК Характеристика,
		|	Т.Склад          КАК Склад,
		|
		|	ВЫБОР КОГДА Т.Заказ2.Партнер ЕСТЬ NULL ТОГДА
		|				Т.Заказ1.Партнер
		|			КОГДА Т.Заказ3.Партнер ЕСТЬ NULL ТОГДА
		|				ВЫБОР КОГДА Т.Заказ1.Партнер = Т.Заказ2.Партнер ТОГДА
		|							Т.Заказ1.Партнер
		|						ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
		|					КОНЕЦ
		|			ИНАЧЕ
		|				ВЫБОР КОГДА Т.Заказ1.Партнер = Т.Заказ2.Партнер ИЛИ Т.Заказ1.Партнер = Т.Заказ3.Партнер ТОГДА
		|							Т.Заказ1.Партнер
		|						КОГДА Т.Заказ2.Партнер = Т.Заказ3.Партнер ТОГДА
		|							Т.Заказ2.Партнер
		|						ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
		|					КОНЕЦ
		|		КОНЕЦ       КАК Партнер,
		|
		|	ВЫБОР КОГДА Т.Заказ2.Партнер ЕСТЬ NULL ТОГДА
		|				Т.Заказ1.Соглашение
		|			КОГДА Т.Заказ3.Партнер ЕСТЬ NULL ТОГДА
		|				ВЫБОР КОГДА Т.Заказ1.Соглашение = Т.Заказ2.Соглашение ТОГДА
		|							Т.Заказ1.Соглашение
		|						ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СоглашенияСПоставщиками.ПустаяСсылка)
		|					КОНЕЦ
		|			ИНАЧЕ
		|				ВЫБОР КОГДА Т.Заказ1.Соглашение = Т.Заказ2.Соглашение ИЛИ Т.Заказ1.Соглашение = Т.Заказ3.Соглашение ТОГДА
		|							Т.Заказ1.Соглашение
		|						КОГДА Т.Заказ2.Соглашение = Т.Заказ3.Соглашение ТОГДА
		|							Т.Заказ2.Соглашение
		|						ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СоглашенияСПоставщиками.ПустаяСсылка)
		|					КОНЕЦ
		|		КОНЕЦ       КАК Соглашение,
		|
		|	ВЫБОР КОГДА Т.Заказ2.Партнер ЕСТЬ NULL ТОГДА
		|				Т.Заказ1.Соглашение.Валюта
		|			КОГДА Т.Заказ3.Партнер ЕСТЬ NULL ТОГДА
		|				ВЫБОР КОГДА Т.Заказ1.Соглашение = Т.Заказ2.Соглашение ТОГДА
		|							Т.Заказ1.Соглашение.Валюта
		|						ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СоглашенияСПоставщиками.ПустаяСсылка)
		|					КОНЕЦ
		|			ИНАЧЕ
		|				ВЫБОР КОГДА Т.Заказ1.Соглашение = Т.Заказ2.Соглашение ИЛИ Т.Заказ1.Соглашение = Т.Заказ3.Соглашение ТОГДА
		|							Т.Заказ1.Соглашение.Валюта
		|						КОГДА Т.Заказ2.Соглашение = Т.Заказ3.Соглашение ТОГДА
		|							Т.Заказ2.Соглашение.Валюта
		|						ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СоглашенияСПоставщиками.ПустаяСсылка)
		|					КОНЕЦ
		|		КОНЕЦ       КАК Валюта,
		|
		|	ВЫРАЗИТЬ(ВЫБОР КОГДА Т.Заказ2.Партнер ЕСТЬ NULL ТОГДА
		|				Т.Заказ1.Соглашение
		|			КОГДА Т.Заказ3.Партнер ЕСТЬ NULL ТОГДА
		|				ВЫБОР КОГДА Т.Заказ1.Соглашение = Т.Заказ2.Соглашение ТОГДА
		|							Т.Заказ1.Соглашение
		|						ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СоглашенияСПоставщиками.ПустаяСсылка)
		|					КОНЕЦ
		|			ИНАЧЕ
		|				ВЫБОР КОГДА Т.Заказ1.Соглашение = Т.Заказ2.Соглашение ИЛИ Т.Заказ1.Соглашение = Т.Заказ3.Соглашение ТОГДА
		|							Т.Заказ1.Соглашение
		|						КОГДА Т.Заказ2.Соглашение = Т.Заказ3.Соглашение ТОГДА
		|							Т.Заказ2.Соглашение
		|						ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СоглашенияСПоставщиками.ПустаяСсылка)
		|					КОНЕЦ
		|		КОНЕЦ КАК Справочник.СоглашенияСПоставщиками).ВидЦеныПоставщика КАК ВидЦеныПоставщика
		|
		|ПОМЕСТИТЬ ВтДанныеЗаполненияЗаказаПоставщику
		|ИЗ
		|	ВтТриПоследнихЗаказа КАК Т
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// 6) Цены номенклатуры.
		|ВЫБРАТЬ
		|		ЦеныПоставщиков.Номенклатура           КАК Номенклатура,
		|		ЦеныПоставщиков.Характеристика         КАК Характеристика,
		|		ЦеныПоставщиков.Партнер                КАК Партнер,
		|		ЦеныПоставщиков.ВидЦеныПоставщика      КАК ВидЦеныПоставщика,
		|		ЦеныПоставщиков.Цена                   КАК Цена,
		|		ЦеныПоставщиков.Упаковка               КАК Упаковка,
		|		ЦеныПоставщиков.Валюта                 КАК Валюта
		|ПОМЕСТИТЬ ЦеныПоставщиков
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатурыПоставщиков.СрезПоследних(
		|		КОНЕЦПЕРИОДА(&НачалоПериода, ДЕНЬ),
		|		(Номенклатура, Характеристика, Партнер, ВидЦеныПоставщика)
		|			В (
		|				ВЫБРАТЬ
		|					ТаблицаОтбора.Номенклатура                                      КАК Номенклатура,
		|					ТаблицаОтбора.Характеристика                                    КАК Характеристика,
		|					ТаблицаОтбора.СпособОбеспечения.ИсточникОбеспеченияПотребностей КАК Партнер,
		|					ТаблицаОтбора.СпособОбеспечения.ВидЦеныПоставщика               КАК ВидЦеныПоставщика
		|				ИЗ
		|					ВтТовары КАК ТаблицаОтбора
		|
		|				ОБЪЕДИНИТЬ ВСЕ
		|
		|				ВЫБРАТЬ
		|					Т.Номенклатура           КАК Номенклатура,
		|					Т.Характеристика         КАК Характеристика,
		|					Т.Партнер                КАК Партнер,
		|					Т.ВидЦеныПоставщика      КАК ВидЦеныПоставщика
		|				ИЗ
		|					ВтДанныеЗаполненияЗаказаПоставщику КАК Т)
		|	) КАК ЦеныПоставщиков
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика,
		|	Партнер,
		|	ВидЦеныПоставщика
		|
		|;
		|////////////////////////////////////////////////////////////////////////////////
		// 7) Курсы валют.
		|ВЫБРАТЬ
		|		КурсыВалют.Валюта                      КАК Валюта,
		|		КурсыВалют.Курс * КурсыВалют.Кратность КАК Коэффициент
		|ПОМЕСТИТЬ КурсыВалют
		|ИЗ
		|	РегистрСведений.КурсыВалют.СрезПоследних(КОНЕЦПЕРИОДА(&НачалоПериода, ДЕНЬ), ) КАК КурсыВалют
		|	
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
		|
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|";

	Возврат ТекстЗапроса;

КонецФункции

Функция ВременнаяТаблицаСвободныеОстатки(ВТаблицеОтбораЕстьНазначения)
	
	Текст =
		"ВЫБРАТЬ
		|	Товары.Склад                                 КАК Склад,
		|	Товары.Номенклатура                          КАК Номенклатура,
		|	Товары.Характеристика                        КАК Характеристика,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	Товары.ВНаличииОстаток                       КАК ВНаличии,
		|	Товары.ВРезервеПодЗаказОстаток               КАК ВРезервеПодЗаказ,
		|	Товары.ВРезервеСоСкладаОстаток               КАК ВРезервеСоСклада
		|ПОМЕСТИТЬ ВтСвободныеОстаткиВсехСкладов
		|ИЗ
		|	РегистрНакопления.СвободныеОстатки.Остатки(
		|			,
		|			(Склад, Номенклатура, Характеристика) В(
		|				ВЫБРАТЬ
		|					ТаблицаОтбора.Склад          КАК Склад,
		|					ТаблицаОтбора.Номенклатура   КАК Номенклатура,
		|					ТаблицаОтбора.Характеристика КАК Характеристика
		|				ИЗ
		|					РазличныеТоварыИСклады КАК ТаблицаОтбора)) КАК Товары
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Товары.Склад                   КАК Склад,
		|	Товары.Номенклатура            КАК Номенклатура,
		|	Товары.Характеристика          КАК Характеристика,
		|	Товары.Назначение              КАК Назначение,
		|	Товары.НаличиеПодЗаказОстаток  КАК ВНаличии,
		|	0                              КАК ВРезервеПодЗаказ,
		|	0                              КАК ВРезервеСоСклада
		|ИЗ
		|	РегистрНакопления.ОбеспечениеЗаказов.Остатки(
		|			,
		|			(Склад, Номенклатура, Характеристика, Назначение) В(
		|				ВЫБРАТЬ
		|					ТаблицаОтбора.Склад          КАК Склад,
		|					ТаблицаОтбора.Номенклатура   КАК Номенклатура,
		|					ТаблицаОтбора.Характеристика КАК Характеристика,
		|					&ПодстановкаНазначениеВыбора КАК Назначение
		|				ИЗ
		|					РазличныеТоварыИСклады КАК ТаблицаОтбора)) КАК Товары
		|;
		|
		|///////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Склад                         КАК Склад,
		|	Т.Номенклатура                  КАК Номенклатура,
		|	Т.Характеристика                КАК Характеристика,
		|	Т.Назначение                    КАК Назначение,
		|	СУММА(Т.ВНаличии)
		|		- СУММА(Т.ВРезервеПодЗаказ) КАК ВНаличии,
		|	СУММА(Т.ВНаличии)
		|		- СУММА(Т.ВРезервеПодЗаказ)
		|		- СУММА(Т.ВРезервеСоСклада) КАК ВНаличииМинусРезерв
		|ПОМЕСТИТЬ ВтСвободныеОстатки
		|ИЗ
		|	(ВЫБРАТЬ
		|		Т.Номенклатура                    КАК Номенклатура,
		|		Т.Характеристика                  КАК Характеристика,
		|		Т.Назначение                      КАК Назначение,
		|		Т.Склад                           КАК Склад,
		|		Т.ВНаличии                        КАК ВНаличии,
		|		Т.ВРезервеПодЗаказ                КАК ВРезервеПодЗаказ,
		|		Т.ВРезервеСоСклада                КАК ВРезервеСоСклада
		|	ИЗ
		|		ВтСвободныеОстаткиВсехСкладов КАК Т
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Т.Номенклатура                    КАК Номенклатура,
		|		Т.Характеристика                  КАК Характеристика,
		|		Т.Назначение                      КАК Назначение,
		|		Топология.Центр                   КАК Склад,
		|		Т.ВНаличии                        КАК ВНаличии,
		|		Т.ВРезервеПодЗаказ                КАК ВРезервеПодЗаказ,
		|		Т.ВРезервеСоСклада                КАК ВРезервеСоСклада
		|	ИЗ
		|		ВтСвободныеОстаткиВсехСкладов КАК Т
		|			
		|			ЛЕВОЕ СОЕДИНЕНИЕ ТопологияСкладов КАК Топология
		|			ПО Т.Номенклатура   = Топология.Номенклатура
		|			 И Т.Характеристика = Топология.Характеристика
		|			 И Т.Склад          = Топология.Склад
		|			 И Т.Склад          <> Топология.Центр
		|ГДЕ
		|	НЕ Топология.Номенклатура ЕСТЬ NULL) КАК Т
		|
		|СГРУППИРОВАТЬ ПО
		|	Т.Номенклатура, Т.Характеристика, Т.Склад, Т.Назначение
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Склад, Номенклатура, Характеристика, Назначение
		|;
		|
		|///////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВтСвободныеОстаткиВсехСкладов";
	
	Подстановка = "ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)";
	ПодстановкаНазначение = "ТаблицаОтбора.Назначение";
	ПодстановкаИтоговая = ?(ВТаблицеОтбораЕстьНазначения, ПодстановкаНазначение, Подстановка);
		
	Текст = СтрЗаменить(Текст, "&ПодстановкаНазначениеВыбора", ПодстановкаИтоговая);
	Возврат Текст;
	
КонецФункции

Функция ВременнаяТаблицаЗаказыКПоступлениюИОтгрузке(ВТаблицеОтбораЕстьНазначения)
	
	Текст =
		"ВЫБРАТЬ
		|	Набор.Номенклатура              КАК Номенклатура,
		|	Набор.Характеристика            КАК Характеристика,
		|	Набор.Склад                     КАК Склад,
		|	Набор.Назначение                КАК Назначение,
		|	СУММА(Набор.ЗаказыКПоступлению) КАК ЗаказыКПоступлению,
		|	СУММА(Набор.ЗаказыКОтгрузке)    КАК ЗаказыКОтгрузке
		|ПОМЕСТИТЬ ВтЗаказыВсехСкладов
		|ИЗ(
		|	ВЫБРАТЬ
		|		ГрафикПоступления.Номенклатура                               КАК Номенклатура,
		|		ГрафикПоступления.Характеристика                             КАК Характеристика,
		|		ГрафикПоступления.Склад                                      КАК Склад,
		|		ГрафикПоступления.Назначение                                 КАК Назначение,
		|		ГрафикПоступления.КоличествоИзЗаказовСНеподтвержденными
		|			+ ГрафикПоступления.КоличествоПодЗаказСНеподтвержденными КАК ЗаказыКПоступлению,
		|		0                                                            КАК ЗаказыКОтгрузке
		|	ИЗ
		|		РегистрНакопления.ГрафикПоступленияТоваров КАК ГрафикПоступления
		|			
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РазличныеТоварыИСклады КАК ТаблицаОтбора
		|			ПО ГрафикПоступления.Номенклатура   = ТаблицаОтбора.Номенклатура
		|			 И ГрафикПоступления.Характеристика = ТаблицаОтбора.Характеристика
		|			 И ГрафикПоступления.Склад          = ТаблицаОтбора.Склад
		|			 И ГрафикПоступления.Назначение     = &ПодстановкаНазначенияСоединения
		|	ГДЕ
		|		ГрафикПоступления.Активность
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ГрафикОтгрузки.Номенклатура            КАК Номенклатура,
		|		ГрафикОтгрузки.Характеристика          КАК Характеристика,
		|		ГрафикОтгрузки.Склад                   КАК Склад,
		|		ГрафикОтгрузки.Назначение              КАК Назначение,
		|		0                                      КАК ЗаказыКПоступлению,
		|		ГрафикОтгрузки.КоличествоИзЗаказов     КАК ЗаказыКОтгрузке
		|	ИЗ
		|		РегистрНакопления.ГрафикОтгрузкиТоваров КАК ГрафикОтгрузки
		|			
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РазличныеТоварыИСклады КАК ТаблицаОтбора
		|			ПО ГрафикОтгрузки.Номенклатура   = ТаблицаОтбора.Номенклатура
		|			 И ГрафикОтгрузки.Характеристика = ТаблицаОтбора.Характеристика
		|			 И ГрафикОтгрузки.Склад          = ТаблицаОтбора.Склад
		|			 И ГрафикОтгрузки.Назначение     = &ПодстановкаНазначенияСоединения
		|			 И ГрафикОтгрузки.Назначение     = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	ГДЕ
		|		ГрафикОтгрузки.Активность) КАК Набор
		|
		|СГРУППИРОВАТЬ ПО
		|	Набор.Номенклатура, Набор.Характеристика, Набор.Склад, Набор.Назначение
		|;
		|
		|///////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвижениеТоваров.Номенклатура   КАК Номенклатура,
		|	ДвижениеТоваров.Характеристика КАК Характеристика,
		|	ДвижениеТоваров.Назначение     КАК Назначение,
		|	ДвижениеТоваров.Распоряжение   КАК Заказ,
		|	МАКСИМУМ(Топология.Центр)      КАК Центр,
		|	МАКСИМУМ(
		|				ЗаказыНаПеремещение.СкладОтправитель
		|		) КАК СкладОтправитель,
		|
		|	СУММА(ДвижениеТоваров.ПланируемоеПоступлениеСНеподтвержденными) КАК КоличествоВсего
		|
		|ПОМЕСТИТЬ ВтЗаказыНаПеремещениеВнутриРаспределительнойСети
		|ИЗ
		|	ТопологияСкладов КАК Топология
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДвижениеТоваров КАК ДвижениеТоваров
		|		ПО ДвижениеТоваров.Номенклатура   = Топология.Номенклатура
		|		 И ДвижениеТоваров.Характеристика = Топология.Характеристика
		|		 И ДвижениеТоваров.Склад          = Топология.Склад
		|		 И (ДвижениеТоваров.ПланируемоеПоступлениеСНеподтвержденными > 0
		|				ИЛИ ДвижениеТоваров.ПланируемоеПоступлениеПодЗаказСНеподтвержденными > 0)
		|		 И ДвижениеТоваров.Активность
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПеремещение КАК ЗаказыНаПеремещение
		|		ПО ЗаказыНаПеремещение.Ссылка = ДвижениеТоваров.Распоряжение
		|		
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТопологияСкладов КАК СкладыСети
		|		ПО СкладыСети.Номенклатура   =  Топология.Номенклатура
		|		 И СкладыСети.Характеристика =  Топология.Характеристика
		|		 И СкладыСети.Центр          =  Топология.Центр
		|		 И СкладыСети.Склад =
		|									ЗаказыНаПеремещение.СкладОтправитель
		|		 
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(ДвижениеТоваров.Распоряжение) В(
		|			ТИП(Документ.ЗаказНаПеремещение))
		|	И НЕ СкладыСети.Номенклатура ЕСТЬ NULL
		|	И &ПодстановкаНазначенияОтбора
		|СГРУППИРОВАТЬ ПО
		|	ДвижениеТоваров.Номенклатура,
		|	ДвижениеТоваров.Характеристика,
		|	ДвижениеТоваров.Назначение,
		|	ДвижениеТоваров.Распоряжение
		|;
		|
		|///////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НаборПоТовару.Центр                            КАК ЦентрСети,
		|	НаборПоТовару.Номенклатура                     КАК Номенклатура,
		|	НаборПоТовару.Характеристика                   КАК Характеристика,
		|	НаборПоТовару.Назначение                       КАК Назначение,
		|	СУММА(НаборПоТовару.КоличествоВсего)
		|		- СУММА(НаборПоТовару.КоличествоОтгрузить) КАК КОтгрузке,
		|	СУММА(НаборПоТовару.КоличествоВсего)
		|		- СУММА(НаборПоТовару.КоличествоОтгрузить)
		|		- СУММА(НаборПоТовару.КоличествоВРезерве)  КАК КРезервированию
		|ПОМЕСТИТЬ ВтЗаказыРаспределительнойСетиНеОтгруженные
		|ИЗ(
		|	ВЫБРАТЬ
		|		НаборПоЗаказу.Заказ                      КАК Заказ,
		|		НаборПоЗаказу.Номенклатура               КАК Номенклатура,
		|		НаборПоЗаказу.Характеристика             КАК Характеристика,
		|		НаборПоЗаказу.Назначение                 КАК Назначение,
		|		НаборПоЗаказу.Центр                      КАК Центр,
		|		МАКСИМУМ(НаборПоЗаказу.КоличествоВсего)  КАК КоличествоВсего,
		|		СУММА(НаборПоЗаказу.КоличествоОтгрузить) КАК КоличествоОтгрузить,
		|		СУММА(НаборПоЗаказу.КоличествоВРезерве)  КАК КоличествоВРезерве
		|	ИЗ(
		|		ВЫБРАТЬ
		|			Заказы.Заказ                            КАК Заказ,
		|			Заказы.Номенклатура                     КАК Номенклатура,
		|			Заказы.Характеристика                   КАК Характеристика,
		|			Заказы.Назначение                       КАК Назначение,
		|			Заказы.Центр                            КАК Центр,
		|			Заказы.КоличествоВсего                  КАК КоличествоВсего,
		|			ВЫБОР КОГДА Заказы.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) ТОГДА
		|					СвободныеОстатки.ВНаличии
		|						- СвободныеОстатки.ВРезервеПодЗаказ
		|					ИНАЧЕ
		|						СвободныеОстатки.ВРезервеПодЗаказ
		|				КОНЕЦ
		|					* ВЫБОР КОГДА ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
		|								1
		|							ИНАЧЕ
		|								-1
		|						КОНЕЦ                       КАК КоличествоОтгрузить,
		|			СвободныеОстатки.ВРезервеСоСклада
		|				* ВЫБОР КОГДА ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
		|							-1
		|						ИНАЧЕ
		|							1
		|					КОНЕЦ КАК КоличествоВРезерве
		|		ИЗ
		|			ВтЗаказыНаПеремещениеВнутриРаспределительнойСети КАК Заказы
		|				
		|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СвободныеОстатки КАК СвободныеОстатки
		|				ПО СвободныеОстатки.Номенклатура   = Заказы.Номенклатура
		|				 И СвободныеОстатки.Характеристика = Заказы.Характеристика
		|				 И СвободныеОстатки.Склад          = Заказы.СкладОтправитель
		|				 И СвободныеОстатки.Регистратор    = Заказы.Заказ
		|				 И СвободныеОстатки.Активность
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			
		|			Заказы.Заказ                            КАК Заказ,
		|			Заказы.Номенклатура                     КАК Номенклатура,
		|			Заказы.Характеристика                   КАК Характеристика,
		|			Заказы.Назначение                       КАК Назначение,
		|			Заказы.Центр                            КАК Центр,
		|			Заказы.КоличествоВсего                  КАК КоличествоВсего,
		|			0                                       КАК КоличествоОтгрузить,
		|			ГрафикОтгрузки.КоличествоИзЗаказов      КАК КоличествоВРезерве
		|		ИЗ
		|			ВтЗаказыНаПеремещениеВнутриРаспределительнойСети КАК Заказы
		|				
		|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ГрафикОтгрузкиТоваров КАК ГрафикОтгрузки
		|				ПО ГрафикОтгрузки.Номенклатура   = Заказы.Номенклатура
		|				 И ГрафикОтгрузки.Характеристика = Заказы.Характеристика
		|				 И ГрафикОтгрузки.Склад          = Заказы.СкладОтправитель
		|				 И ГрафикОтгрузки.Назначение     = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|				 И ГрафикОтгрузки.Назначение     = Заказы.Назначение
		|				 И ГрафикОтгрузки.Регистратор    = Заказы.Заказ
		|				 И ГрафикОтгрузки.Активность) КАК НаборПоЗаказу
		|	СГРУППИРОВАТЬ ПО
		|		НаборПоЗаказу.Заказ,
		|		НаборПоЗаказу.Номенклатура,
		|		НаборПоЗаказу.Характеристика,
		|		НаборПоЗаказу.Назначение,
		|		НаборПоЗаказу.Центр) КАК НаборПоТовару
		|СГРУППИРОВАТЬ ПО
		|	НаборПоТовару.Номенклатура,
		|	НаборПоТовару.Характеристика,
		|	НаборПоТовару.Назначение,
		|	НаборПоТовару.Центр
		|;
		|
		|///////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Набор.Номенклатура                 КАК Номенклатура,
		|	Набор.Характеристика               КАК Характеристика,
		|	Набор.Назначение                   КАК Назначение,
		|	Набор.Склад                        КАК Склад,
		|	СУММА(Набор.ЗаказыКПоступлению)    КАК Поступит,
		|	СУММА(Набор.ЗаказыКПоступлению)
		|		- СУММА(Набор.ЗаказыКОтгрузке) КАК ПоступитМинусРезерв
		|ПОМЕСТИТЬ ВтЗаказы
		|ИЗ(
		|	ВЫБРАТЬ
		|		График.Номенклатура                    КАК Номенклатура,
		|		График.Характеристика                  КАК Характеристика,
		|		График.Назначение                      КАК Назначение,
		|		График.Склад                           КАК Склад,
		|		График.ЗаказыКПоступлению              КАК ЗаказыКПоступлению,
		|		График.ЗаказыКОтгрузке                 КАК ЗаказыКОтгрузке
		|	ИЗ
		|		ВтЗаказыВсехСкладов КАК График
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		График.Номенклатура                    КАК Номенклатура,
		|		График.Характеристика                  КАК Характеристика,
		|		График.Назначение                      КАК Назначение,
		|		Топология.Центр                        КАК Склад,
		|		График.ЗаказыКПоступлению              КАК ЗаказыКПоступлению,
		|		График.ЗаказыКОтгрузке                 КАК ЗаказыКОтгрузке
		|	ИЗ
		|		ВтЗаказыВсехСкладов КАК График
		|			
		|			ЛЕВОЕ СОЕДИНЕНИЕ ТопологияСкладов КАК Топология
		|			ПО График.Номенклатура   = Топология.Номенклатура
		|			 И График.Характеристика = Топология.Характеристика
		|			 И График.Склад          = Топология.Склад
		|			 И График.Склад          <> Топология.Центр
		|ГДЕ
		|	НЕ Топология.Номенклатура ЕСТЬ NULL) КАК Набор
		|
		|СГРУППИРОВАТЬ ПО
		|	Набор.Номенклатура, Набор.Характеристика, Набор.Склад, Набор.Назначение
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Склад, Номенклатура, Характеристика, Назначение
		|;
		|
		|///////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВтЗаказыВсехСкладов;
		|УНИЧТОЖИТЬ ВтЗаказыНаПеремещениеВнутриРаспределительнойСети";
	
	Подстановка = "ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)";
	ПодстановкаНазначение = "ТаблицаОтбора.Назначение";
	ПодстановкаИтоговая = ?(ВТаблицеОтбораЕстьНазначения, ПодстановкаНазначение, Подстановка);
		
	Текст = СтрЗаменить(Текст, "&ПодстановкаНазначенияСоединения", ПодстановкаИтоговая);
	
	Подстановка = "ДвижениеТоваров.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)";
	ПодстановкаНазначение = "ИСТИНА";
	ПодстановкаИтоговая = ?(ВТаблицеОтбораЕстьНазначения, ПодстановкаНазначение, Подстановка);
		
	Текст = СтрЗаменить(Текст, "&ПодстановкаНазначенияОтбора", ПодстановкаИтоговая);
	Возврат Текст;
	
КонецФункции

Функция ТекстПолейУсловияЗакупок()

	Текст =
		"	ЕСТЬNULL(ДанныеЗаполнения.ВидЦеныПоставщика, Т.СпособОбеспечения.ВидЦеныПоставщика) КАК ВидЦены,
		|	ЕСТЬNULL(ДанныеЗаполнения.Валюта, Т.СпособОбеспечения.Соглашение.Валюта)            КАК ВалютаСоглашения,
		|
		|	ВЫБОР КОГДА НЕ ЕСТЬNULL(ДанныеЗаполнения.Соглашение.ЦенаВключаетНДС, Т.СпособОбеспечения.Соглашение.ЦенаВключаетНДС) ТОГДА
		|
		|				ВЫБОР КОГДА Т.Номенклатура.СтавкаНДС В(
		|								ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10),
		|								ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10_110)) ТОГДА
		|							1.1
		|						КОГДА Т.Номенклатура.СтавкаНДС В(
		|								ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18),
		|								ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118)) ТОГДА
		|							1.18
		|						КОГДА Т.Номенклатура.СтавкаНДС В(
		|								ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20),
		|								ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20_120)) ТОГДА
		|							1.20
		|						ИНАЧЕ
		|							1
		|					КОНЕЦ
		|			ИНАЧЕ
		|				1
		|		КОНЕЦ
		|	* ЦеныПоставщиков.Цена * КурсыВалют.Коэффициент / КурсВалютаСоглашения.Коэффициент
		|	/ ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1)                                          КАК ЦенаВВалютеСоглашения,
		|
		|	ВЫБОР КОГДА НЕ ЕСТЬNULL(ДанныеЗаполнения.Соглашение.ЦенаВключаетНДС, Т.СпособОбеспечения.Соглашение.ЦенаВключаетНДС) ТОГДА
		|
		|				ВЫБОР КОГДА Т.Номенклатура.СтавкаНДС В(
		|								ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10),
		|								ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10_110)) ТОГДА
		|							1.1
		|						КОГДА Т.Номенклатура.СтавкаНДС В(
		|								ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18),
		|								ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118)) ТОГДА
		|							1.18
		|						КОГДА Т.Номенклатура.СтавкаНДС В(
		|								ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20),
		|								ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20_120)) ТОГДА
		|							1.20
		|						ИНАЧЕ
		|							1
		|					КОНЕЦ
		|			ИНАЧЕ
		|				1
		|		КОНЕЦ
		|	* ЦеныПоставщиков.Цена * КурсыВалют.Коэффициент / &КоэффициентВалютыУпрУчета
		|	/ ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1)                                КАК ЦенаВВалютеУправленческогоУчета,
		|
		|	ВЫБОР КОГДА Т.СпособОбеспечения.ТипОбеспечения = ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.СборкаРазборка) ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ВЫБОР ЕСТЬNULL(Т.СпособОбеспечения.ИсточникОбеспеченияПотребностей, НЕОПРЕДЕЛЕНО)
		|					КОГДА НЕОПРЕДЕЛЕНО ТОГДА
		|						ЛОЖЬ
		|					КОГДА ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) ТОГДА
		|						ЛОЖЬ
		|					КОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка) ТОГДА
		|						ЛОЖЬ
		|					КОГДА ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) ТОГДА
		|						ЛОЖЬ
		|					ИНАЧЕ
		|						ИСТИНА //источник обеспечения устанавливается в способе обеспечения потребностей
		|				КОНЕЦ
		|		КОНЕЦ                                                                      КАК ИсточникОбеспеченияТолькоПросмотр,
		|";
	Текст = СтрЗаменить(Текст, "&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ЦеныПоставщиков.Упаковка",
			"ЦеныПоставщиков.Номенклатура"));
	
	Возврат Текст;

КонецФункции

Функция ТекстУсловияАссортимент()

	Текст =
		"		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияИзмененияФорматовМагазинов КАК ИсторияНастроек
		|		ПО Т.Склад = ИсторияНастроек.Склад
		|			И ИсторияНастроек.Период В(
		|				ВЫБРАТЬ
		|					МАКСИМУМ (История.Период) КАК Период
		|				ИЗ
		|					РегистрСведений.ИсторияИзмененияФорматовМагазинов КАК История
		|				ГДЕ
		|					История.Склад = ИсторияНастроек.Склад
		|					И История.Период < ЕСТЬNULL(ТаблицаСпособовОбеспечения.ДатаВозможнойПоставки, &НачалоТекущегоДня))
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Ассортимент КАК Ассортимент
		|		ПО Т.Склад.ТипСклада = ЗНАЧЕНИЕ(Перечисление.ТипыСкладов.РозничныйМагазин)
		|		 И ЕСТЬNULL(ИсторияНастроек.КонтролироватьАссортимент, ЛОЖЬ)
		|		 И Ассортимент.Номенклатура = Т.Номенклатура
		|		 И Ассортимент.ОбъектПланирования = %ФорматМагазина%
		|		 И Ассортимент.РазрешеныЗакупки
		|		 И Ассортимент.Период В(
		|			ВЫБРАТЬ
		|					МАКСИМУМ (Ассорти.Период) КАК Период
		|				ИЗ
		|					РегистрСведений.Ассортимент КАК Ассорти
		|				ГДЕ
		|					Ассорти.Номенклатура = Ассортимент.Номенклатура
		|					И Ассорти.ОбъектПланирования = Ассортимент.ОбъектПланирования
		|					И Ассорти.Период <= ЕСТЬNULL(ТаблицаСпособовОбеспечения.ДатаВозможнойПоставки, &НачалоТекущегоДня))
		|ГДЕ
		|	ЕСТЬNULL(Т.Склад.ТипСклада, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.ТипыСкладов.РозничныйМагазин)
		|		ИЛИ НЕ ЕСТЬNULL(ИсторияНастроек.КонтролироватьАссортимент, ЛОЖЬ)
		|		ИЛИ НЕ Ассортимент.Номенклатура ЕСТЬ NULL";

	ИспользоватьФорматыМагазинов = ПолучитьФункциональнуюОпцию("ИспользоватьФорматыМагазинов");
	ТекстФорматМагазина = ?(ИспользоватьФорматыМагазинов,
		"ЕСТЬNULL(ИсторияНастроек.ФорматМагазина, ЗНАЧЕНИЕ(Справочник.ФорматыМагазинов.ПустаяСсылка))",
		"Т.Склад");
	Текст = СтрЗаменить(Текст, "%ФорматМагазина%", ТекстФорматМагазина);

	Возврат Текст;

	

КонецФункции

Процедура ЗаписатьДокумент(ДокументОбъект, СообщенияОбОшибках)

	ОшибокНеОбнаружено = ДокументОбъект.ПроверитьЗаполнение();

	Если Не ОшибокНеОбнаружено Тогда

		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);

	Иначе //нет ошибок, проводим документ

		Попытка

			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);

		Исключение

			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
			ИмяСобытияЖурнала = НСтр("ru = 'Обеспечение потребностей.Создание заказов'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			ЗаписьЖурналаРегистрации(ИмяСобытияЖурнала, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));

		КонецПопытки;

	КонецЕсли;

	Ошибки = ПолучитьСообщенияПользователю(Истина);
	Для Каждого Ошибка Из Ошибки Цикл

		Ошибка.КлючДанных = ДокументОбъект.Ссылка;
		СообщенияОбОшибках.Добавить(Ошибка);

	КонецЦикла;

КонецПроцедуры

Функция ВыполнитьОбеспечениеЗаказа(Заказ, ИндексыСтрок, ПереченьВариантов)
	
	ЕстьОшибки = Ложь;
	МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Заказ);
	Документ = Заказ.ПолучитьОбъект();
	Попытка
		Документ.Заблокировать();
	Исключение
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Невозможно разместить заказ %1. Возможно документ редактируется другим пользователем.'");
		Сообщение.Текст = СтрШаблон(Сообщение.Текст, Документ.Ссылка);
		Сообщение.Сообщить();
		ИмяСобытияЖурнала = НСтр("ru = 'Обеспечение потребностей.Размещение в заказах'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		ЗаписьЖурналаРегистрации(ИмяСобытияЖурнала, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЕстьОшибки = Истина;
		Возврат ЕстьОшибки;
	КонецПопытки;

	ПараметрыЗаполнения = МенеджерДокумента.ПараметрыВыбораОбеспечения(Документ.Статус);
	ТаблицаОбеспечения = ОбеспечениеСервер.ТаблицаЗаполнениеОбеспеченияДокумента(
		Документ, ПереченьВариантов, ПараметрыЗаполнения, ИндексыСтрок);

	Если ТипЗнч(ТаблицаОбеспечения) = Тип("ТаблицаЗначений") Тогда
		
		МенеджерДокумента.ЗаполнитьВариантОбеспечения(Документ, Неопределено, "ИндексыСтрок", ТаблицаОбеспечения);
		Попытка
			Документ.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = НСтр("ru = 'Невозможно разместить заказ %1.'");
			Сообщение.Текст = СтрШаблон(Сообщение.Текст, Документ.Ссылка);
			Сообщение.Сообщить();
			ИмяСобытияЖурнала = НСтр("ru = 'Обеспечение потребностей.Размещение в заказах'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			ЗаписьЖурналаРегистрации(ИмяСобытияЖурнала, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЕстьОшибки = Истина;
			
		КонецПопытки;
		
	Иначе
		
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = НСтр("ru = 'Невозможно разместить заказ %1. Неверные данные об отгрузках / оформлении накладных по заказу.'");
			Сообщение.Текст = СтрШаблон(Сообщение.Текст, Документ.Ссылка);
			Сообщение.Сообщить();
			ИмяСобытияЖурнала = НСтр("ru = 'Обеспечение потребностей.Размещение в заказах'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			ЗаписьЖурналаРегистрации(ИмяСобытияЖурнала, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЕстьОшибки = Истина;
			
	КонецЕсли;

	Возврат ЕстьОшибки;
	
КонецФункции


Функция НайтиОтборРекурсивно(КоллекцияЭлементов, ЗначениеПоиска)
	
	Для Каждого ЭлементОтбора Из КоллекцияЭлементов Цикл
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			
			Если Не ЭлементОтбора.Использование Тогда
				Продолжить;
			КонецЕсли;
			
			МассивИменПолей = СтрРазделить(Строка(ЭлементОтбора.ЛевоеЗначение), ".");
			Если Новый ПолеКомпоновкиДанных(МассивИменПолей[0]) = ЗначениеПоиска Тогда
				Возврат Истина;
			КонецЕсли;
			
		Иначе
			
			Возврат НайтиОтборРекурсивно(ЭлементОтбора, ЗначениеПоиска);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
