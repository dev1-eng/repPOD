#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Команды

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов.
//
Функция ДобавитьКомандуСостояниеОбеспеченияСпискаЗаказовКлиента(КомандыОтчетов) Экспорт
	
	Если ПравоДоступа("Просмотр", Метаданные.Обработки.СостояниеОбеспечения) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Обработки.СостояниеОбеспечения.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Состояние обеспечения'");
		
		КомандаОтчет.МножественныйВыбор = Истина;
		КомандаОтчет.Важность = "Важное";
		КомандаОтчет.ФункциональныеОпции = "ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента";
		
		Возврат КомандаОтчет;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаполнитьВариантОбеспечения(Объект, Форма, ДанныеЗаполнения, ДатаОтгрузки = Неопределено) Экспорт

	СоСклада = Перечисления.ВариантыОбеспечения.СоСклада;

	ТекСтрокаТовары = Неопределено;
	Идентификатор   = Неопределено;
	Для Каждого СтрокаОбеспечения Из ДанныеЗаполнения Цикл

		// Выбор существующей, либо добавление новой строки.
		Если Идентификатор <> СтрокаОбеспечения.Идентификатор Тогда
			Идентификатор = СтрокаОбеспечения.Идентификатор;
			СтрокаТовары = Объект.ТаблицаЗаказа.НайтиПоИдентификатору(Идентификатор);
			ТекСтрокаТовары = СтрокаТовары;
		Иначе
			ТекСтрокаТовары = Объект.ТаблицаЗаказа.Вставить(Объект.ТаблицаЗаказа.Индекс(ТекСтрокаТовары) + 1);
			ЗаполнитьЗначенияСвойств(ТекСтрокаТовары, СтрокаТовары);
			ТекСтрокаТовары.КодСтроки = 0;
		КонецЕсли;

		// Заполнение полей обеспечения.
		ЗаполнитьЗначенияСвойств(ТекСтрокаТовары, СтрокаОбеспечения, "Количество, ВариантОбеспечения, Склад");
		ТекСтрокаТовары.Модифицирована = Истина;

		ДатаПоУмолчанию = Макс(НачалоДня(ТекущаяДатаСеанса()), ТекСтрокаТовары.ЖелаемаяДатаОтгрузки);
		Если ДатаОтгрузки <> Неопределено Тогда
			ДатаПоУмолчанию = Макс(ДатаПоУмолчанию, ДатаОтгрузки);
		КонецЕсли;

		Если СтрокаОбеспечения.Отгружено = 0 Тогда
			ТекСтрокаТовары.ДатаОтгрузки = Макс(СтрокаОбеспечения.ДатаОтгрузки, ДатаПоУмолчанию);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
