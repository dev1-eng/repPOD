#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

#Область РаботаСТаблицейСоответствия

Функция ЗначениеПоИдентификаторуКлиентскогоAPI(Идентификатор) Экспорт 
	
	ТаблицаСоответствий = ТаблицаСтатусовКлиентскогоAPI();
	
	Возврат СоответствиеПоЗначениюСервиса(ТаблицаСоответствий, Идентификатор);
	
КонецФункции

Функция ИдентификаторКлиентскогоAPIПоЗначению(Значение) Экспорт 
	
	ТаблицаСоответствий = ТаблицаСтатусовКлиентскогоAPI();
	
	Возврат СоответствиеПоЗначениюИБ(ТаблицаСоответствий, Значение);
	
КонецФункции

#КонецОбласти

#Область ЗаполнениеТаблицыСоответствия

Функция ТаблицаСтатусовКлиентскогоAPI()
	
	Результат = ТаблицаСоответствийЗначений();
	
	ДобавитьСтрокуСоответствия(Результат, Перечисления.НаправленияСобытийEDI.ПустаяСсылка(), "");
	ДобавитьСтрокуСоответствия(Результат, Перечисления.НаправленияСобытийEDI.Входящее,       "incomming");
	ДобавитьСтрокуСоответствия(Результат, Перечисления.НаправленияСобытийEDI.Исходящее,       "outgoing");
	
	Возврат Результат;
	
КонецФункции

Процедура ДобавитьСтрокуСоответствия(СоответствиеЗначений, ЗначениеИБ, ЗначениеСервиса)
	
	НоваяСтрока = СоответствиеЗначений.Добавить();
	
	НоваяСтрока.ЗначениеСервиса = ЗначениеСервиса;
	НоваяСтрока.ЗначениеИБ = ЗначениеИБ;
	
КонецПроцедуры

Функция ТаблицаСоответствийЗначений()
	
	Результат = Новый ТаблицаЗначений;
	
	Результат.Колонки.Добавить("ЗначениеСервиса");
	Результат.Колонки.Добавить("ЗначениеИБ");
	
	Возврат Результат ;
	
КонецФункции

Функция СоответствиеПоЗначениюСервиса(СоответствиеЗначений, ЗначениеДляПоиска)
	
	Результат = СоответствиеЗначений.Найти(ЗначениеДляПоиска, "ЗначениеСервиса");
	
	Если Результат = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат Результат.ЗначениеИБ;
	КонецЕсли;
	
КонецФункции

Функция СоответствиеПоЗначениюИБ(СоответствиеЗначений, ЗначениеДляПоиска)
	
	Результат = СоответствиеЗначений.Найти(ЗначениеДляПоиска, "ЗначениеИБ");
	
	Если Результат = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат Результат.ЗначениеСервиса;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
