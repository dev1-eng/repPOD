#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	// Ниже приведеный код, должен выполняться до проверки:
	// Если ОбменДанными.Загрузка Тогда
	//	Возврат
	// КонецЕсли;
	// т.к. существует проверка на доп. свойство ДляПроведения, и 
	// данный объект в РИБ при записи должен создавать запись р/с Задания к закрытию месяца.
	
	Если Не ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		
		БлокироватьДляИзменения = Истина;
		
		// Текущее состояние набора помещается во временную таблицу,
		// чтобы при записи получить изменение нового набора относительно текущего.
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	СуммыДокументовВВалютеРегл.Регистратор КАК Регистратор,
		|	СуммыДокументовВВалютеРегл.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	СуммыДокументовВВалютеРегл.СуммаБезНДС КАК СуммаБезНДС,
		|	СуммыДокументовВВалютеРегл.СуммаНДС КАК СуммаНДС,
		|	СуммыДокументовВВалютеРегл.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|	СуммыДокументовВВалютеРегл.СтавкаНДС КАК СтавкаНДС,
		|	НАЧАЛОПЕРИОДА(СуммыДокументовВВалютеРегл.Период, МЕСЯЦ) КАК Месяц
		|ПОМЕСТИТЬ СуммыДокументовВВалютеРеглПередЗаписью
		|ИЗ
		|	РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыДокументовВВалютеРегл
		|ГДЕ
		|	СуммыДокументовВВалютеРегл.Регистратор = &Регистратор
		|	И НЕ &ЭтоНовый
		|	И НЕ &ОбменДанными
		|");
		
		Запрос.УстановитьПараметр("Регистратор",              Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("ЭтоНовый",                 ?(ДополнительныеСвойства.Свойство("ЭтоНовый"), ДополнительныеСвойства.ЭтоНовый, Ложь));
		Запрос.УстановитьПараметр("ОбменДанными",             ОбменДанными.Загрузка);
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	КонецЕсли;
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	// Проверка:
	// Если ОбменДанными.Загрузка Тогда
	//	Возврат
	// КонецЕсли;
	// Не требуется, т.к. существует проверка на доп. свойство ДляПроведения,
	// а данный объект в РИБ при записи должен создавать запись р/с Задания к закрытию месяца.
	
	Если НЕ ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		
		Запрос = Новый Запрос;
		
		МассивТекстовЗапроса = Новый Массив;
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Таблица.Месяц КАК Месяц,
		|	Таблица.Регистратор КАК Документ,
		|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ДанныеПервичныхДокументов.Организация КАК Организация,
		|	Таблица.СтавкаНДС
		|ПОМЕСТИТЬ СуммыДокументовВВалютеРеглИзменения
		|ИЗ
		|	(ВЫБРАТЬ
		|		СуммыДокументовВВалютеРеглПередЗаписью.Регистратор КАК Регистратор,
		|		СуммыДокументовВВалютеРеглПередЗаписью.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|		СуммыДокументовВВалютеРеглПередЗаписью.СуммаБезНДС КАК СуммаБезНДС,
		|		СуммыДокументовВВалютеРеглПередЗаписью.СуммаНДС КАК СуммаНДС,
		|		СуммыДокументовВВалютеРеглПередЗаписью.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|		СуммыДокументовВВалютеРеглПередЗаписью.Месяц КАК Месяц,
		|		СуммыДокументовВВалютеРеглПередЗаписью.СтавкаНДС КАК СтавкаНДС
		|	ИЗ
		|		СуммыДокументовВВалютеРеглПередЗаписью КАК СуммыДокументовВВалютеРеглПередЗаписью
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		СуммыДокументовВВалютеРегл.Регистратор,
		|		СуммыДокументовВВалютеРегл.ИдентификаторСтроки,
		|		-СуммыДокументовВВалютеРегл.СуммаБезНДС,
		|		-СуммыДокументовВВалютеРегл.СуммаНДС,
		|		-СуммыДокументовВВалютеРегл.СуммаВзаиморасчетов,
		|		НАЧАЛОПЕРИОДА(СуммыДокументовВВалютеРегл.Период, МЕСЯЦ),
		|		СуммыДокументовВВалютеРегл.СтавкаНДС
		|	ИЗ
		|		РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыДокументовВВалютеРегл
		|	ГДЕ
		|		СуммыДокументовВВалютеРегл.Регистратор = &Регистратор) КАК Таблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
		|		ПО Таблица.Регистратор = ДанныеПервичныхДокументов.Документ
		|
		|СГРУППИРОВАТЬ ПО
		|	Таблица.ИдентификаторСтроки,
		|	Таблица.Регистратор,
		|	ДанныеПервичныхДокументов.Организация,
		|	Таблица.Месяц,
		|	Таблица.СтавкаНДС
		|
		|ИМЕЮЩИЕ
		|	СУММА(Таблица.СуммаБезНДС) <> 0 ИЛИ
		|	СУММА(Таблица.СуммаНДС) <> 0 ИЛИ
		|	СУММА(Таблица.СуммаВзаиморасчетов) <> 0";
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Месяц КАК Месяц,
		|	ЗНАЧЕНИЕ(Перечисление.ОперацииЗакрытияМесяца.ФормированиеДвиженийПоРасчетамСПартнерамиИПереоценкаРасчетов) КАК Операция,
		|	Таблица.Организация КАК Организация,
		|	&Регистратор КАК Документ
		|ПОМЕСТИТЬ СуммыДокументовВВалютеРеглЗаданияКЗакрытиюМесяца
		|ИЗ
		|	СуммыДокументовВВалютеРеглИзменения КАК Таблица";
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
		МассивТекстовЗапроса.Добавить("УНИЧТОЖИТЬ СуммыДокументовВВалютеРеглПередЗаписью");
		МассивТекстовЗапроса.Добавить("УНИЧТОЖИТЬ СуммыДокументовВВалютеРеглИзменения");
		
		Запрос.Текст = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
