#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	БизнесПроцессы.Задание.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Команда = Документы.ЗаписьКнигиПродаж.ДобавитьКомандуСоздатьНаОснованииИсправлениеПрочегоНачисленияНДС(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(Команда, "ХозяйственнаяОперация", 
					Перечисления.ХозяйственныеОперации.ПрочееНачислениеНДС, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
КонецПроцедуры

// Добавляет команду создания документа "Запись книги продаж".
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Функция ДобавитьКомандуСоздатьНаОснованииИсправлениеПрочегоНачисленияНДС(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.СчетФактураВыданный) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Обработчик = "УчетНДСРФКлиент.ИсправлениеПрочегоНачисленияНДС";
		КомандаСоздатьНаОсновании.Идентификатор = "ИсправлениеПрочегоНачисленияНДС";
		КомандаСоздатьНаОсновании.Представление = НСтр("ru = 'Исправление прочего начисления НДС'");
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

// Добавляет команду создания документа "Запись книги продаж".
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.ЗаписьКнигиПродаж) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ЗаписьКнигиПродаж.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ЗаписьКнигиПродаж);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ФормироватьОтчетностьПоНДС";
		
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
КонецПроцедуры

// Заполняет таблицу реквизитов, зависимых от хозяйственной операции
//
// Параметры:
//	ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - хозяйственная операция соглашения
//	МассивВсехРеквизитов - Массив - реквизиты, которые не зависят от хозяйственной операции
//	МассивРеквизитовОперации - Массив - реквизиты, которые зависят от хозяйственной операции.
//
Процедура ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(ХозяйственнаяОперация, МассивВсехРеквизитов, МассивРеквизитовОперации) Экспорт
	
	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("ДокументРасчетов");
	МассивВсехРеквизитов.Добавить("ИсправляемыйДокумент");
	МассивВсехРеквизитов.Добавить("Грузоотправитель");
	МассивВсехРеквизитов.Добавить("Грузополучатель");
	МассивВсехРеквизитов.Добавить("Грузополучатель");
	МассивВсехРеквизитов.Добавить("Руководитель");
	МассивВсехРеквизитов.Добавить("ГлавныйБухгалтер");
	МассивВсехРеквизитов.Добавить("ЗаписьДополнительногоЛиста");
	МассивВсехРеквизитов.Добавить("КорректируемыйПериод");
	
	МассивВсехРеквизитов.Добавить("Ценности.Номенклатура");
	МассивВсехРеквизитов.Добавить("Ценности.Количество");
	МассивВсехРеквизитов.Добавить("Ценности.Цена");
	МассивВсехРеквизитов.Добавить("Ценности.НомерГТД");
	
	МассивРеквизитовОперации = Новый Массив;
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочееНачислениеНДС Тогда
		
		МассивРеквизитовОперации.Добавить("Грузоотправитель");
		МассивРеквизитовОперации.Добавить("Грузополучатель");
		МассивРеквизитовОперации.Добавить("Грузополучатель");
		МассивРеквизитовОперации.Добавить("Руководитель");
		МассивРеквизитовОперации.Добавить("ГлавныйБухгалтер");
		
		МассивРеквизитовОперации.Добавить("ЗаписьДополнительногоЛиста");
		МассивРеквизитовОперации.Добавить("КорректируемыйПериод");
		
		МассивРеквизитовОперации.Добавить("Ценности.Номенклатура");
		МассивРеквизитовОперации.Добавить("Ценности.Количество");
		МассивРеквизитовОперации.Добавить("Ценности.Цена");
		МассивРеквизитовОперации.Добавить("Ценности.НомерГТД");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ИсправлениеПрочегоНачисленияНДС Тогда
		
		МассивРеквизитовОперации.Добавить("ИсправляемыйДокумент");
		
		МассивРеквизитовОперации.Добавить("Грузоотправитель");
		МассивРеквизитовОперации.Добавить("Грузополучатель");
		МассивРеквизитовОперации.Добавить("Грузополучатель");
		МассивРеквизитовОперации.Добавить("Руководитель");
		МассивРеквизитовОперации.Добавить("ГлавныйБухгалтер");
		
		МассивРеквизитовОперации.Добавить("Ценности.Номенклатура");
		МассивРеквизитовОперации.Добавить("Ценности.Количество");
		МассивРеквизитовОперации.Добавить("Ценности.Цена");
		МассивРеквизитовОперации.Добавить("Ценности.НомерГТД");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВосстановлениеНДС Тогда
		
		МассивРеквизитовОперации.Добавить("ДокументРасчетов");
		МассивРеквизитовОперации.Добавить("ЗаписьДополнительногоЛиста");
		МассивРеквизитовОперации.Добавить("КорректируемыйПериод");
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет табличную часть "Расхождения"
//
// Параметры:
// 	Объект - ДокументОбъект.ЗаписьКнигиПродаж - Документ для которого необходимо заполнить расхождения.
//
Процедура ЗаполнитьРасхождения(Объект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗаписьКнигиПродаж.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ДанныеПоследнегоИсправления
	|ИЗ
	|	Документ.ЗаписьКнигиПродаж КАК ЗаписьКнигиПродаж
	|ГДЕ
	|	ЗаписьКнигиПродаж.Проведен
	|	И ЗаписьКнигиПродаж.Ссылка <> &Ссылка
	|	И ЗаписьКнигиПродаж.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИсправлениеПрочегоНачисленияНДС)
	|	И ЗаписьКнигиПродаж.ИсправляемыйДокумент = &ИсправляемыйДокумент
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаписьКнигиПродаж.МоментВремени УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЦенности.Номенклатура	КАК Номенклатура,
	|	ТаблицаЦенности.ВидЦенности		КАК ВидЦенности,
	|	ТаблицаЦенности.Сумма			КАК Сумма,
	|	ТаблицаЦенности.СтавкаНДС		КАК СтавкаНДС,
	|	ТаблицаЦенности.СуммаНДС		КАК СуммаНДС,
	|	ТаблицаЦенности.НомерГТД		КАК НомерГТД,
	|	ТаблицаЦенности.КодТНВЭД		КАК КодТНВЭД,
	|	ТаблицаЦенности.Событие			КАК Событие,
	|	ТаблицаЦенности.СчетУчета		КАК СчетУчета,
	|	ТаблицаЦенности.Субконто1		КАК Субконто1,
	|	ТаблицаЦенности.Субконто2		КАК Субконто2,
	|	ТаблицаЦенности.Субконто3		КАК Субконто3
	|
	|ПОМЕСТИТЬ ИсходныеДанные
	|ИЗ
	|	Документ.ЗаписьКнигиПродаж.Ценности КАК ТаблицаЦенности
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеПоследнегоИсправления КАК ДанныеПоследнегоИсправления
	|		ПО ТаблицаЦенности.Ссылка = ДанныеПоследнегоИсправления.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаЦенности.Номенклатура	КАК Номенклатура,
	|	ТаблицаЦенности.ВидЦенности		КАК ВидЦенности,
	|	ТаблицаЦенности.Сумма			КАК Сумма,
	|	ТаблицаЦенности.СтавкаНДС		КАК СтавкаНДС,
	|	ТаблицаЦенности.СуммаНДС		КАК СуммаНДС,
	|	ТаблицаЦенности.НомерГТД		КАК НомерГТД,
	|	ТаблицаЦенности.КодТНВЭД		КАК КодТНВЭД,
	|	ТаблицаЦенности.Событие			КАК Событие,
	|	ТаблицаЦенности.СчетУчета		КАК СчетУчета,
	|	ТаблицаЦенности.Субконто1		КАК Субконто1,
	|	ТаблицаЦенности.Субконто2		КАК Субконто2,
	|	ТаблицаЦенности.Субконто3		КАК Субконто3
	|ИЗ
	|	Документ.ЗаписьКнигиПродаж.Ценности КАК ТаблицаЦенности
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоследнегоИсправления КАК ДанныеПоследнегоИсправления
	|		ПО ИСТИНА
	|ГДЕ
	|	ДанныеПоследнегоИсправления.Ссылка ЕСТЬ NULL
	|	И ТаблицаЦенности.Ссылка = &ИсправляемыйДокумент
	|;
	|
	|ВЫБРАТЬ
	|	НовыеДанные.Номенклатура		КАК Номенклатура,
	|	НовыеДанные.ВидЦенности			КАК ВидЦенности,
	|	НовыеДанные.Сумма				КАК Сумма,
	|	НовыеДанные.СтавкаНДС			КАК СтавкаНДС,
	|	НовыеДанные.СуммаНДС			КАК СуммаНДС,
	|	НовыеДанные.НомерГТД			КАК НомерГТД,
	|	НовыеДанные.КодТНВЭД			КАК КодТНВЭД,
	|	НовыеДанные.Событие				КАК Событие,
	|	НовыеДанные.СчетУчета			КАК СчетУчета,
	|	НовыеДанные.Субконто1			КАК Субконто1,
	|	НовыеДанные.Субконто2			КАК Субконто2,
	|	НовыеДанные.Субконто3			КАК Субконто3
	|ПОМЕСТИТЬ НовыеДанныеИзТаблицы
	|ИЗ
	|	&НовыеДанные КАК НовыеДанные
	|;
	|
	|ВЫБРАТЬ
	|	ИсходныеДанные.Номенклатура		КАК Номенклатура,
	|	ИсходныеДанные.ВидЦенности		КАК ВидЦенности,
	|	-ИсходныеДанные.Сумма			КАК Сумма,
	|	ИсходныеДанные.СтавкаНДС		КАК СтавкаНДС,
	|	-ИсходныеДанные.СуммаНДС		КАК СуммаНДС,
	|	ИсходныеДанные.НомерГТД			КАК НомерГТД,
	|	ИсходныеДанные.КодТНВЭД			КАК КодТНВЭД,
	|	ИсходныеДанные.Событие			КАК Событие,
	|	ИсходныеДанные.СчетУчета		КАК СчетУчета,
	|	ИсходныеДанные.Субконто1		КАК Субконто1,
	|	ИсходныеДанные.Субконто2		КАК Субконто2,
	|	ИсходныеДанные.Субконто3		КАК Субконто3
	|ПОМЕСТИТЬ ДанныеДляРасхождения
	|ИЗ
	|	ИсходныеДанные КАК ИсходныеДанные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НовыеДанныеИзТаблицы.Номенклатура		КАК Номенклатура,
	|	НовыеДанныеИзТаблицы.ВидЦенности		КАК ВидЦенности,
	|	НовыеДанныеИзТаблицы.Сумма				КАК Сумма,
	|	НовыеДанныеИзТаблицы.СтавкаНДС			КАК СтавкаНДС,
	|	НовыеДанныеИзТаблицы.СуммаНДС			КАК СуммаНДС,
	|	НовыеДанныеИзТаблицы.НомерГТД			КАК НомерГТД,
	|	НовыеДанныеИзТаблицы.КодТНВЭД			КАК КодТНВЭД,
	|	НовыеДанныеИзТаблицы.Событие			КАК Событие,
	|	НовыеДанныеИзТаблицы.СчетУчета			КАК СчетУчета,
	|	НовыеДанныеИзТаблицы.Субконто1			КАК Субконто1,
	|	НовыеДанныеИзТаблицы.Субконто2			КАК Субконто2,
	|	НовыеДанныеИзТаблицы.Субконто3			КАК Субконто3
	|ИЗ
	|	НовыеДанныеИзТаблицы КАК НовыеДанныеИзТаблицы
	|;
	|
	|ВЫБРАТЬ
	|	ДанныеДляРасхождения.Номенклатура		КАК Номенклатура,
	|	ДанныеДляРасхождения.ВидЦенности		КАК ВидЦенности,
	|	ДанныеДляРасхождения.СтавкаНДС			КАК СтавкаНДС,
	|	ДанныеДляРасхождения.СчетУчета			КАК СчетУчета,
	|	ДанныеДляРасхождения.НомерГТД			КАК НомерГТД,
	|	ДанныеДляРасхождения.КодТНВЭД			КАК КодТНВЭД,
	|	ДанныеДляРасхождения.Событие			КАК Событие,
	|	ДанныеДляРасхождения.Субконто1			КАК Субконто1,
	|	ДанныеДляРасхождения.Субконто2			КАК Субконто2,
	|	ДанныеДляРасхождения.Субконто3			КАК Субконто3,
	|	СУММА(ДанныеДляРасхождения.Сумма)		КАК Сумма,
	|	СУММА(ДанныеДляРасхождения.СуммаНДС)	КАК СуммаНДС
	|ИЗ
	|	ДанныеДляРасхождения
	|	
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДляРасхождения.Номенклатура,
	|	ДанныеДляРасхождения.ВидЦенности,
	|	ДанныеДляРасхождения.СтавкаНДС,
	|	ДанныеДляРасхождения.НомерГТД,
	|	ДанныеДляРасхождения.КодТНВЭД,
	|	ДанныеДляРасхождения.Событие,
	|	ДанныеДляРасхождения.СчетУчета,
	|	ДанныеДляРасхождения.Субконто1,
	|	ДанныеДляРасхождения.Субконто2,
	|	ДанныеДляРасхождения.Субконто3
	|
	|ИМЕЮЩИЕ
	|	СУММА(ДанныеДляРасхождения.Сумма) <> 0
	|	ИЛИ СУММА(ДанныеДляРасхождения.СуммаНДС) <> 0
	|";
	Запрос.УстановитьПараметр("НовыеДанные",          Объект.Ценности.Выгрузить());
	Запрос.УстановитьПараметр("ИсправляемыйДокумент", Объект.ИсправляемыйДокумент);
	Запрос.УстановитьПараметр("Ссылка",               Объект.Ссылка);
	
	Объект.Расхождения.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры


// Инициализирует параметры регистрации счетов-фактур (выданных)
//
// Параметры:
//  Объект		- ДокументОбъект.ЗаписьКнигиПродаж, ДанныеФормыСтруктура	- документ, для которого необходимо получить параметры.
//
// Возвращаемое значение:
//  Структура - структура параметров, см. УчетНДСУПКлиентСервер.ПараметрыРегистрацииСчетовФактурВыданных().
//
Функция ПараметрыРегистрацииСчетовФактурВыданных(Объект) Экспорт
	
	ПараметрыРегистрации = УчетНДСУПКлиентСервер.ПараметрыРегистрацииСчетовФактурВыданных();
	ПараметрыРегистрации.Ссылка = Объект.Ссылка;
	ПараметрыРегистрации.Дата = Объект.Дата;
	ПараметрыРегистрации.Организация = Объект.Организация;
	ПараметрыРегистрации.Контрагент = Объект.Контрагент;
	ПараметрыРегистрации.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПустаяСсылка();
	Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ИсправлениеПрочегоНачисленияНДС Тогда
		ПараметрыРегистрации.ИсправлениеОшибок = Истина;
	Иначе
		ПараметрыРегистрации.ПрочееНачислениеНДС = Истина;
	КонецЕсли;
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт

	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаТаблицаНДСЗаписиКнигиПродаж(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаСуммыДокументовВВалютеРегл(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаДанныеОснованийСчетовФактур(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаЗаполнениеТоваровСФВыданный(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеДокумента.Дата КАК Период,
		|	ДанныеДокумента.Организация КАК Организация,
		|	ДанныеДокумента.Контрагент КАК Контрагент,
		|	ДанныеДокумента.ДокументРасчетов КАК ДокументРасчетов,
		|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	ДанныеДокумента.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеДокумента.Валюта КАК Валюта,
		|	ДанныеДокумента.ЗаписьДополнительногоЛиста КАК ЗаписьДополнительногоЛиста,
		|	ДанныеДокумента.ФормироватьСторнирующиеЗаписиДопЛистовВручную КАК ФормироватьСторнирующиеЗаписиДопЛистовВручную,
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.ЗаписьДополнительногоЛиста
		|			ТОГДА ДанныеДокумента.КорректируемыйПериод
		|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		|	КОНЕЦ КАК КорректируемыйПериод
		|ИЗ
		|	Документ.ЗаписьКнигиПродаж КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период",							Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация",					Реквизиты.Организация);
	Запрос.УстановитьПараметр("Контрагент",						Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("СчетФактура",					?(ЗначениеЗаполнено(Реквизиты.ДокументРасчетов), Реквизиты.ДокументРасчетов, ДокументСсылка));
	Запрос.УстановитьПараметр("Валюта",							Реквизиты.Валюта);
	Запрос.УстановитьПараметр("КодВидаОперации",				Реквизиты.КодВидаОперации);
	Запрос.УстановитьПараметр("ЗаписьДополнительногоЛиста",		Реквизиты.ЗаписьДополнительногоЛиста);
	Запрос.УстановитьПараметр("КорректируемыйПериод",			Реквизиты.КорректируемыйПериод);
	Запрос.УстановитьПараметр("ВалютаРеглУчета",				Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ХозяйственнаяОперация",			Реквизиты.ХозяйственнаяОперация);
	Запрос.УстановитьПараметр("ЭтоКорректировкаНДС",			ЗначениеЗаполнено(Реквизиты.ДокументРасчетов));
	Запрос.УстановитьПараметр("ТипСчетаФактуры",				ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.СчетФактураВыданный"));
	Запрос.УстановитьПараметр("ТипыНоменклатурыТовар",			УчетНДСУПСлужебный.ТипыНоменклатурыТовар());

КонецПроцедуры

Процедура ИнициализироватьВтСуммыДокументовВалютахУчета(Запрос, ТекстыЗапроса)
	
	ТекстЗапросаДанных =
	"ВЫБРАТЬ
	|	Ценности.Ссылка КАК Ссылка,
	|	Ценности.Ссылка.Дата КАК Дата,
	|	Ценности.Ссылка.Валюта КАК ВалютаДокумента,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаВзаиморасчетов,
	|	Ценности.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Ценности.СтавкаНДС КАК СтавкаНДС,
	|	Ценности.Сумма + Ценности.СуммаНДС КАК СуммаСНДС ,
	|	Ценности.СуммаНДС КАК СуммаНДС,
	|	Ценности.Сумма КАК СуммаБезНДС,
	|	0 КАК СуммаВзаиморасчетов,
	|	0 КАК СуммаНДСВзаиморасчетов
	|ИЗ
	|	Документ.ЗаписьКнигиПродаж.Ценности КАК Ценности
	|ГДЕ
	|	Ценности.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Расхождения.Ссылка КАК Ссылка,
	|	Расхождения.Ссылка.Дата КАК Дата,
	|	Расхождения.Ссылка.Валюта КАК ВалютаДокумента,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаВзаиморасчетов,
	|	Расхождения.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Расхождения.СтавкаНДС КАК СтавкаНДС,
	|	Расхождения.Сумма + Расхождения.СуммаНДС КАК СуммаСНДС ,
	|	Расхождения.СуммаНДС КАК СуммаНДС,
	|	Расхождения.Сумма КАК СуммаБезНДС,
	|	0 КАК СуммаВзаиморасчетов,
	|	0 КАК СуммаНДСВзаиморасчетов
	|ИЗ
	|	Документ.ЗаписьКнигиПродаж.Расхождения КАК Расхождения
	|ГДЕ
	|	Расхождения.Ссылка = &Ссылка
	|";
	
	ПроведениеСерверУТ.ИнициализироватьВтСуммыДокументовВВалютахУчета(Запрос, ТекстыЗапроса, ТекстЗапросаДанных);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаНДСЗаписиКнигиПродаж(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "НДСЗаписиКнигиПродаж";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ИнициализироватьВтСуммыДокументовВалютахУчета(Запрос, ТекстыЗапроса);

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	&Контрагент КАК Покупатель,
	|	&СчетФактура КАК СчетФактура,
	|	ТаблицаЦенности.ВидЦенности КАК ВидЦенности,
	|	ТаблицаЦенности.Событие КАК Событие,
	|	&Период КАК ДатаСобытия,
	|	ТаблицаЦенности.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(Суммы.БазаНДСРегл) КАК СуммаБезНДС,
	|	СУММА(Суммы.СуммаНДСРегл) КАК НДС,
	|	&ЗаписьДополнительногоЛиста КАК ЗаписьДополнительногоЛиста,
	|	&КорректируемыйПериод КАК КорректируемыйПериод,
	|	ВЫБОР
	|		КОГДА &ЗаписьДополнительногоЛиста 
	|				И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВосстановлениеНДС)
	|			ТОГДА ТаблицаЦенности.СторнирующаяЗаписьДопЛиста
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СторнирующаяЗаписьДопЛиста,
	|	НЕОПРЕДЕЛЕНО КАК НомерДокументаОплаты,
	|	НЕОПРЕДЕЛЕНО КАК ДатаДокументаОплаты,
	|	ТаблицаЦенности.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&КодВидаОперации КАК КодВидаОперации
	|ИЗ
	|	Документ.ЗаписьКнигиПродаж.Ценности КАК ТаблицаЦенности
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСуммыДокументовВВалютахУчета КАК Суммы
	|	ПО
	|		ТаблицаЦенности.Ссылка = Суммы.Ссылка
	|		И ТаблицаЦенности.ИдентификаторСтроки = Суммы.ИдентификаторСтроки
	|ГДЕ
	|	ТаблицаЦенности.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочееНачислениеНДС),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВосстановлениеНДС))
	|	
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЦенности.СтавкаНДС,
	|	ТаблицаЦенности.ВидЦенности,
	|	ТаблицаЦенности.Событие,
	|	ТаблицаЦенности.НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА &ЗаписьДополнительногоЛиста 
	|			И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВосстановлениеНДС)
	|			ТОГДА ТаблицаЦенности.СторнирующаяЗаписьДопЛиста
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	&Контрагент КАК Покупатель,
	|	&СчетФактура КАК СчетФактура,
	|	ТаблицаЦенности.ВидЦенности КАК ВидЦенности,
	|	ТаблицаЦенности.Событие КАК Событие,
	|	&Период КАК ДатаСобытия,
	|	ТаблицаЦенности.СтавкаНДС КАК СтавкаНДС,
	|	0 КАК СуммаБезНДС,
	|	0 КАК НДС,
	|	&ЗаписьДополнительногоЛиста КАК ЗаписьДополнительногоЛиста,
	|	&КорректируемыйПериод КАК КорректируемыйПериод,
	|	ВЫБОР
	|		КОГДА &ЗаписьДополнительногоЛиста 
	|				И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВосстановлениеНДС)
	|			ТОГДА ТаблицаЦенности.СторнирующаяЗаписьДопЛиста
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СторнирующаяЗаписьДопЛиста,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Номер, ДокументыОплаты.ДокументОплаты.Номер) КАК НомерДокументаОплаты,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Дата, ДокументыОплаты.ДокументОплаты.Дата) КАК ДатаДокументаОплаты,
	|	ТаблицаЦенности.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&КодВидаОперации КАК КодВидаОперации
	|ИЗ
	|	Документ.ЗаписьКнигиПродаж.Ценности КАК ТаблицаЦенности
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ЗаписьКнигиПродаж.ДокументыОплаты КАК ДокументыОплаты
	|	ПО
	|		ТаблицаЦенности.Ссылка = ДокументыОплаты.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|	ПО
	|		ДанныеПервичныхДокументов.Организация = &Организация
	|		И ДокументыОплаты.ДокументОплаты = ДанныеПервичныхДокументов.Документ
	|ГДЕ
	|	ТаблицаЦенности.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочееНачислениеНДС),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВосстановлениеНДС))
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаСуммыДокументовВВалютеРегл(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СуммыДокументовВВалютеРегл";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ИнициализироватьВтСуммыДокументовВалютахУчета(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Суммы.Дата КАК Период,
	|	Суммы.ВалютаДокумента КАК Валюта,
	|	Суммы.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Суммы.СуммаБезНДС КАК СуммаБезНДС,
	|	Суммы.СтавкаНДС КАК СтавкаНДС,
	|	Суммы.СуммаНДС КАК СуммаНДС,
	|	Суммы.СуммаБезНДСРегл КАК СуммаБезНДСРегл,
	|	Суммы.СуммаНДСРегл КАК СуммаНДСРегл,
	|	Суммы.СуммаБезНДСУпр КАК СуммаБезНДСУпр,
	|	Суммы.СуммаНДСУпр КАК СуммаНДСУпр,
	|	Суммы.БазаНДСРегл КАК БазаНДСРегл,
	|	НЕОПРЕДЕЛЕНО КАК ТипРасчетов
	|ИЗ
	|	ВтСуммыДокументовВВалютахУчета  КАК Суммы
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДанныеОснованийСчетовФактур(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДанныеОснованийСчетовФактур";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 	
	
	ИнициализироватьВтСуммыДокументовВалютахУчета(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Ценности.Ссылка.Дата КАК Период,
	|	&ТипСчетаФактуры КАК ТипСчетаФактуры,
	|	Ценности.ВидЦенности КАК ВидЦенности,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(Ценности.Номенклатура.ТипНоменклатуры, НЕОПРЕДЕЛЕНО) В (&ТипыНоменклатурыТовар)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар) 
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга) 
	|	КОНЕЦ КАК ТипЗапасов,
	|	Ценности.СтавкаНДС КАК СтавкаНДС,
	|	Ценности.НомерГТД КАК НомерГТД,
	|	Ценности.КодТНВЭД КАК КодТНВЭД,
	|	ВЫБОР
	|		КОГДА Ценности.СуммаНДС < 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК УменьшениеСуммы,
	|
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаСчетаФактурыКомиссионера,
	|	НЕОПРЕДЕЛЕНО КАК ПокупательКомиссионногоТовара,
	|	НЕОПРЕДЕЛЕНО КАК СчетФактураВыданныйКомиссионером,
	|
	|	СУММА(Суммы.СуммаБезНДС) КАК СуммаБезНДС,
	|	СУММА(Суммы.СуммаНДС) КАК СуммаНДС,
	|	СУММА(Суммы.СуммаБезНДСРегл) КАК СуммаБезНДСРегл,
	|	СУММА(Суммы.БазаНДСРегл) КАК БазаНДСРегл,
	|	СУММА(Суммы.СуммаНДСРегл) КАК СуммаНДСРегл,
	|
	|	Ценности.Ссылка.Дата КАК Дата,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС) КАК НалогообложениеНДС,
	|	Ценности.Ссылка.Валюта КАК Валюта,
	|	Ценности.Ссылка.Организация КАК Организация,
	|	Ценности.Ссылка.Подразделение КАК Подразделение,
	|	Ценности.Ссылка.Контрагент КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК Договор,
	|	Ценности.Ссылка.Грузоотправитель КАК Грузоотправитель,
	|	Ценности.Ссылка.Грузополучатель КАК Грузополучатель,
	|	ЛОЖЬ КАК ЭтоВозврат,
	|	ЛОЖЬ КАК ИсправлениеОшибок,
	|	ЛОЖЬ КАК КорректировкаПоСогласованиюСторон,
	|	НЕОПРЕДЕЛЕНО КАК КорректируемыйДокумент
	|ИЗ
	|	Документ.ЗаписьКнигиПродаж.Ценности КАК Ценности
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСуммыДокументовВВалютахУчета КАК Суммы
	|	ПО 
	|		Ценности.Ссылка = Суммы.Ссылка
	|		И Ценности.ИдентификаторСтроки = Суммы.ИдентификаторСтроки
	|ГДЕ
	|	Ценности.Ссылка = &Ссылка
	|	И Ценности.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочееНачислениеНДС)
	|
	|СГРУППИРОВАТЬ ПО
	|	Ценности.Ссылка,
	|	Ценности.ВидЦенности,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(Ценности.Номенклатура.ТипНоменклатуры, НЕОПРЕДЕЛЕНО) В (&ТипыНоменклатурыТовар)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар) 
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга) 
	|	КОНЕЦ,
	|	Ценности.СтавкаНДС,
	|	Ценности.НомерГТД,
	|	Ценности.КодТНВЭД,
	|	ВЫБОР
	|		КОГДА Ценности.СуммаНДС < 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Расхождения.Ссылка.Дата КАК Период,
	|	&ТипСчетаФактуры КАК ТипСчетаФактуры,
	|	Расхождения.ВидЦенности КАК ВидЦенности,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Расхождения.Номенклатура.ТипНоменклатуры, НЕОПРЕДЕЛЕНО) В (&ТипыНоменклатурыТовар)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
	|	КОНЕЦ КАК ТипЗапасов,
	|	Расхождения.СтавкаНДС КАК СтавкаНДС,
	|	Расхождения.НомерГТД КАК НомерГТД,
	|	Расхождения.КодТНВЭД КАК КодТНВЭД,
	|	ВЫБОР
	|		КОГДА Расхождения.СуммаНДС < 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК УменьшениеСуммы,
	|
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаСчетаФактурыКомиссионера,
	|	НЕОПРЕДЕЛЕНО КАК ПокупательКомиссионногоТовара,
	|	НЕОПРЕДЕЛЕНО КАК СчетФактураВыданныйКомиссионером,
	|
	|	СУММА(Суммы.СуммаБезНДС) КАК СуммаБезНДС,
	|	СУММА(Суммы.СуммаНДС) КАК СуммаНДС,
	|	СУММА(Суммы.СуммаБезНДСРегл) КАК СуммаБезНДСРегл,
	|	СУММА(Суммы.БазаНДСРегл) КАК БазаНДСРегл,
	|	СУММА(Суммы.СуммаНДСРегл) КАК СуммаНДСРегл,
	|	
	|	Расхождения.Ссылка.Дата КАК Дата,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС) КАК НалогообложениеНДС,
	|	Расхождения.Ссылка.Валюта КАК Валюта,
	|	Расхождения.Ссылка.Организация КАК Организация,
	|	Расхождения.Ссылка.Подразделение КАК Подразделение,
	|	Расхождения.Ссылка.Контрагент КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК Договор,
	|	Расхождения.Ссылка.Грузоотправитель КАК Грузоотправитель,
	|	Расхождения.Ссылка.Грузополучатель КАК Грузополучатель,
	|	ЛОЖЬ КАК ЭтоВозврат,
	|	ИСТИНА КАК ИсправлениеОшибок,
	|	ЛОЖЬ КАК КорректировкаПоСогласованиюСторон,
	|	Расхождения.Ссылка.ИсправляемыйДокумент КАК КорректируемыйДокумент
	|ИЗ
	|	Документ.ЗаписьКнигиПродаж.Расхождения КАК Расхождения
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСуммыДокументовВВалютахУчета КАК Суммы
	|	ПО
	|		Расхождения.Ссылка = Суммы.Ссылка
	|		И Расхождения.ИдентификаторСтроки = Суммы.ИдентификаторСтроки
	|ГДЕ
	|	Расхождения.Ссылка = &Ссылка
	|	И Расхождения.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИсправлениеПрочегоНачисленияНДС)
	|
	|СГРУППИРОВАТЬ ПО
	|	Расхождения.Ссылка,
	|	&ТипСчетаФактуры,
	|	Расхождения.ВидЦенности,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Расхождения.Номенклатура.ТипНоменклатуры, НЕОПРЕДЕЛЕНО) В (&ТипыНоменклатурыТовар)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
	|	КОНЕЦ,
	|	Расхождения.СтавкаНДС,
	|	Расхождения.НомерГТД,
	|	Расхождения.КодТНВЭД,
	|	ВЫБОР
	|		КОГДА Расхождения.СуммаНДС < 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаЗаполнениеТоваровСФВыданный(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ЗаполнениеТоваровСФВыданный";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 	
	
	ИнициализироватьВтСуммыДокументовВалютахУчета(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.Ссылка КАК ИсходныйСчетФактура,
	|	Ценности.Номенклатура КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК НоменклатураНабора,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаНабора,
	|	"""" КАК Содержание,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(Ценности.Номенклатура.ТипНоменклатуры, НЕОПРЕДЕЛЕНО) В (&ТипыНоменклатурыТовар)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар) 
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга) 
	|	КОНЕЦ КАК ТипЗапасов,
	|	Ценности.СтавкаНДС КАК СтавкаНДС,
	|	Ценности.НомерГТД КАК НомерГТД,
	|	Ценности.КодТНВЭД КАК КодТНВЭД,
	|	Ценности.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|
	|	СУММА(Ценности.Количество) КАК Количество,
	|	СУММА(Суммы.СуммаБезНДС + Суммы.СуммаНДС) КАК Сумма,
	|	СУММА(Суммы.СуммаНДС) КАК СуммаНДС,
	|	СУММА(Суммы.БазаНДСРегл + Суммы.СуммаНДСРегл) КАК СуммаРегл,
	|	СУММА(Суммы.СуммаНДСРегл) КАК СуммаНДСРегл,
	|
	|	Ценности.Ссылка.Валюта КАК Валюта,
	|	Ценности.Ссылка КАК ДокументРеализации
	|ИЗ
	|	Документ.ЗаписьКнигиПродаж.Ценности КАК Ценности
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСуммыДокументовВВалютахУчета КАК Суммы
	|	ПО 
	|		Ценности.Ссылка = Суммы.Ссылка
	|		И Ценности.ИдентификаторСтроки = Суммы.ИдентификаторСтроки
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|	ПО
	|		Ценности.Ссылка = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|ГДЕ
	|	Ценности.Ссылка = &Ссылка
	|	И Ценности.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочееНачислениеНДС)
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетФактураВыданныйДокументыОснования.Ссылка,
	|	Ценности.Номенклатура,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(Ценности.Номенклатура.ТипНоменклатуры, НЕОПРЕДЕЛЕНО) В (&ТипыНоменклатурыТовар)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар) 
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга) 
	|	КОНЕЦ,
	|	Ценности.СтавкаНДС,
	|	Ценности.НомерГТД,
	|	Ценности.КодТНВЭД,
	|	Ценности.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.Ссылка КАК ИсходныйСчетФактура,
	|	Расхождения.Номенклатура КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК НоменклатураНабора,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаНабора,
	|	"""" КАК Содержание,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Расхождения.Номенклатура.ТипНоменклатуры, НЕОПРЕДЕЛЕНО) В (&ТипыНоменклатурыТовар)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
	|	КОНЕЦ КАК ТипЗапасов,
	|	Расхождения.СтавкаНДС КАК СтавкаНДС,
	|	Расхождения.НомерГТД КАК НомерГТД,
	|	Расхождения.КодТНВЭД КАК КодТНВЭД,
	|	Расхождения.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|
	|	0 КАК Количество,
	|	СУММА(Суммы.СуммаБезНДС + Суммы.СуммаНДС) КАК Сумма,
	|	СУММА(Суммы.СуммаНДС) КАК СуммаНДС,
	|	СУММА(Суммы.БазаНДСРегл + Суммы.СуммаНДСРегл) КАК СуммаРегл,
	|	СУММА(Суммы.СуммаНДСРегл) КАК СуммаНДСРегл,
	|	
	|	Расхождения.Ссылка.Валюта КАК Валюта,
	|	Расхождения.Ссылка.ИсправляемыйДокумент КАК ДокументРеализации
	|ИЗ
	|	Документ.ЗаписьКнигиПродаж.Расхождения КАК Расхождения
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСуммыДокументовВВалютахУчета КАК Суммы
	|	ПО
	|		Расхождения.Ссылка = Суммы.Ссылка
	|		И Расхождения.ИдентификаторСтроки = Суммы.ИдентификаторСтроки
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|	ПО
	|		Расхождения.Ссылка.ИсправляемыйДокумент = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|ГДЕ
	|	Расхождения.Ссылка = &Ссылка
	|	И Расхождения.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИсправлениеПрочегоНачисленияНДС)
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетФактураВыданныйДокументыОснования.Ссылка,
	|	Расхождения.Номенклатура,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Расхождения.Номенклатура.ТипНоменклатуры, НЕОПРЕДЕЛЕНО) В (&ТипыНоменклатурыТовар)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
	|	КОНЕЦ,
	|	Расхождения.СтавкаНДС,
	|	Расхождения.НомерГТД,
	|	Расхождения.КодТНВЭД,
	|	Расхождения.Ссылка
	|";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	// Счет-фактура
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
	КомандаПечати.Идентификатор = "СчетФактура";
	КомандаПечати.Представление = НСтр("ru = 'Счет-фактура'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнительныеПараметры.Вставить("ПечатьВВалюте", Ложь);

КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
КонецПроцедуры

// Формирует временную таблицу, содержащую табличную часть по таблице данных документов.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц, содержащий таблицу ТаблицаДанныхДокументов с полями:
//		Ссылка,
//		Валюта.
//
//	ПараметрыЗаполнения - Структура - структура, возвращаемая функцией ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров.
//
Процедура ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения = Неопределено) Экспорт
	
	Если ПараметрыЗаполнения = Неопределено Тогда
		ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПересчитыватьВВалютуРегл", ПараметрыЗаполнения.ПересчитыватьВВалютуРегл);
	Запрос.УстановитьПараметр("ВключаяНомераГТД",         ПараметрыЗаполнения.ВключаяНомераГТД);
	Запрос.УстановитьПараметр("ПустаяХарактеристика",     Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяУпаковка",           Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяГТД",                Справочники.НомераГТД.ПустаяСсылка());
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                                 КАК Ссылка,
	|	ТаблицаДокумента.НомерСтроки                            КАК НомерСтроки,
	|	ТаблицаДокумента.Номенклатура                           КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА
	|			ТаблицаДокумента.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|		ТОГДА
	|			ТаблицаДокумента.ВидЦенности
	|		ИНАЧЕ
	|			""""
	|	КОНЕЦ КАК Содержание,
	|	&ПустаяХарактеристика                                   КАК Характеристика,
	|	ВЫБОР КОГДА &ВключаяНомераГТД ТОГДА
	|		ТаблицаДокумента.НомерГТД
	|	ИНАЧЕ
	|		&ПустаяГТД
	|	КОНЕЦ КАК НомерГТД,
	|	ТаблицаДокумента.КодТНВЭД                               КАК КодТНВЭД,
	|	&ПустаяУпаковка                                         КАК Упаковка,
	|	ТаблицаДокумента.Количество                             КАК Количество,
	|	ТаблицаДокумента.Количество                             КАК КоличествоУпаковок,
	|	ТаблицаДокумента.Цена                                   КАК Цена,
	|	
	|	ЕСТЬNULL(
	|		СуммыДокументовВВалютеРегл.СуммаБезНДСРегл,
	|		ТаблицаДокумента.Сумма
	|	) КАК СуммаБезНДС,
	|	
	|	ТаблицаДокумента.СтавкаНДС                              КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(
	|		СуммыДокументовВВалютеРегл.СуммаНДСРегл,
	|		ТаблицаДокумента.СуммаНДС
	|	) КАК СуммаНДС,
	|	
	|	ВЫБОР КОГДА ТаблицаДокумента.Номенклатура.ТипНоменклатуры В
	|				(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                                   КАК ЭтоТовар,
	|	ЛОЖЬ                                                    КАК ВернутьМногооборотнуюТару
	|
	|ПОМЕСТИТЬ ЗаписьКнигиПродажТаблицаТоваров
	|ИЗ
	|	Документ.ЗаписьКнигиПродаж.Ценности КАК ТаблицаДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокументов КАК ДанныеДокументов
	|	ПО
	|		ТаблицаДокумента.Ссылка = ДанныеДокументов.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыДокументовВВалютеРегл
	|	ПО
	|		ТаблицаДокумента.Ссылка = СуммыДокументовВВалютеРегл.Регистратор
	|		И ТаблицаДокумента.ИдентификаторСтроки = СуммыДокументовВВалютеРегл.ИдентификаторСтроки
	|		И СуммыДокументовВВалютеРегл.Активность
	|		И &ПересчитыватьВВалютуРегл
	|";
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Формирует текст запроса для получения данных основания при печати Счет-фактуры.
//
Функция ТекстЗапросаДанныхОснованияДляПечатнойФормыСчетФактура() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка                                   КАК Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
	|	ДанныеДокументов.Валюта                                   КАК Валюта,
	|	ДанныеДокументов.Организация                              КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                              КАК НалогообложениеНДС,
	|	НЕОПРЕДЕЛЕНО                                              КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)                  КАК Склад,
	|	ДанныеДокументов.Грузоотправитель                         КАК Грузоотправитель,
	|	ДанныеДокументов.Грузополучатель                          КАК Грузополучатель,
	|	ЛОЖЬ                                                      КАК РасчетыЧерезОтдельногоКонтрагента,
	|	НЕОПРЕДЕЛЕНО                                              КАК Номенклатура,
	|	""""                                                      КАК Содержание,
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)             КАК Комиссионер,
	|	НЕОПРЕДЕЛЕНО                                              КАК Основание,
	|	НЕОПРЕДЕЛЕНО                                              КАК ОснованиеДата,
	|	НЕОПРЕДЕЛЕНО                                              КАК ОснованиеНомер,
	|	НЕОПРЕДЕЛЕНО                                              КАК БанковскийСчетОрганизации,
	|	НЕОПРЕДЕЛЕНО                                              КАК БанковскийСчетКонтрагента,
	|	НЕОПРЕДЕЛЕНО                                              КАК БанковскийСчетГрузоотправителя,
	|	НЕОПРЕДЕЛЕНО                                              КАК БанковскийСчетГрузополучателя,
	|	НЕОПРЕДЕЛЕНО                                              КАК ДоверенностьНомер,
	|	НЕОПРЕДЕЛЕНО                                              КАК ДоверенностьДата,
	|	НЕОПРЕДЕЛЕНО                                              КАК ДоверенностьВыдана,
	|	НЕОПРЕДЕЛЕНО                                              КАК ДоверенностьЛицо,
	|	НЕОПРЕДЕЛЕНО                                              КАК Кладовщик,
	|	НЕОПРЕДЕЛЕНО                                              КАК ДолжностьКладовщика
	|
	|//ОператорПОМЕСТИТЬ
	|ИЗ
	|	Документ.ЗаписьКнигиПродаж КАК ДанныеДокументов
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&ДокументОснование_ЗаписьКнигиПродаж)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли