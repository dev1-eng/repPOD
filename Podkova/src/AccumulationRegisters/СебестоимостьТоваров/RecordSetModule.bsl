#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.ДвиженияЗаписываютсяРасчетомПартийИСебестоимости(ЭтотОбъект) Тогда
		
		Если ДополнительныеСвойства.Свойство("ДополнитьДвижениямиИзИБЗаПериод") Тогда
			
			Если НЕ РасчетСебестоимостиПрикладныеАлгоритмы.ФормироватьДвиженияРегистровУчетаСебестоимости(ЭтотОбъект) Тогда
				Возврат;
			КонецЕсли;
			
			ДополнительныеСвойства.Вставить("ТочноНужноСохранитьДвижения");
			
			// Сохраним неизмененные первичные движения.
			СохранитьНеИзмененныеПервичныеДвижения();
			
			// Сохраним расчетные движения за периоды, в которых есть первичные движения.
			РасчетСебестоимостиПрикладныеАлгоритмы.СохранитьДвиженияСформированныеРасчетомПартийИСебестоимости(ЭтотОбъект, Замещение);
			
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если НЕ РасчетСебестоимостиПрикладныеАлгоритмы.ФормироватьДвиженияРегистровУчетаСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	// Сохраним неизмененные первичные движения.
	СохранитьНеИзмененныеПервичныеДвижения();
	
	// Сохраним расчетные движения за периоды, в которых есть первичные движения.
	РасчетСебестоимостиПрикладныеАлгоритмы.СохранитьДвиженияСформированныеРасчетомПартийИСебестоимости(ЭтотОбъект, Замещение);
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено
	 ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	*
	|ПОМЕСТИТЬ СебестоимостьТоваровПередЗаписью
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Себестоимость
	|ГДЕ
	|	Себестоимость.Регистратор = &Регистратор";
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ РасчетСебестоимостиПрикладныеАлгоритмы.ФормироватьДвиженияРегистровУчетаСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
	 ИЛИ ПланыОбмена.ГлавныйУзел() <> Неопределено
	 ИЛИ РасчетСебестоимостиПрикладныеАлгоритмы.ДвиженияЗаписываютсяРасчетомПартийИСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ) КАК Месяц,
	|	Таблица.Организация                  КАК Организация,
	|	Таблица.КорОрганизация               КАК КорОрганизация,
	|	Таблица.Регистратор                  КАК Документ,
	|	ЛОЖЬ                                 КАК ИзмененыДанныеДляПартионногоУчетаВерсии21
	|ПОМЕСТИТЬ СебестоимостьТоваровЗаданияКРасчетуСебестоимостиПредварительная
	|ИЗ
	|	(ВЫБРАТЬ
	|		Себестоимость.Период                        КАК Период,
	|		Себестоимость.Регистратор                   КАК Регистратор,
	|		Себестоимость.АналитикаУчетаНоменклатуры    КАК АналитикаУчетаНоменклатуры,
	|		Себестоимость.РазделУчета                   КАК РазделУчета,
	|		Себестоимость.ВидЗапасов                    КАК ВидЗапасов,
	|		Себестоимость.Организация                   КАК Организация,
	|		Себестоимость.Партия                   		КАК Партия,
	|		Себестоимость.АналитикаУчетаПартий          КАК АналитикаУчетаПартий,
	|		Себестоимость.АналитикаФинансовогоУчета     КАК АналитикаФинансовогоУчета,
	|		Себестоимость.ВидДеятельностиНДС            КАК ВидДеятельностиНДС,
	|
	|		Себестоимость.Количество             		КАК Количество,
	|		Себестоимость.Стоимость              		КАК Стоимость,
	|		Себестоимость.СтоимостьБезНДС        		КАК СтоимостьБезНДС,
	|		Себестоимость.ДопРасходы       				КАК ДопРасходы,
	|		Себестоимость.ДопРасходыБезНДС 				КАК ДопРасходыБезНДС,
	|		Себестоимость.СтоимостьРегл          		КАК СтоимостьРегл,
	|		Себестоимость.ПостояннаяРазница      		КАК ПостояннаяРазница,
	|		Себестоимость.ВременнаяРазница       		КАК ВременнаяРазница,
	|		Себестоимость.СтоимостьЗабалансовая       	КАК СтоимостьЗабалансовая,
	|		Себестоимость.Трудозатраты       			КАК Трудозатраты,
	|		Себестоимость.ПостатейныеПостоянныеСНДС     КАК ПостатейныеПостоянныеСНДС,
	|		Себестоимость.ПостатейныеПеременныеСНДС		КАК ПостатейныеПеременныеСНДС,
	|		Себестоимость.ПостатейныеПостоянныеБезНДС   КАК ПостатейныеПостоянныеБезНДС,
	|		Себестоимость.ПостатейныеПеременныеБезНДС   КАК ПостатейныеПеременныеБезНДС,
	|		Себестоимость.СтоимостьЗабалансоваяРегл     КАК СтоимостьЗабалансоваяРегл,
	|		Себестоимость.ДопРасходыРегл       			КАК ДопРасходыРегл,
	|		Себестоимость.ТрудозатратыРегл       		КАК ТрудозатратыРегл,
	|		Себестоимость.ПостатейныеПостоянныеРегл     КАК ПостатейныеПостоянныеРегл,
	|		Себестоимость.ПостатейныеПеременныеРегл     КАК ПостатейныеПеременныеРегл,
	|		Себестоимость.СтоимостьУпр             		КАК СтоимостьУпр,
	|		Себестоимость.ДопРасходыУпр    				КАК ДопРасходыУпр,
	|		Себестоимость.ТрудозатратыУпр     			КАК ТрудозатратыУпр,
	|		Себестоимость.ПостатейныеПостоянныеУпр		КАК ПостатейныеПостоянныеУпр,
	|		Себестоимость.ПостатейныеПеременныеУпр		КАК ПостатейныеПеременныеУпр,
	|
	|		ВЫБОР КОГДА Себестоимость.Количество > 0
	|			ТОГДА 1
	|			ИНАЧЕ -1
	|		КОНЕЦ КАК Знак,
	|
	|		Себестоимость.ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
	|		Себестоимость.КорАналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
	|		Себестоимость.КорРазделУчета                КАК КорРазделУчета,
	|		Себестоимость.КорВидЗапасов                 КАК КорВидЗапасов,
	|		Себестоимость.КорОрганизация                КАК КорОрганизация,
	|		Себестоимость.КорСтоимость                  КАК КорСтоимость,
	|		Себестоимость.АналитикаУчетаПоПартнерам     КАК АналитикаУчетаПоПартнерам,
	|		Себестоимость.ЗаказКлиента                  КАК ЗаказКлиента,
	|		Себестоимость.Подразделение                 КАК Подразделение,
	|		Себестоимость.АналитикаРасходов             КАК АналитикаРасходов,
	|		Себестоимость.СтатьяРасходовСписания        КАК СтатьяРасходовСписания,
	|		Себестоимость.СтатьяДоходов                 КАК СтатьяДоходов,
	|		Себестоимость.АналитикаДоходов              КАК АналитикаДоходов,
	|		Себестоимость.ПериодПродажи                 КАК ПериодПродажи,
	|		Себестоимость.СтатьяАктивовПассивов         КАК СтатьяАктивовПассивов,
	|		Себестоимость.АналитикаАктивовПассивов      КАК АналитикаАктивовПассивов,
	|		Себестоимость.ДокументДвижения              КАК ДокументДвижения,
	|		Себестоимость.ИдентификаторСтроки           КАК ИдентификаторСтроки,
	|		Себестоимость.ГруппаПродукции           	КАК ГруппаПродукции,
	|		Себестоимость.КорПартия           			КАК КорПартия,
	|		Себестоимость.КорАналитикаУчетаПартий       КАК КорАналитикаУчетаПартий,
	|		Себестоимость.КорАналитикаФинансовогоУчета  КАК КорАналитикаФинансовогоУчета,
	|		Себестоимость.КорВидДеятельностиНДС         КАК КорВидДеятельностиНДС,
	|		Себестоимость.НДСРегл           			КАК НДСРегл,
	|		Себестоимость.НДСУпр           				КАК НДСУпр,
	|		Себестоимость.СтатьяКалькуляции           	КАК СтатьяКалькуляции,
	|		Себестоимость.ТипЗаписи           			КАК ТипЗаписи,
	|		Себестоимость.ДокументИсточник           	КАК ДокументИсточник,
	|		Себестоимость.КорНаправлениеДеятельности    КАК КорНаправлениеДеятельности
	|	ИЗ
	|		СебестоимостьТоваровПередЗаписью КАК Себестоимость
	|		
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Себестоимость.Период                        КАК Период,
	|		Себестоимость.Регистратор                   КАК Регистратор,
	|		Себестоимость.АналитикаУчетаНоменклатуры    КАК АналитикаУчетаНоменклатуры,
	|		Себестоимость.РазделУчета                   КАК РазделУчета,
	|		Себестоимость.ВидЗапасов                    КАК ВидЗапасов,
	|		Себестоимость.Организация                   КАК Организация,
	|		Себестоимость.Партия                   		КАК Партия,
	|		Себестоимость.АналитикаУчетаПартий          КАК АналитикаУчетаПартий,
	|		Себестоимость.АналитикаФинансовогоУчета     КАК АналитикаФинансовогоУчета,
	|		Себестоимость.ВидДеятельностиНДС            КАК ВидДеятельностиНДС,
	|
	|		-Себестоимость.Количество             		КАК Количество,
	|		-Себестоимость.Стоимость              		КАК Стоимость,
	|		-Себестоимость.СтоимостьБезНДС        		КАК СтоимостьБезНДС,
	|		-Себестоимость.ДопРасходы       			КАК ДопРасходы,
	|		-Себестоимость.ДопРасходыБезНДС 			КАК ДопРасходыБезНДС,
	|		-Себестоимость.СтоимостьРегл          		КАК СтоимостьРегл,
	|		-Себестоимость.ПостояннаяРазница      		КАК ПостояннаяРазница,
	|		-Себестоимость.ВременнаяРазница       		КАК ВременнаяРазница,
	|		-Себестоимость.СтоимостьЗабалансовая       	КАК СтоимостьЗабалансовая,
	|		-Себестоимость.Трудозатраты       			КАК Трудозатраты,
	|		-Себестоимость.ПостатейныеПостоянныеСНДС    КАК ПостатейныеПостоянныеСНДС,
	|		-Себестоимость.ПостатейныеПеременныеСНДС    КАК ПостатейныеПеременныеСНДС,
	|		-Себестоимость.ПостатейныеПостоянныеБезНДС  КАК ПостатейныеПостоянныеБезНДС,
	|		-Себестоимость.ПостатейныеПеременныеБезНДС  КАК ПостатейныеПеременныеБезНДС,
	|		-Себестоимость.СтоимостьЗабалансоваяРегл    КАК СтоимостьЗабалансоваяРегл,
	|		-Себестоимость.ДопРасходыРегл       		КАК ДопРасходыРегл,
	|		-Себестоимость.ТрудозатратыРегл       		КАК ТрудозатратыРегл,
	|		-Себестоимость.ПостатейныеПостоянныеРегл    КАК ПостатейныеПостоянныеРегл,
	|		-Себестоимость.ПостатейныеПеременныеРегл    КАК ПостатейныеПеременныеРегл,
	|		-Себестоимость.СтоимостьУпр            		КАК СтоимостьУпр,
	|		-Себестоимость.ДопРасходыУпр       			КАК ДопРасходыУпр,
	|		-Себестоимость.ТрудозатратыУпр     			КАК ТрудозатратыУпр,
	|		-Себестоимость.ПостатейныеПостоянныеУпр		КАК ПостатейныеПостоянныеУпр,
	|		-Себестоимость.ПостатейныеПеременныеУпр		КАК ПостатейныеПеременныеУпр,
	|
	|		ВЫБОР КОГДА Себестоимость.Количество > 0
	|			ТОГДА 1
	|			ИНАЧЕ -1
	|		КОНЕЦ КАК Знак,
	|
	|		Себестоимость.ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
	|		Себестоимость.КорАналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
	|		Себестоимость.КорРазделУчета                КАК КорРазделУчета,
	|		Себестоимость.КорВидЗапасов                 КАК КорВидЗапасов,
	|		Себестоимость.КорОрганизация                КАК КорОрганизация,
	|		Себестоимость.КорСтоимость                  КАК КорСтоимость,
	|		Себестоимость.АналитикаУчетаПоПартнерам     КАК АналитикаУчетаПоПартнерам,
	|		Себестоимость.ЗаказКлиента                  КАК ЗаказКлиента,
	|		Себестоимость.Подразделение                 КАК Подразделение,
	|		Себестоимость.АналитикаРасходов             КАК АналитикаРасходов,
	|		Себестоимость.СтатьяРасходовСписания        КАК СтатьяРасходовСписания,
	|		Себестоимость.СтатьяДоходов                 КАК СтатьяДоходов,
	|		Себестоимость.АналитикаДоходов              КАК АналитикаДоходов,
	|		Себестоимость.ПериодПродажи                 КАК ПериодПродажи,
	|		Себестоимость.СтатьяАктивовПассивов         КАК СтатьяАктивовПассивов,
	|		Себестоимость.АналитикаАктивовПассивов      КАК АналитикаАктивовПассивов,
	|		Себестоимость.ДокументДвижения              КАК ДокументДвижения,
	|		Себестоимость.ИдентификаторСтроки           КАК ИдентификаторСтроки,
	|		Себестоимость.ГруппаПродукции           	КАК ГруппаПродукции,
	|		Себестоимость.КорПартия           			КАК КорПартия,
	|		Себестоимость.КорАналитикаУчетаПартий       КАК КорАналитикаУчетаПартий,
	|		Себестоимость.КорАналитикаФинансовогоУчета  КАК КорАналитикаФинансовогоУчета,
	|		Себестоимость.КорВидДеятельностиНДС         КАК КорВидДеятельностиНДС,
	|		Себестоимость.НДСРегл           			КАК НДСРегл,
	|		Себестоимость.НДСУпр           				КАК НДСУпр,
	|		Себестоимость.СтатьяКалькуляции           	КАК СтатьяКалькуляции,
	|		Себестоимость.ТипЗаписи           			КАК ТипЗаписи,
	|		Себестоимость.ДокументИсточник           	КАК ДокументИсточник,
	|		Себестоимость.КорНаправлениеДеятельности    КАК КорНаправлениеДеятельности
	|	ИЗ
	|		РегистрНакопления.СебестоимостьТоваров КАК Себестоимость
	|	ГДЕ
	|		Себестоимость.Регистратор = &Регистратор
	|	) КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ),
	|	Таблица.Период,
	|	Таблица.Регистратор,
	|	Таблица.АналитикаУчетаНоменклатуры,
	|	Таблица.РазделУчета,
	|	Таблица.ВидЗапасов,
	|	Таблица.Организация,
	|	Таблица.Партия,
	|	Таблица.АналитикаУчетаПартий,
	|	Таблица.АналитикаФинансовогоУчета,
	|	Таблица.ВидДеятельностиНДС,
	|	Таблица.ХозяйственнаяОперация,
	|	Таблица.КорАналитикаУчетаНоменклатуры,
	|	Таблица.КорРазделУчета,
	|	Таблица.КорВидЗапасов,
	|	Таблица.КорОрганизация,
	|	Таблица.КорСтоимость,
	|	Таблица.АналитикаУчетаПоПартнерам,
	|	Таблица.ЗаказКлиента,
	|	Таблица.Подразделение,
	|	Таблица.АналитикаРасходов,
	|	Таблица.СтатьяРасходовСписания,
	|	Таблица.СтатьяДоходов,
	|	Таблица.АналитикаДоходов,
	|	Таблица.ПериодПродажи,
	|	Таблица.СтатьяАктивовПассивов,
	|	Таблица.АналитикаАктивовПассивов,
	|	Таблица.ДокументДвижения,
	|	Таблица.ИдентификаторСтроки,
	|	Таблица.ГруппаПродукции,
	|	Таблица.КорПартия,
	|	Таблица.КорАналитикаУчетаПартий,
	|	Таблица.КорАналитикаФинансовогоУчета,
	|	Таблица.КорВидДеятельностиНДС,
	|	Таблица.НДСРегл,
	|	Таблица.НДСУпр,
	|	Таблица.СтатьяКалькуляции,
	|	Таблица.ТипЗаписи,
	|	Таблица.ДокументИсточник,
	|	Таблица.КорНаправлениеДеятельности,
	|	Таблица.Знак
	|
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.Количество) <> 0
	|	ИЛИ СУММА(Таблица.Стоимость) <> 0
	|	ИЛИ СУММА(Таблица.СтоимостьБезНДС) <> 0
	|	ИЛИ СУММА(Таблица.ДопРасходы) <> 0
	|	ИЛИ СУММА(Таблица.ДопРасходыБезНДС) <> 0
	|	ИЛИ СУММА(Таблица.СтоимостьРегл) <> 0
	|	ИЛИ СУММА(Таблица.ПостояннаяРазница) <> 0
	|	ИЛИ СУММА(Таблица.ВременнаяРазница) <> 0
	|	ИЛИ СУММА(Таблица.СтоимостьЗабалансовая) <> 0
	|	ИЛИ СУММА(Таблица.Трудозатраты) <> 0
	|	ИЛИ СУММА(Таблица.ПостатейныеПостоянныеСНДС) <> 0
	|	ИЛИ СУММА(Таблица.ПостатейныеПеременныеСНДС) <> 0
	|	ИЛИ СУММА(Таблица.ПостатейныеПостоянныеБезНДС) <> 0
	|	ИЛИ СУММА(Таблица.ПостатейныеПеременныеБезНДС) <> 0
	|	ИЛИ СУММА(Таблица.СтоимостьЗабалансоваяРегл) <> 0
	|	ИЛИ СУММА(Таблица.ДопРасходыРегл) <> 0
	|	ИЛИ СУММА(Таблица.ТрудозатратыРегл) <> 0
	|	ИЛИ СУММА(Таблица.ПостатейныеПостоянныеРегл) <> 0
	|	ИЛИ СУММА(Таблица.ПостатейныеПеременныеРегл) <> 0
	|	ИЛИ СУММА(Таблица.СтоимостьУпр) <> 0
	|	ИЛИ СУММА(Таблица.ДопРасходыУпр) <> 0
	|	ИЛИ СУММА(Таблица.ТрудозатратыУпр) <> 0
	|	ИЛИ СУММА(Таблица.ПостатейныеПостоянныеУпр) <> 0
	|	ИЛИ СУММА(Таблица.ПостатейныеПеременныеУпр) <> 0
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Месяц,
	|	Т.Организация КАК Организация,
	|	Т.Документ,
	|	Т.ИзмененыДанныеДляПартионногоУчетаВерсии21
	|ПОМЕСТИТЬ СебестоимостьТоваровЗаданияКРасчетуСебестоимости
	|ИЗ
	|	СебестоимостьТоваровЗаданияКРасчетуСебестоимостиПредварительная КАК Т
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Месяц,
	|	Т.КорОрганизация,
	|	Т.Документ,
	|	Т.ИзмененыДанныеДляПартионногоУчетаВерсии21
	|ИЗ
	|	СебестоимостьТоваровЗаданияКРасчетуСебестоимостиПредварительная КАК Т
	|ГДЕ
	|	Т.КорОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СебестоимостьТоваровЗаданияКРасчетуСебестоимостиПредварительная
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СебестоимостьТоваровПередЗаписью
	|");
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СохранитьНеИзмененныеПервичныеДвижения()
	
	Если РасчетСебестоимостиПовтИсп.ЗначенияТехнологическихПараметров().НеСохранятьРасчетныеДвижения
	 ИЛИ ДополнительныеСвойства.Свойство("НеСохранятьРасчетныеДвижения") Тогда
		Возврат;
	КонецЕсли;
	
	Регистратор     = Отбор.Регистратор.Значение;
	МетаРегистратор = Регистратор.Метаданные();
	
	Если ДополнительныеСвойства.Свойство("ДатаРегистратора") Тогда
		ДатаРегистратора = ДополнительныеСвойства.ДатаРегистратора;
	Иначе
		ДатаРегистратора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Регистратор, "Дата");
	КонецЕсли;
	
	Если НЕ РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии22(НачалоМесяца(ДатаРегистратора)) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ТочноНужноСохранитьДвижения") Тогда
		
		Если НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
		 ИЛИ НЕ ДополнительныеСвойства.Свойство("РежимЗаписи")
		 ИЛИ ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	#Область ДополнитьДвижениямиИзИБЗаПериод
	
	// Если при проведении документа известно, что данные какого-то периода точно не изменились,
	// то надо просто добавить их в набор записей из ИБ.
	Если ДополнительныеСвойства.Свойство("ДополнитьДвижениямиИзИБЗаПериод") Тогда
		
		Запрос.УстановитьПараметр("НачалоПериода", ДополнительныеСвойства.ДополнитьДвижениямиИзИБЗаПериод.НачалоПериода);
		Запрос.УстановитьПараметр("КонецПериода",  ДополнительныеСвойства.ДополнитьДвижениямиИзИБЗаПериод.КонецПериода);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	*
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК Т
		|ГДЕ
		|	Т.Регистратор = &Регистратор
		|	И Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И НЕ Т.РасчетПартий
		|	И НЕ Т.РасчетСебестоимости";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = ЭтотОбъект.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		КонецЦикла;
		
	КонецЕсли;
	
	#КонецОбласти
	
	ТаблицаЗаписей = ЭтотОбъект.Выгрузить();
	Запрос.УстановитьПараметр("НаборЗаписей", 				ТаблицаЗаписей);
	Запрос.УстановитьПараметр("ТипыЗаписейПервичныхПартий", РасчетСебестоимостиПрикладныеАлгоритмы.ТипыЗаписейПервичныхПартий());
	
	// Создадим ВТПравилаЗаполненияПоляТипЗаписи
	РасчетСебестоимостиПрикладныеАлгоритмы.СоздатьТаблицуПравилЗаполненияПоляТипЗаписи(
		Запрос.МенеджерВременныхТаблиц,
		РасчетСебестоимостиПовтИсп.ПравилаЗаполненияПоляТипЗаписи(МетаРегистратор.Имя));
	
	#Область ШаблоныТекстаЗапроса
	
	// Подготовим шаблоны для формирования текстов запросов.
	ПоляИсключения    = Новый Структура("НомерСтроки, Активность, Регистратор, МоментВремени");
	ПоляПартий     	  = Новый Структура("Партия, АналитикаУчетаПартий, АналитикаФинансовогоУчета, ВидДеятельностиНДС,
		|КорПартия, КорАналитикаУчетаПартий, КорАналитикаФинансовогоУчета,
		|КорАналитикаУчетаНоменклатуры, КорВидДеятельностиНДС, ДокументИсточник, РасчетНеЗавершен");
	НесуммируемыеПоля = Новый Структура("КодСтроки");
	
	Если МетаРегистратор <> Метаданные.Документы.ПеремещениеТоваров
	 И МетаРегистратор <> Метаданные.Документы.КорректировкаНазначенияТоваров
	 И МетаРегистратор <> Метаданные.Документы.ВнутреннееПотреблениеТоваров
	 И МетаРегистратор <> Метаданные.Документы.ПересортицаТоваров
	 И МетаРегистратор <> Метаданные.Документы.ПорчаТоваров Тогда
		// Для перечисленных типов документов кор. вид деятельности НДС определяется в ПартионныйУчет22.ТекстТекущиеОборотыПартийТоваров()
		// Для остальных документов этот реквизит определяется при проведении документа.
		ПоляПартий.Удалить("КорВидДеятельностиНДС");
 	КонецЕсли;
 	
	ТекстПоляРегистра = "";
	ТекстПоляРегистраСМинусом = "";
	ТекстПоляРегистраБезПартий = "";
	ТекстПоляГруппировкиБезПартий = "";
	ТекстПоляСуммирования = "";
	ТекстПоляУсловия = "";
	
	Для Каждого ТекущаяКолонка Из ТаблицаЗаписей.Колонки Цикл
		
		Если ПоляИсключения.Свойство(ТекущаяКолонка.Имя) Тогда
			Продолжить;
		КонецЕсли;
		
		ЭтоСуммируемоеПоле = Метаданные().Ресурсы.Найти(ТекущаяКолонка.Имя) <> Неопределено
			ИЛИ (Метаданные().Реквизиты.Найти(ТекущаяКолонка.Имя) <> Неопределено
					И ТекущаяКолонка.ТипЗначения.СодержитТип(Тип("Число"))
					И НЕ НесуммируемыеПоля.Свойство(ТекущаяКолонка.Имя));
		 
		ТекстПоляРегистра = ТекстПоляРегистра + ?(ТекстПоляРегистра = "", "", ",
			|	") + "Т." + ТекущаяКолонка.Имя;
		ТекстПоляРегистраСМинусом = ТекстПоляРегистраСМинусом + ?(ТекстПоляРегистраСМинусом = "", "", ",
			|	") + ?(ЭтоСуммируемоеПоле, "-", "") + "Т." + ТекущаяКолонка.Имя;
			
		Если ЭтоСуммируемоеПоле Тогда
			
			ТекстПоляСуммирования = ТекстПоляСуммирования + ?(ТекстПоляСуммирования = "", "", ",
				|	") + "СУММА(Т." + ТекущаяКолонка.Имя + ") КАК " + ТекущаяКолонка.Имя;
			ТекстПоляУсловия = ТекстПоляУсловия + ?(ТекстПоляУсловия = "", "", "
				|	ИЛИ ") + "СУММА(Т." + ТекущаяКолонка.Имя + ") <> 0";
			
		Иначе
			
			Если НЕ ПоляПартий.Свойство(ТекущаяКолонка.Имя) Тогда
				
				Если НРег(ТекущаяКолонка.Имя) = НРег("КорВидДеятельностиНДС") Тогда
					
					// Для приходных движений КорВидДеятельностиНДС не заполняется при проведении документа, он заполняется при расчете партий для целей учета НДС.
					// Для расходных движений КорВидДеятельностиНДС всегда заполняется самим документом при проведении.
					ТекстПоляРегистраБезПартий = ТекстПоляРегистраБезПартий + ?(ТекстПоляРегистраБезПартий = "", "", ",
						|	") + "ВЫБОР КОГДА Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Перемещение)
						|	  И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
						|		ТОГДА НЕОПРЕДЕЛЕНО
						| ИНАЧЕ Т." + ТекущаяКолонка.Имя + " КОНЕЦ КАК " + ТекущаяКолонка.Имя;
					ТекстПоляГруппировкиБезПартий = ТекстПоляГруппировкиБезПартий + ?(ТекстПоляГруппировкиБезПартий = "", "", ",
						|	") + "ВЫБОР КОГДА Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Перемещение)
						|	  И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
						|		ТОГДА НЕОПРЕДЕЛЕНО
						| ИНАЧЕ Т." + ТекущаяКолонка.Имя + " КОНЕЦ";
					
				Иначе
						
					ТекстПоляРегистраБезПартий = ТекстПоляРегистраБезПартий + ?(ТекстПоляРегистраБезПартий = "", "", ",
						|	") + "Т." + ТекущаяКолонка.Имя;
					ТекстПоляГруппировкиБезПартий = ТекстПоляГруппировкиБезПартий + ?(ТекстПоляГруппировкиБезПартий = "", "", ",
						|	") + "Т." + ТекущаяКолонка.Имя;
					
				КонецЕсли;
				
			Иначе
				
				Если НРег(ТекущаяКолонка.Имя) = НРег("ДокументИсточник") Тогда
					ДопУсловиеДляПоля =
						 "ИЛИ (Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И Правила.ДокументИсточникВПриходе)
					|	  ИЛИ (Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И Правила.ДокументИсточникВРасходе)
					|	  ";
				ИначеЕсли НРег(ТекущаяКолонка.Имя) = НРег("КорВидДеятельностиНДС")
				 ИЛИ НРег(ТекущаяКолонка.Имя) = НРег("КорАналитикаУчетаНоменклатуры") Тогда
					ДопУсловиеДляПоля =
						 "И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
					|	  ";
				Иначе
					ДопУсловиеДляПоля = "";
				КонецЕсли;
				
				ТекстПоляРегистраБезПартий = ТекстПоляРегистраБезПартий + ?(ТекстПоляРегистраБезПартий = "", "", ",
					|	") + "ВЫБОР КОГДА Т.ТипЗаписи В (&ТипыЗаписейПервичныхПартий)
					|	  " + ДопУсловиеДляПоля + "
					|		ТОГДА Т." + ТекущаяКолонка.Имя
					+ " ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ КАК " + ТекущаяКолонка.Имя;
				ТекстПоляГруппировкиБезПартий = ТекстПоляГруппировкиБезПартий + ?(ТекстПоляГруппировкиБезПартий = "", "", ",
					|	") + "ВЫБОР КОГДА Т.ТипЗаписи В (&ТипыЗаписейПервичныхПартий)
					|	  " + ДопУсловиеДляПоля + "
					|		ТОГДА Т." + ТекущаяКолонка.Имя
					+ " ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ";
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	#КонецОбласти
	
	#Область СозданиеВременныхТаблиц
	
	// Проанализируем наличие измененных первичных движений в разрезе периодов (месяцев).
	ШаблонТекстаЗапроса =
	"ВЫБРАТЬ
	|	%1
	|ПОМЕСТИТЬ ВТНаборЗаписей
	|ИЗ
	|	&НаборЗаписей КАК Т
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	%3,
	|	%4
	|ПОМЕСТИТЬ ВТИзмененныеДвижения
	|ИЗ
	|(ВЫБРАТЬ
	|	%1
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.Регистратор = &Регистратор
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	%2
	|ИЗ
	|	ВТНаборЗаписей КАК Т
	|ГДЕ
	|	НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|) КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТПравилаЗаполненияПоляТипЗаписи КАК Правила
	|		ПО Правила.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|			ИЛИ Т.ХозяйственнаяОперация = Правила.ХозяйственнаяОперация
	|
	|СГРУППИРОВАТЬ ПО
	|	%5
	|ИМЕЮЩИЕ
	|	%6
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	Т.Период КАК Период,
	|	МАКСИМУМ(Т.ЕстьИзмененныеДвижения) КАК ЕстьИзмененныеДвижения
	|ПОМЕСТИТЬ ВТПериодыДвижений
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		НАЧАЛОПЕРИОДА(Т.Период, МЕСЯЦ) КАК Период,
	|		ЛОЖЬ КАК ЕстьИзмененныеДвижения
	|	ИЗ
	|		ВТНаборЗаписей КАК Т
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		НАЧАЛОПЕРИОДА(Т.Период, МЕСЯЦ) КАК Период,
	|		ИСТИНА КАК ЕстьИзмененныеДвижения
	|	ИЗ
	|		ВТИзмененныеДвижения КАК Т
	|	) КАК Т
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Период
	|;
	|";
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонТекстаЗапроса,
		ТекстПоляРегистра,
		ТекстПоляРегистраСМинусом,
		ТекстПоляРегистраБезПартий,
		ТекстПоляСуммирования,
		ТекстПоляГруппировкиБезПартий,
		ТекстПоляУсловия);
		
	Запрос.Выполнить(); // сформируем временные таблицы с описанием движений
	
	#КонецОбласти
	
	#Область ЗаполнениеНабораЗаписей
	
	// Движения не измененных периодов возьмем из ИБ,
	// а движения измененных периодов и расчетные движения - из набора записей.
	ШаблонТекстаЗапроса =
	"ВЫБРАТЬ
	|	&Регистратор КАК Регистратор,
	|	%1
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.Регистратор = &Регистратор
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И НАЧАЛОПЕРИОДА(Т.Период, МЕСЯЦ) В
	|		(ВЫБРАТЬ Т.Период
	|		 ИЗ ВТПериодыДвижений КАК Т
	|		 ГДЕ НЕ Т.ЕстьИзмененныеДвижения)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Регистратор КАК Регистратор,
	|	%1
	|ИЗ
	|	ВТНаборЗаписей КАК Т
	|ГДЕ
	|	НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И НАЧАЛОПЕРИОДА(Т.Период, МЕСЯЦ) В
	|		(ВЫБРАТЬ Т.Период
	|		 ИЗ ВТПериодыДвижений КАК Т
	|		 ГДЕ Т.ЕстьИзмененныеДвижения)
	|";
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонТекстаЗапроса,
		ТекстПоляРегистра);
	
	ТаблицаЗаписей = Запрос.Выполнить().Выгрузить();
	
	ЭтотОбъект.Загрузить(ТаблицаЗаписей);
	
	#КонецОбласти
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
