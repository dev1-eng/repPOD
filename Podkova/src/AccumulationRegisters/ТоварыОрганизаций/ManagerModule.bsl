#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
// Возвращает коэффициенты по РНПТ для указанной Организации и списка товаров.
//
// Параметры:
//	ИсключаемыйДокумент - ДокументСсылка - документ, движения которого исключаются при расчета коэффициента по РНПТ.
//	Организация - СправочникСсылка.Организации - организация, для которой рассчитываются коэффициенты по РНПТ.
//	Товары - ТаблицаЗначений - таблица, содержащая сведения о товарах.
//	
// Возвращаемое значение:
//	ТаблицаЗначений - содержит следующие колонки:
//		* Номенклатура - СправочникСсылка.Номенклатура - номенклатура, для которой необходимо получить значение
//															коэффициента по РНПТ.
//		* Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика номенклатуры, для которой
//																			необходимо получить значение коэффициента
//																			по РНПТ.
//		* МестоХранения - СправочникСсылка.Склады, СправочникСсылка.ДоговорыКонтрагентов - место хранения, 
//											где хранится номенклатура, для которой необходимо получить значение 
//											коэффициента по РНПТ.
//		* Коэффициент - Число - значение коэффициента по РНПТ.
//
Функция ПолучитьКоэффициентыПрослеживаемыхТоваров(ИсключаемыйДокумент, Организация, Товары) Экспорт
	
	КлючеваяОперация	= "РегистрНакопления.ТоварыОрганизаций.МодульМенеджера.ПолучитьКоэффициентыПрослеживаемыхТоваров";
	ОписаниеЗамера		= ОценкаПроизводительности.НачатьЗамерДлительнойОперации(КлючеваяОперация);
	
	ВозвращаемоеЗначение = КоэффициентыПрослеживаемыхТоваров(ИсключаемыйДокумент, Организация, Товары);
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, ВозвращаемоеЗначение.Количество());
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КоэффициентыПрослеживаемыхТоваров(ИсключаемыйДокумент, Организация, Товары)
	
	ВозвращаемоеЗначение = Новый ТаблицаЗначений;
	ВозвращаемоеЗначение.Колонки.Добавить("Номенклатура",	Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ВозвращаемоеЗначение.Колонки.Добавить("Характеристика",	Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ВозвращаемоеЗначение.Колонки.Добавить("МестоХранения",	Новый ОписаниеТипов("СправочникСсылка.Склады, СправочникСсылка.ДоговорыКонтрагентов"));
	ВозвращаемоеЗначение.Колонки.Добавить("Коэффициент",	Новый ОписаниеТипов("Число"));
	
	//++ Локализация
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(Товары.Характеристика КАК Справочник.ХарактеристикиНоменклатуры) КАК Характеристика,
	|	ВЫРАЗИТЬ(Товары.НомерГТД КАК Справочник.НомераГТД) КАК НомерГТД,
	|	Товары.МестоХранения КАК МестоХранения
	|ПОМЕСТИТЬ ВТВходящаяТаблица
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.МестоХранения КАК МестоХранения,
	|	Товары.НомерГТД КАК НомерГТД
	|ПОМЕСТИТЬ ВТТовары
	|ИЗ
	|	ВТВходящаяТаблица КАК Товары
	|ГДЕ
	|	ЕСТЬNULL(Товары.Номенклатура.КодТНВЭД.ПрослеживаемыйТовар, ЛОЖЬ)
	|	И Товары.Номенклатура.ВестиУчетПоГТД
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	МестоХранения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АналитикаУчетаНоменклатуры.КлючАналитики КАК Аналитика,
	|	ВТТовары.НомерГТД
	|ПОМЕСТИТЬ ВТАналитикиУчетаНоменклатуры
	|ИЗ
	|	РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТТовары
	|		ПО АналитикаУчетаНоменклатуры.Номенклатура = ВТТовары.Номенклатура
	|			И АналитикаУчетаНоменклатуры.Характеристика = ВТТовары.Характеристика
	|			И АналитикаУчетаНоменклатуры.МестоХранения = ВТТовары.МестоХранения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Аналитика,
	|	НомерГТД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НаборДанных.Номенклатура,
	|	НаборДанных.Характеристика,
	|	НаборДанных.МестоХранения,
	|	НаборДанных.КоличествоРНПТ / НаборДанных.Количество КАК Коэффициент
	|ПОМЕСТИТЬ ВТКоэффициентыПоДаннымТоваровОрганизаций
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|		ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|		ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры.МестоХранения КАК МестоХранения,
	|		ТоварыОрганизацийОстатки.КоличествоОстаток КАК Количество,
	|		ТоварыОрганизацийОстатки.КоличествоПоРНПТОстаток КАК КоличествоРНПТ
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций.Остатки(
	|			,
	|			Организация = &Организация
	|			И (АналитикаУчетаНоменклатуры, НомерГТД) В
	|					(ВЫБРАТЬ
	|						ТаблицаАналитик.Аналитика,
	|						ТаблицаАналитик.НомерГТД
	|					ИЗ ВТАналитикиУчетаНоменклатуры КАК ТаблицаАналитик)
	|		
	|		) КАК ТоварыОрганизацийОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыОрганизацийСторно.АналитикаУчетаНоменклатуры.Номенклатура,
	|		ТоварыОрганизацийСторно.АналитикаУчетаНоменклатуры.Характеристика,
	|		ТоварыОрганизацийСторно.АналитикаУчетаНоменклатуры.МестоХранения,
	|		-ТоварыОрганизацийСторно.Количество КАК Количество,
	|		-ТоварыОрганизацийСторно.КоличествоПоРНПТ КАК КоличествоРНПТ
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизацийСторно
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТАналитикиУчетаНоменклатуры КАК ВТТовары
	|			ПО ТоварыОрганизацийСторно.АналитикаУчетаНоменклатуры = ВТТовары.Аналитика
	|			И ТоварыОрганизацийСторно.НомерГТД = ВТТовары.НомерГТД
	|	
	|	ГДЕ
	|		ТоварыОрганизацийСторно.Регистратор = &ИсключаемыйДокумент
	|	
	|	) КАК НаборДанных
	|
	|ГДЕ
	|	НаборДанных.КоличествоРНПТ <> 0
	|	И НаборДанных.Количество <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.МестоХранения,
	|	1 КАК Коэффициент
	|ПОМЕСТИТЬ ВТКоэффициентыДляТоваровБезОстатка
	|ИЗ
	|	ВТТовары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКоэффициентыПоДаннымТоваровОрганизаций КАК ДанныеТоваровОрганизаций
	|		ПО Товары.Номенклатура = ДанныеТоваровОрганизаций.Номенклатура
	|		И Товары.Характеристика = ДанныеТоваровОрганизаций.Характеристика
	|		И Товары.МестоХранения = ДанныеТоваровОрганизаций.МестоХранения
	|ГДЕ
	|	ДанныеТоваровОрганизаций.Номенклатура ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	ДанныеТоваровОрганизаций.Номенклатура,
	|	ДанныеТоваровОрганизаций.Характеристика,
	|	ДанныеТоваровОрганизаций.МестоХранения,
	|	ДанныеТоваровОрганизаций.Коэффициент КАК Коэффициент
	|ИЗ
	|	ВТКоэффициентыПоДаннымТоваровОрганизаций КАК ДанныеТоваровОрганизаций
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеТоваровБезОстатка.Номенклатура,
	|	ДанныеТоваровБезОстатка.Характеристика,
	|	ДанныеТоваровБезОстатка.МестоХранения,
	|	ДанныеТоваровБезОстатка.Коэффициент КАК Коэффициент
	|ИЗ
	|	ВТКоэффициентыДляТоваровБезОстатка КАК ДанныеТоваровБезОстатка";
	
	Запрос.УстановитьПараметр("ИсключаемыйДокумент",	ИсключаемыйДокумент);
	Запрос.УстановитьПараметр("Организация",			Организация);
	Запрос.УстановитьПараметр("Товары",					Товары);
	
	ВозвращаемоеЗначение = Запрос.Выполнить().Выгрузить();
	//-- Локализация
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыНакопления.ТоварыОрганизаций.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.4.11.19";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("81618f46-e0b2-40c1-84e2-4c9604155ea8");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.ТоварыОрганизаций.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Выполняется переформирование движений этапов производства с учетом заполнения новых табличных частей видов запасов.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыНакопления.ТоварыОрганизаций.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыНакопления.ТоварыОрганизаций.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();


КонецПроцедуры

// Обработчик обновления УТ 11.4.11
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МетаданныеОбъекта = Метаданные.РегистрыНакопления.ТоварыОрганизаций;
	ПолноеИмяОбъекта = МетаданныеОбъекта.ПолноеИмя();
	
	СписокДокументов = Новый Массив;
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = СтрСоединить(СписокДокументов, ",");
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяОбъекта;
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Регистратор.Дата УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Регистратор");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПараметрыВыборки.ПолныеИменаРегистров;
	
	Для каждого ПолноеИмяДокумента Из СписокДокументов Цикл
		
		ИмяДокумента = СтрРазделить(ПолноеИмяДокумента, ".")[1];
		
		ТекстЗапросаМеханизмаПроведения = Документы[ИмяДокумента].АдаптированныйТекстЗапросаДвиженийПоРегистру("ТоварыОрганизаций");
		Регистраторы = ОбновлениеИнформационнойБазыУТ.РегистраторыДляПерепроведения(
			ТекстЗапросаМеханизмаПроведения,
			ПолноеИмяОбъекта,
			ПолноеИмяДокумента);
		
		ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяОбъекта);
		
	КонецЦикла;
	
КонецПроцедуры

// Обработчик обновления УТ 11.4.11 в движениях регистра.
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Регистраторы = Новый Массив;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(
		Регистраторы,
		"РегистрНакопления.ТоварыОрганизаций",
		Параметры.Очередь);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
