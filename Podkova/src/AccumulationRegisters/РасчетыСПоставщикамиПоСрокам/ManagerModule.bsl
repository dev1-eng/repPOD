
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс


#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК Т
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Т1 
	|	ПО Т.АналитикаУчетаПоПартнерам = Т1.КлючАналитики
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Т1.Организация)
	|	И ЗначениеРазрешено(Т1.Партнер)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.4.8.92";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("da61b003-8fb1-4d3b-93c0-0f8c05596f2a");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Исправляет движения в регистре взаиморасчетов.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоДокументам.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.КлючиАналитикиУчетаПоПартнерам.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.ПолноеИмя());
	Изменяемые.Добавить(Метаданные.Справочники.КлючиАналитикиУчетаПоПартнерам.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Справочники.КлючиАналитикиУчетаПоПартнерам.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РасчетыСКлиентамиПоСрокам.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрНакопления.РасчетыСПоставщикамиПоСрокам";
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = "Справочник.КлючиАналитикиУчетаПоПартнерам";
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивАналитики", Новый Массив);
	Запрос.УстановитьПараметр("ПоВсем", Истина);
	Запрос.УстановитьПараметр("НоваяАрхитектураВзаиморасчетов", ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов"));
	Запрос.Текст = ТекстЗапросаНекорректныеРегистраторы();
	
	Данные = Запрос.Выполнить().Выгрузить();
	Данные.Свернуть("АналитикаУчетаПоПартнерам");
	Данные = Данные.ВыгрузитьКолонку("АналитикаУчетаПоПартнерам");
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Данные);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрНакопления.РасчетыСПоставщикамиПоСрокам";
	МетаданныеРегистра = Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам;
	
	Если НЕ ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, "РегистрНакопления.РасчетыСПоставщикамиПоДокументам") Тогда
		Возврат;
	КонецЕсли;
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = НЕ ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, "Справочник.КлючиАналитикиУчетаПоПартнерам");
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивАналитики", ОбновляемыеДанные.ВыгрузитьКолонку("Ссылка"));
	Запрос.УстановитьПараметр("ПоВсем", Ложь);
	Запрос.УстановитьПараметр("НоваяАрхитектураВзаиморасчетов", ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов"));
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапросаНекорректныеРегистраторы("НекорректныеРегистраторы") + "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НекорректныеРегистраторы.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	НекорректныеРегистраторы.ОбъектРасчетов КАК ОбъектРасчетов,
	|	НекорректныеРегистраторы.Валюта КАК Валюта,
	|	НекорректныеРегистраторы.АналитикаУчетаПоПартнерам.Организация КАК Организация
	|ПОМЕСТИТЬ КортежиКПроверке
	|ИЗ
	|	НекорректныеРегистраторы КАК НекорректныеРегистраторы
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаПоПартнерам, ОбъектРасчетов
	|;
	//Движения для перезаписи
	|ВЫБРАТЬ 
	|	РасчетыПоСрокам.ВидДвижения КАК ВидДвижения,
	|	РасчетыПоСрокам.Период КАК Период,
	|	РасчетыПоСрокам.Регистратор КАК Регистратор,
	|
	|	РасчетыПоСрокам.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыПоСрокам.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыПоСрокам.Валюта КАК Валюта,
	|	РасчетыПоСрокам.РасчетныйДокумент КАК РасчетныйДокумент,
	|	РасчетыПоСрокам.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|	РасчетыПоСрокам.ДатаВозникновения КАК ДатаВозникновения,
	|	РасчетыПоСрокам.Предоплата КАК Предоплата,
	|	РасчетыПоСрокам.ПредоплатаРегл КАК ПредоплатаРегл,
	|	РасчетыПоСрокам.ПредоплатаУпр КАК ПредоплатаУпр,
	|	РасчетыПоСрокам.Долг КАК Долг,
	|	РасчетыПоСрокам.ДолгРегл КАК ДолгРегл,
	|	РасчетыПоСрокам.ДолгУпр КАК ДолгУпр,
	|	РасчетыПоСрокам.ПорядокЗачета КАК ПорядокЗачета,
	|	РасчетыПоСрокам.ПорядокОперации КАК ПорядокОперации,
	|	РасчетыПоСрокам.ВалютаДокумента КАК ВалютаДокумента,
	|	РасчетыПоСрокам.СвязанныйДокумент КАК СвязанныйДокумент,
	|	РасчетыПоСрокам.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	РасчетыПоСрокам.КорОбъектРасчетов КАК КорОбъектРасчетов,
	|	РасчетыПоСрокам.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	РасчетыПоСрокам.ДокументРегистратор КАК ДокументРегистратор,
	|	РасчетыПоСрокам.КорАналитикаУчетаПоПартнерам КАК КорАналитикаУчетаПоПартнерам,
	|	НекорректныеРегистраторы.Регистратор ЕСТЬ NULL КАК ТребуетсяПерезапись
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК РасчетыПоСрокам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КортежиКПроверке КАК КортежиКПроверке
	|			ПО КортежиКПроверке.АналитикаУчетаПоПартнерам = РасчетыПоСрокам.АналитикаУчетаПоПартнерам
	|				И КортежиКПроверке.ОбъектРасчетов         = РасчетыПоСрокам.ОбъектРасчетов
	|				И КортежиКПроверке.Валюта                 = РасчетыПоСрокам.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ НекорректныеРегистраторы КАК НекорректныеРегистраторы
	|			ПО  НекорректныеРегистраторы.АналитикаУчетаПоПартнерам = РасчетыПоСрокам.АналитикаУчетаПоПартнерам
	|				И НекорректныеРегистраторы.ОбъектРасчетов         = РасчетыПоСрокам.ОбъектРасчетов
	|				И НекорректныеРегистраторы.Валюта                 = РасчетыПоСрокам.Валюта
	|				И НекорректныеРегистраторы.Регистратор                 = РасчетыПоСрокам.ДокументРегистратор
	|ГДЕ
	|	РасчетыПоСрокам.Активность
	|;
	//КортежиКПроверке
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	КортежиКПроверке КАК КортежиКПроверке
	|;
	//РегистраторыРасчетов для добавления записей
	|ВЫБРАТЬ
	|	РегистраторРасчетов.Ссылка                    КАК Ссылка,
	|	РегистраторРасчетов.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РегистраторРасчетов.ОбъектРасчетов            КАК ОбъектРасчетов,
	|	РегистраторРасчетов.Валюта                    КАК Валюта
	|ИЗ
	|	Документ.РегистраторРасчетов КАК РегистраторРасчетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КортежиКПроверке КАК КортежиКПроверке
	|			ПО КортежиКПроверке.АналитикаУчетаПоПартнерам = РегистраторРасчетов.АналитикаУчетаПоПартнерам
	|				И КортежиКПроверке.ОбъектРасчетов         = РегистраторРасчетов.ОбъектРасчетов
	|				И КортежиКПроверке.Валюта                 = РегистраторРасчетов.Валюта
	|ГДЕ
	|	РегистраторРасчетов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|;
	|ВЫБРАТЬ
	|	НекорректныеРегистраторы.Регистратор КАК Регистратор,
	|	НекорректныеРегистраторы.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	НекорректныеРегистраторы.ОбъектРасчетов КАК ОбъектРасчетов,
	|	НекорректныеРегистраторы.Валюта КАК Валюта
	|ИЗ
	|	НекорректныеРегистраторы КАК НекорректныеРегистраторы
	|";
	
	Результаты = Запрос.ВыполнитьПакет();
	
	ДвиженияДляПерезаписи = Результаты[4].Выгрузить();
	ДвиженияДляПерезаписи.Индексы.Добавить("АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта");
	СтруктураПоиска =  Новый Структура("АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта");

	КортежиКПроверке = Результаты[5].Выгрузить();
	КортежиКПроверке.Индексы.Добавить("АналитикаУчетаПоПартнерам");
	
	РегистраторыРасчетов = Результаты[6].Выгрузить();
	РегистраторыРасчетов.Индексы.Добавить("АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта");
	
	НекорректныеРегистраторы = Результаты[7].Выгрузить();
	НекорректныеРегистраторы.Индексы.Добавить("АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта");
	
	Запрос.Текст = ТекстЗапросаИсправленныеДвижения();
	Запрос.УстановитьПараметр("ХозяйственныеОперацииНеОтгрузка", ОперативныеВзаиморасчетыСервер.ХозяйственныеОперацииНеОтгрузка());
	Запрос.УстановитьПараметр("ТипыПлатежныхДокументов", ОперативныеВзаиморасчетыСервер.ТипыПлатежныхДокументов());
	Запрос.УстановитьПараметр("ТипыПервичногоДокументаПлатежки", ОперативныеВзаиморасчетыСервер.ТипыПервичногоДокументаПлатежки());
	ИсправленныеДвижения = Запрос.Выполнить().Выгрузить();
	ИсправленныеДвижения.Индексы.Добавить("АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта");
	
	Для Каждого СтрокаОбновляемыхДанных Из ОбновляемыеДанные Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыСПоставщикамиПоСрокам");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			ЭлементБлокировки.УстановитьЗначение("АналитикаУчетаПоПартнерам", СтрокаОбновляемыхДанных.Ссылка);
			
			Блокировка.Заблокировать();
			
			ОбрабатываемыйКлюч = СтрокаОбновляемыхДанных.Ссылка;
			
			КортежиДляОбработки = КортежиКПроверке.НайтиСтроки(Новый Структура("АналитикаУчетаПоПартнерам", ОбрабатываемыйКлюч));;
			
			Для Каждого Кортеж Из КортежиДляОбработки Цикл
				
				ЗаполнитьЗначенияСвойств(СтруктураПоиска, Кортеж);
				
				//1. Удаляем некорректные движения документов из регистраторов расчетов.
				
				ДвиженияДляПерезаписиАналитики = ДвиженияДляПерезаписи.Скопировать(СтруктураПоиска);
				ДвиженияДляПерезаписиАналитики.Индексы.Добавить("Регистратор");
				
				РегистраторыРасчетовДляПерезаписи = ДвиженияДляПерезаписиАналитики.Скопировать(,"Регистратор");
				РегистраторыРасчетовДляПерезаписи.Свернуть("Регистратор");
				РегистраторыРасчетовДляПерезаписи = РегистраторыРасчетовДляПерезаписи.ВыгрузитьКолонку("Регистратор");
				
				НаборЗаписей = Неопределено;
				Для Каждого Регистратор Из РегистраторыРасчетовДляПерезаписи Цикл
					НаборЗаписей = РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
					НаборЗаписей.Загрузить(ДвиженияДляПерезаписи.Скопировать(Новый Структура("Регистратор, ТребуетсяПерезапись", Регистратор, Истина)));
					НаборЗаписей.Записать();
				КонецЦикла;
				
				//2.Обрабатываем кортежи
				
				Если НаборЗаписей = Неопределено Тогда
					
					РегистраторыРасчетовАналитики = РегистраторыРасчетов.НайтиСтроки(СтруктураПоиска);
					Если РегистраторыРасчетовАналитики.Количество() > 0 Тогда
						РегистраторРасчетов = РегистраторыРасчетовАналитики[0].Ссылка;
					Иначе
						ДокументОбъект = Документы.РегистраторРасчетов.СоздатьДокумент();
						ЗаполнитьЗначенияСвойств(ДокументОбъект, Кортеж);
						ДокументОбъект.ТипРасчетов               = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
						ДокументОбъект.Дата                      = ТекущаяДатаСеанса();
						ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
						РегистраторРасчетов = ДокументОбъект.Ссылка;
					КонецЕсли;
					
					НаборЗаписей = РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.Регистратор.Установить(РегистраторРасчетов);
					НаборЗаписей.Прочитать();
				КонецЕсли;
				
				ИсправленныеСтрокиДвижений = ИсправленныеДвижения.Скопировать(СтруктураПоиска);
				ИсправленныеСтрокиДвижений.Индексы.Добавить("ДокументРегистратор");
				
				СтрокиНекорректныхРегистраторов = НекорректныеРегистраторы.НайтиСтроки(СтруктураПоиска);
				
				Для Каждого СтрокаНекорректныхРегистраторов Из СтрокиНекорректныхРегистраторов Цикл
					
					ДвиженияРегистратора = ИсправленныеСтрокиДвижений.НайтиСтроки(Новый Структура("ДокументРегистратор", СтрокаНекорректныхРегистраторов.Регистратор));
					
					Для Каждого Движение Из ДвиженияРегистратора Цикл
						ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Движение);
					КонецЦикла;
					
				КонецЦикла;
				
				НаборЗаписей.Записать();
				
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(СтрокаОбновляемыхДанных.Ссылка);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru = 'Не удалось обработать объект: %Ссылка% по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", СтрокаОбновляемыхДанных.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
									УровеньЖурналаРегистрации.Предупреждение,
									МетаданныеРегистра,
									СтрокаОбновляемыхДанных.Ссылка,
									ТекстСообщения);
									
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = НЕ ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, "Справочник.КлючиАналитикиУчетаПоПартнерам");
	
КонецПроцедуры

Функция ТекстЗапросаНекорректныеРегистраторы(ИмяВТ = "")
	
	ТекстЗапроса = "
		|ВЫБРАТЬ
		|	РасчетыПоДокументам.Регистратор                    КАК Регистратор,
		|	РасчетыПоДокументам.ЗаказПоставщику                КАК ОбъектРасчетов,
		|	РасчетыПоДокументам.Валюта                         КАК Валюта,
		|	РасчетыПоДокументам.АналитикаУчетаПоПартнерам      КАК АналитикаУчетаПоПартнерам,
		|	МИНИМУМ(НАЧАЛОПЕРИОДА(РасчетыПоДокументам.Период)) КАК Месяц,
		|	СУММА(ВЫБОР КОГДА РасчетыПоДокументам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			ТОГДА - РасчетыПоДокументам.Долг - РасчетыПоДокументам.Предоплата
		|			ИНАЧЕ РасчетыПоДокументам.Долг + РасчетыПоДокументам.Предоплата
		|		КОНЕЦ)                                         КАК СуммаДвижений
		|ПОМЕСТИТЬ ВтРасчетыПоДокументам
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК РасчетыПоДокументам
		|ГДЕ
		|	РасчетыПоДокументам.Активность
		|	И &НоваяАрхитектураВзаиморасчетов
		|	И (РасчетыПоДокументам.АналитикаУчетаПоПартнерам В (&МассивАналитики) ИЛИ &ПоВсем)
		|СГРУППИРОВАТЬ ПО
		|	РасчетыПоДокументам.Регистратор,
		|	РасчетыПоДокументам.ЗаказПоставщику,
		|	РасчетыПоДокументам.Валюта,
		|	РасчетыПоДокументам.АналитикаУчетаПоПартнерам
		|ИНДЕКСИРОВАТЬ ПО
		|	АналитикаУчетаПоПартнерам, ОбъектРасчетов
		|;
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РасчетыПоДокументам.Регистратор               КАК Регистратор,
		|	РасчетыПоДокументам.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	РасчетыПоДокументам.ОбъектРасчетов            КАК ОбъектРасчетов
		|ПОМЕСТИТЬ НеизмененныеРегистраторы
		|ИЗ
		|	ВтРасчетыПоДокументам КАК РасчетыПоДокументам
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКРаспределениюРасчетовСПоставщиками КАК Задания
		|			ПО РасчетыПоДокументам.ОбъектРасчетов = Задания.ОбъектРасчетов
		|				И РасчетыПоДокументам.АналитикаУчетаПоПартнерам = Задания.АналитикаУчетаПоПартнерам
		|				И Задания.Месяц <= РасчетыПоДокументам.Месяц
		|				ИЛИ РасчетыПоДокументам.Регистратор = Задания.Документ
		|				И Задания.Месяц <= РасчетыПоДокументам.Месяц
		|ГДЕ
		|	Задания.Месяц ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	РасчетыПоСрокам.ДокументРегистратор       КАК Регистратор,
		|	РасчетыПоСрокам.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	РасчетыПоСрокам.ОбъектРасчетов            КАК ОбъектРасчетов
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК РасчетыПоСрокам
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщиками КАК Расчеты
		|			ПО Расчеты.Регистратор = РасчетыПоСрокам.ДокументРегистратор
		|ГДЕ
		|	Расчеты.Регистратор ЕСТЬ NULL
		|	И (РасчетыПоСрокам.АналитикаУчетаПоПартнерам В (&МассивАналитики) ИЛИ &ПоВсем)
		|;
		|
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ВложенныйЗапрос.ОбъектРасчетов КАК ОбъектРасчетов,
		|	ВложенныйЗапрос.Валюта КАК Валюта,
		|	ВложенныйЗапрос.Регистратор КАК Регистратор,
		|	СУММА(ВложенныйЗапрос.СуммаПоДокументам) КАК СуммаПоДокументам,
		|	СУММА(ВложенныйЗапрос.СуммаПоСрокам) КАК СуммаПоСрокам
		|%ТекстПоместить%
		|ИЗ (ВЫБРАТЬ
		|		РасчетыПоДокументам.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|		РасчетыПоДокументам.ОбъектРасчетов КАК ОбъектРасчетов,
		|		РасчетыПоДокументам.Валюта КАК Валюта,
		|		РасчетыПоДокументам.Регистратор КАК Регистратор,
		|		РасчетыПоДокументам.СуммаДвижений КАК СуммаПоДокументам,
		|		0 КАК СуммаПоСрокам
		|	ИЗ ВтРасчетыПоДокументам КАК РасчетыПоДокументам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НеизмененныеРегистраторы КАК НеизмененныеРегистраторы
		|			ПО НеизмененныеРегистраторы.Регистратор = РасчетыПоДокументам.Регистратор
		|			И НеизмененныеРегистраторы.АналитикаУчетаПоПартнерам = РасчетыПоДокументам.АналитикаУчетаПоПартнерам
		|			И НеизмененныеРегистраторы.ОбъектРасчетов = РасчетыПоДокументам.ОбъектРасчетов
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		РасчетыПоСрокам.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|		РасчетыПоСрокам.ОбъектРасчетов            КАК ОбъектРасчетов,
		|		РасчетыПоСрокам.Валюта                    КАК Валюта,
		|		РасчетыПоСрокам.ДокументРегистратор       КАК Регистратор,
		|		0 КАК СуммаПоДокументам,
		|		ВЫБОР КОГДА РасчетыПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РасчетыПоСрокам.Предоплата - РасчетыПоСрокам.Долг
		|		ИНАЧЕ РасчетыПоСрокам.Долг - РасчетыПоСрокам.Предоплата
		|		КОНЕЦ                                     КАК СуммаПоСрокам
		|	ИЗ РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК РасчетыПоСрокам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НеизмененныеРегистраторы КАК НеизмененныеРегистраторы
		|			ПО НеизмененныеРегистраторы.Регистратор = РасчетыПоСрокам.ДокументРегистратор
		|			И НеизмененныеРегистраторы.АналитикаУчетаПоПартнерам = РасчетыПоСрокам.АналитикаУчетаПоПартнерам
		|			И НеизмененныеРегистраторы.ОбъектРасчетов = РасчетыПоСрокам.ОбъектРасчетов
		|	ГДЕ
		|		РасчетыПоСрокам.АналитикаУчетаПоПартнерам В (&МассивАналитики) ИЛИ &ПоВсем) КАК ВложенныйЗапрос
		|ГДЕ
		|	НЕ ВложенныйЗапрос.Регистратор ССЫЛКА Документ.КорректировкаРегистров
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам,
		|	ВложенныйЗапрос.ОбъектРасчетов,
		|	ВложенныйЗапрос.Валюта,
		|	ВложенныйЗапрос.Регистратор
		|ИМЕЮЩИЕ
		|	СУММА(ВложенныйЗапрос.СуммаПоДокументам) <> СУММА(ВложенныйЗапрос.СуммаПоСрокам)
		|;
		|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ТекстПоместить%", ?(ИмяВТ="","","ПОМЕСТИТЬ "+ИмяВт));
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаИсправленныеДвижения()
	
	ТекстЗапроса = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВЫБОР КОГДА РасчетыСПоставщиками.РасчетныйДокумент <> Неопределено 
		|		ТОГДА РасчетыСПоставщиками.РасчетныйДокумент
		|		ИНАЧЕ РасчетыСПоставщиками.Регистратор 
		|	КОНЕЦ                                         КАК Регистратор,
		|	МИНИМУМ(РасчетыСПоставщиками.ПорядокОперации) КАК ПорядокОперации,
		|	МИНИМУМ(РасчетыСПоставщиками.ПорядокЗачетаПоДатеПлатежа) КАК ПорядокЗачетаПоДатеПлатежа,
		|	МИНИМУМ(ВЫБОР КОГДА РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.ВводОстатков
		|						ТОГДА РасчетыСПоставщиками.ДатаРегистратора
		|				ИНАЧЕ РасчетыСПоставщиками.Период
		|			КОНЕЦ)                                КАК ДатаВозникновения,
		|	МИНИМУМ(РасчетыСПоставщиками.ВалютаДокумента) КАК ВалютаДокумента,
		|	МАКСИМУМ(РасчетыСПоставщиками.ДатаПлатежа)    КАК ДатаПлатежа
		|ПОМЕСТИТЬ ПорядокДокументов
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КортежиКПроверке КАК КортежиКПроверке
		|			ПО КортежиКПроверке.АналитикаУчетаПоПартнерам = РасчетыСПоставщиками.АналитикаУчетаПоПартнерам
		|				И КортежиКПроверке.ОбъектРасчетов         = РасчетыСПоставщиками.ЗаказПоставщику
		|				И КортежиКПроверке.Валюта                 = РасчетыСПоставщиками.Валюта
		|ГДЕ
		|	РасчетыСПоставщиками.Активность
		|	И НЕ РасчетыСПоставщиками.ПорядокОперации = """"
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР КОГДА РасчетыСПоставщиками.РасчетныйДокумент <> Неопределено 
		|		ТОГДА РасчетыСПоставщиками.РасчетныйДокумент
		|		ИНАЧЕ РасчетыСПоставщиками.Регистратор 
		|	КОНЕЦ
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасчетыСПоставщикамиПоДокументам.АналитикаУчетаПоПартнерам      КАК АналитикаУчетаПоПартнерам,
		|	РасчетыСПоставщикамиПоДокументам.ЗаказПоставщику                КАК ОбъектРасчетов,
		|	РасчетыСПоставщикамиПоДокументам.Валюта                         КАК Валюта,
		|	РасчетыСПоставщикамиПоДокументам.Регистратор                    КАК ДокументРегистратор,
		|	РасчетыСПоставщикамиПоДокументам.РасчетныйДокумент              КАК РасчетныйДокумент,
		|	РасчетыСПоставщикамиПоДокументам.ХозяйственнаяОперация          КАК ХозяйственнаяОперация,
		|	РасчетыСПоставщикамиПоДокументам.СтатьяДвиженияДенежныхСредств  КАК СтатьяДвиженияДенежныхСредств,
		|	РасчетыСПоставщикамиПоДокументам.Период                         КАК Период,
		|	ВЫБОР КОГДА ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И (РасчетыСПоставщикамиПоДокументам.Долг < 0
		|				ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгРегл < 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгУпр < 0
		|				ИЛИ РасчетыСПоставщикамиПоДокументам.Предоплата > 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл > 0
		|				ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр > 0)
		|		ИЛИ ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И (РасчетыСПоставщикамиПоДокументам.Предоплата < 0
		|				ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл < 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр < 0
		|				ИЛИ РасчетыСПоставщикамиПоДокументам.Долг > 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгРегл > 0 
		|				ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгУпр > 0) ТОГДА
		|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	ИНАЧЕ
		|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	КОНЕЦ                                                           КАК ВидДвижения,
		|	ВЫРАЗИТЬ(РасчетыСПоставщикамиПоДокументам.РасчетныйДокумент КАК Документ.КорректировкаПриобретения).ДокументОснование КАК СвязанныйДокумент,
		|
		|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.Предоплата < 0  ТОГДА
		|		-РасчетыСПоставщикамиПоДокументам.Предоплата 
		|	ИНАЧЕ
		|		РасчетыСПоставщикамиПоДокументам.Предоплата
		|	КОНЕЦ                                                           КАК Предоплата,
		|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр < 0  ТОГДА
		|		-РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр 
		|	ИНАЧЕ
		|		РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр
		|	КОНЕЦ                                                           КАК ПредоплатаУпр,
		|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл < 0  ТОГДА
		|		-РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл 
		|	ИНАЧЕ
		|		РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл
		|	КОНЕЦ                                                           КАК ПредоплатаРегл,
		|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.Долг < 0  ТОГДА
		|		-РасчетыСПоставщикамиПоДокументам.Долг 
		|	ИНАЧЕ
		|		РасчетыСПоставщикамиПоДокументам.Долг
		|	КОНЕЦ                                                           КАК Долг,
		|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.ДолгУпр < 0  ТОГДА
		|		-РасчетыСПоставщикамиПоДокументам.ДолгУпр 
		|	ИНАЧЕ
		|		РасчетыСПоставщикамиПоДокументам.ДолгУпр
		|	КОНЕЦ                                                           КАК ДолгУпр,
		|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.ДолгРегл < 0  ТОГДА
		|		-РасчетыСПоставщикамиПоДокументам.ДолгРегл 
		|	ИНАЧЕ
		|		РасчетыСПоставщикамиПоДокументам.ДолгРегл
		|	КОНЕЦ                                                           КАК ДолгРегл,
		|
		|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.Предоплата <> 0 ТОГДА ДАТАВРЕМЯ(1,1,1)
		|		КОГДА (ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И (РасчетыСПоставщикамиПоДокументам.Долг < 0
		|				ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгРегл < 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгУпр < 0
		|				ИЛИ РасчетыСПоставщикамиПоДокументам.Предоплата > 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл > 0
		|				ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр > 0)
		|		ИЛИ ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И (РасчетыСПоставщикамиПоДокументам.Предоплата < 0
		|				ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл < 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр < 0
		|				ИЛИ РасчетыСПоставщикамиПоДокументам.Долг > 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгРегл > 0 
		|				ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгУпр > 0)) 
		|			И НЕ (РасчетыСПоставщикамиПоДокументам.Регистратор ССЫЛКА Документ.ВводОстатков И 
		|					РасчетыСПоставщикамиПоДокументам.РасчетныйДокумент <> Неопределено)
		|			И НЕ РасчетыСПоставщикамиПоДокументам.Регистратор ССЫЛКА Документ.РасчетКурсовыхРазниц ТОГДА
		|			ЕСТЬNULL(ПорядокДокументов.ДатаПлатежа, ДАТАВРЕМЯ(1,1,1))
		|	ИНАЧЕ
		|		ЕСТЬNULL(ДатыВозникновения.ДатаПлатежа, ДАТАВРЕМЯ(1,1,1))
		|	КОНЕЦ                                                           КАК ДатаПлановогоПогашения,
		|	ЕСТЬNULL(ДатыВозникновения.ДатаВозникновения, ДАТАВРЕМЯ(1,1,1)) КАК ДатаВозникновения,
		|	ЕСТЬNULL(ПорядокДокументов.ПорядокОперации,"""")                КАК ПорядокОперации,
		|	ЕСТЬNULL(ПорядокДокументов.ПорядокЗачетаПоДатеПлатежа,"""")     КАК ПорядокЗачетаРегистратора,
		|	ЕСТЬNULL(ДатыВозникновения.ПорядокЗачетаПоДатеПлатежа,"""")     КАК ПорядокЗачета,
		|	ЕСТЬNULL(ДатыВозникновения.ВалютаДокумента,
		|		ЕСТЬNULL(ПорядокДокументов.ВалютаДокумента,
		|			ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)))              КАК ВалютаДокумента
		|ПОМЕСТИТЬ ВтРасчеты
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК РасчетыСПоставщикамиПоДокументам
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокДокументов КАК ПорядокДокументов
		|		ПО РасчетыСПоставщикамиПоДокументам.РасчетныйДокумент = ПорядокДокументов.Регистратор
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокДокументов КАК ДатыВозникновения
		|		ПО РасчетыСПоставщикамиПоДокументам.РасчетныйДокумент = ДатыВозникновения.Регистратор
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КортежиКПроверке КАК КортежиКПроверке
		|			ПО КортежиКПроверке.АналитикаУчетаПоПартнерам = РасчетыСПоставщикамиПоДокументам.АналитикаУчетаПоПартнерам
		|				И КортежиКПроверке.ОбъектРасчетов         = РасчетыСПоставщикамиПоДокументам.ЗаказПоставщику
		|				И КортежиКПроверке.Валюта                 = РасчетыСПоставщикамиПоДокументам.Валюта
		|ГДЕ
		|	РасчетыСПоставщикамиПоДокументам.Регистратор ССЫЛКА Документ.ВводОстатков
		|	И РасчетыСПоставщикамиПоДокументам.Регистратор <> РасчетыСПоставщикамиПоДокументам.РасчетныйДокумент
		|	И (РасчетыСПоставщикамиПоДокументам.Предоплата <> 0
		|		ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл <> 0
		|		ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр <> 0
		|		ИЛИ РасчетыСПоставщикамиПоДокументам.Долг <> 0
		|		ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгРегл <> 0
		|		ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгУпр <> 0)
		|	И РасчетыСПоставщикамиПоДокументам.Активность
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РасчетыСПоставщикамиПоДокументам.АналитикаУчетаПоПартнерам      КАК АналитикаУчетаПоПартнерам,
		|	РасчетыСПоставщикамиПоДокументам.ЗаказПоставщику                КАК ОбъектРасчетов,
		|	РасчетыСПоставщикамиПоДокументам.Валюта                         КАК Валюта,
		|	РасчетыСПоставщикамиПоДокументам.Регистратор                    КАК ДокументРегистратор,
		|	РасчетыСПоставщикамиПоДокументам.РасчетныйДокумент              КАК РасчетныйДокумент,
		|	РасчетыСПоставщикамиПоДокументам.ХозяйственнаяОперация          КАК ХозяйственнаяОперация,
		|	РасчетыСПоставщикамиПоДокументам.СтатьяДвиженияДенежныхСредств  КАК СтатьяДвиженияДенежныхСредств,
		|	РасчетыСПоставщикамиПоДокументам.Период                         КАК Период,
		|	ВЫБОР КОГДА ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И (РасчетыСПоставщикамиПоДокументам.Долг < 0
		|				ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгРегл < 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгУпр < 0
		|				ИЛИ РасчетыСПоставщикамиПоДокументам.Предоплата > 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл > 0
		|				ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр > 0)
		|		ИЛИ ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И (РасчетыСПоставщикамиПоДокументам.Предоплата < 0
		|				ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл < 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр < 0
		|				ИЛИ РасчетыСПоставщикамиПоДокументам.Долг > 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгРегл > 0 
		|				ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгУпр > 0) ТОГДА
		|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	ИНАЧЕ
		|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	КОНЕЦ                                                           КАК ВидДвижения,
		|	ВЫРАЗИТЬ(РасчетыСПоставщикамиПоДокументам.РасчетныйДокумент КАК Документ.КорректировкаПриобретения).ДокументОснование КАК СвязанныйДокумент,
		|
		|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.Предоплата < 0  ТОГДА
		|		-РасчетыСПоставщикамиПоДокументам.Предоплата 
		|	ИНАЧЕ
		|		РасчетыСПоставщикамиПоДокументам.Предоплата
		|	КОНЕЦ                                                           КАК Предоплата,
		|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр < 0  ТОГДА
		|		-РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр 
		|	ИНАЧЕ
		|		РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр
		|	КОНЕЦ                                                           КАК ПредоплатаУпр,
		|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл < 0  ТОГДА
		|		-РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл 
		|	ИНАЧЕ
		|		РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл
		|	КОНЕЦ                                                           КАК ПредоплатаРегл,
		|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.Долг < 0  ТОГДА
		|		-РасчетыСПоставщикамиПоДокументам.Долг 
		|	ИНАЧЕ
		|		РасчетыСПоставщикамиПоДокументам.Долг
		|	КОНЕЦ                                                           КАК Долг,
		|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.ДолгУпр < 0  ТОГДА
		|		-РасчетыСПоставщикамиПоДокументам.ДолгУпр 
		|	ИНАЧЕ
		|		РасчетыСПоставщикамиПоДокументам.ДолгУпр
		|	КОНЕЦ                                                           КАК ДолгУпр,
		|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.ДолгРегл < 0  ТОГДА
		|		-РасчетыСПоставщикамиПоДокументам.ДолгРегл 
		|	ИНАЧЕ
		|		РасчетыСПоставщикамиПоДокументам.ДолгРегл
		|	КОНЕЦ                                                           КАК ДолгРегл,
		|
		|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.Предоплата <> 0 ТОГДА ДАТАВРЕМЯ(1,1,1)
		|		КОГДА (ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И (РасчетыСПоставщикамиПоДокументам.Долг < 0
		|				ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгРегл < 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгУпр < 0
		|				ИЛИ РасчетыСПоставщикамиПоДокументам.Предоплата > 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл > 0
		|				ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр > 0)
		|		ИЛИ ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И (РасчетыСПоставщикамиПоДокументам.Предоплата < 0
		|				ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл < 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр < 0
		|				ИЛИ РасчетыСПоставщикамиПоДокументам.Долг > 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгРегл > 0 
		|				ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгУпр > 0)) 
		|			И НЕ (РасчетыСПоставщикамиПоДокументам.Регистратор ССЫЛКА Документ.ВводОстатков И 
		|					РасчетыСПоставщикамиПоДокументам.РасчетныйДокумент <> Неопределено)
		|			И НЕ РасчетыСПоставщикамиПоДокументам.Регистратор ССЫЛКА Документ.РасчетКурсовыхРазниц ТОГДА
		|			ЕСТЬNULL(ПорядокДокументов.ДатаПлатежа, ДАТАВРЕМЯ(1,1,1))
		|	ИНАЧЕ
		|		ЕСТЬNULL(ДатыВозникновения.ДатаПлатежа, ДАТАВРЕМЯ(1,1,1))
		|	КОНЕЦ                                                           КАК ДатаПлановогоПогашения,
		|	ЕСТЬNULL(ДатыВозникновения.ДатаВозникновения, ДАТАВРЕМЯ(1,1,1)) КАК ДатаВозникновения,
		|	ЕСТЬNULL(ПорядокДокументов.ПорядокОперации,"""")                КАК ПорядокОперации,
		|	ЕСТЬNULL(ПорядокДокументов.ПорядокЗачетаПоДатеПлатежа,"""")     КАК ПорядокЗачетаРегистратора,
		|	ЕСТЬNULL(ДатыВозникновения.ПорядокЗачетаПоДатеПлатежа,"""")     КАК ПорядокЗачета,
		|	ЕСТЬNULL(ДатыВозникновения.ВалютаДокумента,
		|		ЕСТЬNULL(ПорядокДокументов.ВалютаДокумента,
		|			ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)))              КАК ВалютаДокумента
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК РасчетыСПоставщикамиПоДокументам
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокДокументов КАК ПорядокДокументов
		|		ПО РасчетыСПоставщикамиПоДокументам.Регистратор = ПорядокДокументов.Регистратор
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокДокументов КАК ДатыВозникновения
		|		ПО РасчетыСПоставщикамиПоДокументам.РасчетныйДокумент = ДатыВозникновения.Регистратор
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КортежиКПроверке КАК КортежиКПроверке
		|			ПО КортежиКПроверке.АналитикаУчетаПоПартнерам = РасчетыСПоставщикамиПоДокументам.АналитикаУчетаПоПартнерам
		|				И КортежиКПроверке.ОбъектРасчетов         = РасчетыСПоставщикамиПоДокументам.ЗаказПоставщику
		|				И КортежиКПроверке.Валюта                 = РасчетыСПоставщикамиПоДокументам.Валюта
		|ГДЕ
		|	(НЕ РасчетыСПоставщикамиПоДокументам.Регистратор ССЫЛКА Документ.ВводОстатков
		|		ИЛИ РасчетыСПоставщикамиПоДокументам.Регистратор = РасчетыСПоставщикамиПоДокументам.РасчетныйДокумент)
		|	И (РасчетыСПоставщикамиПоДокументам.Предоплата <> 0
		|		ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл <> 0
		|		ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр <> 0
		|		ИЛИ РасчетыСПоставщикамиПоДокументам.Долг <> 0
		|		ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгРегл <> 0
		|		ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгУпр <> 0)
		|	И РасчетыСПоставщикамиПоДокументам.Активность
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументРегистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Расчеты.АналитикаУчетаПоПартнерам     КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ОбъектРасчетов                КАК ОбъектРасчетов,
		|	Расчеты.Валюта                        КАК Валюта,
		|	Расчеты.ДокументРегистратор           КАК ДокументРегистратор,
		|	Расчеты.РасчетныйДокумент             КАК РасчетныйДокумент,
		|	Расчеты.ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
		|	Расчеты.Период                        КАК Период,
		|	Расчеты.ДатаПлановогоПогашения        КАК ДатаПлановогоПогашения,
		|	Расчеты.ДатаВозникновения             КАК ДатаВозникновения,
		|	Расчеты.ВидДвижения                   КАК ВидДвижения,
		|	СУММА(Расчеты.Предоплата)             КАК Предоплата,
		|	СУММА(Расчеты.ПредоплатаУпр)          КАК ПредоплатаУпр,
		|	СУММА(Расчеты.ПредоплатаРегл)         КАК ПредоплатаРегл,
		|	СУММА(Расчеты.Долг)                   КАК Долг,
		|	СУММА(Расчеты.ДолгУпр)                КАК ДолгУпр,
		|	СУММА(Расчеты.ДолгРегл)               КАК ДолгРегл,
		|	Расчеты.СвязанныйДокумент             КАК СвязанныйДокумент,
		|	Расчеты.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	Расчеты.ПорядокОперации               КАК ПорядокОперации,
		|	Расчеты.ПорядокЗачета                 КАК ПорядокЗачета,
		|	Расчеты.ВалютаДокумента               КАК ВалютаДокумента,
		|	Расчеты.Организация                   КАК Организация
		|ИЗ (
		|	ВЫБРАТЬ
		|		ЗачетыОплат.АналитикаУчетаПоПартнерам                            КАК АналитикаУчетаПоПартнерам,
		|		ЗачетыОплат.ОбъектРасчетов                                       КАК ОбъектРасчетов,
		|		ЗачетыОплат.Валюта                                               КАК Валюта,
		|		ЗачетыОплат.ДокументРегистратор                                  КАК ДокументРегистратор,
		|		ЗачетыОплат.ДокументРегистратор                                  КАК РасчетныйДокумент,
		|		ЗачетыОплат.ХозяйственнаяОперация                                КАК ХозяйственнаяОперация,
		|		ЗачетыОплат.Период                                               КАК Период,
		|		ДатыВозникновения.ДатаПлатежа                                    КАК ДатаПлановогоПогашения,
		|		ЗачетыОплат.Период                                               КАК ДатаВозникновения,
		|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                           КАК ВидДвижения,
		|		0                                                                КАК Предоплата,
		|		0                                                                КАК ПредоплатаУпр,
		|		0                                                                КАК ПредоплатаРегл,
		|		ЗачетыОплат.Предоплата                                           КАК Долг,
		|		ЗачетыОплат.ПредоплатаУпр                                        КАК ДолгУпр,
		|		ЗачетыОплат.ПредоплатаРегл                                       КАК ДолгРегл,
		|		Неопределено                                                     КАК СвязанныйДокумент,
		|		ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)  КАК СтатьяДвиженияДенежныхСредств,
		|		ЗачетыОплат.ПорядокОперации                                      КАК ПорядокОперации,
		|		ЗачетыОплат.ПорядокЗачетаРегистратора                            КАК ПорядокЗачета,
		|		ЗачетыОплат.ВалютаДокумента                                      КАК ВалютаДокумента,
		|		ЗачетыОплат.АналитикаУчетаПоПартнерам.Организация                КАК Организация
		|	ИЗ ВтРасчеты КАК ЗачетыОплат
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокДокументов КАК ДатыВозникновения
		|			ПО ЗачетыОплат.ДокументРегистратор = ДатыВозникновения.Регистратор
		|	ГДЕ
		|		ЗачетыОплат.Предоплата > 0 
		|		И ЗачетыОплат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		И ЗачетыОплат.ХозяйственнаяОперация НЕ В (&ХозяйственныеОперацииНеОтгрузка)
		|			И ТИПЗНАЧЕНИЯ(ЗачетыОплат.ОбъектРасчетов) НЕ В (&ТипыПлатежныхДокументов)
		|			И НЕ (ЗачетыОплат.ОбъектРасчетов ССЫЛКА Документ.ПервичныйДокумент
		|				И ВЫРАЗИТЬ(ЗачетыОплат.ОбъектРасчетов КАК Документ.ПервичныйДокумент).ТипПервичногоДокумента В (&ТипыПервичногоДокументаПлатежки))
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗачетыОплат.АналитикаУчетаПоПартнерам                              КАК АналитикаУчетаПоПартнерам,
		|		ЗачетыОплат.ОбъектРасчетов                                         КАК ОбъектРасчетов,
		|		ЗачетыОплат.Валюта                                                 КАК Валюта,
		|		ЗачетыОплат.ДокументРегистратор                                    КАК ДокументРегистратор,
		|		ЗачетыОплат.ДокументРегистратор                                    КАК РасчетныйДокумент,
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетАвансаПоставщику) КАК ХозяйственнаяОперация,
		|		ЗачетыОплат.Период                                                 КАК Период,
		|		ДатыВозникновения.ДатаПлатежа                                      КАК ДатаПлановогоПогашения,
		|		ЗачетыОплат.Период                                                 КАК ДатаВозникновения,
		|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                             КАК ВидДвижения,
		|		0                                                                  КАК Предоплата,
		|		0                                                                  КАК ПредоплатаУпр,
		|		0                                                                  КАК ПредоплатаРегл,
		|		ЗачетыОплат.Предоплата                                             КАК Долг,
		|		ЗачетыОплат.ПредоплатаУпр                                          КАК ДолгУпр,
		|		ЗачетыОплат.ПредоплатаРегл                                         КАК ДолгРегл,
		|		Неопределено                                                       КАК СвязанныйДокумент,
		|		ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)    КАК СтатьяДвиженияДенежныхСредств,
		|		ЗачетыОплат.ПорядокОперации                                        КАК ПорядокОперации,
		|		ЗачетыОплат.ПорядокЗачетаРегистратора                              КАК ПорядокЗачета,
		|		ЗачетыОплат.ВалютаДокумента                                        КАК ВалютаДокумента,
		|		ЗачетыОплат.АналитикаУчетаПоПартнерам.Организация                  КАК Организация
		|	ИЗ ВтРасчеты КАК ЗачетыОплат
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокДокументов КАК ДатыВозникновения
		|			ПО ЗачетыОплат.ДокументРегистратор = ДатыВозникновения.Регистратор
		|	ГДЕ
		|		ЗачетыОплат.Предоплата > 0 
		|		И ЗачетыОплат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		И ЗачетыОплат.ХозяйственнаяОперация НЕ В (&ХозяйственныеОперацииНеОтгрузка)
		|			И ТИПЗНАЧЕНИЯ(ЗачетыОплат.ОбъектРасчетов) НЕ В (&ТипыПлатежныхДокументов)
		|			И НЕ (ЗачетыОплат.ОбъектРасчетов ССЫЛКА Документ.ПервичныйДокумент
		|				И ВЫРАЗИТЬ(ЗачетыОплат.ОбъектРасчетов КАК Документ.ПервичныйДокумент).ТипПервичногоДокумента В (&ТипыПервичногоДокументаПлатежки))
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		РасчетыПоДокументам.АналитикаУчетаПоПартнерам     КАК АналитикаУчетаПоПартнерам,
		|		РасчетыПоДокументам.ОбъектРасчетов                КАК ОбъектРасчетов,
		|		РасчетыПоДокументам.Валюта                        КАК Валюта,
		|		РасчетыПоДокументам.ДокументРегистратор           КАК ДокументРегистратор,
		|		РасчетыПоДокументам.РасчетныйДокумент             КАК РасчетныйДокумент,
		|		ВЫБОР КОГДА РасчетыПоДокументам.Предоплата <> 0 И РасчетыПоДокументам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				И РасчетыПоДокументам.ХозяйственнаяОперация НЕ В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности),
		|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеКредиторскойЗадолженности))
		|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетАвансаПоставщику)
		|			КОГДА РасчетыПоДокументам.Долг <> 0 И РасчетыПоДокументам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				И РасчетыПоДокументам.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности)
		|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПогашениеЗадолженностиПоставщику)
		|			ИНАЧЕ РасчетыПоДокументам.ХозяйственнаяОперация 
		|		КОНЕЦ КАК ХозяйственнаяОперация,
		|		РасчетыПоДокументам.Период                        КАК Период,
		|		РасчетыПоДокументам.ДатаПлановогоПогашения        КАК ДатаПлановогоПогашения,
		|		РасчетыПоДокументам.ДатаВозникновения             КАК ДатаВозникновения,
		|		РасчетыПоДокументам.ВидДвижения                   КАК ВидДвижения,
		|		РасчетыПоДокументам.Предоплата                    КАК Предоплата,
		|		РасчетыПоДокументам.ПредоплатаУпр                 КАК ПредоплатаУпр,
		|		РасчетыПоДокументам.ПредоплатаРегл                КАК ПредоплатаРегл,
		|		РасчетыПоДокументам.Долг                          КАК Долг,
		|		РасчетыПоДокументам.ДолгУпр                       КАК ДолгУпр,
		|		РасчетыПоДокументам.ДолгРегл                      КАК ДолгРегл,
		|		ВЫБОР КОГДА РасчетыПоДокументам.Предоплата > 0
		|			ТОГДА РасчетыПоДокументам.СвязанныйДокумент
		|			ИНАЧЕ Неопределено
		|		КОНЕЦ                                             КАК СвязанныйДокумент,
		|		РасчетыПоДокументам.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|		РасчетыПоДокументам.ПорядокОперации               КАК ПорядокОперации,
		|		РасчетыПоДокументам.ПорядокЗачета                 КАК ПорядокЗачета,
		|		РасчетыПоДокументам.ВалютаДокумента               КАК ВалютаДокумента,
		|		РасчетыПоДокументам.АналитикаУчетаПоПартнерам.Организация КАК Организация
		|	ИЗ ВтРасчеты КАК РасчетыПоДокументам) КАК Расчеты
		|СГРУППИРОВАТЬ ПО
		|	Расчеты.АналитикаУчетаПоПартнерам,
		|	Расчеты.ОбъектРасчетов,
		|	Расчеты.Валюта,
		|	Расчеты.ДокументРегистратор,
		|	Расчеты.РасчетныйДокумент,
		|	Расчеты.ХозяйственнаяОперация,
		|	Расчеты.Период,
		|	Расчеты.ДатаПлановогоПогашения,
		|	Расчеты.ДатаВозникновения,
		|	Расчеты.ВидДвижения,
		|	Расчеты.СвязанныйДокумент,
		|	Расчеты.СтатьяДвиженияДенежныхСредств,
		|	Расчеты.ПорядокОперации,
		|	Расчеты.ПорядокЗачета,
		|	Расчеты.ВалютаДокумента,
		|	Расчеты.Организация
		|	";
	
	Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли