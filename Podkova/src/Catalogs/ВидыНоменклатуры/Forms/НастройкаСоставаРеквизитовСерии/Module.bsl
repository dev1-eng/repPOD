#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры,
		Справочники.ВидыНоменклатуры.ИменаРеквизитовДляФормыНастройкаСоставаРеквизитовСерии("ОткрытиеФормыРедактирования"));
	
	ИменаСохраняемыхРеквизитов = Справочники.ВидыНоменклатуры.ИменаРеквизитовДляФормыНастройкаСоставаРеквизитовСерии(
																"СохранениеРезультатов");
	
	НастроитьФорму();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если АвтоматическиГенерироватьСерии Тогда
		
		Если Не ИспользоватьДатуПроизводстваСерии
			И ((АлкогольнаяПродукция
					И Не ИспользоватьПроизводителяЕГАИССерии
					И Не ИспользоватьСправку2ЕГАИССерии)
				Или ((ПодконтрольнаяПродукцияВЕТИС
					  Или ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС"))
					И Не ИспользоватьПроизводителяВЕТИССерии
					И Не ИспользоватьЗаписьСкладскогоЖурналаВЕТИССерии
					И Не ИспользоватьИдентификаторПартииВЕТИССерии)) Тогда
			
			ТекстСообщения = НСтр("ru = 'Автоматическая генерация серий возможна по реквизитам: ""Дата производства"", ""%Производитель%"", ""%ОсобенностьУчета%"". Включите использование одного из этих реквизитов.'");
			Производитель = ?(АлкогольнаяПродукция,
							Элементы.ИспользоватьПроизводителяЕГАИССерии.Заголовок,
							Элементы.ИспользоватьПроизводителяВЕТИССерии.Заголовок);
			
			ТекстОсобенностьУчета = ?(АлкогольнаяПродукция,
								Элементы.ИспользоватьСправку2ЕГАИССерии.Заголовок,
								Элементы.ИспользоватьЗаписьСкладскогоЖурналаВЕТИССерии.Заголовок + ""","
								+ Символы.НПП + """" +  Элементы.ИспользоватьИдентификаторПартииВЕТИССерии.Заголовок);
			
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Производитель%",    Производитель);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОсобенностьУчета%", ТекстОсобенностьУчета);
			
			ПоказатьПредупреждение(,ТекстСообщения);
			
			Возврат;
		КонецЕсли;
		
	Иначе
		
		Если Не ИспользоватьНомерСерии
			И Не ИспользоватьСрокГодностиСерии
			И Не ИспользоватьНомерКИЗГИСМСерии
			И Не ИспользоватьДатуПроизводстваСерии
			И Не ИспользоватьПроизводителяЕГАИССерии
			И Не ИспользоватьСправку2ЕГАИССерии
			И Не ИспользоватьПроизводителяВЕТИССерии
			И Не ИспользоватьЗаписьСкладскогоЖурналаВЕТИССерии
			И Не ИспользоватьИдентификаторПартииВЕТИССерии 
			И НЕ ИспользоватьМРЦМОТПСерии Тогда
			
			ТекстСообщения = НСтр("ru = 'Не выполнена настройка использования реквизитов серии.'");
			ПоказатьПредупреждение(,ТекстСообщения);
			
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Результат = Новый Структура(ИменаСохраняемыхРеквизитов);
	ЗаполнитьЗначенияСвойств(Результат, ЭтотОбъект);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапки

&НаКлиенте
Процедура ИспользоватьСрокГодностиСерииПриИзменении(Элемент)
	
	ИспользоватьДатуПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьДатуПроизводстваСерииПриИзменении(Элемент)
	
	ИспользоватьДатуПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаИспользованияСерийПриИзменении(Элемент)
	
	ЭтоЭкземпляр = (НастройкаИспользованияСерий
					= ПредопределенноеЗначение("Перечисление.НастройкиИспользованияСерийНоменклатуры.ЭкземплярТовара"));
	
	Если ЭтоЭкземпляр Тогда
		ИспользоватьНомерСерии     = Не КиЗГИСМ;
		ИспользоватьRFIDМеткиСерии = ПродукцияМаркируемаяДляГИСМ
									Или КиЗГИСМ; 
	Иначе
		ИспользоватьRFIDМеткиСерии = Ложь;
	КонецЕсли;
	
	НастроитьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура АвтоматическиГенерироватьСерииПриИзменении(Элемент)
	
	ИспользоватьНомерСерии = Ложь;
	
	НастроитьФорму();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИспользоватьДатуПриИзменении()
	
	Если (ИспользоватьСрокГодностиСерии
		Или ИспользоватьДатуПроизводстваСерии) Тогда
		
		Если Не ЗначениеЗаполнено(ТочностьУказанияСрокаГодностиСерии) Тогда
			ТочностьУказанияСрокаГодностиСерии = ПредопределенноеЗначение("Перечисление.ТочностиУказанияСрокаГодности.СТочностьюДоДней");
		КонецЕсли;
	Иначе
		ТочностьУказанияСрокаГодностиСерии = ПредопределенноеЗначение("Перечисление.ТочностиУказанияСрокаГодности.ПустаяСсылка");
	КонецЕсли;
	
	НастроитьФорму();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФорму()
	
	ПродукцияИСМП = Новый Массив;
	ПродукцияИСМП.Добавить(Перечисления.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС);
	ПродукцияИСМП.Добавить(Перечисления.ОсобенностиУчетаНоменклатуры.ТабачнаяПродукция);
	ПродукцияИСМП.Добавить(Перечисления.ОсобенностиУчетаНоменклатуры.Велосипеды);
	ПродукцияИСМП.Добавить(Перечисления.ОсобенностиУчетаНоменклатуры.Духи);
	ПродукцияИСМП.Добавить(Перечисления.ОсобенностиУчетаНоменклатуры.КреслаКоляски);
	ПродукцияИСМП.Добавить(Перечисления.ОсобенностиУчетаНоменклатуры.ЛегкаяПромышленность);
	ПродукцияИСМП.Добавить(Перечисления.ОсобенностиУчетаНоменклатуры.Фотоаппараты);
	ПродукцияИСМП.Добавить(Перечисления.ОсобенностиУчетаНоменклатуры.Шины);
	ПродукцияИСМП.Добавить(Перечисления.ОсобенностиУчетаНоменклатуры.АльтернативныйТабак);
	ПродукцияИСМП.Добавить(Перечисления.ОсобенностиУчетаНоменклатуры.УпакованнаяВода);
	ПродукцияИСМП.Добавить(Перечисления.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияБезВЕТИС);
	
	МолочнаяПродукцияПодконтрольнаяВЕТИС = (ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС);
	
	Элементы.ИспользоватьНомерКИЗГИСМСерии.Видимость = ПродукцияМаркируемаяДляГИСМ
														Или КиЗГИСМ;
	
	Элементы.ИспользоватьНомерСерии.Доступность = Не (НастройкаИспользованияСерий = Перечисления.НастройкиИспользованияСерийНоменклатуры.ЭкземплярТовара
														Или ПродукцияМаркируемаяДляГИСМ
														Или КиЗГИСМ)
														И Не АвтоматическиГенерироватьСерии;
		
	Элементы.ИспользоватьСрокГодностиСерии.Доступность     = Не КиЗГИСМ;
	Элементы.ИспользоватьДатуПроизводстваСерии.Доступность = Не КиЗГИСМ;
	
	Элементы.НастройкаИспользованияСерий.Доступность = Не (ПродукцияМаркируемаяДляГИСМ
															Или КиЗГИСМ)
														И Не АлкогольнаяПродукция
														И Не ПодконтрольнаяПродукцияВЕТИС
														И Не МолочнаяПродукцияПодконтрольнаяВЕТИС;
	
	Элементы.ТочностьУказанияСрокаГодностиСерии.Доступность = ИспользоватьСрокГодностиСерии
																Или ИспользоватьДатуПроизводстваСерии;
	
	Элементы.ИспользоватьRFIDМетки.Видимость = (НастройкаИспользованияСерий
													= Перечисления.НастройкиИспользованияСерийНоменклатуры.ЭкземплярТовара);
	
	Элементы.ИспользоватьRFIDМетки.Доступность = Не ПродукцияМаркируемаяДляГИСМ
													И Не КиЗГИСМ;
	
	Элементы.АвтоматическиГенерироватьСерии.Видимость = АлкогольнаяПродукция
														Или ПодконтрольнаяПродукцияВЕТИС
														Или ПродукцияИСМП.Найти(ОсобенностьУчета) <> Неопределено;
	
	Элементы.ИспользоватьПроизводителяЕГАИССерии.Видимость = АлкогольнаяПродукция;
	Элементы.ИспользоватьСправку2ЕГАИССерии.Видимость      = АлкогольнаяПродукция;
	
	Элементы.ИспользоватьПроизводителяВЕТИССерии.Видимость           = ПодконтрольнаяПродукцияВЕТИС Или МолочнаяПродукцияПодконтрольнаяВЕТИС;
	Элементы.ИспользоватьЗаписьСкладскогоЖурналаВЕТИССерии.Видимость = ПодконтрольнаяПродукцияВЕТИС Или МолочнаяПродукцияПодконтрольнаяВЕТИС;
	Элементы.ИспользоватьИдентификаторПартииВЕТИССерии.Видимость     = ПодконтрольнаяПродукцияВЕТИС Или МолочнаяПродукцияПодконтрольнаяВЕТИС;
	
	Элементы.ИспользоватьМРЦМОТПСерии.Видимость = (ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.ТабачнаяПродукция);
	
КонецПроцедуры

#КонецОбласти
