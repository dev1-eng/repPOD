#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		ОбщегоНазначенияУТ.ПодготовитьДанныеДляСинхронизацииКлючей(ЭтотОбъект, ПараметрыСинхронизацииКлючей());
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПараметрыШаблона = ЗначениеНастроекПовтИсп.НастройкиИспользованияСерий(ВидНоменклатуры);
	
	Номер = СокрЛП(Номер);
	
	Если ПараметрыШаблона.ТочностьУказанияСрокаГодностиСерии = Перечисления.ТочностиУказанияСрокаГодности.СТочностьюДоЧасов Тогда
		ГоденДо = НачалоЧаса(ГоденДо);
	ИначеЕсли ПараметрыШаблона.ТочностьУказанияСрокаГодностиСерии = Перечисления.ТочностиУказанияСрокаГодности.СТочностьюДоМесяцев Тогда
		ГоденДо = НачалоМесяца(ГоденДо);
	Иначе
		ГоденДо = НачалоДня(ГоденДо);
	КонецЕсли;
	
	Наименование = НоменклатураКлиентСервер.ПредставлениеСерии(ПараметрыШаблона, ЭтотОбъект);
	
	ОбщегоНазначенияУТ.ПодготовитьДанныеДляСинхронизацииКлючей(ЭтотОбъект, ПараметрыСинхронизацииКлючей());
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	ОбщегоНазначенияУТ.СинхронизироватьКлючи(ЭтотОбъект);
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ПараметрыШаблона = ЗначениеНастроекПовтИсп.НастройкиИспользованияСерий(ВидНоменклатуры);

	Если Не ПараметрыШаблона.ИспользоватьНомерСерии Тогда 
		МассивНепроверяемыхРеквизитов.Добавить("Номер");
	КонецЕсли;
	
	Если Не ПараметрыШаблона.ИспользоватьСрокГодностиСерии Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ГоденДо");
	КонецЕсли;
	
	Если Не ПараметрыШаблона.ИспользоватьДатуПроизводстваСерии Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДатаПроизводства");
	КонецЕсли;
	
	Если Не ПараметрыШаблона.ИспользоватьПроизводителяЕГАИССерии Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ПроизводительЕГАИС");
	КонецЕсли;
		
	Если Не ПараметрыШаблона.ИспользоватьСправку2ЕГАИССерии Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Справка2ЕГАИС");
	КонецЕсли;
	
	Если Не ПараметрыШаблона.ИспользоватьПроизводителяВЕТИССерии Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ПроизводительВЕТИС");
	КонецЕсли;
		
	Если Не ПараметрыШаблона.ИспользоватьЗаписьСкладскогоЖурналаВЕТИССерии Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ЗаписьСкладскогоЖурналаВЕТИС");
	КонецЕсли;
	
	Если Не ПараметрыШаблона.ИспользоватьИдентификаторПартииВЕТИССерии Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ИдентификаторПартииВЕТИС");
	КонецЕсли;
	
	Если НЕ ПараметрыШаблона.ИспользоватьМРЦМОТПСерии Тогда
		МассивНепроверяемыхРеквизитов.Добавить("МаксимальнаяРозничнаяЦенаМОТП");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ДанныеЗаполнения = "АвтоТест" Тогда
		Возврат;
	КонецЕсли;
	
	ВидНоменклатуры = ДанныеЗаполнения.ВидНоменклатуры;
	ПараметрыПолитики = Новый ФиксированнаяСтруктура(ЗначениеНастроекПовтИсп.НастройкиИспользованияСерий(ВидНоменклатуры));
	
	СтруктураПолей = Новый Структура;
	
	Если ПараметрыПолитики.ИспользоватьДатуПроизводстваСерии Тогда
		СтруктураПолей.Вставить("ДатаПроизводства");	
	КонецЕсли;
	
	Если ПараметрыПолитики.ИспользоватьСрокГодностиСерии Тогда
		СтруктураПолей.Вставить("ГоденДо");	
	КонецЕсли;
	
	Если ПараметрыПолитики.ИспользоватьНомерСерии Тогда
		СтруктураПолей.Вставить("Номер");	
	КонецЕсли;
	
	Если ПараметрыПолитики.ИспользоватьПроизводителяЕГАИССерии Тогда
		СтруктураПолей.Вставить("ПроизводительЕГАИС");	
	КонецЕсли;
	
	Если ПараметрыПолитики.ИспользоватьСправку2ЕГАИССерии Тогда
		СтруктураПолей.Вставить("Справка2ЕГАИС");	
	КонецЕсли;
	
	Если ПараметрыПолитики.ИспользоватьПроизводителяВЕТИССерии Тогда
		СтруктураПолей.Вставить("ПроизводительВЕТИС");	
	КонецЕсли;
	
	Если ПараметрыПолитики.ИспользоватьЗаписьСкладскогоЖурналаВЕТИССерии Тогда
		СтруктураПолей.Вставить("ЗаписьСкладскогоЖурналаВЕТИС");	
	КонецЕсли;
	
	Если ПараметрыПолитики.ИспользоватьИдентификаторПартииВЕТИССерии Тогда
		СтруктураПолей.Вставить("ИдентификаторПартииВЕТИС");	
	КонецЕсли;
	
	Если ПараметрыПолитики.ИспользоватьНомерКИЗГИСМСерии Тогда
		СтруктураПолей.Вставить("НомерКиЗГИСМ");	
	КонецЕсли;
	
	Если ПараметрыПолитики.ИспользоватьМРЦМОТПСерии Тогда
		СтруктураПолей.Вставить("МаксимальнаяРозничнаяЦенаМОТП");
	КонецЕсли;
	
	Если ПараметрыПолитики.ИспользоватьRFIDМеткиСерии Тогда
		СтруктураПолей.Вставить("RFIDTID");	
		СтруктураПолей.Вставить("RFIDUser");	
		СтруктураПолей.Вставить("RFIDEPC");	
		СтруктураПолей.Вставить("EPCGTIN");	
		СтруктураПолей.Вставить("RFIDМеткаНеЧитаемая");	
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(СтруктураПолей, ДанныеЗаполнения);
	
	Если СтруктураПолей.Свойство("ДатаПроизводства") 
		И ТипЗнч(ДанныеЗаполнения) = Тип("СтрокаТаблицыЗначений")
		И ДанныеЗаполнения.Владелец().Колонки.Найти("Справка1ДатаРозлива") <> Неопределено Тогда
		СтруктураПолей.ДатаПроизводства = ДанныеЗаполнения.Справка1ДатаРозлива;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураПолей);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПараметрыСинхронизацииКлючей()
	
	Результат = Новый Соответствие;
	
	Результат.Вставить("Справочник.КлючиАналитикиУчетаНоменклатуры", "ПометкаУдаления");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
#КонецЕсли