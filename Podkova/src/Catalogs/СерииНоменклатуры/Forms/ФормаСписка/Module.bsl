
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Возврат при получении формы для анализа.
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Справочники.СерииНоменклатуры);
	
	Элементы.ФормаИзменитьВыделенные.Видимость                 = МожноРедактировать;
	Элементы.СписокКонтекстноеМенюИзменитьВыделенные.Видимость = МожноРедактировать;
	
	ВидНоменклатурыПриИзмененииСервер();
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидНоменклатурыПриИзменении(Элемент)
	
	ВидНоменклатурыПриИзмененииСервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Не Копирование Тогда
		Отказ = Истина;
		
		Если Не ЗначениеЗаполнено(ВидНоменклатуры) Тогда
			ТекстПредупреждения = НСтр("ru = 'Перед добавлением серии необходимо указать вид номенклатуры.'");
			
			ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
			
			Возврат;
		ИначеЕсли ЗначениеЗаполнено(ПараметрыШаблона)
			И Не ПараметрыШаблона.ИспользоватьСерии Тогда
			
			ТекстПредупреждения = НСтр("ru = 'Невозможно добавить новую серию, так как в настройках вида номенклатуры выключена возможность использования серий.'");
			
			ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
			
			Возврат;
			
		КонецЕсли;
		
		ЗначенияЗаполнения = Новый Структура("ВидНоменклатуры", ВидНоменклатуры);
		
		ОткрытьФорму("Справочник.СерииНоменклатуры.ФормаОбъекта",
			Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения), Элементы.Список);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВидНоменклатурыПриИзмененииСервер()
	
	ПараметрыШаблона = Неопределено;
	ВладелецСерий    = Справочники.ВидыНоменклатуры.ПустаяСсылка();
	
	Если ЗначениеЗаполнено(ВидНоменклатуры) Тогда
		ПараметрыШаблона = Новый ФиксированнаяСтруктура(
									ЗначениеНастроекПовтИсп.НастройкиИспользованияСерий(ВидНоменклатуры));
		
		ВладелецСерий = ПараметрыШаблона.ВладелецСерий;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
																			"ВидНоменклатуры",
																			ВладелецСерий,
																			ВидСравненияКомпоновкиДанных.Равно,
																			,
																			Истина);
	
	НастроитьФорму();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФорму()
	
	Если ПараметрыШаблона = Неопределено
		Или (ЗначениеЗаполнено(ПараметрыШаблона)
			И Не ПараметрыШаблона.ИспользоватьСерии) Тогда
		
		ВестиСведенияДляДекларацийАлкоВРознице = ПолучитьФункциональнуюОпцию("ВестиСведенияДляДекларацийАлкоВРознице");
		ВестиУчетПодконтрольныхТоваровВЕТИС    = ПолучитьФункциональнуюОпцию("ВестиУчетПодконтрольныхТоваровВЕТИС");
		
		Элементы.Номер.Видимость = Истина;
		
		Элементы.ДатаПроизводства.Видимость = Истина;
		Элементы.ГоденДо.Видимость          = Истина;
		
		Элементы.НомерКИЗГИСМ.Видимость = ПолучитьФункциональнуюОпцию("ВестиУчетМаркировкиПродукцииВГИСМ");
		
		Элементы.ПроизводительЕГАИС.Видимость = ВестиСведенияДляДекларацийАлкоВРознице;
		Элементы.Справка2ЕГАИС.Видимость      = ВестиСведенияДляДекларацийАлкоВРознице;
		
		Элементы.ПроизводительВЕТИС.Видимость           = ВестиУчетПодконтрольныхТоваровВЕТИС;
		Элементы.ЗаписьСкладскогоЖурналаВЕТИС.Видимость = ВестиУчетПодконтрольныхТоваровВЕТИС;
		
	Иначе
		
		Элементы.Номер.Видимость = ПараметрыШаблона.ИспользоватьНомерСерии;
		
		Элементы.ДатаПроизводства.Видимость = ПараметрыШаблона.ИспользоватьДатуПроизводстваСерии;
		Элементы.ГоденДо.Видимость          = ПараметрыШаблона.ИспользоватьСрокГодностиСерии;
		
		Если ПараметрыШаблона.ИспользоватьДатуПроизводстваСерии Тогда
			Элементы.ДатаПроизводства.Формат = ПараметрыШаблона.ФорматнаяСтрокаСрокаГодности;
		КонецЕсли;
		
		Если ПараметрыШаблона.ИспользоватьСрокГодностиСерии Тогда
			Элементы.ГоденДо.Формат = ПараметрыШаблона.ФорматнаяСтрокаСрокаГодности;
		КонецЕсли;
		
		Элементы.НомерКИЗГИСМ.Видимость = ПараметрыШаблона.ИспользоватьНомерКИЗГИСМСерии;
		
		Элементы.ПроизводительЕГАИС.Видимость = ПараметрыШаблона.ИспользоватьПроизводителяЕГАИССерии;
		Элементы.Справка2ЕГАИС.Видимость      = ПараметрыШаблона.ИспользоватьСправку2ЕГАИССерии;
		
		Элементы.ПроизводительВЕТИС.Видимость           = ПараметрыШаблона.ИспользоватьПроизводителяВЕТИССерии;
		Элементы.ЗаписьСкладскогоЖурналаВЕТИС.Видимость = ПараметрыШаблона.ИспользоватьЗаписьСкладскогоЖурналаВЕТИССерии;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти