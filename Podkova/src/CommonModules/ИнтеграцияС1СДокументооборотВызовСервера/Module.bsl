////////////////////////////////////////////////////////////////////////////////
// Подсистема "Интеграция с 1С:Документооборотом"
// Модуль ИнтеграцияС1СДокументооборотВызовСервера: сервер, вызов сервера
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ОбщиеПроцедурыИФункции

//Проверяет доступность функционала версии сервиса.
//Параметры:
//	ВерсияСервиса - версия сервиса, содержащая нужный функционал
//Возвращает:
//	Признак доступности функционала версии сервиса
Функция ДоступенФункционалВерсииСервиса(ВерсияСервиса = "") Экспорт
	
	Возврат ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса(ВерсияСервиса);
	
КонецФункции

// Получает настройки базы Документооборота.
//
// Возвращаемое значение:
//   Структура настроек Документооборота со свойствами:
//     * НужноИзвлечьТекст - Булево
//     * ИспользоватьЭлектронныеЦифровыеПодписи - Булево
//     * ДобавлятьРаботуВЕжедневныйОтчетПриВыполненииЗадачи - Булево
//     * ФактическийИсполнительЗадач - DMActualTasksPerformer
//     * ВестиУчетСканКопийОригиналовДокументов - Булево
//     * ПоказыватьЗанятыеФайлыПриЗавершенииРаботы - Булево
//     * ИспользоватьАвтозаполнениеФайлов - Булево
//     * ИзменятьЗаданияЗаднимЧислом - Булево
//
Функция ПолучитьНастройки() Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetSettingsRequest");
	
	НастройкиXDTO = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, НастройкиXDTO);
	
	Настройки = Новый Структура("НужноИзвлечьТекст, ИспользоватьЭлектронныеЦифровыеПодписи",
		НастройкиXDTO.needExtractText, НастройкиXDTO.useDigitalSignatures);
	Если НастройкиXDTO.Свойства().Получить("addActualWorkUponTaskExecution") <> Неопределено Тогда
		Настройки.Вставить("ДобавлятьРаботуВЕжедневныйОтчетПриВыполненииЗадачи",
			НастройкиXDTO.addActualWorkUponTaskExecution);
	Иначе
		Настройки.Вставить("ДобавлятьРаботуВЕжедневныйОтчетПриВыполненииЗадачи", Ложь);
	КонецЕсли;
	
	Настройки.Вставить("ФактическийИсполнительЗадач", "taskPerformer");
	Если НастройкиXDTO.Свойства().Получить("actualTasksPerformer") <> Неопределено
		И НастройкиXDTO.Установлено("actualTasksPerformer") Тогда
		Настройки.ФактическийИсполнительЗадач = НастройкиXDTO.actualTasksPerformer;
	КонецЕсли;
	
	Если НастройкиXDTO.Свойства().Получить("accountForScannedOriginals") <> Неопределено Тогда
		Настройки.Вставить("ВестиУчетСканКопийОригиналовДокументов",
			НастройкиXDTO.accountForScannedOriginals);
	Иначе
		Настройки.Вставить("ВестиУчетСканКопийОригиналовДокументов",
			Ложь);
	КонецЕсли;
	
	Если НастройкиXDTO.Свойства().Получить("showLockedFilesOnExit") <> Неопределено Тогда
		Настройки.Вставить("ПоказыватьЗанятыеФайлыПриЗавершенииРаботы",
			НастройкиXDTO.showLockedFilesOnExit);
	Иначе
		Настройки.Вставить("ПоказыватьЗанятыеФайлыПриЗавершенииРаботы",
			Ложь);
	КонецЕсли;
	
	Если НастройкиXDTO.Свойства().Получить("useAutoFill") <> Неопределено Тогда
		Настройки.Вставить("ИспользоватьАвтозаполнениеФайлов",
			НастройкиXDTO.useAutoFill);
	Иначе
		Настройки.Вставить("ИспользоватьАвтозаполнениеФайлов",
			Ложь);
	КонецЕсли;
	
	Если НастройкиXDTO.Свойства().Получить("allowToChangeEndDate") <> Неопределено Тогда
		Настройки.Вставить("ИзменятьЗаданияЗаднимЧислом",
			НастройкиXDTO.allowToChangeEndDate);
	Иначе
		Настройки.Вставить("ИзменятьЗаданияЗаднимЧислом",
			Истина);
	КонецЕсли;
	
	Возврат Новый ФиксированнаяСтруктура(Настройки);
	
	
КонецФункции

// Устарела. Рекомендуется использовать ИнтеграцияС1СДокументооборотВызовСервера.ПодходящиеПравила.
//
Функция ИмяТипаОбъектаДОИзНастроекЗаполнения(СсылкаНаВнешнийОбъект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиЗаполнения.ТипОбъектаИС КАК ТипОбъектаИС,
		|	НастройкиЗаполнения.ТипОбъектаДО КАК ТипОбъектаДО
		|ИЗ
		|	Справочник.ПравилаИнтеграцииС1СДокументооборотом КАК НастройкиЗаполнения
		|ГДЕ
		|	НастройкиЗаполнения.ТипОбъектаИС = &ТипВнешнегоОбъекта";
	
	Запрос.УстановитьПараметр("ТипВнешнегоОбъекта", СсылкаНаВнешнийОбъект.Метаданные().ПолноеИмя()); 
	Результат = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ИмяТипа = ВыборкаДетальныеЗаписи.ТипОбъектаДО;
		Прервать;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ИмяТипа;
	
КонецФункции

// Устарела. Рекомендуется использовать ИнтеграцияС1СДокументооборотВызовСервера.ПодходящиеПравила.
//
Функция ИмяТипаОбъектаДОИзНастроекЗаполненияПоПолномуИмени(МетаданныеПолноеИмя) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиЗаполнения.ТипОбъектаИС КАК ТипОбъектаИС,
		|	НастройкиЗаполнения.ТипОбъектаДО КАК ТипОбъектаДО
		|ИЗ
		|	Справочник.ПравилаИнтеграцииС1СДокументооборотом КАК НастройкиЗаполнения
		|ГДЕ
		|	НастройкиЗаполнения.ТипОбъектаИС = &ТипВнешнегоОбъекта";
	
	Запрос.УстановитьПараметр("ТипВнешнегоОбъекта", МетаданныеПолноеИмя); 
	Результат = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ИмяТипа = ВыборкаДетальныеЗаписи.ТипОбъектаДО;
		Прервать;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ИмяТипа;
	
КонецФункции

//Получает представление для указанного типа XDTO.
//Параметры:
//	ОбъектТип - строка типа объекта XDTO
//Возвращает:
//	Строку представления типа объекта 1С:Документооборота
Функция ПредставлениеТипа(ОбъектТип)

	ПредставлениеТипа = "";
	
	Если ОбъектТип = "DMFile" Тогда
		ПредставлениеТипа = НСтр("ru='Файл'");
		
	ИначеЕсли ОбъектТип = "DMInternalDocument" Тогда
		ПредставлениеТипа =  НСтр("ru='Внутренний документ'");
		
	ИначеЕсли ОбъектТип = "DMIncomingDocument" Тогда
		ПредставлениеТипа =  НСтр("ru='Входящий документ'");
		
	ИначеЕсли ОбъектТип = "DMOutgoingDocument" Тогда
		ПредставлениеТипа =  НСтр("ru='Исходящий документ'");
		
	КонецЕсли;
	
	Возврат ПредставлениеТипа;

КонецФункции

// Возвращает структуру данных для заполнения объекта ИС на основании объекта ДО.
//
// Параметры:
//   Правило - СправочникСсылка.ПравилаИнтеграцииС1СДокументооборотом - используемое правило.
//   ТипОбъектаДО - Строка - имя типа XDTO-объекта Документооборота
//   ИдентификаторОбъектаДО - Строка - идентификатор объекта Документооборота
//
// Возвращаемое значение:
//   Структура - данные заполнения со свойствами:
//     ИмяФормы - Строка - имя формы объекта этой конфигурации.
//     Объект1СДокументооборота - структура, описывающая объект Документооборота.
//       ID - Строка - идентификатор объекта.
//       Тип - Строка - тип объекта.
//     ТипСоздаваемогоОбъекта - Строка - тип создаваемого объекта ИС.
//     ЗначенияРеквизитов - Структура, ключ - имя реквизита, значение - значение реквизита.
//     ДополнительныеРеквизиты - Структура, значения дополнительных реквизитов.
//     НаличиеПрисоединенныхФайлов - Булево, Неопределено - признак наличия присоединенных файлов.
//
Функция ДанныеЗаполненияИнтегрируемогоОбъекта(Правило, ТипОбъектаДО, ИдентификаторОбъектаДО) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMRetrieveRequest");
	
	ОбъектИд = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, ИдентификаторОбъектаДО, ТипОбъектаДО);
	Запрос.objectIds.Добавить(ОбъектИд);
	
	Результат = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	ОбъектXDTO = Результат.objects[0];
	
	ТипОбъектаИС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Правило, "ТипОбъектаИС");
	ИмяФормы = ТипОбъектаИС + ".ФормаОбъекта";
	
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ТипОбъектаИС);
	МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(ТипОбъектаИС);
	Если ОбщегоНазначения.ЭтоСправочник(МетаданныеОбъекта)
		Или ОбщегоНазначения.ЭтоПланВидовРасчета(МетаданныеОбъекта) Тогда
		Объект = МенеджерОбъекта.СоздатьЭлемент();
	ИначеЕсли ОбщегоНазначения.ЭтоДокумент(МетаданныеОбъекта) Тогда
		Объект = МенеджерОбъекта.СоздатьДокумент();
	КонецЕсли;
	
	Справочники.ПравилаИнтеграцииС1СДокументооборотом.ЗаполнитьОбъектПоОбъектуXDTO(Прокси, Объект, ОбъектXDTO, Правило);
	
	РеквизитыОбъекта = Справочники.ПравилаИнтеграцииС1СДокументооборотом.ПолучитьРеквизитыОбъектаИС(ТипОбъектаИС);
	
	// Перенесем данные объекта в структуру.
	ЗначенияРеквизитов = Новый Структура;
	РеквизитыШапки = РеквизитыОбъекта.НайтиСтроки(
		Новый Структура("ЭтоДополнительныйРеквизитИС, ЭтоТаблица, Таблица", Ложь, Ложь, ""));
	Для Каждого РеквизитыШапки Из РеквизитыШапки Цикл
		ЗначенияРеквизитов.Вставить(РеквизитыШапки.Имя, Объект[РеквизитыШапки.Имя]);
	КонецЦикла;
	ТабличныеЧасти = РеквизитыОбъекта.НайтиСтроки(
		Новый Структура("ЭтоДополнительныйРеквизитИС, ЭтоТаблица", Ложь, Истина));
	Для Каждого ТабличнаяЧасть Из ТабличныеЧасти Цикл
		ЗначенияРеквизитов.Вставить(ТабличнаяЧасть.Имя, Новый Массив);
		РеквизитыТабличнойЧасти = РеквизитыОбъекта.НайтиСтроки(
			Новый Структура("ЭтоДополнительныйРеквизитИС, Таблица", Ложь, ТабличнаяЧасть.Имя));
		Для Каждого Строка Из Объект[ТабличнаяЧасть.Имя] Цикл
			СтруктураСтроки = Новый Структура;
			Для Каждого РеквизитТабличнойЧасти Из РеквизитыТабличнойЧасти Цикл
				СтруктураСтроки.Вставить(РеквизитТабличнойЧасти.Имя, Строка[РеквизитТабличнойЧасти.Имя]);
			КонецЦикла;
			ЗначенияРеквизитов[ТабличнаяЧасть.Имя].Добавить(СтруктураСтроки);
		КонецЦикла;
	КонецЦикла;
	
	ДополнительныеРеквизиты = Новый Массив;
	
	Если Объект.Ссылка.Метаданные().ТабличныеЧасти.Найти("ДополнительныеРеквизиты") <> Неопределено Тогда
		Для каждого Строка Из Объект.ДополнительныеРеквизиты Цикл
			ДопРеквизит = Новый Структура;
			ДопРеквизит.Вставить("Свойство",Строка.Свойство);
			ДопРеквизит.Вставить("Значение", Строка.Значение);
			ДополнительныеРеквизиты.Добавить(ДопРеквизит);
		КонецЦикла;
	КонецЕсли;
	
	Объект1СДокументооборота = Новый Структура("ID, Тип", ИдентификаторОбъектаДО, ТипОбъектаДО);
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("ИмяФормы", ИмяФормы);
	ДанныеЗаполнения.Вставить("Объект1СДокументооборота", Объект1СДокументооборота);
	ДанныеЗаполнения.Вставить("ТипСоздаваемогоОбъекта", ТипОбъектаИС);
	ДанныеЗаполнения.Вставить("ЗначенияРеквизитов", ЗначенияРеквизитов);
	ДанныеЗаполнения.Вставить("ДополнительныеРеквизиты", ДополнительныеРеквизиты);
	
	Если ОбъектXDTO.Свойства().Получить("files") <> Неопределено
		И ОбъектXDTO.Установлено("files") Тогда
		ДанныеЗаполнения.Вставить("НаличиеПрисоединенныхФайлов", ОбъектXDTO.files.Количество() <> 0);
	Иначе
		ДанныеЗаполнения.Вставить("НаличиеПрисоединенныхФайлов", Неопределено);
	КонецЕсли;
	
	Возврат ДанныеЗаполнения;
	
КонецФункции // ДанныеЗаполненияИнтегрируемогоОбъекта

// Возвращает правила интеграции по объекту ДО в виде списка значений для предъявления пользователю.
//
// Параметры:
//   ОбъектДО - ОбъектXDTO - объект ДО, источник данных заполнения.
//
// Возвращаемое значение:
//   СписокЗначений - правила интеграции и представления создаваемых объектов ИС.
//
Функция ПравилаЗаполненияИнтегрированныхОбъектовСписком(ОбъектДО) Экспорт
	
	Результат = Новый СписокЗначений;
	
	Правила = ПодходящиеПравила(, ОбъектДО);
	
	Для каждого Правило Из Правила Цикл
		Результат.Добавить(Правило.Ссылка, Правило.ПредставлениеОбъектаИС);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Добавляет связь объекта ИС с объектом ДО.
//
// Параметры:
//   ID - Строка - идентификатор объекта Документооборота.
//   Тип - Строка - XDTO-тип объекта Документооборота.
//   ИнтегрированныйОбъект - ЛюбаяСсылка - объект ИС.
//   НаличиеПрисоединенныхФайлов - Булево - Истина, если в ДО есть присоединенные файлы.
//
Процедура ДобавитьСвязь(ID, Тип, ИнтегрированныйОбъект, НаличиеПрисоединенныхФайлов = Неопределено) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		РегистрыСведений.ОбъектыИнтегрированныеС1СДокументооборотом.ДобавитьСвязь(ID, Тип, ИнтегрированныйОбъект);
		
		Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
		
		Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMAddObjectLinkRequest");
		
		Запрос.ownerObject = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "ExternalObject");
		Запрос.ownerObject.id = Строка(ИнтегрированныйОбъект.УникальныйИдентификатор());
		Запрос.ownerObject.type = ИнтегрированныйОбъект.Метаданные().ПолноеИмя();
		Запрос.ownerObject.name = Строка(ИнтегрированныйОбъект);
		
		Запрос.linkedObject = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectID");
		Запрос.linkedObject.id = ID;
		Запрос.linkedObject.type = Тип;
		
		Ответ = Прокси.execute(Запрос);
		ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
		
		Если НаличиеПрисоединенныхФайлов = Истина Тогда
			ИнтеграцияС1СДокументооборотПереопределяемый.ПриПоявленииПрисоединенныхФайловДокументооборота(
				ID,
				Тип,
				ИнтегрированныйОбъект);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ЗаписьЖурналаРегистрации(
			ИнтеграцияС1СДокументооборот.ИмяСобытияЖурналаРегистрации(
				НСтр("ru = 'Не удалось добавить связь'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка())),
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		
	КонецПопытки;
	
КонецПроцедуры

//Удаляет связь объекта с объектом Документооборота.
//Параметры:
//	ID - идентификатор объекта Документооборота
//	Тип - тип XDTO объекта Документооборота
//	ИнтегрированныйОбъект - ссылка на объект системы
//
Процедура УдалитьСвязь(ID, Тип, ИнтегрированныйОбъект) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		РегистрыСведений.ОбъектыИнтегрированныеС1СДокументооборотом.УдалитьСвязь(ID, Тип, ИнтегрированныйОбъект);
		
		Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
		
		Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMRemoveObjectLinkRequest");
				
		Запрос.ownerObject = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "ExternalObject");
		Запрос.ownerObject.id = Строка(ИнтегрированныйОбъект.УникальныйИдентификатор());
		Запрос.ownerObject.type = ИнтегрированныйОбъект.Метаданные().ПолноеИмя();
		Запрос.ownerObject.name = Строка(ИнтегрированныйОбъект);
		
		Запрос.linkedObject = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectID");
		Запрос.linkedObject.id = ID;
		Запрос.linkedObject.type = Тип;
		
		Ответ = Прокси.execute(Запрос);
		ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ЗаписьЖурналаРегистрации(
			ИнтеграцияС1СДокументооборот.ИмяСобытияЖурналаРегистрации(
				НСтр("ru = 'Не удалось удалить связь'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка())),
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		
	КонецПопытки;
	
КонецПроцедуры

// Сохраняет имя пользователя ДО и, если необходимо, пароль.
//
// Параметры:
//   ИмяПользователя - Строка - имя пользователя ДО.
//   Пароль - Строка - пароль пользователя ДО.
//   ИмяКомпьютера - Строка - имя компьютера, где хранится часть пароля, или:
//                 - Неопределено - признак того, что пароль сохранять не нужно.
//   ЧастьПароляВИБ - Строка - пароль целиком или его часть, хранимая в ИБ.
//   ВременныйФайлЧастиПароля - Строка - путь ко временному файлу с другой частью пароля.
//   ИспользуетсяАутентификацияОС - Булево - Истина, если используется аутентификация операционной системы.
//
Процедура СохранитьНастройкиАвторизации(Знач ИмяПользователя, Знач Пароль,
	Знач ИмяКомпьютера = Неопределено, Знач ЧастьПароляВИБ = "",
	Знач ВременныйФайлЧастиПароля = "", Знач ИспользуетсяАутентификацияОС = Ложь) Экспорт

	Если ИмяКомпьютера = Неопределено Тогда // веб-клиент
		ПарольСохранен = Ложь;
		ХешИмениКомпьютера = 0;
	Иначе
		ПарольСохранен = Истина;
		ХешИмениКомпьютера = ХешСуммаCRC32(ИмяКомпьютера);
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.НастройкиАвторизацииВ1СДокументообороте.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(Пользователи.ТекущийПользователь());
	НаборЗаписей.Отбор.ПарольСохранен.Установить(ПарольСохранен);
	НаборЗаписей.Отбор.ХешИмениКомпьютера.Установить(ХешИмениКомпьютера);
	
	Запись = НаборЗаписей.Добавить();
	Запись.Пользователь = Пользователи.ТекущийПользователь();
	Запись.ПарольСохранен = ПарольСохранен;
	Запись.ХешИмениКомпьютера = ХешИмениКомпьютера;
	
	Запись.ИмяПользователя = ИмяПользователя;
	Запись.ЧастьПароляВИнформационнойБазе = ЧастьПароляВИБ; // не имеет смысла для веб-клиента
	Запись.ВременныйФайлЧастиПароля = ВременныйФайлЧастиПароля; // не имеет смысла для веб-клиента
	Запись.ИспользуетсяАутентификацияОС = ИспользуетсяАутентификацияОС;
	
	НаборЗаписей.Записать();
	
	УстановитьНастройкиАвторизацииВПараметрыСеанса(ИмяПользователя, Пароль, ИспользуетсяАутентификацияОС);

КонецПроцедуры

// Сохраняет признак использования аутентификации ОС для текущего пользователя, не изменяя настройки авторизации
// 1С:Предприятия.
//
// Параметры:
//   ИмяКомпьютера - Строка - имя компьютера, где хранится часть пароля, или:
//                 - Неопределено - признак работы с веб-клиента.
//   ИспользуетсяАутентификацияОС - Булево - Истина, если используется аутентификация операционной системы.
//
Процедура СохранитьНастройкиИспользованияАутентификацииОС(ИмяКомпьютера, ИспользуетсяАутентификацияОС) Экспорт

	Если ИмяКомпьютера = Неопределено Тогда // веб-клиент
		ПарольСохранен = Ложь;
		ХешИмениКомпьютера = 0;
	Иначе
		ПарольСохранен = Истина;
		ХешИмениКомпьютера = ХешСуммаCRC32(ИмяКомпьютера);
	КонецЕсли;
	
	Запись = РегистрыСведений.НастройкиАвторизацииВ1СДокументообороте.СоздатьМенеджерЗаписи();
	Запись.Пользователь = Пользователи.ТекущийПользователь();
	Запись.ПарольСохранен = ПарольСохранен;
	Запись.ХешИмениКомпьютера = ХешИмениКомпьютера;
	
	Запись.Прочитать();
	
	Запись.Пользователь = Пользователи.ТекущийПользователь();
	Запись.ПарольСохранен = ПарольСохранен;
	Запись.ХешИмениКомпьютера = ХешИмениКомпьютера;
	
	Запись.ИспользуетсяАутентификацияОС = ИспользуетсяАутентификацияОС;
	
	Запись.Записать();
	
КонецПроцедуры

// Сохраняет имя пользователя ДО для обмена и его пароль.
//
// Параметры:
//   ИмяПользователя - Строка - имя пользователя ДО для выполнения обмена данными.
//   Пароль - Строка - пароль этого пользователя.
//
Процедура СохранитьНастройкиАвторизацииДляОбмена(Знач ИмяПользователя, Знач Пароль) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Константы.ИнтеграцияС1СДокументооборотИмяПользователяДляОбмена.Установить(ИмяПользователя);
	Константы.ИнтеграцияС1СДокументооборотПарольДляОбмена.Установить(Пароль);
	
КонецПроцедуры

// Получает имя пользователя ДО и пароль из ИБ (в веб-клиенте - только имя пользователя). Все параметры -
// неявно возвращаемые значения.
//
// Параметры:
//   ИмяПользователя - Строка - имя пользователя ДО.
//   ИмяКомпьютера - Строка - имя компьютера, хранящего часть пароля, или:
//                 - Неопределено - признак того, что пароль восстанавливать не нужно.
//   ПарольСохранен - Булево - Истина, если пароль сохранен.
//   ЧастьПароляВИБ - Строка - часть пароля, хранимая в ИБ.
//   ВременныйФайлЧастиПароля - Строка - путь ко временному файлу с другой частью пароля.
//   ИспользуетсяАутентификацияОС - Булево - Истина, если используется аутентификация операционной системы.
//
Процедура ПрочитатьНастройкиАвторизации(ИмяПользователя, Знач ИмяКомпьютера = Неопределено,
		ПарольСохранен = Ложь, ЧастьПароляВИБ = "",
		ВременныйФайлЧастиПароля = "", ИспользуетсяАутентификацияОС = Ложь) Экспорт
		
	ИмяПользователя = Неопределено;
	ПарольСохранен = Ложь;
	ИспользуетсяАутентификацияОС = Ложь;
	
	Если Не ИнтеграцияС1СДокументооборотПереопределяемый.ПользователюРазрешеноИспользованиеИнтеграции() Тогда
		Возврат;
	КонецЕсли;
	
	// Выберем имя пользователя и данные для восстановления пароля, или хотя бы имя пользователя. Приоритет -
	// у данных, сохраненных с текущего компьютера, чтобы обеспечить восстановление раздельно хранимого пароля.
	Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	ПарольСохранен КАК ПарольСохранен,
		|	ИмяПользователя КАК ИмяПользователя,
		|	ЧастьПароляВИнформационнойБазе КАК ЧастьПароляВИнформационнойБазе,
		|	ВременныйФайлЧастиПароля КАК ВременныйФайлЧастиПароля,
		|	ИспользуетсяАутентификацияОС КАК ИспользуетсяАутентификацияОС,
		|	ВЫБОР
		|		КОГДА ХешИмениКомпьютера = &ХешИмениКомпьютера ТОГДА 0
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК Приоритет
		|ИЗ
		|	РегистрСведений.НастройкиАвторизацииВ1СДокументообороте
		|ГДЕ
		|	Пользователь = &Пользователь
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет
		|");
	Если ИмяКомпьютера = Неопределено Тогда // веб-клиент
		Запрос.УстановитьПараметр("ХешИмениКомпьютера", 0);
	Иначе // восстановим данные для пароля, если компьютер тот же, или хотя бы имя
		Запрос.УстановитьПараметр("ХешИмениКомпьютера", ХешСуммаCRC32(ИмяКомпьютера));
	КонецЕсли;
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ИмяПользователя = Выборка.ИмяПользователя;
		ПарольСохранен = Выборка.ПарольСохранен;
		ЧастьПароляВИБ = Выборка.ЧастьПароляВИнформационнойБазе;
		ВременныйФайлЧастиПароля = Выборка.ВременныйФайлЧастиПароля;
		ИспользуетсяАутентификацияОС = Выборка.ИспользуетсяАутентификацияОС;
	КонецЕсли;
	
КонецПроцедуры

// Получает настройки авторизации в ДО из старого хранилища (из хранилища общих настроек).
//
// Параметры:
//   ИмяПользователя - Строка - имя пользователя ДО.
//   ПарольСохранен - Булево - Истина, если пароль сохранен.
//   Пароль - Строка - пароль.
//
Процедура ПрочитатьНастройкиАвторизацииИзХранилищаОбщихНастроек(ИмяПользователя, Пароль, ПарольСохранен) Экспорт

	ИмяПользователя = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ИнтеграцияС1СДокументооборот", "Пользователь", "");
	Пароль = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ИнтеграцияС1СДокументооборот", "Пароль", "");
	ПарольСохранен = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ИнтеграцияС1СДокументооборот", "СохранитьПароль", Ложь);

КонецПроцедуры

// Удаляет настройки авторизации в ДО из старого хранилища (из хранилища общих настроек).
//
Процедура УдалитьНастройкиАвторизацииИзХранилищаОбщихНастроек() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ПользовательИБ = ПользователиИнформационнойБазы.ТекущийПользователь();
	ОбщегоНазначения.ХранилищеОбщихНастроекУдалить("ИнтеграцияС1СДокументооборот", "Пользователь", ПользовательИБ.Имя);
	ОбщегоНазначения.ХранилищеОбщихНастроекУдалить("ИнтеграцияС1СДокументооборот", "Пароль", ПользовательИБ.Имя);
	ОбщегоНазначения.ХранилищеОбщихНастроекУдалить("ИнтеграцияС1СДокументооборот", "СохранитьПароль", ПользовательИБ.Имя);
	
КонецПроцедуры

// Возвращает Истина, если текущий пользователь - тот, под которым выполняется регламентное задание обмена.
//
Функция ЭтоПользовательЗаданияОбмена() Экспорт

	УстановитьПривилегированныйРежим(Истина);
	ПользовательИБ = ПользователиИнформационнойБазы.ТекущийПользователь();
	Попытка
		ЗаданиеОбмена = РегламентныеЗаданияСервер.Задание(
			Метаданные.РегламентныеЗадания.ИнтеграцияС1СДокументооборотВыполнитьОбменДанными);
		Возврат (ЗаданиеОбмена.ИмяПользователя = ПользовательИБ.Имя);
	Исключение
		Возврат Ложь;
	КонецПопытки;

КонецФункции

// Устанавливает настройки авторизации ДО в параметры сеанса.
//
// Параметры:
//   ИмяПользователя - Строка - имя пользователя ДО.
//   Пароль - Строка - пароль пользователя ДО.
//   ИспользуетсяАутентификацияОС - Булево - Истина, если используется аутентификация операционной системы.
//
Процедура УстановитьНастройкиАвторизацииВПараметрыСеанса(Знач ИмяПользователя, Знач Пароль,
	Знач ИспользуетсяАутентификацияОС) Экспорт

	ПараметрыСеанса.ИнтеграцияС1СДокументооборотИмяПользователя = Строка(ИмяПользователя);
	ПараметрыСеанса.ИнтеграцияС1СДокументооборотПароль = Строка(Пароль);
	ПараметрыСеанса.ИнтеграцияС1СДокументооборотПарольИзвестен = Истина;
	ПараметрыСеанса.ИнтеграцияС1СДокументооборотИспользуетсяАутентификацияОС = ИспользуетсяАутентификацияОС;
	ОбновитьПовторноИспользуемыеЗначения();

КонецПроцедуры

// Устанавливает версию сервиса в параметры сеанса.
//
// Параметры:
//   ВерсияСервиса - Строка - устанавливаемая версия сервиса.
//                 - Неопределено - признак необходимости получить версию сервиса перед установкой.
//
Процедура УстановитьВерсиюСервисаВПараметрыСеанса(Знач ВерсияСервиса = Неопределено) Экспорт

	Если ВерсияСервиса = Неопределено Тогда // получим ее
		ИнтеграцияС1СДокументооборот.УстановитьВерсиюСервиса(Неопределено,
			ПараметрыСеанса.ИнтеграцияС1СДокументооборотИмяПользователя,
			ПараметрыСеанса.ИнтеграцияС1СДокументооборотПароль,
			ПараметрыСеанса.ИнтеграцияС1СДокументооборотИспользуетсяАутентификацияОС);
	Иначе
		ПараметрыСеанса.ИнтеграцияС1СДокументооборотВерсияСервиса = ВерсияСервиса;
	КонецЕсли;

КонецПроцедуры

// Проверяет подключение к веб-сервису ДО и возвращает результат проверки.
//
// Параметры:
//   АдресСервиса - Строка - адрес веб-сервиса ДО, или
//                - Неопределено - признак необходимости использовать ранее сохраненный адрес.
//   ИмяПользователя - Строка - имя пользователя ДО.
//   Пароль - Строка - пароль пользователя ДО.
//   ТекстСообщенияОбОшибке - Строка - неявно возвращаемое значение, текст сообщения об ошибке.
//   ИспользуетсяАутентификацияОС - Булево - неявно возвращаемое значение, Истина, если используется
//     аутентификация операционной системы.
//
// Возвращаемое значение:
//   Булево - Истина, если подключиться удалось, и Ложь в противном случае.
//
Функция ПроверитьПодключение(Знач АдресСервиса = Неопределено, Знач ИмяПользователя, Знач Пароль,
	ТекстСообщенияОбОшибке = "", ИспользуетсяАутентификацияОС = Ложь) Экспорт
	
	Если АдресСервиса = Неопределено Тогда
		АдресСервиса = Константы.АдресВебСервиса1СДокументооборот.Получить();
	КонецЕсли;

	Если Не ЗначениеЗаполнено(АдресСервиса) Тогда
		ТекстСообщенияОбОшибке = НСтр("ru = 'Не указан адрес веб-сервиса 1С:Документооборота.'");
		Возврат Ложь;
	КонецЕсли;
	
	ПоддерживаетсяАутентификацияОС = ИнтеграцияС1СДокументооборот.ПоддерживаетсяАутентификацияОС();
	
	// При использовании аутентификации ОС имя пользователя не нужно.
	Если Не ЗначениеЗаполнено(ИмяПользователя)
		И Не (ИспользуетсяАутентификацияОС И ПоддерживаетсяАутентификацияОС) Тогда
		ТекстСообщенияОбОшибке = НСтр("ru = 'Не указано имя пользователя 1С:Документооборота.'");
		Возврат Ложь;
	КонецЕсли;

	// Соберем адрес WSDL.
	МестоположениеWSDL = АдресСервиса;
	Если ЗначениеЗаполнено(МестоположениеWSDL) И 
		Прав(МестоположениеWSDL, 1) <> "/" И Прав(МестоположениеWSDL, 1) <> "\" Тогда
		МестоположениеWSDL = МестоположениеWSDL + "/";
	КонецЕсли;
	
	// При необходимости создадим защищенное соединение. Используем сертификаты из хранилища
	// Windows, если это имеет смысл для текущей платформы.
	ЭтоСоединениеSSL = СтрНачинаетсяС(МестоположениеWSDL, "https");
	Если ЭтоСоединениеSSL Тогда
		Если ИнтеграцияС1СДокументооборот.СерверРаботаетПодWindows() Тогда
			ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL(
				Новый СертификатКлиентаWindows(),
				Новый СертификатыУдостоверяющихЦентровWindows());
		Иначе
			ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL();
		КонецЕсли;
	Иначе
		ЗащищенноеСоединение = Неопределено;
	КонецЕсли;
	
	Таймаут = ИнтеграцияС1СДокументооборот.ТаймаутСервиса();
	ИнтеграцияС1СДокументооборотПереопределяемый.ПриОпределенииТаймаутаСервиса(Таймаут);
	
	ИнтернетПрокси = Неопределено;
	ИнтеграцияС1СДокументооборотПереопределяемый.ПриПолученииWSПрокси(ИнтернетПрокси);
	
	Попытка
		Определения = Новый WSОпределения(МестоположениеWSDL + "ws/dm.1cws?wsdl",
			ИмяПользователя,
			Пароль,
			ИнтернетПрокси,
			Таймаут,
			ЗащищенноеСоединение,
			ПоддерживаетсяАутентификацияОС И ИспользуетсяАутентификацияОС);
	Исключение
		Определения = Неопределено;
		ТекстСообщенияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	Если Определения = Неопределено Тогда
		Попытка
			Определения = Новый WSОпределения(МестоположениеWSDL + "ws/DMService?wsdl",
				ИмяПользователя,
				Пароль,
				ИнтернетПрокси,
				Таймаут,
				ЗащищенноеСоединение, 
				ПоддерживаетсяАутентификацияОС И ИспользуетсяАутентификацияОС);
		Исключение
			
			Определения = Неопределено;
			ТекстСообщенияОбОшибке = ТекстСообщенияОбОшибке
				+ Символы.ПС
				+ ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				
		КонецПопытки;
			
	КонецЕсли;
	
	Если Определения = Неопределено Тогда
		
		ЗаписьЖурналаРегистрации(
			ИнтеграцияС1СДокументооборот.ИмяСобытияЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстСообщенияОбОшибке);
				
		Возврат Ложь;
		
	КонецЕсли;
	
	Попытка
		Прокси = Новый WSПрокси(Определения,
			"http://www.1c.ru/dm",
			"DMService",
			"DMServiceSoap",
			ИнтернетПрокси,
			Таймаут,
			ЗащищенноеСоединение,,
			ПоддерживаетсяАутентификацияОС И ИспользуетсяАутентификацияОС);
	Исключение
		
		ТекстСообщенияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(
			ИнтеграцияС1СДокументооборот.ИмяСобытияЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстСообщенияОбОшибке);
		
		Возврат Ложь;
		
	КонецПопытки;
	
	ПараметрыСеанса.ИнтеграцияС1СДокументооборотИспользуетсяАутентификацияОС = 
		Прокси.ИспользоватьАутентификациюОС;

	Возврат Истина;

КонецФункции

// Возвращает Истина, если пароль пользователя ДО уже известен.
//
Функция ПарольИзвестен() Экспорт

	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюС1СДокументооборот")
		И ПараметрыСеанса.ИнтеграцияС1СДокументооборотПарольИзвестен;

КонецФункции

// Возвращает Истина, если используется аутентификация ОС.
//
Функция ИспользуетсяАутентификацияОС() Экспорт

	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюС1СДокументооборот")
		И ПараметрыСеанса.ИнтеграцияС1СДокументооборотИспользуетсяАутентификацияОС;

КонецФункции

// Получает версию сервиса ДО.
//
// Параметры:
//   Таймаут - Число - неявно возвращаемое значение, установленный для прокси таймаут в секундах,
//     в течение которого клиент может ожидать успешного подключения или ответа о недоступности
//     сервиса.
//
Функция ВерсияСервиса(Таймаут = Неопределено) Экспорт

	Возврат ИнтеграцияС1СДокументооборот.ВерсияСервиса(Таймаут);

КонецФункции

// Возвращает массив подходящих правил. Обращается к сервису за объектом ДО при неоднозначности
// выбора правила, если объект не передан, а передан его тип и идентификатор.
//
// Параметры:
//   ОбъектИС - Произвольный - объект интегрируемой системы.
//   ОбъектДО - ОбъектXDTO - объект Документооборота.
//   ТипОбъектаИС - Строка - тип объекта ИС.
//   ТипОбъектаДО - Строка - тип объекта Документооборота.
//   ИдентификаторОбъектаДО - Строка - идентификатор объекта Документооборота.
//
// Возвращаемое значение:
//   Массив - описание правил, структуры со свойствами:
//     Ссылка - СправочникСсылка.ПравилаИнтеграцииС1СДокументооборотом - правило.
//     ПредставлениеОбъектаДО - Строка - представление объекта ДО.
//     ПредставлениеОбъектаИС - Строка - представление объекта ИС.
//     ТипОбъектаДО - Строка - тип объекта ДО.
//     ТипОбъектаИС - Строка - тип объекта ИС.
//     ИдентификаторВидаДокумента - Строка - идентификатор вида документа ДО.
//     ТипВидаДокумента - Строка - тип вида документа ДО.
//
Функция ПодходящиеПравила(ОбъектИС = Неопределено, ОбъектДО = Неопределено,
	ТипОбъектаИС = Неопределено, ТипОбъектаДО = Неопределено, ИдентификаторОбъектаДО = Неопределено) Экспорт
	
	Если ТипЗнч(ОбъектДО) = Тип("ОбъектXDTO") Тогда
		ОбъектXDTO = ОбъектДО;
	Иначе
		ОбъектXDTO = Неопределено;
	КонецЕсли;
	
	Достаточно = Ложь;
	
	Пока Не Достаточно Цикл
		
		Правила = Новый Массив;
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	Правила.Ссылка КАК Ссылка,
			|	Правила.ТипОбъектаИС КАК ТипОбъектаИС,
			|	Правила.ТипОбъектаДО КАК ТипОбъектаДО,
			|	Правила.ПредставлениеОбъектаИС КАК ПредставлениеОбъектаИС,
			|	Правила.ПредставлениеОбъектаДО КАК ПредставлениеОбъектаДО,
			|	Правила.УсловиеПрименимостиПриВыгрузке КАК УсловиеПрименимостиПриВыгрузке,
			|	Правила.УсловиеПрименимостиПриЗагрузке КАК УсловиеПрименимостиПриЗагрузке,
			|	КлючевыеРеквизиты.ПравилоВыгрузки КАК ПравилоВыгрузки,
			|	КлючевыеРеквизиты.ИмяРеквизита КАК ИмяРеквизита,
			|	КлючевыеРеквизиты.ЗначениеРеквизита КАК ЗначениеРеквизита,
			|	КлючевыеРеквизиты.ЗначениеРеквизитаДОТип КАК ЗначениеРеквизитаДОТип,
			|	КлючевыеРеквизиты.ЗначениеРеквизитаДОID КАК ЗначениеРеквизитаДОID,
			|	КлючевыеРеквизиты.ЭтоДополнительныйРеквизит КАК ЭтоДополнительныйРеквизит,
			|	КлючевыеРеквизиты.ДополнительныйРеквизитID КАК ДополнительныйРеквизитID,
			|	КлючевыеРеквизиты.ДополнительныйРеквизитТип КАК ДополнительныйРеквизитТип
			|ИЗ
			|	Справочник.ПравилаИнтеграцииС1СДокументооборотом КАК Правила
			|
			|ЛЕВОЕ СОЕДИНЕНИЕ (
			|
			|	ВЫБРАТЬ
			|		ПравилаВыгрузки.Ссылка КАК Ссылка,
			|		ИСТИНА КАК ПравилоВыгрузки,
			|		ПравилаВыгрузки.ИмяРеквизитаОбъектаДО КАК ИмяРеквизита,
			|		ПравилаВыгрузки.ЭтоДополнительныйРеквизитДО КАК ЭтоДополнительныйРеквизит,
			|		ПравилаВыгрузки.ДополнительныйРеквизитДОID КАК ДополнительныйРеквизитID,
			|		ПравилаВыгрузки.ДополнительныйРеквизитДОТип КАК ДополнительныйРеквизитТип,
			|		ВЫБОР КОГДА ПравилаВыгрузки.Вариант = ЗНАЧЕНИЕ(Перечисление.ВариантыПравилЗаполненияРеквизитов.ИзШаблона)
			|			ТОГДА ПравилаВыгрузки.ШаблонЗначение
			|			ИНАЧЕ ПравилаВыгрузки.ЗначениеРеквизитаДО
			|		КОНЕЦ КАК ЗначениеРеквизита,
			|		ВЫБОР КОГДА ПравилаВыгрузки.Вариант = ЗНАЧЕНИЕ(Перечисление.ВариантыПравилЗаполненияРеквизитов.ИзШаблона)
			|			ТОГДА ПравилаВыгрузки.ШаблонТип
			|			ИНАЧЕ ПравилаВыгрузки.ЗначениеРеквизитаДОТип
			|		КОНЕЦ КАК ЗначениеРеквизитаДОТип,
			|		ВЫБОР КОГДА ПравилаВыгрузки.Вариант = ЗНАЧЕНИЕ(Перечисление.ВариантыПравилЗаполненияРеквизитов.ИзШаблона)
			|			ТОГДА ПравилаВыгрузки.ШаблонID
			|			ИНАЧЕ ПравилаВыгрузки.ЗначениеРеквизитаДОID
			|		КОНЕЦ КАК ЗначениеРеквизитаДОID
			|	ИЗ
			|		Справочник.ПравилаИнтеграцииС1СДокументооборотом.ПравилаЗаполненияРеквизитовДО КАК ПравилаВыгрузки
			|	ГДЕ
			|		ПравилаВыгрузки.Ключевой
			|
			|	ОБЪЕДИНИТЬ ВСЕ
			|
			|	ВЫБРАТЬ
			|		ПравилаЗагрузки.Ссылка КАК Ссылка,
			|		ЛОЖЬ КАК ПравилоВыгрузки,
			|		ПравилаЗагрузки.ИмяРеквизитаОбъектаИС КАК ИмяРеквизита,
			|		ПравилаЗагрузки.ЭтоДополнительныйРеквизитИС КАК ЭтоДополнительныйРеквизит,
			|		ПравилаЗагрузки.ДополнительныйРеквизитИС КАК ДополнительныйРеквизитID,
			|		Неопределено КАК ДополнительныйРеквизитТип,
			|		ПравилаЗагрузки.ЗначениеРеквизитаИС КАК ЗначениеРеквизита,
			|		Неопределено КАК ЗначениеРеквизитаДОТип,
			|		Неопределено КАК ЗначениеРеквизитаДОID
			|	ИЗ
			|		Справочник.ПравилаИнтеграцииС1СДокументооборотом.ПравилаЗаполненияРеквизитовИС КАК ПравилаЗагрузки
			|	ГДЕ
			|		ПравилаЗагрузки.Ключевой
			|
			|) КАК КлючевыеРеквизиты
			|
			|ПО
			|	Правила.Ссылка = КлючевыеРеквизиты.Ссылка
			|
			|ГДЕ
			|	НЕ Правила.ПометкаУдаления
			|	И (&УсловиеОбъектИС)
			|	И (&УсловиеОбъектДО)
			|
			|ИТОГИ ПО
			|	Правила.Ссылка
			|");
			
		Если ЗначениеЗаполнено(ОбъектИС) Тогда
			ОбъектИСЭтоСсылка = ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ОбъектИС));
			ТипОбъектаИС = ОбъектИС.Метаданные().ПолноеИмя();
			УсловиеОбъектИС = "Правила.ТипОбъектаИС = &ТипОбъектаИС";
			Запрос.УстановитьПараметр("ТипОбъектаИС", ТипОбъектаИС);
		ИначеЕсли ЗначениеЗаполнено(ТипОбъектаИС) Тогда
			УсловиеОбъектИС = "Правила.ТипОбъектаИС = &ТипОбъектаИС";
			Запрос.УстановитьПараметр("ТипОбъектаИС", ТипОбъектаИС);
		Иначе
			ОбъектИСЭтоСсылка = Ложь;
			УсловиеОбъектИС = "ИСТИНА";
		КонецЕсли;
		
		Если ТипЗнч(ОбъектXDTO) = Тип("ОбъектXDTO") Тогда
			ТипОбъектаДО = ОбъектXDTO.objectId.type;
			УсловиеОбъектДО = "Правила.ТипОбъектаДО = &ТипОбъектаДО";
			Запрос.УстановитьПараметр("ТипОбъектаДО", ТипОбъектаДО);
		ИначеЕсли ЗначениеЗаполнено(ТипОбъектаДО) Тогда
			УсловиеОбъектДО = "Правила.ТипОбъектаДО = &ТипОбъектаДО";
			Запрос.УстановитьПараметр("ТипОбъектаДО", ТипОбъектаДО);
		Иначе
			УсловиеОбъектДО = "ИСТИНА";
		КонецЕсли;
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОбъектИС", УсловиеОбъектИС);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОбъектДО", УсловиеОбъектДО);
		
		ВыборкаПравила = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПравила.Следующий() Цикл
			
			ИдентификаторВидаДокумента = Неопределено;
			ТипВидаДокумента = Неопределено;
			
			// Проверка ключевых реквизитов.
			
			ПодходитПоКлючевымРеквизитам = Истина;
			
			ВыборкаКлючевыеРеквизиты = ВыборкаПравила.Выбрать();
			Пока ВыборкаКлючевыеРеквизиты.Следующий() Цикл
				
				Если ВыборкаКлючевыеРеквизиты.ПравилоВыгрузки = Истина Тогда
					
					Если ВыборкаКлючевыеРеквизиты.ИмяРеквизита = "documentType" Тогда
						ИдентификаторВидаДокумента = ВыборкаКлючевыеРеквизиты.ЗначениеРеквизитаДОID;
						ТипВидаДокумента = ВыборкаКлючевыеРеквизиты.ЗначениеРеквизитаДОТип;
					КонецЕсли;
					
					Если ТипЗнч(ОбъектXDTO) <> Тип("ОбъектXDTO") Тогда
						Продолжить;
					КонецЕсли;
					
					Если Не ВыборкаКлючевыеРеквизиты.ЭтоДополнительныйРеквизит Тогда
						Если Не ОбъектXDTO.Установлено(ВыборкаКлючевыеРеквизиты.ИмяРеквизита) Тогда
							ЗначениеРеквизита = Неопределено;
						Иначе
							ЗначениеРеквизита = ОбъектXDTO.Получить(ВыборкаКлючевыеРеквизиты.ИмяРеквизита);
						КонецЕсли;
					Иначе
						Если ОбъектXDTO.Установлено("additionalProperties") Тогда
							ЗначениеРеквизита = Неопределено;
							Для Каждого ДопРеквизит Из ОбъектXDTO.additionalProperties Цикл
								Если ДопРеквизит.objectId.id = ВыборкаКлючевыеРеквизиты.ДополнительныйРеквизитID
									И ДопРеквизит.objectId.type = ВыборкаКлючевыеРеквизиты.ДополнительныйРеквизитТип Тогда
									Если ДопРеквизит.Установлено("propertySimpleValue") Тогда
										ЗначениеРеквизита = ДопРеквизит.propertySimpleValue;
									Иначе
										ЗначениеРеквизита = ДопРеквизит.propertyObjectValue;
									КонецЕсли;
								КонецЕсли;
							КонецЦикла;
						Иначе
							ЗначениеРеквизита = Неопределено;
						КонецЕсли;
					КонецЕсли;
					
					Если ВыборкаКлючевыеРеквизиты.ЗначениеРеквизитаДОТип = "Строка"
						Или ВыборкаКлючевыеРеквизиты.ЗначениеРеквизитаДОТип = "Число"
						Или ВыборкаКлючевыеРеквизиты.ЗначениеРеквизитаДОТип = "Дата"
						Или ВыборкаКлючевыеРеквизиты.ЗначениеРеквизитаДОТип = "Булево" Тогда
						
						Если ЗначениеРеквизита <> ВыборкаКлючевыеРеквизиты.ЗначениеРеквизита Тогда
							ПодходитПоКлючевымРеквизитам = Ложь;
							Прервать;
						КонецЕсли;
						
					ИначеЕсли ЗначениеРеквизита = Неопределено Тогда
						
						ПодходитПоКлючевымРеквизитам = Ложь;
						Прервать;
						
					Иначе // ссылочный тип
						
						Если Не ЗначениеРеквизита.Установлено("objectId") 
							Или ЗначениеРеквизита.objectId.id <> ВыборкаКлючевыеРеквизиты.ЗначениеРеквизитаДОID
							Или ЗначениеРеквизита.objectId.type <> ВыборкаКлючевыеРеквизиты.ЗначениеРеквизитаДОТип Тогда
							ПодходитПоКлючевымРеквизитам = Ложь;
							Прервать;
						КонецЕсли;
						
					КонецЕсли;
					
				ИначеЕсли ВыборкаКлючевыеРеквизиты.ПравилоВыгрузки = Ложь Тогда // правило загрузки
					
					Отказ = Ложь;
					ИнтеграцияС1СДокументооборотПереопределяемый.ПриПроверкеСоответствияПравилаФункциональнымОпциям(
						ВыборкаПравила.Ссылка,
						ВыборкаПравила.ТипОбъектаДО,
						ВыборкаПравила.ТипОбъектаИС,
						Отказ,
						ВыборкаКлючевыеРеквизиты.ИмяРеквизита,
						ВыборкаКлючевыеРеквизиты.ЗначениеРеквизита);
						
					Если Отказ Тогда
						ПодходитПоКлючевымРеквизитам = Ложь;
						Прервать;
					КонецЕсли;
					
					Если Не ЗначениеЗаполнено(ОбъектИС) Тогда
						Продолжить;
					КонецЕсли;
					
					Если Не ВыборкаКлючевыеРеквизиты.ЭтоДополнительныйРеквизит Тогда
						
						Если ОбъектИСЭтоСсылка Тогда
							ЗначениеРеквизита = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
								ОбъектИС,
								ВыборкаКлючевыеРеквизиты.ИмяРеквизита);
						Иначе // объект
							ЗначениеРеквизита = ОбъектИС[ВыборкаКлючевыеРеквизиты.ИмяРеквизита];
						КонецЕсли;
						
					Иначе // доп. реквизит
						
						ЗначениеРеквизита = Неопределено;
						
						Если ОбъектИСЭтоСсылка Тогда
							
							ЗапросЗначениеРеквизита = Новый Запрос(
								"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
								|	ТаблицаСвойств.Значение КАК ЗначениеРеквизита
								|ИЗ
								|	[ИмяОбъектаСоСвойствами].ДополнительныеРеквизиты КАК ТаблицаСвойств
								|ГДЕ
								|	ТаблицаСвойств.Ссылка = &Ссылка
								|	И ТаблицаСвойств.Свойство = &Свойство");
								
							ЗапросЗначениеРеквизита.Текст = СтрЗаменить(ЗапросЗначениеРеквизита.Текст,
								"[ИмяОбъектаСоСвойствами]",
								ОбщегоНазначения.ИмяТаблицыПоСсылке(ОбъектИС.Ссылка));
							
							ЗапросЗначениеРеквизита.УстановитьПараметр("Ссылка", ОбъектИС.Ссылка);
							ЗапросЗначениеРеквизита.УстановитьПараметр("Свойство", ВыборкаКлючевыеРеквизиты.ДополнительныйРеквизитID);
							
							ВыборкаЗначениеРеквизита = ЗапросЗначениеРеквизита.Выполнить().Выбрать();
							Если ВыборкаЗначениеРеквизита.Следующий() Тогда
								ЗначениеРеквизита = ВыборкаЗначениеРеквизита.ЗначениеРеквизита;
							КонецЕсли;
							
						Иначе // объект
							
							СтруктураПоиска = Новый Структура("Свойство", ВыборкаКлючевыеРеквизиты.ДополнительныйРеквизитID);
							СтрокиРеквизита = ОбъектИС.ДополнительныеРеквизиты.НайтиСтроки(СтруктураПоиска); 
							Если СтрокиРеквизита.Количество() <> 0 Тогда
								ЗначениеРеквизита = СтрокиРеквизита[0].Значение;
							КонецЕсли;
							
						КонецЕсли;
					КонецЕсли;
						
					Если ЗначениеРеквизита <> ВыборкаКлючевыеРеквизиты.ЗначениеРеквизита Тогда
						ПодходитПоКлючевымРеквизитам = Ложь;
						Прервать;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если Не ПодходитПоКлючевымРеквизитам Тогда
				Продолжить;
			КонецЕсли;
			
			// Проверка условий применимости.
			
			Если ЗначениеЗаполнено(ОбъектИС)
				И ЗначениеЗаполнено(ВыборкаПравила.УсловиеПрименимостиПриВыгрузке) Тогда
				
				Параметры = Новый Структура;
				Параметры.Вставить("Источник", ОбъектИС);
				Параметры.Вставить("Результат", Истина);
				
				ОбщегоНазначения.ВыполнитьВБезопасномРежиме(ВыборкаПравила.УсловиеПрименимостиПриВыгрузке,
					Параметры);
				
				Если Параметры.Результат <> Истина Тогда
					Продолжить;
				КонецЕсли;
				
			КонецЕсли;
			
			Если ТипЗнч(ОбъектXDTO) = Тип("ОбъектXDTO")
				И ЗначениеЗаполнено(ВыборкаПравила.УсловиеПрименимостиПриЗагрузке) Тогда
				
				Параметры = Новый Структура;
				Параметры.Вставить("Источник", ОбъектXDTO);
				Параметры.Вставить("Результат", Истина);
				
				ОбщегоНазначения.ВыполнитьВБезопасномРежиме(ВыборкаПравила.УсловиеПрименимостиПриЗагрузке,
					Параметры);
				
				Если Параметры.Результат <> Истина Тогда
					Продолжить;
				КонецЕсли;
				
			КонецЕсли;
			
			Правило = Новый Структура;
			Правило.Вставить("Ссылка", ВыборкаПравила.Ссылка);
			Правило.Вставить("ТипОбъектаИС", ВыборкаПравила.ТипОбъектаИС);
			Правило.Вставить("ТипОбъектаДО", ВыборкаПравила.ТипОбъектаДО);
			Правило.Вставить("ПредставлениеОбъектаИС", ВыборкаПравила.ПредставлениеОбъектаИС);
			Правило.Вставить("ПредставлениеОбъектаДО", ВыборкаПравила.ПредставлениеОбъектаДО);
			Правило.Вставить("ИдентификаторВидаДокумента", ИдентификаторВидаДокумента);
			Правило.Вставить("ТипВидаДокумента", ТипВидаДокумента);
			
			Правила.Добавить(Правило);
			
		КонецЦикла;
		
		// Возможно, следует уточнить выборку, обратившись к сервису за самим объектом.
		Если Правила.Количество() > 1
			И ТипЗнч(ОбъектXDTO) <> Тип("ОбъектXDTO")
			И ЗначениеЗаполнено(ИдентификаторОбъектаДО)
			И ЗначениеЗаполнено(ТипОбъектаДО) Тогда
			
			Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
			
			ЗапросОбъекта = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMRetrieveRequest");
			
			ОбъектИд = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси,
				ИдентификаторОбъектаДО,
				ТипОбъектаДО);
			ЗапросОбъекта.objectIds.Добавить(ОбъектИд);
			
			Результат = Прокси.execute(ЗапросОбъекта);
			ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
			
			ОбъектXDTO = Результат.objects[0];
			
		Иначе
			
			Достаточно = Истина;
			
		КонецЕсли;
	
	КонецЦикла; // Пока Не Достаточно
	
	Возврат Правила;
	
КонецФункции

// Проверяет, настроена ли интеграция для указанного объекта интегрируемой системы.
//
// Параметры:
//   ОбъектИС - Произвольный - проверяемый объект ИС.
//
Функция НастроенаИнтеграцияДляОбъекта(ОбъектИС) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюС1СДокументооборот") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Результат = Неопределено;
	
	Если ТипЗнч(Результат) = Тип("Булево") Тогда
		
		Возврат Результат;
		
	Иначе
		
		Правила = ПодходящиеПравила(ОбъектИС);
		Возврат (Правила.Количество() <> 0);
		
	КонецЕсли;
	
КонецФункции

// Проверяет, следует ли использовать присоединенные файлы ДО для указанного объекта. Возвращает
// Истина, если интеграция настроена, а своих присоединенных файлов у объекта ИС нет.
//
// Параметры:
//   ОбъектИС - Произвольный - проверяемый объект ИС.
//
// Возвращаемое значение:
//   Булево - Истина, если следует использовать присоединенные файлы ДО.
//
Функция ИспользоватьПрисоединенныеФайлы1СДокументооборотаДляОбъекта(ОбъектИС) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьПрисоединенныеФайлы1СДокументооборота") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не НастроенаИнтеграцияДляОбъекта(ОбъектИС) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Не ЕстьХранимыеФайлы(ОбъектИС);
	
КонецФункции

// Проверяет, используются ли присоединенные файлы ДО. Предназначена для определения ФО на клиенте.
//
Функция ИспользоватьПрисоединенныеФайлы1СДокументооборота() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьПрисоединенныеФайлы1СДокументооборота");
	
КонецФункции

// Проверяет, является ли текущий сеанс неразделенным при работе в модели сервиса.
//
// Возвращаемое значение:
//   Булево - Истина, если сеанс неразделенный, и Ложь в разделенном сеансе и в локальном режиме.
//
Функция ЭтоНеразделенныйСеансРазделеннойИБ() Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаВМоделиСервиса") Тогда
		МодульРаботаВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль("РаботаВМоделиСервиса");
		ЕстьРазделители = МодульРаботаВМоделиСервиса.ЭтоРазделеннаяКонфигурация();
	Иначе
		ЕстьРазделители = Ложь;
	КонецЕсли;
	
	Возврат ОбщегоНазначения.РазделениеВключено()
		И ЕстьРазделители;
	
КонецФункции

// Возвращает значение реквизита, прочитанного из информационной базы по ссылке на объект.
// 
//  Если доступа к реквизиту нет, возникнет исключение прав доступа.
//  Если необходимо зачитать реквизит независимо от прав текущего пользователя,
//  то следует использовать предварительный переход в привилегированный режим.
// 
// Параметры:
//  Ссылка       - ссылка на объект, - элемент справочника, документ, ...
//  ИмяРеквизита - Строка, например, "Код".
// 
// Возвращаемое значение:
//  Произвольный    - зависит от типа значения прочитанного реквизита.
// 
Функция ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита) Экспорт
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита);
	
КонецФункции 

// Записывает предупреждение в ЖР.
//
Процедура ЗаписатьПредупреждение(ТекстПредупреждения) Экспорт
	
	ЗаписьЖурналаРегистрации(
		ИнтеграцияС1СДокументооборот.ИмяСобытияЖурналаРегистрации(), 
		УровеньЖурналаРегистрации.Предупреждение,,,
		ТекстПредупреждения);
		
КонецПроцедуры

#КонецОбласти

#Область Хронометраж

// Возвращает структуру параметров хронометража для указанного объекта.
//
Функция ПараметрыХронометражаОбъекта(ВнешнийОбъект) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetChronometrationSettingsRequest");
	
	ОбъектXDTO = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "ExternalObject");
	ОбъектXDTO.id = Строка(ВнешнийОбъект.УникальныйИдентификатор());
	ОбъектXDTO.type = ВнешнийОбъект.Метаданные().ПолноеИмя();
	ОбъектXDTO.name = Строка(ВнешнийОбъект);
	
	Запрос.objects.Добавить(ОбъектXDTO);
	
	Результат = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	ПараметрыХронометражаXDTO = Результат.settings[0];
	
	Если ПараметрыХронометражаXDTO.beginDate = Неопределено Тогда
		ДатаНачалаХронометража = '00010101';
	Иначе
		ДатаНачалаХронометража = ПараметрыХронометражаXDTO.beginDate;
	КонецЕсли;
	
	Если ПараметрыХронометражаXDTO.endDate = Неопределено Тогда
		ДатаКонцаХронометража = '00010101';
	Иначе
		ДатаКонцаХронометража = ПараметрыХронометражаXDTO.endDate;
	КонецЕсли;
	
	ПараметрыХронометража = Новый Структура;
	ПараметрыХронометража.Вставить("ВключенХронометраж", ПараметрыХронометражаXDTO.chronometrationOn);
	ПараметрыХронометража.Вставить("ДатаНачалаХронометража", ДатаНачалаХронометража);
	ПараметрыХронометража.Вставить("ДатаКонцаХронометража", ДатаКонцаХронометража);
	Если ПараметрыХронометражаXDTO.objectID <> Неопределено Тогда
		ПараметрыХронометража.Вставить("ИсточникID", ПараметрыХронометражаXDTO.objectID.id);
		ПараметрыХронометража.Вставить("ИсточникТип",  ПараметрыХронометражаXDTO.objectID.type);
		ПараметрыХронометража.Вставить("Источник",  ПараметрыХронометражаXDTO.objectID.presentation);
	КонецЕсли;
	
	Возврат ПараметрыХронометража;
	
КонецФункции

// Возвращает массив активных записей хронометража.
//
Функция АктивныеЗаписиХронометража() Экспорт
	
	АктивныеЗаписи = Новый Массив;
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetChronometrationSettingsRequest");
	
	Результат = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	Для Каждого ПараметрыХронометражаXDTO Из Результат.settings Цикл
		
		ПараметрыХронометража = Новый Структура;
		ПараметрыХронометража.Вставить("ВключенХронометраж", ПараметрыХронометражаXDTO.chronometrationOn);
		ПараметрыХронометража.Вставить("ДатаНачалаХронометража", ПараметрыХронометражаXDTO.beginDate);
		ПараметрыХронометража.Вставить("ДатаКонцаХронометража", ПараметрыХронометражаXDTO.endDate);
		Если ПараметрыХронометражаXDTO.objectID <> Неопределено Тогда
			ПараметрыХронометража.Вставить("ИсточникID", ПараметрыХронометражаXDTO.objectID.id);
			ПараметрыХронометража.Вставить("ИсточникТип",  ПараметрыХронометражаXDTO.objectID.type);
			ПараметрыХронометража.Вставить("Источник",  ПараметрыХронометражаXDTO.objectID.presentation);
		КонецЕсли;
		
		АктивныеЗаписи.Добавить(ПараметрыХронометража);
		
	КонецЦикла;
	
	Возврат АктивныеЗаписи;
	
КонецФункции

// Переключает хронометраж и возвращает результирующие параметры хронометража по внешнему объекту.
//
Функция ПереключитьХронометражПоВнешнемуОбъекту(ВнешнийОбъект, ПараметрыХронометража) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMSetChronometrationSettingsRequest");
	
	ОбъектXDTO = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "ExternalObject");
	ОбъектXDTO.id = Строка(ВнешнийОбъект.УникальныйИдентификатор());
	ОбъектXDTO.type = ВнешнийОбъект.Метаданные().ПолноеИмя();
	ОбъектXDTO.name = Строка(ВнешнийОбъект);
	
	Запрос.objects.Добавить(ОбъектXDTO);
	
	Результат = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);

	ПараметрыХронометражаXDTO = Результат.settings[0];
	
	Если ПараметрыХронометражаXDTO.beginDate = Неопределено Тогда
		ДатаНачалаХронометража = '00010101';
	Иначе
		ДатаНачалаХронометража = ПараметрыХронометражаXDTO.beginDate;
	КонецЕсли;
	
	Если ПараметрыХронометражаXDTO.endDate = Неопределено Тогда
		ДатаКонцаХронометража = '00010101';
	Иначе
		ДатаКонцаХронометража = ПараметрыХронометражаXDTO.endDate;
	КонецЕсли;
	
	ПараметрыХронометража = Новый Структура;
	ПараметрыХронометража.Вставить("ВключенХронометраж", ПараметрыХронометражаXDTO.chronometrationOn);
	ПараметрыХронометража.Вставить("ДатаНачалаХронометража", ДатаНачалаХронометража);
	ПараметрыХронометража.Вставить("ДатаКонцаХронометража", ДатаКонцаХронометража);
	
	Возврат ПараметрыХронометража; 
	
КонецФункции

// Переключает хронометраж и возвращает параметры хронометража по объектам 1С:Документооборота.
//
// Параметры:
//   Источники - Массив - массив структур, описывающих объекты ДО, со свойствами ИсточникID и ИсточникТип.
//
// Возвращаемое значение:
//   Структура - параметры хронометража.
//
Функция ПереключитьХронометражПоОбъектамДокументооборота(Источники) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMSetChronometrationSettingsRequest");
	
	Для Каждого Источник Из Источники Цикл
		
		ОбъектXDTO = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectID");
		ОбъектXDTO.id = Источник.ИсточникID;
		ОбъектXDTO.type = Источник.ИсточникТип;
		Запрос.objects.Добавить(ОбъектXDTO);
		
	КонецЦикла;
	
	Результат = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	АктивныеЗаписи = Новый Массив;
	
	Для Каждого ПараметрыХронометражаXDTO Из Результат.settings Цикл
		
		ПараметрыХронометража = Новый Структура;
		ПараметрыХронометража.Вставить("ВключенХронометраж", ПараметрыХронометражаXDTO.chronometrationOn);
		ПараметрыХронометража.Вставить("ДатаНачалаХронометража", ПараметрыХронометражаXDTO.beginDate);
		ПараметрыХронометража.Вставить("ДатаКонцаХронометража", ПараметрыХронометражаXDTO.endDate);
		Если ПараметрыХронометражаXDTO.objectID <> Неопределено Тогда
			ПараметрыХронометража.Вставить("ИсточникID", ПараметрыХронометражаXDTO.objectID.id);
			ПараметрыХронометража.Вставить("ИсточникТип",  ПараметрыХронометражаXDTO.objectID.type);
			ПараметрыХронометража.Вставить("Источник",  ПараметрыХронометражаXDTO.objectID.presentation);
		КонецЕсли;
		
		АктивныеЗаписи.Добавить(ПараметрыХронометража);
		
	КонецЦикла;
	
	Возврат АктивныеЗаписи; 
	
КонецФункции

#КонецОбласти

#Область ЗапросыКДокументообороту

// Готовит данные для выбора из списка при обработке события АвтоПодбор у полей ввода.
//
// Параметры:
//   ТипыЗначений - Строка - имена классов XDTO, разделенные ';'.
//   ДанныеВыбора - СписокЗначений - наполняемый список.
//   Текст - Строка - текст подбора.
//   СтандартнаяОбработка - Булево - Ложь, если стандартная обработка подменяется.
//   Отбор - Структура - необязательный, дополнительный отбор.
//
Процедура ДанныеДляАвтоПодбора(ТипыЗначений, ДанныеВыбора, Текст, СтандартнаяОбработка, Отбор = Неопределено) Экспорт
	
	ДанныеВыбора = Новый СписокЗначений;
	
	СтандартнаяОбработка = Ложь;
	
	Если СтрДлина(Текст) < 2 Тогда
		Возврат;
	КонецЕсли;
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
		
	МассивТипов = ИнтеграцияС1СДокументооборот.РазложитьСтрокуВМассивПодстрок(ТипыЗначений, ";");
	
	Для Каждого ТипXDTO Из МассивТипов Цикл
		
		Если НЕ ЗначениеЗаполнено(ТипXDTO) Тогда
			Продолжить;
		КонецЕсли;
		
		СписокУсловий = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListQuery");
		
		ДополнитьНаборКолонокУсловийВыбора(СписокУсловий.columnSet, ТипXDTO);
		
		Условие = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
		Условие.property = "name";
		Условие.value = Текст;
		
		СписокУсловий.conditions.Добавить(Условие);
		
		// Наложим дополнительный отбор.
		Если Отбор <> Неопределено Тогда
			Для Каждого СтрокаОтбора Из Отбор Цикл
				Условие = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
				Условие.property = СтрокаОтбора.Ключ;
				Если ТипЗнч(СтрокаОтбора.Значение) = Тип("Структура") Тогда
					Условие.value =	ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси,
						СтрокаОтбора.Значение.id,
						СтрокаОтбора.Значение.type);
				Иначе
					Условие.value = СтрокаОтбора.Значение;
				КонецЕсли;
				СписокУсловий.conditions.Добавить(Условие);
			КонецЦикла;
		КонецЕсли;
		
		// Ограничим размер списка.
		Если СписокУсловий.Свойства().Получить("limit") <> Неопределено Тогда
			СписокУсловий.limit = ИнтеграцияС1СДокументооборотПереопределяемый.
				ПредельноеКоличествоВыбираемыхОбъектов(ТипXDTO);
		КонецЕсли;
		
		Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetObjectListRequest");
		Запрос.type = ТипXDTO;
		Запрос.query = СписокУсловий;
		
		Результат = Прокси.execute(Запрос);
		ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
		
		Для Каждого Элемент Из Результат.items Цикл
			
			Имя = Элемент.object.name + ?(Элемент.object.objectId.type = "DMBusinessProcessExecutorRole", " (роль)", "");
			
			ЭлементДанных = Новый Структура;
			ЭлементДанных.Вставить("id", Элемент.object.objectId.id);
			ЭлементДанных.Вставить("type", Элемент.object.objectId.type);
			ЭлементДанных.Вставить("name", Имя);
			
			// Прочие свойства, зависящие от типа (см. ДополнитьСписокУсловийВыбора);
			Для Каждого Колонка Из СписокУсловий.columnSet Цикл
				ЭлементДанных.Вставить(Колонка, Элемент.object[Колонка]);
			КонецЦикла;
			
			ДанныеВыбора.Добавить(ЭлементДанных, Имя);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Получает из Документооборота документ, связанный с объектом конфигурации-потребителя.
//
// Параметры:
//   СсылкаНаВнешнийОбъект - ЛюбаяСсылка - ссылка на объект конфигурации-потребителя.
//
// Возвращаемое значение:
//   Структура со свойствами:
//     name - Строка - имя документа Документооборота.
//     id - Строка - уникальный идентификатор документа в Документообороте.
//     type - Строка - имя типа XDTO, соответствующего документу.
//     ВнешнийОбъект - ЛюбаяСсылка - ссылка на связанный объект ИС.
//   Неопределено - если связанного объекта не существует.
//
Функция ДанныеОбъектаДОПоВнешнемуОбъекту(СсылкаНаВнешнийОбъект) Экспорт
	
	Если ЗначениеЗаполнено(СсылкаНаВнешнийОбъект) Тогда
		Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
		ВнешнийОбъект = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "ExternalObject");
		ВнешнийОбъект.id = Строка(СсылкаНаВнешнийОбъект.УникальныйИдентификатор());
		ВнешнийОбъект.type = СсылкаНаВнешнийОбъект.Метаданные().ПолноеИмя();
		ВнешнийОбъект.name = Строка(СсылкаНаВнешнийОбъект);
		
		Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetDocumentListRequest");
		Запрос.externalObjects.Добавить(ВнешнийОбъект);
		
		Запрос.columnSet.Добавить("name");
		Запрос.columnSet.Добавить("documentType");
		
		Результат = Прокси.execute(Запрос);
		ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
		
		Если Результат.documents.Количество() > 0 Тогда
			
			Объект = Результат.documents[0];
			
			ДанныеВозврата = Новый Структура();
			ДанныеВозврата.Вставить("ВнешнийОбъект", СсылкаНаВнешнийОбъект);
			ДанныеВозврата.Вставить("id", Объект.objectId.id);
			ДанныеВозврата.Вставить("type", Объект.objectId.type);
			ДанныеВозврата.Вставить("name", Объект.name);
			
			Если ДанныеВозврата.type = "DMInternalDocument"
				Или ДанныеВозврата.type = "DMIncomingDocument"
				Или ДанныеВозврата.type = "DMOutgoingDocument" Тогда
				
				Если Объект.documentType = Неопределено Тогда
					ВызватьИсключение СтрШаблон(НСтр("ru = 'Невозможно получить данные 1С:Документооборота: %1.
						|Обратитесь к администратору.'"),
						Объект.name);
				КонецЕсли;
				
				ТипДокумента = Новый Структура("id, type, name",
					Объект.documentType.objectID.id,
					Объект.documentType.objectID.type,
					Объект.documentType.name);
				
				ДанныеВозврата.Вставить("documentType", ТипДокумента);
				
			КонецЕсли;
			
			Возврат ДанныеВозврата;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Устарела. Рекомендуется использовать ИнтеграцияС1СДокументооборотКлиент.НачатьПоискСвязанногоОбъектаДО.
//
Функция НайтиСоздатьСвязанныйОбъектДО(ОбъектИС) Экспорт
	
	Результат = ДанныеОбъектаДОПоВнешнемуОбъекту(ОбъектИС);
	Если Результат = Неопределено Тогда
		Правила = ПодходящиеПравила(ОбъектИС);
		Если Правила.Количество() = 0 Тогда
			Результат = НСтр("ru = 'Необходимо настроить
				|правила интеграции. Обратитесь к администратору.'");
			
		Иначе // правило существует, может быть использовано для создания
			ОбъектXDTO = СоздатьОбъектДОПоПравилу(ОбъектИС, Правила[0].Ссылка);
			Если ТипЗнч(ОбъектXDTO) = Тип("ОбъектXDTO") Тогда
				Результат = Новый Структура;
				Результат.Вставить("name", ОбъектXDTO.name);
				Результат.Вставить("id", ОбъектXDTO.objectId.id);
				Результат.Вставить("type", ОбъектXDTO.objectId.type);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Создает новый объект Документооборота по объекту ИС и указанному правилу.
//
// Параметры:
//   Прокси - WSПрокси - прокси веб-сервиса DMService.
//   ОбъектИС - ЛюбаяСсылка - объект ИС, источник данных заполнения.
//   Правило - СправочникСсылка.ПравилаИнтеграцииС1СДокументооборотом - правило заполнения.
//
// Возвращаемое значение:
//   Структура - описание созданного объекта, или
//   Строка - сообщение об ошибке.
//
Функция СоздатьОбъектДОПоПравилу(ОбъектИС, Правило) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	ОбъектДОИлиСообщение = ИнтеграцияС1СДокументооборот.СоздатьОбъектДОПоПравилу(
		Прокси, ОбъектИС, Правило);
		
	Если ТипЗнч(ОбъектДОИлиСообщение) = Тип("Строка") Тогда
		Возврат ОбъектДОИлиСообщение;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("name", ОбъектДОИлиСообщение.name);
	Результат.Вставить("id", ОбъектДОИлиСообщение.objectId.id);
	Результат.Вставить("type", ОбъектДОИлиСообщение.objectId.type);
	
	Возврат Результат;
	
КонецФункции

// Возвращает список значений, заполненный значениями элементов указанного типа с учетом отборов.
//
// Параметры:
//   ТипОбъектаВыбора - Cтрока - имя класса XDTO
//   Отбор - Структура - с перечислением свойств и их значений
//
Функция ЗначенияДляВыбора(ТипОбъектаВыбора, Отбор = Неопределено) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	СписокУсловий = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListQuery");
	
	Если ТипЗнч(Отбор) = Тип("Структура") Тогда
		Для Каждого СтрокаОтбора Из Отбор Цикл
			Условие = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
			Условие.property = СтрокаОтбора.Ключ;
			
			Если ТипЗнч(СтрокаОтбора.Значение) = Тип("Структура") Тогда
				Условие.value = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси,
					СтрокаОтбора.Значение.id,
					СтрокаОтбора.Значение.type);
			Иначе
				Условие.value = СтрокаОтбора.Значение;
			КонецЕсли;
			
			СписокУсловий.conditions.Добавить(Условие);
		КонецЦикла;
	КонецЕсли;
	
	ДополнитьНаборКолонокУсловийВыбора(СписокУсловий.columnSet, ТипОбъектаВыбора);
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetObjectListRequest");
	Запрос.type = ТипОбъектаВыбора;
	Запрос.query = СписокУсловий;
	
	Результат = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	СписокВыбора = Новый СписокЗначений;
	
	Для Каждого Элемент из Результат.items Цикл
		
		// Стандартные свойства.
		СтруктураВыбора = Новый Структура;
		СтруктураВыбора.Вставить("РеквизитПредставление", Элемент.object.name);
		СтруктураВыбора.Вставить("РеквизитID", Элемент.object.objectId.id);
		СтруктураВыбора.Вставить("РеквизитТип", Элемент.object.objectId.type);
		
		// Прочие свойства, зависящие от типа (см. ДополнитьСписокУсловийВыбора);
		Для Каждого Колонка Из СписокУсловий.columnSet Цикл
			СтруктураВыбора.Вставить(Колонка, Элемент.object[Колонка]);
		КонецЦикла;
		
		СписокВыбора.Добавить(СтруктураВыбора, Элемент.object.name);
		
	КонецЦикла;
	
	Возврат СписокВыбора;
	
КонецФункции

// Ищет объект в ДО по его типу и по точному совпадению наименования.
//
// Параметры:
//   ТипОбъекта - Строка - тип объекта, поиск по которому требуется выполнить.
//   Имя - Строка - наименование объекта.
//
// Возвращаемое значение:
//   XDTO Объект типа DMObject - если объект найден, Неопределено - если не найден.
//
Функция НайтиОбъектВДОПоИмени(ТипОбъекта, Имя) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	// Определим структуру поиска
	ОтборИмя = Новый Структура;
	ОтборИмя.Вставить("Значение", Имя);
	ОтборИмя.Вставить("ОператорСравнения", "=");
	ОтборИмя.Вставить("Представление", "Наименование");
	ОтборИмя.Вставить("ПредставлениеУсловия", СтрШаблон("= ""%1""", Имя));
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("name", ОтборИмя);
	
	// Выполним поиск
	АдресВоВременномХранилище = "";
	КоличествоРезультатов = 0;
	ПредельноеКоличествоРезультатов = 0;
	Обработки.ИнтеграцияС1СДокументооборот.ВыполнитьПоискПоРеквизитам(
		ТипОбъекта,
		СтруктураОтбора,
		АдресВоВременномХранилище,
		КоличествоРезультатов,
		ПредельноеКоличествоРезультатов);
	
	// Получим объект
	РезультатыПоиска = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище);
	
	Если РезультатыПоиска.НайденныеОбъекты.Количество() = 0 Тогда
		Возврат Неопределено;
	Иначе
		ОбъектыXDTO = ИнтеграцияС1СДокументооборот.ПолучитьОбъект(Прокси,
			ТипОбъекта, РезультатыПоиска.НайденныеОбъекты[0].ID);
		Если ОбъектыXDTO.objects.Количество() = 0 Тогда
			Возврат Неопределено;
		Иначе
			Возврат ОбъектыXDTO.objects[0];
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ПроцессыИЗадачи

// Получает список шаблонов, определенных в Документообороте для указанного типа БП и предмета.
//
// Параметры:
//   ТипБизнесПроцесса - имя типа XDTO.
//   ПредметБизнесПроцесса - Структура.
//     id - уникальный идентификатор предмета бизнес-процесса в Документообороте.
//     type - имя типа XDTO предмета бизнес-процесса.
//
// Возвращаемое значение:
//   СписокЗначений со структурами:
//     name - имя шаблона.
//     id - уникальный идентификатор шаблона.
//     type - имя типа XDTO шаблона.
//
Функция ШаблоныБизнесПроцесса(Знач ТипБизнесПроцесса, Знач ПредметБизнесПроцесса) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetBusinessProcessTemplatesRequest");
	Запрос.businessProcessType = ТипБизнесПроцесса;
	
	Если ПредметБизнесПроцесса <> Неопределено Тогда 
		ПредметБизнесПроцессаИд = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectID");
		ПредметБизнесПроцессаИд.id = ПредметБизнесПроцесса.id;
		ПредметБизнесПроцессаИд.type = ПредметБизнесПроцесса.type;
		
		Запрос.businessProcessTargetId = ПредметБизнесПроцессаИд;
	КонецЕсли;
	
	Результат = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	СписокШаблоновДокументов = Новый СписокЗначений;
	Для Каждого Шаблон Из Результат.BusinessProcessTemplates Цикл
		ДанныеШаблона = Новый Структура;
		ДанныеШаблона.Вставить("id", Шаблон.objectId.id);
		ДанныеШаблона.Вставить("type", Шаблон.objectId.type);
		ДанныеШаблона.Вставить("name", Шаблон.name);
		СписокШаблоновДокументов.Добавить(ДанныеШаблона);
	КонецЦикла;
	
	Возврат СписокШаблоновДокументов;
	
КонецФункции

// Заполняет форму бизнес-процесса на основании шаблона.
//
// Параметры:
//   Форма - форма бизнес-процесса 1С:Документооборота.
//   ДанныеОШаблоне - структура шаблона, содержащая поля:
//      - РеквизитID - идентификатор шаблона 1С:Документооборота.
//      - РеквизитТип - тип шаблона XDTO.
//
// Возвращаемое значение:
//   ОбъектXDTO - бизнес-процесс, заполненный по шаблону
//
Функция ЗаполнитьБизнесПроцессПоШаблону(Знач Форма, Знач ДанныеОШаблоне) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetBusinessProcessByTemplateRequest");
	
	Запрос.type = Форма.Тип;
	
	Если ЗначениеЗаполнено(Форма.Предмет)Тогда 
		
		ПредметБизнесПроцессаИд = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectID");
		ПредметБизнесПроцессаИд.id = Форма.ПредметID;
		ПредметБизнесПроцессаИд.type = Форма.ПредметТип;
		
		Запрос.targetId = ПредметБизнесПроцессаИд;
		
	КонецЕсли;
	
	ШаблонБизнесПроцессаИд = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectID");
	ШаблонБизнесПроцессаИд.id = ДанныеОШаблоне.РеквизитID;
	ШаблонБизнесПроцессаИд.type = ДанныеОШаблоне.РеквизитТип;
	
	Запрос.businessProcessTemplateId = ШаблонБизнесПроцессаИд;
	
	Результат = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	Возврат Результат;
	
КонецФункции

// Проверяет возможность запуска согласования в ДО.
//
// Параметры:
//   ПредметСогласования - ЛюбаяСсылка - согласуемый объект ИС.
//   ТекстПредупреждения - Строка - неявно возвращаемое значение, текст предупреждения.
//
// Возвращаемое значение:
//   Булево - Истина, если запуск согласования разрешен, и Ложь в противном случае 
//
Функция ПользователюРазрешенЗапускСогласования(Знач ПредметСогласования, ТекстПредупреждения) Экспорт
	
	Результат = ИнтеграцияС1СДокументооборотПереопределяемый.ПользователюРазрешенЗапускСогласования(
		ПредметСогласования, ТекстПредупреждения);
		
	Если ТипЗнч(Результат) = Тип("Булево") Тогда
		Возврат Результат;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

// Проверяет возможность прерывания согласования в ДО.
//
// Параметры:
//   ПредметСогласования - ЛюбаяСсылка - согласуемый объект ИС.
//   ПредметДО - Структура - описание связанного объекта ДО:
//      name - Строка - представление связанного объекта.
//      id - Строка - идентификатор связанного объекта.
//      type - Строка - имя типа XDTO.
//   ТекстПредупреждения - Строка - неявно возвращаемое значение, текст предупреждения.
//
// Возвращаемое значение:
//   Булево - Истина, если прерывание согласования разрешено, и Ложь в противном случае 
//
Функция ПользователюРазрешеноПрерываниеСогласования(Знач ПредметСогласования, Знач ПредметДО, ТекстПредупреждения) Экспорт
	
	Результат = ИнтеграцияС1СДокументооборотПереопределяемый.ПользователюРазрешеноПрерываниеСогласования(
		ПредметСогласования, ПредметДО, ТекстПредупреждения);
		
	Если ТипЗнч(Результат) = Тип("Булево") Тогда
		Возврат Результат;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

// Проверяет, существуют ли адресованные пользователю задачи согласования по ссылке на объект ИС.
//
// Параметры:
//   ПредметСогласования - ЛюбаяСсылка - согласуемый объект ИС.
//
// Возвращаемое значение:
//   Булево - Истина, если есть задачи согласования, адресованные текущему пользователю.
//
Функция ПользователюАдресованыЗадачиСогласования(Знач ПредметСогласования) Экспорт
	
	Результат = ДанныеОбъектаДОПоВнешнемуОбъекту(ПредметСогласования);
	
	Если Результат = Неопределено Тогда
		Возврат Ложь;
	Иначе
		ЗадачиСогласования = ПолучитьЗадачиСогласования(Результат);
		Возврат ЗадачиСогласования.Количество() <> 0;
	КонецЕсли;
	
КонецФункции

// Проверяет, запущены ли уже процессы согласования в ДО по ссылке на объект ИС.
//
// Параметры:
//   ПредметСогласования - ЛюбаяСсылка - согласуемый объект ИС.
//
// Возвращаемое значение:
//   Булево - Истина, если есть активные процессы согласования, и Ложь в противном случае.
//
Функция ЗапущеноСогласование(Знач ПредметСогласования) Экспорт
	
	Результат = ДанныеОбъектаДОПоВнешнемуОбъекту(ПредметСогласования);
	
	Если Результат = Неопределено Тогда
		Возврат Ложь;
	Иначе
		Возврат ЗапущеноСогласованиеПоПредметуДО(Результат);
	КонецЕсли;
	
КонецФункции

// Проверяет, запущены ли уже процессы согласования в ДО по описанию предмета в ДО.
//
// Параметры:
//   ПредметДО - Структура:
//     name - Строка - представление предмета.
//     id - Строка - идентификатор связанного объекта ДО.
//     type - Строка - имя типа XDTO.
//
// Возвращаемое значение:
//   Булево - Истина, если есть активные процессы согласования, и Ложь в противном случае.
//
Функция ЗапущеноСогласованиеПоПредметуДО(Знач ПредметДО) Экспорт

	// Выберем активные процессы типа "согласование" по указанному предмету.
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetObjectListRequest");
	Запрос.type = "DMBusinessProcessApproval";
	Запрос.query = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListQuery");
	
	Условие = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
	Условие.property = "target";
	Условие.value = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, 
		ПредметДО.id, ПредметДО.type);
	Запрос.query.conditions.Добавить(Условие);

	Условие = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
	Условие.property = "withExecuted";
	Условие.value = Ложь;
	Запрос.query.conditions.Добавить(Условие);
	
	Условие = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
	Условие.property = "withDelayed";
	Условие.value = Ложь;
	Запрос.query.conditions.Добавить(Условие);
	
	Ответ = Прокси.execute(Запрос);
	Попытка
		ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	Исключение
		Возврат Ложь;
	КонецПопытки;

	Возврат (Ответ.items.Количество() <> 0);

КонецФункции

// Запускает согласование по указанному шаблону и предмету и возвращает признак успешности запуска.
//
// Параметры:
//    Шаблон - Структура:
//      name - Строка - представление шаблона.
//      id - Строка - идентификатор шаблона.
//      type - Строка - имя типа XDTO.
//    Предмет - Структура:
//      name - Строка - представление предмета.
//      id - Строка - идентификатор предмета.
//      type - Строка - имя типа XDTO.
//
// Возвращаемое значение:
//    Булево - Истина, если процесс запущен успешно.
//
Функция ЗапуститьСогласованиеПоШаблону(Знач Шаблон, Знач Предмет) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	ПроцессПоШаблону = ИнтеграцияС1СДокументооборот.НовыйБизнесПроцессПоШаблону(Прокси, 
		"DMBusinessProcessApproval", Шаблон, Предмет);
	НовыйПроцесс = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMBusinessProcessApproval");
	ИнтеграцияС1СДокументооборот.ЗаполнитьЗначенияСвойствXDTO(Прокси, НовыйПроцесс, ПроцессПоШаблону);
	
	Попытка
		РезультатЗапуска = ИнтеграцияС1СДокументооборот.ЗапуститьБизнесПроцесс(Прокси, НовыйПроцесс);
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
	Возврат (ИнтеграцияС1СДокументооборот.ПроверитьТип(Прокси, РезультатЗапуска, "DMError") = Ложь);
	
КонецФункции

// Прерывает процессы согласования по указанному предмету.
//
// Параметры:
//   ПредметДО - Структура:
//     name - Строка - представление предмета.
//     id - Строка - идентификатор связанного объекта ДО.
//     type - Строка - имя типа XDTO.
//
// Возвращаемое значение:
//   Булево - Истина, если процесс прерван успешно.
//
Функция ПрерватьСогласование(Знач ПредметДО) Экспорт

	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetObjectListRequest");
	Запрос.type = "DMBusinessProcessApproval";
	Запрос.query = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListQuery");
	
	Условие = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
	Условие.property = "target";
	Условие.value = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, 
		ПредметДО.id, ПредметДО.type);
		
	Запрос.query.conditions.Добавить(Условие);

	Ответ = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	Если Ответ.items.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Процессов согласования может быть несколько. Прервем все.
	ЗапросНаЗапись = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMUpdateRequest");
	Для каждого Элемент из Ответ.items Цикл
		
		Процесс = Элемент.object;
		DMRetrieveRequest = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMRetrieveRequest");
		ObjectId = ИнтеграцияС1СДокументооборот.СоздатьObjectId(Прокси, 
			Процесс.objectId.id, Процесс.objectId.type);
		DMRetrieveRequest.objectIds.Добавить(ObjectId);
		DMRetrieveResponse = Прокси.execute(DMRetrieveRequest);
		ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, DMRetrieveResponse);
		
		ПрочитанныйПроцесс = DMRetrieveResponse.objects[0];
		СостояниеПрерван = ИнтеграцияС1СДокументооборот.СоздатьОбъект(
			Прокси, "DMBusinessProcessState");
		СостояниеПрерван.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectId(
			Прокси, "Прерван", "DMBusinessProcessState");
		СостояниеПрерван.name = "Прерван";
		ПрочитанныйПроцесс.state = СостояниеПрерван;
		ЗапросНаЗапись.objects.Добавить(ПрочитанныйПроцесс);
		
	КонецЦикла;
	
	Ответ = Прокси.execute(ЗапросНаЗапись);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
	Возврат Истина;

КонецФункции

// Получает задачи согласования по предмету, адресованные пользователю.
//
// Параметры:
//   ПредметДО - Структура:
//     name - Строка - представление предмета.
//     id - Строка - идентификатор связанного объекта ДО.
//     type - Строка - имя типа XDTO.
//
// Возвращаемое значение:
//   Массив элементов типа Строка - идентификаторы задач согласования в ДО.
//
Функция ПолучитьЗадачиСогласования(Знач ПредметДО) Экспорт
	
	Результат = Новый Массив;
	
	// Выберем активные задачи пользователя, потребовав указать их тип.
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetObjectListRequest");
	Запрос.type = "DMBusinessProcessApprovalTaskApproval";
	Запрос.query = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListQuery");
	
	Условие = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
	Условие.property = "target";
	Условие.value = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, 
		ПредметДО.id, ПредметДО.type);
	Запрос.query.conditions.Добавить(Условие);

	Условие = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
	Условие.property = "byUser";
	Условие.value = Истина;
	Запрос.query.conditions.Добавить(Условие);

	Условие = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
	Условие.property = "typed";
	Условие.value = Истина;
	Запрос.query.conditions.Добавить(Условие);

	Условие = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
	Условие.property = "withExecuted";
	Условие.value = Ложь;
	Запрос.query.conditions.Добавить(Условие);

	Условие = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
	Условие.property = "withDelayed";
	Условие.value = Ложь;
	Запрос.query.conditions.Добавить(Условие);

	Ответ = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
	// Отберем полученные задачи по типу "Согласование".
	Для каждого Элемент из Ответ.items Цикл
		Задача = Элемент.object;
		Если ИнтеграцияС1СДокументооборот.ПроверитьТип(Прокси, Задача, 
			"DMBusinessProcessApprovalTaskApproval") Тогда
			Результат.Добавить(Задача.objectId.id);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

// Выполняет задачи согласования. Возвращает Истина в случае успеха, Ложь - в случае ошибки.
//
// Параметры:
//   Задачи - Массив элементов типа Строка - идентификаторы задач.
//   Результат - Строка - описание результата:
//     "Согласовано",
//     "СогласованоСЗамечаниями" или
//     "НеСогласовано".
//   Комментарий - Строка - комментарий к результатам "НеСогласовано" или "СогласованоСЗамечаниями".
//   ТекстСообщенияОбОшибке - Строка - неявно возвращаемое значение, описание ошибки.
//
// Возвращаемое значение:
//   Булево - Истина, если согласование всех задач выполнено успешно, и Ложь в противном случае.
//
Функция ВыполнитьСогласование(Знач Задачи, Знач Результат, Знач Комментарий, ТекстСообщенияОбОшибке) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Для Каждого ИдентификаторЗадачи из Задачи Цикл
		
		ОбъектыXDTO = ИнтеграцияС1СДокументооборот.ПолучитьОбъект(Прокси,
			"DMBusinessProcessApprovalTaskApproval", ИдентификаторЗадачи);
		Задача = ОбъектыXDTO.objects[0];
		
		Ответ = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMApprovalResult");
		Ответ.objectID = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси,
			Результат, "DMApprovalResult");
		Если Результат = "Согласовано" Тогда
			Ответ.name = НСтр("ru = 'Согласовано'");
		ИначеЕсли Результат = "СогласованоСЗамечаниями" Тогда
			Ответ.name = НСтр("ru = 'Согласовано с замечаниями'");
		ИначеЕсли Результат = "НеСогласовано" Тогда
			Ответ.name = НСтр("ru = 'Не согласовано'");
		КонецЕсли;
		Задача.approvalResult = Ответ;
		Задача.executed = Истина;
		Задача.endDate = ТекущаяДатаСеанса();
		Задача.executionComment = Комментарий;
		
		Ответ = ИнтеграцияС1СДокументооборот.ЗаписатьОбъект(Прокси, Задача);
		
		Если ИнтеграцияС1СДокументооборот.ПроверитьТип(Прокси, Ответ, "DMError") Тогда
			ТекстСообщенияОбОшибке = Ответ.description;
			Возврат Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

// Вызывается при изменении состояния согласования в ДО. Изменяет состояние на стороне ИС.
//
// Параметры:
//   Идентификатор - Строка - идентификатор связанного объекта ДО.
//   Тип - Строка - тип связанного объекта ДО.
//   Состояние - ПеречислениеСсылка.СостоянияСогласованияВДокументообороте - новое состояние, или
//             - Неопределено - при прерывании согласования.
//   ВызовИзФормыОбъекта - Булево - Истина, если изменение состояния вызвано пользователем из формы объекта.
//   ПредметСогласования - ЛюбаяСсылка - согласуемый объект, или
//                       - Неопределено - признак необходимости найти объект ИС по объекту ДО.
//   Установил - Строка - представление пользователя, установившего новое состояние.
//   ДатаУстановки - Дата - Дата установки нового состояния.
//
Процедура ПриИзмененииСостоянияСогласования(Знач Идентификатор, Знач Тип, Знач Состояние, Знач ВызовИзФормыОбъекта,
	ПредметСогласования = Неопределено, Знач Установил = Неопределено, Знач ДатаУстановки = Неопределено) Экспорт
	
	НаборЗаписей = РегистрыСведений.СостоянияСогласованияВДокументообороте.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ИдентификаторОбъектаДО.Установить(Идентификатор);
	
	Если Состояние <> Неопределено Тогда // Неопределено - удаление
		Запись = НаборЗаписей.Добавить();
		Запись.ИдентификаторОбъектаДО = Идентификатор;
		Запись.Состояние = Состояние;
		Если Установил = Неопределено Тогда
			Запись.Установил = Строка(Пользователи.ТекущийПользователь());
		Иначе
			Запись.Установил = Установил;
		КонецЕсли;
		Если ДатаУстановки = Неопределено Тогда
			Запись.ДатаУстановки = ТекущаяДатаСеанса();
		Иначе
			Запись.ДатаУстановки = ДатаУстановки;
		КонецЕсли;
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
	// Ссылка на предмет в ИС может быть неизвестна, если вызов - из формы задачи ДО. Определим ее.
	Если ПредметСогласования = Неопределено Тогда
		Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	Объект КАК ПредметСогласования
			|ИЗ 
			|	РегистрСведений.ОбъектыИнтегрированныеС1СДокументооборотом КАК ОбъектыИнтегрированныеС1СДокументооборотом
			|ГДЕ 
			|	ТипОбъектаДО = &Тип
			|	И ИдентификаторОбъектаДО = &Идентификатор
			|");
		Запрос.УстановитьПараметр("Тип", Тип);
		Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ПредметСогласования = Выборка.ПредметСогласования;
		КонецЕсли;
	КонецЕсли;
	
	// На стороне ИС не следует выполнять действия при согласовании несвязанных объектов ДО.
	Если ПредметСогласования <> Неопределено Тогда
		ИнтеграцияС1СДокументооборотПереопределяемый.ПриИзмененииСостоянияСогласования(
			ПредметСогласования, Состояние, ВызовИзФормыОбъекта);
	КонецЕсли;

КонецПроцедуры

// Получает состояние согласования в ДО по ссылке на объект ИС
//
// Параметры:
//   ПредметСогласования - ЛюбаяСсылка - ссылка на объект ИС.
//   Пояснение - Строка - неявно возвращаемое значение, пояснение к состоянию.
//
// Возвращаемое значение:
//   ПеречислениеСсылка.СостоянияСогласованияВДокументообороте - состояние согласования или
//   Неопределено - если согласование по объекту не запущено.
//
Функция ПолучитьСостояниеСогласования(Знач ПредметСогласования, Пояснение = "") Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	СостоянияСогласования.Состояние КАК Состояние,
		|	СостоянияСогласования.Установил КАК Установил,
		|	СостоянияСогласования.ДатаУстановки КАК ДатаУстановки
		|ИЗ 
		|	РегистрСведений.СостоянияСогласованияВДокументообороте КАК СостоянияСогласования
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ОбъектыИнтегрированныеС1СДокументооборотом КАК ОбъектыИнтегрированныеС1СДокументооборотом
		|ПО
		|	СостоянияСогласования.ИдентификаторОбъектаДО 
		|		= ОбъектыИнтегрированныеС1СДокументооборотом.ИдентификаторОбъектаДО
		|ГДЕ 
		|	ОбъектыИнтегрированныеС1СДокументооборотом.Объект = &ПредметСогласования
		|");
	Запрос.УстановитьПараметр("ПредметСогласования", ПредметСогласования);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Пояснение = ИнтеграцияС1СДокументооборотПереопределяемый.ПояснениеСостоянияСогласования(
			Выборка.Установил, Выборка.ДатаУстановки);
		Возврат Выборка.Состояние;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Возвращает структуру, описывающую основные действия над объектом Документооборота
//
// Параметры:
//   Идентификатор - идентификатор объекта Документооборота
//   Тип - тип XDTO объекта Документооборота
//
// Возвращаемое значение: 
//   Структура со свойствами:
//     * СсылкаОткрытия - ЛюбаяСсылка - ссылка на связанный объект
//     * СсылкаОткрытияПредставление - Строка - представление этой ссылки
//     * КомандыСоздания - массив структур
//     * Тип - Строка - полное имя типа, например, Справочник.Контрагенты
//     * Представление - Строка - представление типа, например, "Поступление товаров и услуг"
//     * ВидДокументаID - Строка - для документов, вид документа предмета
//
Функция СтруктураКомандСозданияИОткрытия(Знач Тип, Знач Идентификатор) Экспорт
	
	Если Не ЗначениеЗаполнено(Тип) или не ЗначениеЗаполнено(Идентификатор) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Новый Структура;
	
	// существует ли связанный объект?
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	Реквизиты = Новый Массив;
	Реквизиты.Добавить("externalObject");
	
	Если ИнтеграцияС1СДокументооборотКлиентСервер.ЭтоДокумент(Тип) Тогда
		
		Реквизиты.Добавить("documentType");
		ОбъектыXDTO = ИнтеграцияС1СДокументооборот.ПолучитьОбъект(
			Прокси, Тип, Идентификатор, Реквизиты);
		ОбъектXDTO = ОбъектыXDTO.objects[0];
		ВидДокументаID = ОбъектXDTO.documentType.ObjectID.id;
		ВидДокументаТип = ОбъектXDTO.documentType.ObjectID.type;
		
	Иначе // объект иного типа, для которого не имеют смысла виды документов
		
		ОбъектыXDTO = ИнтеграцияС1СДокументооборот.ПолучитьОбъект(
			Прокси, Тип, Идентификатор, Реквизиты);
		ОбъектXDTO = ОбъектыXDTO.objects[0];
		ВидДокументаID = Неопределено;
		ВидДокументаТип = Неопределено;
		
	КонецЕсли;
	
	КомандыСоздания = Новый Массив;
	Если ОбъектXDTO.externalObject = Неопределено Тогда
		ПравилаЗаполнения = Справочники.ПравилаИнтеграцииС1СДокументооборотом.ПравилаИнтеграцииОбъектов(
			Тип, ВидДокументаID, ВидДокументаТип);
		Для каждого ПравилоЗаполнения из ПравилаЗаполнения Цикл
			КомандаСоздания = Новый Структура;
			КомандаСоздания.Вставить("Тип", ПравилоЗаполнения.ТипОбъектаИС);
			ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ПравилоЗаполнения.ТипОбъектаИС);
			Если ОбъектМетаданных = Неопределено Тогда
				Представление = ПравилоЗаполнения.ТипОбъектаИС;
			Иначе
				Если Метаданные.Справочники.Содержит(ОбъектМетаданных) Тогда
					Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Элемент справочника %1'"),
						ОбъектМетаданных.Представление());
				Иначе
					Представление = ОбъектМетаданных.Представление();
				КонецЕсли;
			КонецЕсли;
			КомандаСоздания.Вставить("Представление", Представление);
			КомандыСоздания.Добавить(КомандаСоздания);
		КонецЦикла;
	КонецЕсли;
	Результат.Вставить("КомандыСоздания", КомандыСоздания);
	Результат.Вставить("ВидДокументаID", ВидДокументаID);
	
	Возврат Результат;

КонецФункции

// Возвращает структуру, описывающую варианты исполнения задачи.
//
// Параметры:
//   РеквизитыЗадачи - Строка - cтруктура со свойствами:
//     * ЗадачаТип - Строка - тип задачи строкой.
//     * ЗадачаID - Строка - идентификатор задачи.
//     * ПроцессТип - Строка - имя тип процесса.
//     * ПроцессID - Строка - идентификатор процесса.
//     * ТочкаМаршрутаКратко - Строка - точка маршрута.
//     * Выполнена - Булево - признак выполнения задачи.
//     * ВидВопросаID - Строка - вид вопроса точки маршрута "Рассмотрение инициатором".
//     * РезультатID - Строка - результат выполнения задачи.
//
// Возвращаемое значение:
//   Структура со свойствами:
//     * ФункционалНеДоступен - Булево - признак доступности функционала выполнения задачи в данной версии ДО.
//     * ВыполнитьЗадачуПервая - Строка - заголовок первой кнопки.
//     * ЦветТекстаПервая - Цвет - цвет первой кнопки.
//     * ВыполнитьЗадачуВторая - Строка - заголовок второй кнопки.
//     * ЦветТекстаВторая - Цвет - цвет второй кнопки.
//     * ВыполнитьЗадачуТретья - Строка - заголовок третьей кнопки.
//     * ЦветТекстаТретья - Цвет - цвет третьей кнопки.
//
Функция СтруктураИсполненияЗадачи(Знач РеквизитыЗадачи) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ФункционалНеДоступен", Ложь);
	Результат.Вставить("ВыполнитьЗадачуПервая", "");
	Результат.Вставить("ВыполнитьЗадачуВторая", "");
	Результат.Вставить("ВыполнитьЗадачуТретья", "");
	Результат.Вставить("ЦветТекстаПервая", ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи);
	Результат.Вставить("ЦветТекстаВторая", Новый Цвет);
	Результат.Вставить("ЦветТекстаТретья", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
	
	Если РеквизитыЗадачи.Выполнена Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если РеквизитыЗадачи.ТочкаМаршрутаКратко = "РассмотрениеИнициатором" И РеквизитыЗадачи.ВидВопросаID = "ПереносСрока"
			И Не ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("2.1.16.2.CORP") Тогда
		Результат.ФункционалНеДоступен = Истина;
		Возврат Результат;
	КонецЕсли;
	
	Если РеквизитыЗадачи.Состояние = "Остановлен" Или РеквизитыЗадачи.Состояние = "Прерван" Тогда
		Возврат Результат;
	КонецЕсли;
	
	// Согласование.
	Если РеквизитыЗадачи.ПроцессТип = "DMBusinessProcessApproval" Тогда
		Если РеквизитыЗадачи.ТочкаМаршрутаКратко = "Согласовать" Тогда
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Согласовано'"));
			Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Согласовано
																|с замечаниями'"));
			Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи);
			Результат.Вставить("ВыполнитьЗадачуТретья", НСтр("ru='Не согласовано'"));
		ИначеЕсли РеквизитыЗадачи.ТочкаМаршрутаКратко = "Ознакомиться" Тогда
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Ознакомился'"));
			Если РеквизитыЗадачи.РезультатID = "НеСогласовано" Тогда
				Результат.ВыполнитьЗадачуПервая = НСтр("ru='Завершить согласование'");
				Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Повторить согласование...'"));
				Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
			КонецЕсли;
		КонецЕсли;
	// Утверждение.
	ИначеЕсли РеквизитыЗадачи.ПроцессТип = "DMBusinessProcessConfirmation" Тогда
		Если РеквизитыЗадачи.ТочкаМаршрутаКратко = "Утвердить" Тогда
			Если РеквизитыЗадачи.ЭтоПодписание Тогда
				Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Подписать'"));
				Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Отклонить'"));
			Иначе
				Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Утверждено'"));
				Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Не утверждено'"));
			КонецЕсли;
			Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
		ИначеЕсли РеквизитыЗадачи.ТочкаМаршрутаКратко = "Ознакомиться" Тогда
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Ознакомился'"));
			Если РеквизитыЗадачи.РезультатID = "НеУтверждено" Тогда
				Если РеквизитыЗадачи.ЭтоПодписание Тогда
					Результат.ВыполнитьЗадачуПервая = НСтр("ru='Завершить подписание'");
					Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Повторить подписание...'"));
				Иначе
					Результат.ВыполнитьЗадачуПервая = НСтр("ru='Завершить утверждение'");
					Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Повторить утверждение...'"));
				КонецЕсли;
				Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
			КонецЕсли;
		КонецЕсли;
	// Рассмотрение.
	ИначеЕсли РеквизитыЗадачи.ПроцессТип = "DMBusinessProcessConsideration" Тогда
		Если РеквизитыЗадачи.ТочкаМаршрутаКратко = "Рассмотреть" Тогда
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Рассмотрено'"));
		ИначеЕсли РеквизитыЗадачи.ТочкаМаршрутаКратко = "Ознакомиться" Тогда
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Обработано'"));
		КонецЕсли;
	// Поручение.
	ИначеЕсли РеквизитыЗадачи.ПроцессТип = "DMBusinessProcessOrder" Тогда
		Если РеквизитыЗадачи.ТочкаМаршрутаКратко = "Выполнить" Тогда
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Выполнено'"));
		ИначеЕсли РеквизитыЗадачи.ТочкаМаршрутаКратко = "Проверить" Тогда
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Завершить поручение'"));
			Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Вернуть на доработку'"));
			Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
		ИначеЕсли РеквизитыЗадачи.ТочкаМаршрутаКратко = "Контролировать" Тогда
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Снять с контроля'"));
		КонецЕсли;
	// Исполнение.
	ИначеЕсли РеквизитыЗадачи.ПроцессТип = "DMBusinessProcessPerformance" Тогда
		Если РеквизитыЗадачи.ТочкаМаршрутаКратко = "Исполнить"
				Или РеквизитыЗадачи.ТочкаМаршрутаКратко = "ОтветственноеИсполнение" Тогда
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Исполнено'"));
		ИначеЕсли РеквизитыЗадачи.ТочкаМаршрутаКратко = "Контролировать" Тогда
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Снять с контроля'"));
		ИначеЕсли РеквизитыЗадачи.ТочкаМаршрутаКратко = "Проверить" Тогда
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Завершить'"));
			Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Вернуть...'"));
			Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
		КонецЕсли;
	ИначеЕсли РеквизитыЗадачи.ПроцессТип = "DMComplexBusinessProcess" Тогда
		Если РеквизитыЗадачи.ТочкаМаршрутаКратко = "Контролировать" Тогда
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Проконтролировано'"));
		КонецЕсли;
	// Регистрация.
	ИначеЕсли РеквизитыЗадачи.ПроцессТип = "DMBusinessProcessRegistration" Тогда
		Если РеквизитыЗадачи.ТочкаМаршрутаКратко = "Зарегистрировать" Тогда
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Зарегистрировать'"));
			Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Не регистрировать'"));
			Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
		ИначеЕсли РеквизитыЗадачи.ТочкаМаршрутаКратко = "Ознакомиться" Тогда
			Если РеквизитыЗадачи.РезультатID = "НеЗарегистрировано" Тогда
				Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Завершить регистрацию'"));
				Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Повторить регистрацию'"));
				Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
			Иначе
				Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Ознакомился'"));
			КонецЕсли;
		КонецЕсли;
	// Ознакомление.
	ИначеЕсли РеквизитыЗадачи.ПроцессТип = "DMBusinessProcessAcquaintance" Тогда
		Если РеквизитыЗадачи.ТочкаМаршрутаКратко = "Ознакомиться" Тогда
			Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Ознакомился'"));
		КонецЕсли;
	// Решение вопросов.
	ИначеЕсли РеквизитыЗадачи.ПроцессТип = "DMBusinessProcessIssuesSolution" Тогда
		Если ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.2.7.3") Тогда
			Если РеквизитыЗадачи.ТочкаМаршрутаКратко = "РассмотрениеИнициатором"
					И РеквизитыЗадачи.ВидВопросаID = "Иное" Тогда
				Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Рассмотрено'"));
			ИначеЕсли РеквизитыЗадачи.ТочкаМаршрутаКратко = "РассмотрениеИнициатором"
					И РеквизитыЗадачи.ВидВопросаID = "ПереносСрока" Тогда
				Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Перенести срок'"));
				Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Отказать в переносе'"));
				Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
			ИначеЕсли РеквизитыЗадачи.ТочкаМаршрутаКратко = "ОзнакомлениеСРезультатомРассмотрения" Тогда
				Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Закрыть вопрос'"));
				Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Уточнить'"));
				Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
			Иначе
				Результат.ФункционалНеДоступен = Истина;
				Возврат Результат;
			КонецЕсли;
		КонецЕсли;
	// Приглашение.
	ИначеЕсли РеквизитыЗадачи.ПроцессТип = "DMBusinessProcessInvitation" Тогда
		Если ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.2.7.3") Тогда
			Если РеквизитыЗадачи.ТочкаМаршрутаКратко = "Пригласить" Тогда
				Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Принять приглашение'"));
				Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Отклонить приглашение'"));
				Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
			ИначеЕсли РеквизитыЗадачи.ТочкаМаршрутаКратко = "Ознакомиться" Тогда
				// Зависит от результата принятия.
				Если РеквизитыЗадачи.РезультатID = "ПринятоВсемиУчастниками" Тогда
					Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Подтвердить приглашения'"));
				ИначеЕсли РеквизитыЗадачи.РезультатID = "ПринятоОбязательнымиУчастниками" Тогда
					Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Подтвердить приглашения'"));
					Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Повторить приглашение...'"));
					Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
				ИначеЕсли РеквизитыЗадачи.РезультатID = "НеПринятоОбязательнымиУчастниками"
						Или РеквизитыЗадачи.РезультатID = "НеПринятоВсемиУчастниками" Тогда
					Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Повторить приглашение...'"));
					Результат.Вставить("ВыполнитьЗадачуВторая", НСтр("ru='Отменить приглашение'"));
					Результат.Вставить("ЦветТекстаВторая", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
				КонецЕсли;
			ИначеЕсли РеквизитыЗадачи.ТочкаМаршрутаКратко = "Оповестить" Тогда
				Результат.Вставить("ВыполнитьЗадачуПервая", НСтр("ru='Ознакомился'"));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Останавливает процесс ДО. Вызывает исключение в случае невозможности выполнить это в случае,
// когда прав недостаточно или текущее состояние процесса не позволяет выполнить это.
//
// Параметры:
//   ID - Строка - идентификатор процесса.
//   Тип - Строка - тип XDTO-типа процесса.
//
Процедура ОстановитьПроцесс(ID, Тип) Экспорт
	
	ИмяСостояния = "Остановлен";
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	ЗапросНаЧтение = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMRetrieveRequest");
	ObjectId = ИнтеграцияС1СДокументооборот.СоздатьObjectId(Прокси, ID, Тип);
	ЗапросНаЧтение.objectIds.Добавить(ObjectId);
	
	Ответ = Прокси.execute(ЗапросНаЧтение);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	ПрочитанныйПроцесс = Ответ.objects[0];
	
	ЗапросНаЗапись = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMUpdateRequest");
		
	НовоеСостояние = ИнтеграцияС1СДокументооборот.СоздатьОбъект(
		Прокси, "DMBusinessProcessState");
	НовоеСостояние.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectId(
		Прокси, ИмяСостояния, "DMBusinessProcessState");
	НовоеСостояние.name = ИмяСостояния;
	ПрочитанныйПроцесс.state = НовоеСостояние;
	ЗапросНаЗапись.objects.Добавить(ПрочитанныйПроцесс);
		
	Ответ = Прокси.execute(ЗапросНаЗапись);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
КонецПроцедуры

// Прерывает процесс ДО. Вызывает исключение в случае невозможности выполнить это в случае,
// когда прав недостаточно или текущее состояние процесса не позволяет выполнить это.
//
// Параметры:
//   ID - Строка - идентификатор процесса.
//   Тип - Строка - тип XDTO-типа процесса.
//
Процедура ПрерватьПроцесс(ID, Тип) Экспорт
	
	ИмяСостояния = "Прерван";
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	ЗапросНаЧтение = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMRetrieveRequest");
	ObjectId = ИнтеграцияС1СДокументооборот.СоздатьObjectId(Прокси, ID, Тип);
	ЗапросНаЧтение.objectIds.Добавить(ObjectId);
	
	Ответ = Прокси.execute(ЗапросНаЧтение);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	ПрочитанныйПроцесс = Ответ.objects[0];
	
	ЗапросНаЗапись = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMUpdateRequest");
		
	НовоеСостояние = ИнтеграцияС1СДокументооборот.СоздатьОбъект(
		Прокси, "DMBusinessProcessState");
	НовоеСостояние.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectId(
		Прокси, ИмяСостояния, "DMBusinessProcessState");
	НовоеСостояние.name = ИмяСостояния;
	ПрочитанныйПроцесс.state = НовоеСостояние;
	ЗапросНаЗапись.objects.Добавить(ПрочитанныйПроцесс);
		
	Ответ = Прокси.execute(ЗапросНаЗапись);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
КонецПроцедуры

// Продолжает процесс ДО. Вызывает исключение в случае невозможности выполнить это в случае,
// когда прав недостаточно или текущее состояние процесса не позволяет выполнить это.
//
// Параметры:
//   ID - Строка - идентификатор процесса.
//   Тип - Строка - тип XDTO-типа процесса.
//
Процедура ПродолжитьПроцесс(ID, Тип) Экспорт
	
	ИмяСостояния = "Активен";
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	ЗапросНаЧтение = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMRetrieveRequest");
	ObjectId = ИнтеграцияС1СДокументооборот.СоздатьObjectId(Прокси, ID, Тип);
	ЗапросНаЧтение.objectIds.Добавить(ObjectId);
	
	Ответ = Прокси.execute(ЗапросНаЧтение);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	ПрочитанныйПроцесс = Ответ.objects[0];
	
	ЗапросНаЗапись = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMUpdateRequest");
		
	НовоеСостояние = ИнтеграцияС1СДокументооборот.СоздатьОбъект(
		Прокси, "DMBusinessProcessState");
	НовоеСостояние.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectId(
		Прокси, ИмяСостояния, "DMBusinessProcessState");
	НовоеСостояние.name = ИмяСостояния;
	ПрочитанныйПроцесс.state = НовоеСостояние;
	ЗапросНаЗапись.objects.Добавить(ПрочитанныйПроцесс);
		
	Ответ = Прокси.execute(ЗапросНаЗапись);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
КонецПроцедуры

#КонецОбласти

#Область Документы

// Создает файлы печатных форм объекта по имени команды формы и возвращает их имена.
//
// Параметры:
//   ВнешнийОбъект - ЛюбаяСсылка - ссылка на объект текущей базы данных.
//	 ИмяКоманды - Строка - имя команды кнопки в формате "Менеджер_%_Команда_%".
//	 ФорматФайла - ПеречислениеСсылка.ТипыФайловСохраненияПечатныхФормОбъектов - формат файла для сохранения.
//
// Возвращаемое значение:
//	 Массив структур с ключами ИмяВременногоФайла, Имя, Расширение, ВремяИзменения и ВремяИзмененияУниверсальное
//
Функция СохранитьПечатнуюФормуОбъекта(ВнешнийОбъект, ИмяКоманды, ФорматФайла) Экспорт
	
	ОписанияФайлов = Новый Массив;
	
	КоллекцияПечатныхФорм = Неопределено;
	ИнтеграцияС1СДокументооборотПереопределяемый.ЗаполнитьПечатныеФормы(ВнешнийОбъект, ИмяКоманды, 
		КоллекцияПечатныхФорм);
	Расширение = ИнтеграцияС1СДокументооборот.
		РасширениеСохраняемойПечатнойФормы(ФорматФайла);
	
	Для каждого ПечатнаяФорма из КоллекцияПечатныхФорм Цикл
		Если ПечатнаяФорма.ТабличныйДокумент <> Неопределено И ПечатнаяФорма.ТабличныйДокумент.КоличествоСтраниц() > 0 Тогда
			
			ОписаниеФайла = Новый Структура;
			
			ОписаниеФайла.Вставить("ИмяВременногоФайла", ПолучитьИмяВременногоФайла(Расширение));
			ПечатнаяФорма.ТабличныйДокумент.Записать(ОписаниеФайла.ИмяВременногоФайла,
				ТипФайлаТабличногоДокумента[Расширение]);
			ОписаниеФайла.Вставить("ВремяИзменения", ТекущаяДатаСеанса());
			ОписаниеФайла.Вставить("ВремяИзмененияУниверсальное", ТекущаяУниверсальнаяДата());
			ОписаниеФайла.Вставить("ИмяФайла", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='%1 №%2 от %3'"),
				Строка(?(ЗначениеЗаполнено(ПечатнаяФорма.СинонимМакета),ПечатнаяФорма.СинонимМакета,ПечатнаяФорма.ИмяМакета)),
				Строка(ВнешнийОбъект.Номер),
				Формат(ВнешнийОбъект.Дата,"ДЛФ=D")));
			ОписаниеФайла.Вставить("Расширение", Расширение);
			
			ОписанияФайлов.Добавить(ОписаниеФайла);
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОписанияФайлов;
				
КонецФункции

// Создает файлы печатных форм объектов по имени команды формы документа 1С:Документооборот и присоединяет их к документам.
//
// Параметры:
//   ВнешнийОбъект - ЛюбаяСсылка - ссылка на объект текущей базы данных.
//   ID - Строка - идентификатор документа 1С:Документооборота.
//   Тип - Строка - тип документа 1С:Документооборота.
//   Представление - Строка - представление документа 1С:Документооборота.
//   ИмяКоманды - Строка - имя команды кнопки в формате "Менеджер_%_Команда_%".
//
// Возвращаемое значение:
//   Массив - содержит структуры, описывающие созданные файлы:
//     Идентификатор - Строка - идентификатор созданного файла.
//     Имя - Строка - имя без расширения.
//     Расширение - Строка - расширение без точки.
//
Функция ПрисоединитьПечатнуюФормуОбъектаКДокументу(ВнешнийОбъект, ID, Тип, Представление, ИмяКоманды, ФорматФайла) Экспорт
	
	Результат = Новый Массив;
	
	КоллекцияПечатныхФорм = Неопределено;
	ИнтеграцияС1СДокументооборотПереопределяемый.ЗаполнитьПечатныеФормы(ВнешнийОбъект, ИмяКоманды, КоллекцияПечатныхФорм);
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	ФорматФайлаИмя = ОбщегоНазначения.ИмяЗначенияПеречисления(ФорматФайла);
	
	Для каждого ПечатнаяФорма из КоллекцияПечатныхФорм Цикл
		Если ПечатнаяФорма.ТабличныйДокумент <> Неопределено И ПечатнаяФорма.ТабличныйДокумент.КоличествоСтраниц() > 0 Тогда
			ПечатнаяФорма.ТабличныйДокумент.Записать(ИмяВременногоФайла,ТипФайлаТабличногоДокумента[ФорматФайлаИмя]);
			
			ПараметрыСоздания = НовыеПараметрыСозданияФайла();
			
			ИмяФайла = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='%1 №%2 от %3'"),
				Строка(?(ЗначениеЗаполнено(ПечатнаяФорма.СинонимМакета),ПечатнаяФорма.СинонимМакета,ПечатнаяФорма.ИмяМакета)),
				Строка(ВнешнийОбъект.Номер),
				Формат(ВнешнийОбъект.Дата,"ДЛФ=D")); 
				
			ИмяФайла = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ИмяФайла," ");
			ПараметрыСоздания.Имя = ИмяФайла;
			ПараметрыСоздания.Расширение = ИнтеграцияС1СДокументооборот.
				РасширениеСохраняемойПечатнойФормы(ФорматФайла);
			ПараметрыСоздания.ВебКлиент = Истина;
			ПараметрыСоздания.Текст = "";
			
			Файл = Новый Файл(ИмяВременногоФайла);
			ПараметрыСоздания.Размер = Файл.Размер();
			ПараметрыСоздания.ВремяИзменения = Файл.ПолучитьВремяИзменения();
			ПараметрыСоздания.ВремяИзмененияУниверсальное = Файл.ПолучитьУниверсальноеВремяИзменения();
			Файл = Неопределено;
			
			ДвоичныеДанные = Новый ДвоичныеДанные(ИмяВременногоФайла);
			ПараметрыСоздания.АдресВременногоХранилищаФайла = ПоместитьВоВременноеХранилище(ДвоичныеДанные);;
			ПараметрыСоздания.ЯвляетсяСканКопией = Ложь;
			
			ИдентификаторФайла = СоздатьИзФайлаНаДискеСервер(
				ПараметрыСоздания, ID, Тип, Представление, Истина);
				
			ПараметрыСоздания.Вставить("Идентификатор", ИдентификаторФайла);
			Результат.Добавить(ПараметрыСоздания);
			
		КонецЕсли;
	КонецЦикла;
	
	УдалитьФайлы(ИмяВременногоФайла);
	
	Возврат Результат;
	
КонецФункции

//Возвращает список XDTO документов по объекту Документооборота.
//Параметры:
//	ИдентификаторВладельца - уникальный идентификатор объекта Документооборота
//	ИмяВладельца - представление объекта Документооборота
//	ТипВладельца - тип XDTO объекта Документооборота
//Возвращает:
//	Список объектов XDTO типа DMInternalDocument
Функция ДокументыПоВладельцу(ИдентификаторВладельца, ИмяВладельца, ТипВладельца) Экспорт

	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetDocumentListByOwnerRequest");
	
	ОбъектВладелец =  ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObject");
	ОбъектВладелец.name = ИмяВладельца;
	ОбъектВладелец.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(
		Прокси, Строка(ИдентификаторВладельца), ТипВладельца);
	
	Запрос.owners.Добавить(ОбъектВладелец);
	
	Запрос.columnSet.Добавить("objectId");
	Запрос.columnSet.Добавить("signatures");
	Запрос.columnSet.Добавить("name");
	Запрос.columnSet.Добавить("author");
	
	Файлы = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Файлы);
	
	Возврат Файлы;
	
КонецФункции

// Добавляет связь указанного типа между двумя документами.
//
// Параметры:
//   ИсходныйДокумент - Структура - исходный документ.
//   СвязанныйДокумент - Структура - связываемый документ.
//   ТипСвязи - Структура - тип связи (необязательный).
//
// Свойства структур:
//     ID - Строка - идентификатор объекта ДО.
//     Тип - Строка - тип объекта ДО.
//     Представление - Строка - представление объекта ДО.
//
Процедура ДобавитьСвязьДокументов(ИсходныйДокумент, СвязанныйДокумент, ТипСвязи = Неопределено) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Связь = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMDocumentRelation");
	
	Document = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMDocument");
	Document.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, 
		ИсходныйДокумент.ID, ИсходныйДокумент.Тип);
	Document.name = ИсходныйДокумент.Представление;
	Связь.document = Document;
	
	RelatedDocument = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMDocument");
	RelatedDocument.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, 
		СвязанныйДокумент.ID, СвязанныйДокумент.Тип);
	RelatedDocument.name = СвязанныйДокумент.Представление;
	Связь.relatedDocument = RelatedDocument;
	
	Если ЗначениеЗаполнено(ТипСвязи) Тогда
		RelationType = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMRelationType");
		RelationType.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, 
			ТипСвязи.ID, ТипСвязи.Тип);
		RelationType.name = ТипСвязи.Представление;
		Связь.relationType = RelationType;
	КонецЕсли;
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMAddDocumentRelationRequest");
	Запрос.relation = Связь;
	
	Ответ = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
КонецПроцедуры

// Удаляет связь указанного типа между двумя документами.
//
// Параметры:
//   ИсходныйДокумент - Структура - исходный документ.
//   СвязанныйДокумент - Структура - связанный документ.
//   ТипСвязи - Структура - тип связи. Свойства структур:
//     ID - Строка - идентификатор объекта ДО.
//     Тип - Строка - тип объекта ДО.
//     Представление - Строка - представление объекта ДО.
//
Процедура УдалитьСвязьДокументов(ИсходныйДокумент, СвязанныйДокумент, ТипСвязи) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Document = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMDocument");
	Document.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, 
		ИсходныйДокумент.ID, ИсходныйДокумент.Тип);
	Document.name = ИсходныйДокумент.Представление;
	
	RelatedDocument = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMDocument");
	RelatedDocument.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, 
		СвязанныйДокумент.ID, СвязанныйДокумент.Тип);
	RelatedDocument.name = СвязанныйДокумент.Представление;
	
	RelationType = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMRelationType");
	RelationType.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, 
		ТипСвязи.ID, ТипСвязи.Тип);
	RelationType.name = ТипСвязи.Представление;
	
	Связь = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMDocumentRelation");
	Связь.document = Document;
	Связь.relationType = RelationType;
	Связь.relatedDocument = RelatedDocument;
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMRemoveDocumentRelationRequest");
	Запрос.relation = Связь;
	
	Ответ = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
КонецПроцедуры

// Обновляет HTML-представление резолюций на форме документа.
//
// Параметры:
//   РезолюцииXDTO - СписокXDTO - список резолюций, объектов типа DMResolution.
//   ПредставлениеHTML - Строка - обновляемое HTML-представление.
//   ЭлементФормы - ЭлементФормы - элемент, заголовок которого следует обновить.
//
Процедура ОбновитьПредставлениеРезолюций(РезолюцииXDTO, ПредставлениеHTML, ЭлементФормы) Экспорт
	
	Если РезолюцииXDTO.Количество() = 0 Тогда
		ПредставлениеHTML = "";
		ЭлементФормы.Заголовок = НСтр("ru = 'Резолюции'");
		Возврат;
	КонецЕсли;
	
	Представление = "";
	Для Каждого РезолюцияXDTO Из РезолюцииXDTO Цикл
		Если РезолюцияXDTO.Установлено("reviewer") Тогда
			ПредставлениеИмени = РезолюцияXDTO.reviewer.name;
		Иначе
			ПредставлениеИмени = "";
		КонецЕсли;
		Если РезолюцияXDTO.signed Тогда
			Если РезолюцияXDTO.signatureChecked Тогда
				Если РезолюцияXDTO.signatureValid Тогда
					СтатусПроверки = "true";
				Иначе
					СтатусПроверки = "false";
				КонецЕсли;
			Иначе // не проверялась
				СтатусПроверки = "not";
			КонецЕсли;
		Иначе // не подписана
			СтатусПроверки = "";
		КонецЕсли;
		Представление = СтрШаблон(
			"%1<div class=""field""><p>%2</p><p><span class=""status %3"">%4</span></p><p>%5</p></div>",
			Представление, // 1
			СтрЗаменить(РезолюцияXDTO.text, Символы.ПС, "<br/>"), // 2
			СтатусПроверки, // 3
			ПредставлениеИмени, // 4
			Формат(РезолюцияXDTO.date, "ДФ='dd.MM.yyyy ЧЧ:мм'")); // 5
	КонецЦикла;
		
	ПредставлениеHTML = СтрШаблон(
		"<html>
		|<head><meta http-equiv=""X-UA-Compatible"" content=""IE=edge""></head>
		|<body>
		|<style>
		|* { font-family: sans-serif; font-size: 9pt; }
		|body { padding: 0; margin: 8px; overflow: auto; }
		|a { color: #1E90FF; }
		|p { padding: 0 0 4px 0; margin: 0; }
		|.field { margin-bottom: 6px; padding: 6px 8px 2px 8px; }
		|.active { background-color: #EEEEEE; }
		|.status { background-position: center right; background-repeat: no-repeat; padding: 1px 24px 2px 0; }
		|.not { background-image: url(""data:image/png;base64,iVBORw0KGgoAAAANSUhEUgAAABAAAAAQCAYAAAAf8/9hAAAACXBIWXMAAA7EAAAOxAGVKw4bAAAAB3RJTUUH3gcIDAYg91pdKgAAAAd0RVh0QXV0aG9yAKmuzEgAAAAMdEVYdERlc2NyaXB0aW9uABMJISMAAAAKdEVYdENvcHlyaWdodACsD8w6AAAADnRFWHRDcmVhdGlvbiB0aW1lADX3DwkAAAAJdEVYdFNvZnR3YXJlAF1w/zoAAAALdEVYdERpc2NsYWltZXIAt8C0jwAAAAh0RVh0V2FybmluZwDAG+aHAAAAB3RFWHRTb3VyY2UA9f+D6wAAAAh0RVh0Q29tbWVudAD2zJa/AAAABnRFWHRUaXRsZQCo7tInAAACAklEQVQ4ja2TsWsUQRjF35udOZNKK5tDAgtXyFleaxLBFPkDUooEJKTKXpWrlJAuVrtdECGFdvkDRFJ4mvbaw+Lg8AhnoZXdejs7zyJz4YhREPzK4f2+ed98bygJi5Vl2RrJdQAdSS0AIDkCMJDUL4ri46Ke8wbdbveepG2S9yX1SQ5ms9kIABqNRktSh+S6pM8kT/I8v7hqEOF9AMMkSd5UVbWSJElbUhodjOu6HjrnJnVdPwHQJvkyz/MLCwCStgEMrbWnVVVtGmOaAIYATqPT1Biz4b2fOudOvfdz5pB7e3trJHeNMc+895skfywtLX06Ojr6uThrr9e7VZblqqTb1tp3IYTXko5tnKtfVdWKMaYpqSzL8mmWZbhekr6SbEZtn+S6BdAheZAkSTvaTouiePUbfbmhHZLDqB0AODCSWrPZbCQplTS+CbzmYiwpjUzL3qCZZFm28wd+cv3Akhw1Go0WgDGANM/z939z0O12HwMYR2ZkcZmwTgjh3Biz0ev1zsuyXAXwBcADY8x3AKjr+s7y8vKZpHYI4cwY85DkwMbU7Trn3nrvp2VZri6scTS/eWGNU+fcJITwXNIxJSHLshcAvsUgPSLZJDmcPyrJVFI7wh+891sA7hZFcWij4ETSvvce1tqrKAPYigbGIYQz59zEe38VZeB/fabF+tfv/AvEjVUkU5cRDAAAAABJRU5ErkJggg==""); }
		|.true { background-image: url(""data:image/png;base64,iVBORw0KGgoAAAANSUhEUgAAABAAAAAQCAYAAAAf8/9hAAAACXBIWXMAAA7EAAAOxAGVKw4bAAAAB3RJTUUH3gcIDAcDTCYdGQAAAAd0RVh0QXV0aG9yAKmuzEgAAAAMdEVYdERlc2NyaXB0aW9uABMJISMAAAAKdEVYdENvcHlyaWdodACsD8w6AAAADnRFWHRDcmVhdGlvbiB0aW1lADX3DwkAAAAJdEVYdFNvZnR3YXJlAF1w/zoAAAALdEVYdERpc2NsYWltZXIAt8C0jwAAAAh0RVh0V2FybmluZwDAG+aHAAAAB3RFWHRTb3VyY2UA9f+D6wAAAAh0RVh0Q29tbWVudAD2zJa/AAAABnRFWHRUaXRsZQCo7tInAAABwElEQVQ4ja2TMWsUURSFv7nz8hQbrWwWCQxsIWOZ1s0KpvAHpBQJSCZdunRKSGlnlxEhhXb7A0S2cDXttoPFwOIS1kIru3H2zX0W+0Z3l0UQPOXl3nPePfe8yHvPMqIs3gX6wA7QDeUSGAMjnzcfV/pbgiiL7wAHwF1gBIypKQGwdANhH/gMXPi8ufpNEIZPgMIYfeNq2caQoiQACBMchbE6dU4eAynwwufNlQkvOQAKjA5cLY8QOigFMABASRD2XC0zrA5w0s6cRRzKLnBkjD4Nwz+2ruun+qX/ubyrPY6uzSvpodw0Vt85J6+BcxP2GrlatoNyNa/kSZTFrEJA+YrQCb0joG+COadh5wIh8Xnzig2IsvhwsSopyhg4FaBLTRkMm2waXMMEJQkX6poNDdOgtAnT9YIBSixdZMHs8+b93+SjLH6IMAnZKA2LhO3guETYs8fR5bySHvAFuIfwHQDHra0bOgRJcQwR7gNjwyJ1R8bqW1fLbF5Jb+mMZau8dMZZCNQz4LxN4nPgG0YH1PIAoQMU/DE1AVKUGVY/4GQfuO3z5qw18QI4wQnGrkR5H2ijPFyPMvyvz7Tm8j9951+Qy9eWUE15/AAAAABJRU5ErkJggg==""); }
		|.false { background-image: url(""data:image/png;base64,iVBORw0KGgoAAAANSUhEUgAAABAAAAAQCAYAAAAf8/9hAAAACXBIWXMAAA7EAAAOxAGVKw4bAAAAB3RJTUUH3gcIDAcaKE212QAAAAd0RVh0QXV0aG9yAKmuzEgAAAAMdEVYdERlc2NyaXB0aW9uABMJISMAAAAKdEVYdENvcHlyaWdodACsD8w6AAAADnRFWHRDcmVhdGlvbiB0aW1lADX3DwkAAAAJdEVYdFNvZnR3YXJlAF1w/zoAAAALdEVYdERpc2NsYWltZXIAt8C0jwAAAAh0RVh0V2FybmluZwDAG+aHAAAAB3RFWHRTb3VyY2UA9f+D6wAAAAh0RVh0Q29tbWVudAD2zJa/AAAABnRFWHRUaXRsZQCo7tInAAACBUlEQVQ4ja2TsWtTURTGf/fm3ZcSClZBl1BDX/IGiWNX0wh26B/QUaQgpX9BN6V07F9QROigW0cHkQ5Gu2YNgi99+ihxsIMRpLzcE+91yEuNWgTBbzz3fOd+55zvKO89s0hUbUVj2sAySDyJmgToOqQT++zNbL6aFjhRtUVgA8wtBx2NdL/hE4B5VOwwyxraIO+Ag7rPTi8KnKjaosNsg/RCwmfnjGohYRMkKhSkFturUM4s9j6Ypkb26j47DQolGyC9OdThOXYtRFWBnoNDAA1RiFrNsYMK6jBHCg676j03VzRmK0A9zLFrGvN1ge9vr/sPo9lez9RSeUip5ZArc4Qvx/inDtkPNKbtoDORrapjJB9iHgxVg19RwiGfAqieM6oFhB2Naas+9RcgO2Ai4IuDKPb9J1yCRDU2NaTAVZAUzI4GiSfTlsgh6WXEWUxyJCo4cfBnimSJamxeTpfs90gAJplHYjCphqju+6/+puBENe4BacFJAqDrMMtj7HGIWj1TS8dDSi2Qjxpua8IzAItduIY+Ate0+KOA8I6GbuCQjsZsVSg/z7GDIaXWZI3ZCEimP/9coxtUKGdj/COH7E+d+NjB54mR/N0AqmB606FqTATSHMOggnqd49c13Kj7bHc6xAMw2znC3IWVaWpYL95Tiz+qUM7ywsoge/C/jmkW/3rOPwBzWQNrm2r0hAAAAABJRU5ErkJggg==""); }
		|</style>
		|%1
		|</body>
		|</html>",
		Представление);
		
	ЭлементФормы.Заголовок = СтрШаблон(
		НСтр("ru = 'Резолюции (%1)'"),
		РезолюцииXDTO.Количество());
	
КонецПроцедуры

// Обновляет таблицу виз согласования на форме документа.
//
// Параметры:
//   ВизыXDTO - СписокXDTO - список виз, объектов типа DMVisa.
//   ВизыСогласования - ДанныеФормыКоллекция - обновляемая таблица виз.
//   ЭлементФормы - ЭлементФормы - элемент, заголовок которого следует обновить.
//
Процедура ОбновитьВизыСогласования(ВизыXDTO, ВизыСогласования, ЭлементФормы) Экспорт
	
	ВизыСогласования.Очистить();
	
	Если ВизыXDTO.Количество() = 0 Тогда
		ЭлементФормы.Заголовок = НСтр("ru = 'Визы'");
		Возврат;
	КонецЕсли;
	
	Для Каждого ВизаXDTO Из ВизыXDTO Цикл
		
		Виза = ВизыСогласования.Добавить();
		
		Виза.Наименование = ВизаXDTO.name;
		Виза.ID = ВизаXDTO.objectId.id;
		Виза.Тип = ВизаXDTO.objectId.type;
		
		Если ВизаXDTO.Установлено("addedBy") Тогда
			Виза.Внес = ВизаXDTO.addedBy.name;
			Виза.ВнесID = ВизаXDTO.addedBy.objectId.id;
			Виза.ВнесТип = ВизаXDTO.addedBy.objectId.type;
		КонецЕсли;
		
		Если ВизаXDTO.Установлено("reviewer") Тогда
			Виза.СогласующееЛицо = ВизаXDTO.reviewer.name;
			Виза.СогласующееЛицоID = ВизаXDTO.reviewer.objectId.id;
			Виза.СогласующееЛицоТип = ВизаXDTO.reviewer.objectId.type;
		КонецЕсли;
		
		Если ВизаXDTO.Установлено("result") Тогда
			Виза.Результат = ВизаXDTO.result.name;
			Виза.РезультатID = ВизаXDTO.result.objectId.id;
			Виза.РезультатТип = ВизаXDTO.result.objectId.type;
		КонецЕсли;
		
		Виза.Дата = ВизаXDTO.date;
		Виза.Комментарий = ВизаXDTO.comment;
		
		Если ВизаXDTO.signed Тогда
			Если ВизаXDTO.signatureChecked Тогда
				Если ВизаXDTO.signatureValid Тогда
					Виза.КартинкаСтатусаПодписи = 2;
				Иначе
					Виза.КартинкаСтатусаПодписи = 3;
				КонецЕсли;
			Иначе
				Виза.КартинкаСтатусаПодписи = 1;
			КонецЕсли;
		Иначе
			Виза.КартинкаСтатусаПодписи = 0;
		КонецЕсли;
		Виза.Комментарий = ВизаXDTO.comment;
		
	КонецЦикла;
		
	ЭлементФормы.Заголовок = СтрШаблон(
		НСтр("ru = 'Визы (%1)'"),
		ВизыXDTO.Количество());
	
КонецПроцедуры

// Регистрирует документ, если он еще не зарегистрирован.
//
// Параметры:
//   ДокументТип - Строка - тип документа.
//   ДокументID - Строка - тип документа.
//
Процедура ЗарегистрироватьДокументПриНеобходимости(ДокументТип, ДокументID) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Колонки = СтрРазделить("regNumber,regDate", ",");
	ОбъектыXDTO = ИнтеграцияС1СДокументооборот.ПолучитьОбъект(Прокси, ДокументТип, ДокументID, Колонки);
	ОбъектXDTO = ОбъектыXDTO.objects[0];
	
	Если ЗначениеЗаполнено(ОбъектXDTO.regNumber) 
		И ЗначениеЗаполнено(ОбъектXDTO.regDate) Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектXDTO = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, ДокументТип);
	ОбъектXDTO.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, ДокументID, ДокументТип);
	ОбъектXDTO.name = "";
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMDocumentRegistrationRequest");
	Запрос.document = ОбъектXDTO;
		
	Результат = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
КонецПроцедуры

// Создает вид документов с указанными свойствами.
//
// Параметры:
//   Свойства - Структура - См.
//     ИнтеграцияС1СДокументооборотВызовСервера.СтруктураРеквизитовЗаполняемогоВидаДокументовДО().
//
// Возвращаемое значение:
//   ОбъектXDTO типа DM<...>DocumentType или DMError
//
Функция СоздатьВидДокументаВДО(Свойства) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	ВидДокумента = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, Свойства.Тип);
	ВидДокумента.objectId = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectID");
	ВидДокумента.objectId.id = "";
	ВидДокумента.objectId.type = Свойства.Тип;
	
	ВидДокумента.name = Свойства.Имя;
	ИнтеграцияС1СДокументооборот.ЗаполнитьСвойствоXDTOизСтруктурыРеквизитов(
		Прокси,
		ВидДокумента,
		"automaticNumeration",
		Свойства,
		"АвтоНумерация");
	ИнтеграцияС1СДокументооборот.ЗаполнитьСвойствоXDTOизСтруктурыРеквизитов(
		Прокси,
		ВидДокумента,
		"sumEnabled",
		Свойства,
		"УчитыватьСуммуДокумента");
	ИнтеграцияС1СДокументооборот.ЗаполнитьСвойствоXDTOизСтруктурыРеквизитов(
		Прокси,
		ВидДокумента,
		"performanceDateEnabled",
		Свойства,
		"ИспользоватьСрокИсполнения");
	ИнтеграцияС1СДокументооборот.ЗаполнитьСвойствоXDTOизСтруктурыРеквизитов(
		Прокси,
		ВидДокумента,
		"templateRequired",
		Свойства,
		"ЗапретитьСозданиеДокументовНеПоШаблону");
	
	Если Свойства.Тип = "DMInternalDocumentType" Тогда
		ИнтеграцияС1СДокументооборот.ЗаполнитьСвойствоXDTOизСтруктурыРеквизитов(
			Прокси,
			ВидДокумента,
			"addresseeEnabled",
			Свойства,
			"ВестиУчетПоАдресатам");
		ИнтеграцияС1СДокументооборот.ЗаполнитьСвойствоXDTOизСтруктурыРеквизитов(
			Прокси,
			ВидДокумента,
			"cashFlowDetailsEnabled",
			Свойства,
			"ВестиУчетПоСтатьямДДС");
		ИнтеграцияС1СДокументооборот.ЗаполнитьСвойствоXDTOизСтруктурыРеквизитов(
			Прокси,
			ВидДокумента,
			"correspondentEnabled",
			Свойства,
			"ВестиУчетПоКонтрагентам");
		ИнтеграцияС1СДокументооборот.ЗаполнитьСвойствоXDTOизСтруктурыРеквизитов(
			Прокси,
			ВидДокумента,
			"durationEnabled",
			Свойства,
			"УчитыватьСрокДействия");
		ИнтеграцияС1СДокументооборот.ЗаполнитьСвойствоXDTOизСтруктурыРеквизитов(
			Прокси,
			ВидДокумента,
			"organizationEnabled",
			Свойства,
			"ВестиУчетПоОрганизациям");
		ИнтеграцияС1СДокументооборот.ЗаполнитьСвойствоXDTOизСтруктурыРеквизитов(
			Прокси,
			ВидДокумента,
			"productRowsEnabled",
			Свойства,
			"ВестиУчетТоваровИУслуг");
		ИнтеграцияС1СДокументооборот.ЗаполнитьСвойствоXDTOизСтруктурыРеквизитов(
			Прокси,
			ВидДокумента,
			"signatureOption",
			Свойства,
			"ВариантПодписания");
		Если ИнтеграцияС1СДокументооборот.СвойствоСуществует(ВидДокумента, "useConfirmation") Тогда
			ИнтеграцияС1СДокументооборот.ЗаполнитьСвойствоXDTOизСтруктурыРеквизитов(
				Прокси,
				ВидДокумента,
				"useConfirmation",
				Свойства,
				"ИспользоватьУтверждение");
		КонецЕсли;
		Если ИнтеграцияС1СДокументооборот.СвойствоСуществует(ВидДокумента, "useSigningByManager") Тогда
			ИнтеграцияС1СДокументооборот.ЗаполнитьСвойствоXDTOизСтруктурыРеквизитов(
				Прокси,
				ВидДокумента,
				"useSigningByManager",
				Свойства,
				"ИспользоватьПодписание");
		КонецЕсли;
	ИначеЕсли Свойства.Тип = "DMOutgoingDocumentType" Тогда
		ИнтеграцияС1СДокументооборот.ЗаполнитьСвойствоXDTOизСтруктурыРеквизитов(
			Прокси,
			ВидДокумента,
			"externalNumberEnabled",
			Свойства,
			"УчитыватьВходящийНомерИДатуПолучателя");
	КонецЕсли;
	
	Возврат ИнтеграцияС1СДокументооборот.СоздатьНовыйОбъект(Прокси, ВидДокумента).object;
	
КонецФункции

// Ищет виды документов в ДО по типу объекта и по точному совпадению наименования. Создает новый
// вид документов с указанными свойствами, если ничего не найдено.
//
// Параметры:
//   Свойства - Структура - См.
//     ИнтеграцияС1СДокументооборотВызовСервера.СтруктураРеквизитовЗаполняемогоВидаДокументовДО().
//
// Возвращаемое значение:
//   ОбъектXDTO типа DM<...>DocumentType или DMError
//
Функция НайтиСоздатьВидДокументаВДО(Свойства) Экспорт
	
	РезультатыПоиска = НайтиОбъектВДОПоИмени(Свойства.Тип, Свойства.Имя);
	
	Если РезультатыПоиска = Неопределено Тогда
		Возврат СоздатьВидДокументаВДО(Свойства);
	Иначе
		Возврат РезультатыПоиска;
	КонецЕсли;
	
КонецФункции

// Создает папку внутренних документов с указанными свойствами.
//
// Параметры:
//   Свойства - Структура - См.
//     ИнтеграцияС1СДокументооборотВызовСервера.СтруктураРеквизитовЗаполняемойПапкиВнутреннихДокументов().
//
// Возвращаемое значение:
//   ОбъектXDTO типа DMInternalDocumentFolder или DMError
//
Функция СоздатьПапкуВнутреннихДокументовВДО(Свойства) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	Папка = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMInternalDocumentFolder");
	Папка.objectId = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectID");
	Папка.objectId.id = "";
	Папка.objectId.type = "DMInternalDocumentFolder";
	
	Папка.name = Свойства.Имя;
	ИнтеграцияС1СДокументооборот.ЗаполнитьСвойствоXDTOизСтруктурыРеквизитов(
		Прокси,
		Папка,
		"description",
		Свойства,
		"Описание");
	ИнтеграцияС1СДокументооборот.ЗаполнитьСвойствоXDTOизСтруктурыРеквизитов(
		Прокси,
		Папка,
		"responsible",
		Свойства,
		"Ответственный");
	ИнтеграцияС1СДокументооборот.ЗаполнитьСвойствоXDTOизСтруктурыРеквизитов(
		Прокси,
		Папка,
		"creationDate",
		Свойства,
		"ДатаСоздания");
	ИнтеграцияС1СДокументооборот.ЗаполнитьСвойствоXDTOизСтруктурыРеквизитов(
		Прокси,
		Папка,
		"parent",
		Свойства,
		"Родитель");
	
	Возврат ИнтеграцияС1СДокументооборот.СоздатьНовыйОбъект(Прокси, Папка).object;
	
КонецФункции

// Ищет папки внутренних документов в ДО по точному совпадению наименования. Создает новую
// папку с указанными свойствами, если ничего не найдено.
//
// Параметры:
//   Свойства - Структура - См.
//     ИнтеграцияС1СДокументооборотВызовСервера.СтруктураРеквизитовЗаполняемойПапкиВнутреннихДокументов().
//
// Возвращаемое значение:
//   ОбъектXDTO типа DMInternalDocumentFolder или DMError
//
Функция НайтиСоздатьПапкуВнутреннихДокументовВДО(Свойства) Экспорт
	
	РезультатыПоиска = НайтиОбъектВДОПоИмени("DMInternalDocumentFolder", Свойства.Имя);
	
	Если РезультатыПоиска = Неопределено Тогда
		Возврат СоздатьПапкуВнутреннихДокументовВДО(Свойства);
	Иначе
		Возврат РезультатыПоиска;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область Автообновление

//Загружает параметры Автообновление и ПериодАвтообновления списка из настроек.
//Параметры:
// Форма - форма
// ИмяСписка - Строка - имя элемента формы
Процедура ЗагрузитьНастройкиАвтообновленияСписка(Форма, ИмяСписка) Экспорт
	
	КлючОбъекта = Форма.ИмяФормы + ".Автообновление." + ПолучитьСкоростьКлиентскогоСоединения();
	Настройки = ХранилищеСистемныхНастроек.Загрузить(КлючОбъекта, ИмяСписка);
	Если ЗначениеЗаполнено(Настройки) Тогда
		Форма.Элементы[ИмяСписка].Автообновление = Настройки.Автообновление;
		Форма.Элементы[ИмяСписка].ПериодАвтоОбновления = Настройки.ПериодАвтоОбновления;
	КонецЕсли;
	
КонецПроцедуры

//Сохраняет настройки автообновления списка.
//Параметры:
//	ИмяФормы - имя формы, настройки которой должны быть сохранены
//	ИмяСписка - имя списка формы, который должен обновляться
//	Настройки - настройки обновления списка
//				Структура с полями: 
//					- Автообновление - признак, что автообновление включено
//					- ПериодАвтообновления - период автообновления в секундах
Процедура СохранитьНастройкиАвтообновленияСписка(ИмяФормы, ИмяСписка, Настройки) Экспорт
	
	КлючОбъекта = ИмяФормы + ".Автообновление." + ПолучитьСкоростьКлиентскогоСоединения();
	ХранилищеСистемныхНастроек.Сохранить(КлючОбъекта, ИмяСписка, Настройки);
	
КонецПроцедуры

//Сохраняет настройки автообновления Формы.
//Параметры:
//	ИмяФормы - имя формы, настройки которой должны быть сохранены
//	Настройки - настройки обновления формы
//				Структура с полями: 
//					- Автообновление - признак, что автообновление включено
//					- ПериодАвтообновления - период автообновления в секундах
Процедура СохранитьНастройкиАвтообновленияФормы(ИмяФормы, Настройки) Экспорт
	
	КлючОбъекта = ИмяФормы + ".Автообновление";
	КлючНастройки = Строка(ПолучитьСкоростьКлиентскогоСоединения());
	ХранилищеСистемныхНастроек.Сохранить(КлючОбъекта, КлючНастройки, Настройки);
	
КонецПроцедуры

//Получает параметры Автообновление и ПериодАвтообновления списка из настроек.
//Параметры:
// Форма - форма или имя формы
// ИмяСписка - Строка - имя элемента формы
// 
// Возвращает структуру:
// - Автообновление (Булево)
// - ПериодАвтоОбновления (Число)
Функция НастройкиАвтообновленияФормы(Форма) Экспорт
	
	Результат = Новый Структура("Автообновление, ПериодАвтоОбновления", Ложь, 0);
	
	Если ТипЗнч(Форма) = Тип("Строка") Тогда
		КлючОбъекта = Форма + ".Автообновление";
	Иначе
		КлючОбъекта = Форма.ИмяФормы + ".Автообновление";
	КонецЕсли;
	
	КлючНастройки = Строка(ПолучитьСкоростьКлиентскогоСоединения());
	Настройки = ХранилищеСистемныхНастроек.Загрузить(КлючОбъекта, КлючНастройки);
	Если ЗначениеЗаполнено(Настройки) Тогда
		Результат.Автообновление = Настройки.Автообновление;
		Результат.ПериодАвтоОбновления = Настройки.ПериодАвтоОбновления;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Файлы

// Сохраняет путь к каталогу, используемому при интеграции, во временном хранилище.
//
// Параметры:
//	 Каталог - Строка - путь к каталогу.
//
Процедура СохранитьЛокальныйКаталогФайлов(Каталог) Экспорт
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ИнтеграцияС1СДокументооборот", "ЛокальныйКаталогФайловИнтеграции", Каталог);
	
КонецПроцедуры

//Заносит информацию о подписи файла в базу Документооборот.
//Параметры:
//	ID - идентификатор файла
//	Имя - имя файла
//	ОписаниеФайла - описание файла
//	Тип - тип объекта XDTO файла
//	МассивНовыхПодписей - массив структур новых подписей
//	МассивСуществующихПодписей - массив структур существующих подписей
Процедура ЗанестиИнформациюОПодписяхОбъекта(ID, Имя, ОписаниеФайла, Тип, 
	МассивНовыхПодписей, МассивСуществующихПодписей) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Объект = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, Тип);
	Объект.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, ID, Тип);
	
	// только 2 поля - Имя и Описание передаем при записи.
	Объект.name = Имя;
	Объект.description = ОписаниеФайла;
	
	Для Каждого ДанныеПодписи Из МассивСуществующихПодписей Цикл
		
		ПодписьXDTO = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMSignature");
		ИнтеграцияС1СДокументооборот.ЗаполнитьXDTOПодпись(Прокси, ПодписьXDTO, ДанныеПодписи);
		Объект.signatures.Добавить(ПодписьXDTO);
		
	КонецЦикла;
	
	Для Каждого ДанныеПодписи Из МассивНовыхПодписей Цикл 
		
		ПодписьXDTO = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMSignature");
		
		ПодписьXDTO.author = ДанныеПодписи.КомуВыданСертификат;
		ПодписьXDTO.certificate = ДанныеПодписи.ДвоичныеДанныеСертификата;
		ПодписьXDTO.comment = ДанныеПодписи.Комментарий;
		ПодписьXDTO.date = ?(ЗначениеЗаполнено(ДанныеПодписи.ДатаПодписи), ДанныеПодписи.ДатаПодписи, ТекущаяДатаСеанса());
		ПодписьXDTO.signature = ДанныеПодписи.НоваяПодписьДвоичныеДанные;
		ПодписьXDTO.signatureFileName = ДанныеПодписи.ИмяФайлаПодписи;
		ПодписьXDTO.thumbprint = ДанныеПодписи.Отпечаток;
		
		Объект.signatures.Добавить(ПодписьXDTO);
		
	КонецЦикла;
	
	ИнтеграцияС1СДокументооборот.ЗаписатьОбъект(Прокси, Объект);
	
КонецПроцедуры

// Заполняет список файлов в карточке документа.
//
// Параметры:
//   ФайлыXDTO - СписокXDTO - список объектов XDTO типа DMFile.
//   Файлы - ТаблицаФормы - заполняемая таблица формы.
//   ЭлементДляОбновленияЗаголовка - ЭлементФормы - элемент формы, заголовок которого требуется дополнить
//     количеством файлов.
//
Процедура ОбновитьСписокФайлов(ФайлыXDTO, Файлы, ЭлементДляОбновленияЗаголовка = Неопределено) Экспорт
	
	Файлы.Очистить();
	
	ТекущийПользователь = ИнтеграцияС1СДокументооборотПовтИсп.ТекущийПользовательДокументооборота();
	
	// Файлы.
	Для каждого СведенияОФайле Из ФайлыXDTO Цикл
		НоваяСтрока = Файлы.Добавить();
		
		НоваяСтрока.Наименование = СведенияОФайле.name;
		НоваяСтрока.Расширение = СведенияОФайле.extension;
		НоваяСтрока.ИндексКартинки = РаботаСФайламиСлужебныйКлиентСервер.ПолучитьИндексПиктограммыФайла(
			НоваяСтрока.Расширение);
		Если ИнтеграцияС1СДокументооборот.СвойствоУстановлено(СведенияОФайле, "deletionMark")
				И СведенияОФайле.deletionMark Тогда
			НоваяСтрока.ИндексКартинки = НоваяСтрока.ИндексКартинки + 1;
		КонецЕсли;
		
		Если СведенияОФайле.Свойства().Получить("template") <> Неопределено
			И СведенияОФайле.template <> Неопределено Тогда
			
			НоваяСтрока.ШаблонID = СведенияОФайле.template.objectId.id;
			
			// Пропустим прототипы файлов, которые будут заполнены по шаблонам при записи.
			Если Не ЗначениеЗаполнено(СведенияОФайле.objectId.id) Тогда
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
		НоваяСтрока.Описание = СведенияОФайле.description;
		НоваяСтрока.Размер = Формат(СведенияОФайле.size/1024, "ЧЦ=10; ЧН=0");
		
		НоваяСтрока.ПодписанЭП = СведенияОФайле.signed;
		
		Если НоваяСтрока.Свойство("Зашифрован") Тогда
			НоваяСтрока.Зашифрован = СведенияОФайле.encrypted;
		КонецЕсли;
		
		Если НоваяСтрока.Свойство("НомерКартинкиПодписанЗашифрован") Тогда
			Если СведенияОФайле.signed = Истина
				И СведенияОФайле.encrypted = Истина Тогда
				НоваяСтрока.НомерКартинкиПодписанЗашифрован = 2;
			ИначеЕсли СведенияОФайле.encrypted = Истина Тогда
				НоваяСтрока.НомерКартинкиПодписанЗашифрован = 1;
			ИначеЕсли СведенияОФайле.signed = Истина Тогда
				НоваяСтрока.НомерКартинкиПодписанЗашифрован = 0;
			Иначе
				НоваяСтрока.НомерКартинкиПодписанЗашифрован = -1;
			КонецЕсли;
		КонецЕсли;
		
		НоваяСтрока.Автор = СведенияОФайле.author.name;
		НоваяСтрока.ID = СведенияОФайле.objectId.id;
		НоваяСтрока.ДатаСоздания = СведенияОФайле.creationDate;
		Если НоваяСтрока.Свойство("ДатаМодификацииУниверсальная") Тогда
			НоваяСтрока.ДатаМодификацииУниверсальная = СведенияОФайле.modificationDateUniversal;
		КонецЕсли;
		Если НоваяСтрока.Свойство("ДатаМодификации") Тогда
			НоваяСтрока.ДатаМодификации = 
				МестноеВремя(СведенияОФайле.modificationDateUniversal, ЧасовойПоясСеанса());
		КонецЕсли;
		НоваяСтрока.Редактируется = СведенияОФайле.editing;
		НоваяСтрока.Зашифрован = СведенияОФайле.encrypted;
		
		Если СведенияОФайле.Свойства().Получить("editingUser") <> Неопределено 
			и СведенияОФайле.editingUser <> Неопределено Тогда
			НоваяСтрока.РедактируетсяТекущимПользователем =
				(СведенияОФайле.editingUser.objectId.id = ТекущийПользователь.objectId.id);
			Если НоваяСтрока.Свойство("Редактирует") Тогда
				НоваяСтрока.Редактирует = СведенияОФайле.editingUser.name;
			КонецЕсли;
		Иначе
			НоваяСтрока.РедактируетсяТекущимПользователем = Ложь;
		КонецЕсли;
		
		Если СведенияОФайле.Свойства().Получить("scannedOriginal") <> Неопределено Тогда
			НоваяСтрока.ЯвляетсяОригиналом = СведенияОФайле.scannedOriginal;
		КонецЕсли;
		
		Если СведенияОФайле.Свойства().Получить("deletionMark") <> Неопределено Тогда
			НоваяСтрока.ПометкаУдаления = СведенияОФайле.deletionMark;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЭлементДляОбновленияЗаголовка <> Неопределено Тогда
		Если Файлы.Количество() = 0 Тогда 
			ФайлыЗаголовок = НСтр("ru = 'Файлы'");
		Иначе
			ФайлыЗаголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Файлы (%1)'"),
				Файлы.Количество());
		КонецЕсли;
		ЭлементДляОбновленияЗаголовка.Заголовок = ФайлыЗаголовок;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает путь к каталогу, используемому при интеграции, из временного хранилища.
//
Функция ЛокальныйКаталогФайлов() Экспорт
	
	Возврат ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ИнтеграцияС1СДокументооборот", "ЛокальныйКаталогФайловИнтеграции", "");
	
КонецФункции

//Возвращает максимально допустимый размер файла из соответствующей константы.
//
Функция МаксимальныйРазмерПередаваемогоФайла() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	МаксимальныйРазмерФайлаПередачи = Константы.МаксимальныйРазмерФайлаДляПередачиВ1СДокументооборот.Получить();
	
	Если МаксимальныйРазмерФайлаПередачи = Неопределено ИЛИ МаксимальныйРазмерФайлаПередачи = 0 Тогда
		МаксимальныйРазмерФайлаПередачи = 10485760; // = 10 мб
		Константы.МаксимальныйРазмерФайлаДляПередачиВ1СДокументооборот.Установить(МаксимальныйРазмерФайлаПередачи);
	КонецЕсли;
	
	Возврат МаксимальныйРазмерФайлаПередачи;
	
КонецФункции

// Получает двоичные данные файла и помещает во временное хранилище. При указании даты заема выполняется
// заем файла в ДО текущим пользователем. Для уменьшения числа вызовов неявно возвращаются размер и дата.
//
// Параметры:
//   ID - идентификатор файла
//   УникальныйИдентификатор - идентификатор владельца хранилища.
//   ДатаЗаема - Дата - дата заема файла текущим пользователем. Если не указана, заем не производится.
//   Размер - Число - неявно возвращаемый параметр, размер файла в ДО.
//   ДатаМодификацииУниверсальная - Дата - неявно возвращаемый параметр, дата-время изменения в ДО.
//
// Возвращаемое значение:
//   Строка - адрес во временном хранилище с двоичными данными активной версии файла.
//
Функция ПолучитьФайлИПоместитьВХранилище(ID, УникальныйИдентификатор, ДатаЗаема = Неопределено,
		Размер = Неопределено, ДатаМодификацииУниверсальная = Неопределено) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	ОбъектID = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, ID, "DMFile");
	
	Если ДатаЗаема = Неопределено Тогда
		Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMRetrieveRequest");
		Запрос.objectIds.Добавить(ОбъектID);
		Запрос.columnSet.Добавить("objectId");
		Запрос.columnSet.Добавить("name");
		Запрос.columnSet.Добавить("binaryData");
		Запрос.columnSet.Добавить("extension");
		Запрос.columnSet.Добавить("size");
		Запрос.columnSet.Добавить("modificationDateUniversal");
		Ответ = Прокси.execute(Запрос);
		ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
		ДанныеФайла = Ответ.objects[0];
	Иначе // получение с захватом
		Если Не ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.4.9.1") Тогда
			ВызватьИсключение НСтр("ru = 'Заем файлов не поддерживается используемой версией 1С:Документооборота'");
		КонецЕсли;
		Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMLockFileRequest");
		Запрос.objectId = ОбъектID;
		Запрос.lockDate = ДатаЗаема;
		Запрос.columnSet.Добавить("binaryData");
		Запрос.columnSet.Добавить("size");
		Запрос.columnSet.Добавить("modificationDateUniversal");
		Ответ = Прокси.execute(Запрос);
		ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
		ДанныеФайла = Ответ.object;
	КонецЕсли;
	
	Размер = ДанныеФайла.size;
	ДатаМодификацииУниверсальная = ДанныеФайла.modificationDateUniversal;
	Адрес = ПоместитьВоВременноеХранилище(ДанныеФайла.binaryData, УникальныйИдентификатор);
	
	Возврат Адрес;
	
КонецФункции

// Получает двоичные данные версии файла и помещает их во временное хранилище.
//
// Параметры:
//   ID – Строка - идентификатор версии файла.
//   УникальныйИдентификатор - УникальныйИдентификатор - идентификатор владельца хранилища.
//   Размер - Число - неявно возвращаемый параметр, размер файла в ДО.
//   ДатаМодификацииУниверсальная - Дата - неявно возвращаемый параметр, дата-время изменения в ДО.
//
// Возвращаемое значение:
//   Строка - адрес во временном хранилище с двоичными данными версии файла.
//
Функция ПолучитьВерсиюФайлаИПоместитьВХранилище(ID, УникальныйИдентификатор, 
	Размер = Неопределено, ДатаМодификацииУниверсальная = Неопределено) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMRetrieveRequest");
	
	ОбъектID = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, ID, "DMFileVersion");
	
	Запрос.objectIds.Добавить(ОбъектID);
	
	Запрос.columnSet.Добавить("objectId");
	Запрос.columnSet.Добавить("name");
	Запрос.columnSet.Добавить("binaryData");
	Запрос.columnSet.Добавить("extension");
	Запрос.columnSet.Добавить("size");
	Запрос.columnSet.Добавить("modificationDateUniversal");
	
	Ответ = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	ДанныеВерсии = Ответ.objects[0];
	Размер = ДанныеВерсии.size;
	ДатаМодификацииУниверсальная = ДанныеВерсии.modificationDateUniversal;
	Адрес = ПоместитьВоВременноеХранилище(ДанныеВерсии.binaryData, УникальныйИдентификатор);
	Возврат Адрес;
	
КонецФункции

//Получает двоичные данные файла.
//Параметры:
//	ID - идентификатор файла, данные которого нужно получить
//Возвращает:
//	Двоичные данные файла из Документооборота
Функция ДвоичныеДанныеФайла(ID) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMRetrieveRequest");
	
	ОбъектID = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, ID, "DMFile");
	Запрос.objectIds.Добавить(ОбъектID);
	
	Запрос.columnSet.Добавить("objectId");
	Запрос.columnSet.Добавить("name");
	Запрос.columnSet.Добавить("binaryData");
	Запрос.columnSet.Добавить("extension");
	
	Ответ = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	ДанныеФайла = Ответ.objects[0];
	
	Возврат ДанныеФайла.binaryData;
	
КонецФункции

//Возвращает список XDTO, содержащий данные файлов.
//Параметры:
//	Файлы - массив идентификаторов файлов в Документообороте
Функция ДвоичныеДанныеФайлов(Файлы) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMRetrieveRequest");
	
	Для Каждого Файл Из Файлы Цикл
		
		objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, Файл, "DMFile");
		Запрос.objectIds.Добавить(objectId);
		
	КонецЦикла;
	
	Запрос.columnSet.Добавить("objectId");
	Запрос.columnSet.Добавить("name");
	Запрос.columnSet.Добавить("binaryData");
	
	Ответ = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
	Возврат Ответ;
	
КонецФункции

// Обновляет файл ДО двоичными данными, предварительно помещенными во временное хранилище.
//
// Параметры:
//   Параметры - Cтруктура - параметры создания файла, содержит свойства:
//    Имя - Строка - имя файла.
//    Расширение - Строка - расширение файла.
//    Размер - Число - размер файла в байтах.
//    ДатаМодификации - Дата - дата изменения файла.
//    ДатаМодификацииУниверсальная - Дата - универсальная дата модификации файла.
//    АдресВременногоХранилищаФайла - Строка - адрес временного хранилища, содержащий двоичные данные.
//    Текст - Строка - извлеченный текст файла.
//    Идентификатор - Строка - идентификатор обновляемого файла.
//    ОсвободитьФайл - Булево - Истина, если файл следует освободить.
//    ОбновитьСведенияОРедактировании - Булево - Истина, если следует заполнить обновленные:
//    СведенияОРедактировании - Структура - см. ПолучитьСведенияОРедактированииФайла.
//
// Возвращаемое значение:
//    Булево - Истина, если файл помещен успешно.
//
Функция ОбновитьФайлДвоичнымиДаннымиВременногоХранилища(Знач Параметры) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMUpdateRequest");
	
	Файл = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMFile");
	Файл.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, 
		Параметры.Идентификатор, "DMFile");
	Файл.name = ""; // имя не меняется
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(Параметры.АдресВременногоХранилищаФайла);
	Файл.binaryData = ДвоичныеДанные;
	Если НЕ ПустаяСтрока(Параметры.Текст) Тогда
		Файл.text = Параметры.Текст;
	КонецЕсли;
	
	// В веб-клиенте без расширения размер и дата могут быть неизвестны на клиенте. Получим.
	Если Не ЗначениеЗаполнено(Параметры.Размер) Тогда
		Параметры.Размер = ДвоичныеДанные.Размер();
	КонецЕсли;
	ДатаСеанса = ТекущаяДатаСеанса();
	Если Не ЗначениеЗаполнено(Параметры.ДатаМодификации) Тогда
		Параметры.ДатаМодификации = ДатаСеанса;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Параметры.ДатаМодификацииУниверсальная) Тогда
		Параметры.ДатаМодификацииУниверсальная = УниверсальноеВремя(ДатаСеанса);
	КонецЕсли;
	
	Файл.extension = Параметры.Расширение;
	Файл.size = Параметры.Размер;
	Файл.modificationDate = Параметры.ДатаМодификации;
	Файл.modificationDateUniversal = Параметры.ДатаМодификацииУниверсальная;
	
	Запрос.objects.Добавить(Файл);
	
	Ответ = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
	Если ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.4.9.1") Тогда
		
		Если Параметры.ОсвободитьФайл Тогда
			ОсвободитьФайл(Параметры.Идентификатор);
		КонецЕсли;
		
		Если Параметры.ОбновитьСведенияОРедактировании Тогда
			Если Параметры.Свойство("СведенияОРедактировании") Тогда
				СведенияОРедактировании = Параметры.СведенияОРедактировании;
				ОбновленныйФайл = Ответ.objects[0];
				СведенияОРедактировании.ИдентификаторВерсии = ОбновленныйФайл.activeVersion.objectID.id;
			Иначе
				СведенияОРедактировании = ПолучитьСведенияОРедактированииФайла(
					Параметры.Идентификатор, "DMFile");
			КонецЕсли;
			Если СведенияОРедактировании.Сохранен Тогда
				СведенияОРедактировании.НаЧтение = Параметры.ОсвободитьФайл;
			Иначе
				СведенияОРедактировании.Сохранен = Истина;
				СведенияОРедактировании.Вставить("НаЧтение", Истина);
				СведенияОРедактировании.Вставить("ВРабочемКаталогеВладельца", Ложь);
			КонецЕсли;
			СведенияОРедактировании.Вставить("Размер", Параметры.Размер);
			СведенияОРедактировании.Вставить("ДатаМодификацииУниверсальная", 
				Параметры.ДатаМодификацииУниверсальная);
			СохранитьСведенияОРедактированииФайла(СведенияОРедактировании);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Освобождает файл в ДО.
//
// Параметры:
//   Идентификатор - Строка - идентификатор файла в ДО.
//
Процедура ОсвободитьФайл(Знач Идентификатор) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMUnlockFileRequest");
	Запрос.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, Идентификатор, "DMFile");
	Ответ = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
КонецПроцедуры

// Захватывает файл в ДО.
//
// Параметры:
//   Идентификатор - Строка - идентификатор файла в ДО.
//
Процедура ЗахватитьФайл(Знач Идентификатор, Знач ДатаЗахвата) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMLockFileRequest");
	Запрос.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, Идентификатор, "DMFile");
	Запрос.lockDate = ДатаЗахвата;
	Ответ = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
КонецПроцедуры

// Возвращает структуру описания файла из Документооборота с двоичными данными, если передан
// адрес временного хранилища для них.
//
// Параметры:
//   ID - Строка - идентификатор файла.
//   Форма - УправляемаяФорма -  если задана, помещаем на форму дополнительные реквизиты.
//   АдресВременногоХранилищаФайла - Строка - адрес для получения двоичных данных.
//
// Возвращаемое значение:
//   Структура - описание файла в ДО.
//
Функция ОписаниеФайла(ID, Форма = Неопределено, АдресВременногоХранилищаФайла = "") Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси,"DMRetrieveRequest");
	
	Файл = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, ID, "DMFile");
	
	Запрос.objectIds.Добавить(Файл);
	
	Запрос.columnSet.Добавить("objectId");
	Запрос.columnSet.Добавить("signed");
	Запрос.columnSet.Добавить("name");
	Запрос.columnSet.Добавить("size");
	Запрос.columnSet.Добавить("creationDate");
	Запрос.columnSet.Добавить("modificationDate");
	Запрос.columnSet.Добавить("modificationDateUniversal");
	Запрос.columnSet.Добавить("author");
	Запрос.columnSet.Добавить("extension");
	Запрос.columnSet.Добавить("description");
	Запрос.columnSet.Добавить("editing");
	Запрос.columnSet.Добавить("encrypted");
	// Получение двоичных данных.
	Если ЭтоАдресВременногоХранилища(АдресВременногоХранилищаФайла) Тогда
		Запрос.columnSet.Добавить("binaryData");
	КонецЕсли;
	// Получение владельца файла.
	Если ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.3.1.3") Тогда
		Запрос.columnSet.Добавить("owner");
	КонецЕсли;
	// Получение сведений о редактировании.
	Если ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.4.9.1") Тогда
		Запрос.columnSet.Добавить("editingUser");
		Запрос.columnSet.Добавить("lockDate");
	КонецЕсли;
	
	Запрос.columnSet.Добавить("signatures.author");
	Запрос.columnSet.Добавить("signatures.date");
	Запрос.columnSet.Добавить("signatures.comment");
	Запрос.columnSet.Добавить("signatures.signature");
	Запрос.columnSet.Добавить("signatures.thumbprint");
	Запрос.columnSet.Добавить("signatures.lineNumber");
	Запрос.columnSet.Добавить("signatures.signer");
	Запрос.columnSet.Добавить("signatures.certificate");
	Запрос.columnSet.Добавить("signatures.signatureFileName");
	
	Файлы = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Файлы);
	
	ОписаниеФайла = Новый Структура;
	
	СведенияОФайле = Файлы.objects[0];
	
	ОписаниеФайла.Вставить("Наименование", СведенияОФайле.name);
	ОписаниеФайла.Вставить("Расширение", СведенияОФайле.extension);
	ОписаниеФайла.Вставить("Описание", СведенияОФайле.description);
	ОписаниеФайла.Вставить("Размер", СведенияОФайле.size);
	
	ОписаниеФайла.Вставить("ПодписанЭП", СведенияОФайле.signed);
	ОписаниеФайла.Вставить("Зашифрован", СведенияОФайле.encrypted);
	Если СведенияОФайле.signed = Истина
		И СведенияОФайле.encrypted = Истина Тогда
		НомерКартинкиПодписанЗашифрован = 2;
	ИначеЕсли СведенияОФайле.encrypted = Истина Тогда
		НомерКартинкиПодписанЗашифрован = 1;
	ИначеЕсли СведенияОФайле.signed = Истина Тогда
		НомерКартинкиПодписанЗашифрован = 0;
	Иначе
		НомерКартинкиПодписанЗашифрован = -1;
	КонецЕсли;
	ОписаниеФайла.Вставить("НомерКартинкиПодписанЗашифрован", НомерКартинкиПодписанЗашифрован);
	
	ОписаниеФайла.Вставить("Автор", СведенияОФайле.author.name);
	ОписаниеФайла.Вставить("ID", СведенияОФайле.objectId.id);
	ОписаниеФайла.Вставить("ДатаСоздания", СведенияОФайле.creationDate);
	ОписаниеФайла.Вставить("ДатаМодификацииУниверсальная", СведенияОФайле.modificationDateUniversal);
	ОписаниеФайла.Вставить("ДатаМодификации", СведенияОФайле.modificationDate);
	ОписаниеФайла.Вставить("Редактируется", СведенияОФайле.editing);
	ОписаниеФайла.Вставить("Зашифрован", СведенияОФайле.encrypted);
	
	// Сохранение двоичных данных.
	Если ЭтоАдресВременногоХранилища(АдресВременногоХранилищаФайла) Тогда
		ПоместитьВоВременноеХранилище(СведенияОФайле.binaryData, АдресВременногоХранилищаФайла);
	КонецЕсли;
	
	// Получение владельца файла.
	Если ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.3.1.3") Тогда
		ОписаниеФайла.Вставить("Владелец", СведенияОФайле.owner.name);
		ОписаниеФайла.Вставить("ВладелецID", СведенияОФайле.owner.objectId.ID);
		ОписаниеФайла.Вставить("ВладелецТип", СведенияОФайле.owner.objectId.type);
		ВладелецФайла = РегистрыСведений.ОбъектыИнтегрированныеС1СДокументооборотом.
			СсылкаНаОбъектПоДаннымДокументооборота(СведенияОФайле.owner.objectId.ID, СведенияОФайле.owner.objectId.type);
		ОписаниеФайла.Вставить("ВладелецФайла", ВладелецФайла);
	КонецЕсли;
	
	// Получение сведений о редактировании.
	Если ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.4.9.1") Тогда
		ОписаниеФайла.Вставить("Редактирует", ?(СведенияОФайле.editingUser = Неопределено,
			"",
			СведенияОФайле.editingUser.name));
		ОписаниеФайла.Вставить("РедактируетID", ?(СведенияОФайле.editingUser = Неопределено,
			"",
			СведенияОФайле.editingUser.objectId.id));
		ОписаниеФайла.Вставить("ДатаЗаема", СведенияОФайле.lockDate);
	КонецЕсли;
	
	Подписи = Новый Массив;
	
	Для Каждого ПодписьXDTO Из СведенияОФайле.signatures Цикл
		
		Подпись = Новый Структура;
		Подпись.Вставить("КомуВыданСертификат", ПодписьXDTO.author);
		Подпись.Вставить("ДатаПодписи", ПодписьXDTO.date);
		Подпись.Вставить("Комментарий", ПодписьXDTO.comment);
		Подпись.Вставить("Подпись", ПодписьXDTO.signature);
		Подпись.Вставить("Отпечаток", ПодписьXDTO.thumbprint);
		Подпись.Вставить("Сертификат", ПодписьXDTO.certificate);
		Подпись.Вставить("ИмяФайлаПодписи", ПодписьXDTO.signatureFileName);
		Подпись.Вставить("УстановившийПодпись", ПодписьXDTO.signer.name);
		Подпись.Вставить("УстановившийПодписьИд", ПодписьXDTO.signer.objectId.id);
		
		Подписи.Добавить(Подпись);
		
	КонецЦикла;
	
	ОписаниеФайла.Вставить("Подписи", Подписи);
	
	Если Форма <> Неопределено Тогда
		Обработки.ИнтеграцияС1СДокументооборот.ПоместитьДополнительныеРеквизитыНаФорму(Форма, СведенияОФайле);
		Обработки.ИнтеграцияС1СДокументооборот.УстановитьНавигационнуюСсылку(Форма, СведенияОФайле);
	КонецЕсли;
	
	Возврат ОписаниеФайла;
	
КонецФункции

// Сохраняет подписи файла во временном хранилище.
//
// Параметры:
//	 ID - Строка - идентификатор файла
//
// Возвращаемое значение:
//   Строка - адрес массива подписей во временном хранилище
//
Функция ПоместитьВХранилищеПодписиФайла(ID) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси,"DMRetrieveRequest");
	
	Файл = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, ID, "DMFile");
	
	Запрос.objectIds.Добавить(Файл);
	
	Запрос.columnSet.Добавить("signatures.author");
	Запрос.columnSet.Добавить("signatures.date");
	Запрос.columnSet.Добавить("signatures.comment");
	Запрос.columnSet.Добавить("signatures.signature");
	Запрос.columnSet.Добавить("signatures.thumbprint");
	Запрос.columnSet.Добавить("signatures.lineNumber");
	Запрос.columnSet.Добавить("signatures.signer");
	Запрос.columnSet.Добавить("signatures.certificate");
	Запрос.columnSet.Добавить("signatures.signatureFileName");
	
	Файлы = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Файлы);
	
	СведенияОФайле = Файлы.objects[0];
	
	Подписи = Новый Массив;
	
	Для Каждого ПодписьXDTO Из СведенияОФайле.signatures Цикл
		
		Подпись = Новый Структура;
		Подпись.Вставить("КомуВыданСертификат", ПодписьXDTO.author);
		Подпись.Вставить("ДатаПодписи", ПодписьXDTO.date);
		Подпись.Вставить("Комментарий", ПодписьXDTO.comment);
		Подпись.Вставить("Подпись", ПодписьXDTO.signature);
		Подпись.Вставить("Отпечаток", ПодписьXDTO.thumbprint);
		Подпись.Вставить("Сертификат", Новый ХранилищеЗначения(ПодписьXDTO.certificate));
		Подпись.Вставить("ИмяФайлаПодписи", ПодписьXDTO.signatureFileName);
		Подпись.Вставить("УстановившийПодпись", ПодписьXDTO.signer.name);
		Подпись.Вставить("УстановившийПодписьИд", ПодписьXDTO.signer.objectId.id);
		
		Подписи.Добавить(Подпись);
		
	КонецЦикла;
	
	Возврат ПоместитьВоВременноеХранилище(Подписи, Новый УникальныйИдентификатор);
	
КонецФункции

//Возвращает список XDTO файлов по объекту потребителю.
//Параметры:
//	ИдентификаторВладельца - уникальный идентификатор объекта потребителя
//	ИмяВладельца - представление объекта потребителя
//	ТипВладельца - тип объекта потребителя (используется полное имя метаданного объекта)
//Возвращает:
//	Список объектов XDTO типа DMFile
Функция ФайлыПоОбъектуПотребителю(ИдентификаторВладельца, ИмяВладельца, ТипВладельца) Экспорт

	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetFileListRequest");
	
	ОбъектВладелец = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "ExternalObject");
	
	ОбъектВладелец.name = ИмяВладельца;
	ОбъектВладелец.id = Строка(ИдентификаторВладельца);
	ОбъектВладелец.type = ТипВладельца;
	
	Запрос.externalObjects.Добавить(ОбъектВладелец);
	
	Запрос.columnSet.Добавить("objectId");
	Запрос.columnSet.Добавить("signed");
	Запрос.columnSet.Добавить("name");
	Запрос.columnSet.Добавить("size");
	Запрос.columnSet.Добавить("creationDate");
	Запрос.columnSet.Добавить("modificationDateUniversal");
	Запрос.columnSet.Добавить("author");
	Запрос.columnSet.Добавить("extension");
	Запрос.columnSet.Добавить("description");
	Запрос.columnSet.Добавить("editing");
	Запрос.columnSet.Добавить("encrypted");
	
	Запрос.columnSet.Добавить("signatures.author");
	Запрос.columnSet.Добавить("signatures.date");
	Запрос.columnSet.Добавить("signatures.comment");
	Запрос.columnSet.Добавить("signatures.signature");
	Запрос.columnSet.Добавить("signatures.thumbprint");
	Запрос.columnSet.Добавить("signatures.signer");
	Запрос.columnSet.Добавить("signatures.certificate");
	Запрос.columnSet.Добавить("signatures.signatureFileName");
	
	Файлы = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Файлы);
	
	Возврат Файлы;
	
КонецФункции

// Возвращает список XDTO файлов по объекту Документооборота.
//
// Параметры:
//   ИдентификаторВладельца - Строка - уникальный идентификатор объекта Документооборота.
//   ИмяВладельца - Строка - представление объекта Документооборота.
//   ТипВладельца - Строка - тип XDTO объекта Документооборота.
//   ВключатьПомеченныеНаУдаление - Булево - Истина, если нужно получить файлы без учета пометки.
//
// Возвращает:
//   Список объектов XDTO типа DMFile.
//
Функция ФайлыПоВладельцу(ИдентификаторВладельца, ИмяВладельца, ТипВладельца,
	ВключатьПомеченныеНаУдаление = Ложь) Экспорт

	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetFileListByOwnerRequest");
	
	ОбъектВладелец =  ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObject");
	ОбъектВладелец.name = ИмяВладельца;
	ОбъектВладелец.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(
		Прокси, Строка(ИдентификаторВладельца), ТипВладельца);
	
	Запрос.owners.Добавить(ОбъектВладелец);
	
	Запрос.columnSet.Добавить("objectId");
	Запрос.columnSet.Добавить("signed");
	Запрос.columnSet.Добавить("name");
	Запрос.columnSet.Добавить("size");
	Запрос.columnSet.Добавить("creationDate");
	Запрос.columnSet.Добавить("modificationDateUniversal");
	Запрос.columnSet.Добавить("author");
	Запрос.columnSet.Добавить("extension");
	Запрос.columnSet.Добавить("description");
	Запрос.columnSet.Добавить("encrypted");
	Запрос.columnSet.Добавить("editing");
	
	Если ВключатьПомеченныеНаУдаление Тогда
		Запрос.ignoreDeletionMark = Истина;
		Запрос.columnSet.Добавить("deletionMark");
	КонецЕсли;
	
	// Захват и редактирование файлов.
	Если ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.4.8.1") Тогда
		Запрос.columnSet.Добавить("editingUser");
	КонецЕсли;
	
	Файлы = ИнтеграцияС1СДокументооборот.ВыполнитьЗапрос(Прокси, Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Файлы);
	
	Возврат Файлы;
	
КонецФункции

// Возвращает список версий файла по его идентификатору
//
// Параметры:
//   ИдентификаторФайла - Строка - уникальный идентификатор файла Документооборота
//   ВключатьПомеченныеНаУдаление - Булево - Истина, если нужно получить файлы без учета пометки.
//
// Возвращаемое значение:
//	 ОбъектXDTO типа DMGetObjectListRequest
//
Функция ВерсииФайла(ИдентификаторФайла,
	ВключатьПомеченныеНаУдаление = Ложь) Экспорт

	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Условие = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
	Условие.property = "owner";
	Условие.value = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, ИдентификаторФайла, "DMFile");
	
	СписокУсловий = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListQuery");
	СписокУсловий.conditions.Добавить(Условие);
	
	СписокУсловий.columnSet.Добавить("size");
	СписокУсловий.columnSet.Добавить("creationDate");
	СписокУсловий.columnSet.Добавить("modificationDate");
	СписокУсловий.columnSet.Добавить("author");
	СписокУсловий.columnSet.Добавить("extension");
	
	Если ВключатьПомеченныеНаУдаление Тогда
		
		Условие = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
		Условие.property = "ignoreDeletionMark";
		Условие.value = Истина;
		СписокУсловий.conditions.Добавить(Условие);
		
		СписокУсловий.columnSet.Добавить("deletionMark");
		
	КонецЕсли;
	
	Если ИнтеграцияС1СДокументооборот.ДоступенФункционалВерсииСервиса("1.4.9.1") Тогда
		СписокУсловий.columnSet.Добавить("modificationDateUniversal");
		СписокУсловий.columnSet.Добавить("comment");
	КонецЕсли;
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetObjectListRequest");
	Запрос.type = "DMFileVersion";
	Запрос.Query = СписокУсловий;
	
	Результат = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	Возврат Результат.items;
	
КонецФункции

// Делает активной указанную версию файла.
//
// Параметры:
//   ИдентификаторФайла - Строка - идентификатор файла-владельца.
//   ИдентификаторВерсии - Строка - идентификатор версии, которую следует сделать активной.
//   ТекстСообщения - неявно возвращаемый параметр, текст сообщения о проблеме.
//
// Возвращаемое значение:
//   Булево - Истина, если операция выполнена успешно.
//
Функция СделатьВерсиюАктивной(ИдентификаторФайла, ИдентификаторВерсии, ТекстСообщения = "") Экспорт
	
	Если Не ДоступенФункционалВерсииСервиса("1.4.6.1") Тогда
		ТекстСообщения = 
			НСтр("ru = 'Для работы с версиями нужен 1С:Документооборот версии не ниже 1.4.6.1'");
		Возврат Ложь;
	КонецЕсли;
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси,"DMUpdateRequest");
	Файл = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMFile");
	Файл.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, 
		ИдентификаторФайла, "DMFile");
	Файл.name = "";
	Версия = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMFileVersion");
	Версия.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, 
		ИдентификаторВерсии, "DMFileVersion");
	Версия.name = "";
	Файл.activeVersion = Версия;
	Запрос.objects.Добавить(Файл);
	
	Попытка
		Результат = Прокси.execute(Запрос);
		ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
		Возврат Истина;
	Исключение
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

// Создает в файл в Документообороте по данным из файла на диске.
//
// Параметры:
//   ПараметрыСоздания - Структура - описание создаваемого файла, см.НовыеПараметрыСозданияФайла().
//   ID - Строка - идентификатор владельца файла в Документообороте.
//   Тип - Строка - тип владельца файла в Документообороте.
//   Представление - Строка - представление владельца файла в Документообороте.
//   ПытатьсяОбновить - Булево - Истина, если при обнаружении одноименного файла нужно обновить его, и
//                             - Ложь, если необходимо безусловное создание нового файла.
//
// Возвращаемое значение:
//   Строка - идентификатор созданного файла.
//
Функция СоздатьИзФайлаНаДискеСервер(ПараметрыСоздания, ID, Тип, Представление = "",
		ПытатьсяОбновить = Ложь) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMAddFileRequest");
	
	ОбъектВладелец = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObject");
	ОбъектВладелец.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, ID, Тип);
	ОбъектВладелец.name = Представление;
	
	Запрос.owner = ОбъектВладелец;
	
	Если ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.4.9.1") Тогда
		Запрос.tryToUpdate = ПытатьсяОбновить;
	КонецЕсли;
	
	Запрос.file = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMFile");
	Запрос.file.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, "", "DMFile");
	
	Запрос.file.binaryData = ПолучитьИзВременногоХранилища(
		ПараметрыСоздания.АдресВременногоХранилищаФайла);
	Запрос.file.extension = ПараметрыСоздания.Расширение;
	Запрос.file.modificationDate = ПараметрыСоздания.ВремяИзменения;
	Запрос.file.modificationDateUniversal = ПараметрыСоздания.ВремяИзмененияУниверсальное;
	Запрос.file.name = ПараметрыСоздания.Имя;
	Запрос.file.size = ПараметрыСоздания.Размер;
	Запрос.file.scannedOriginal = ПараметрыСоздания.ЯвляетсяСканКопией;
	
	Размер = Запрос.file.binaryData.Размер();
	МаксРазмерФайла = МаксимальныйРазмерПередаваемогоФайла();
	РазмерВМб = Размер / 1048576;
	РазмерВМбМакс = МаксРазмерФайла / 1048576;
	
	Если Размер > МаксРазмерФайла Тогда
		ВызватьИсключение
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Размер файла ""%1"" (%2 Мб) превышает максимально допустимый размер файла для передачи (%3 Мб).'"),
				ПараметрыСоздания.Имя, 
				РаботаСФайламиСлужебныйКлиентСервер.ПолучитьСтрокуСРазмеромФайла(РазмерВМб),
				РаботаСФайламиСлужебныйКлиентСервер.ПолучитьСтрокуСРазмеромФайла(РазмерВМбМакс));
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ПараметрыСоздания.Текст) Тогда
		Запрос.file.text = ПараметрыСоздания.Текст;
	КонецЕсли;
	
	Ответ = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
	ОбъектИС = РегистрыСведений.ОбъектыИнтегрированныеС1СДокументооборотом.СсылкаНаОбъектПоДаннымДокументооборота(ID, Тип);
	Если ОбъектИС = Неопределено Тогда
		РегистрыСведений.ОбъектыИнтегрированныеС1СДокументооборотом.ДобавитьСвязь(ID, Тип, ПараметрыСоздания.Владелец);
		ИнтеграцияС1СДокументооборотПереопределяемый.
			ПриПоявленииПрисоединенныхФайловДокументооборота(ID, Тип, ПараметрыСоздания.Владелец);
	Иначе
		ИнтеграцияС1СДокументооборотПереопределяемый.
			ПриПоявленииПрисоединенныхФайловДокументооборота(ID, Тип, ОбъектИС);
	КонецЕсли;
	
	Возврат Ответ.file.objectId.id;
	
КонецФункции

// Возвращает пустые параметры создания файла для вызова СоздатьИзФайлаНаДискеСервер.
//
// Возвращаемое значение:
//   Структура - структура параметров создания.
//
Функция НовыеПараметрыСозданияФайла() Экспорт
	
	ПараметрыСоздания = Новый Структура;
	ПараметрыСоздания.Вставить("Имя", "");
	ПараметрыСоздания.Вставить("Расширение", "");
	ПараметрыСоздания.Вставить("Размер", 0);
	ПараметрыСоздания.Вставить("Текст", "");
	ПараметрыСоздания.Вставить("ВремяИзмененияУниверсальное", Дата(1, 1, 1));
	ПараметрыСоздания.Вставить("АдресВременногоХранилищаФайла", Неопределено);
	ПараметрыСоздания.Вставить("ВремяИзменения", Дата(1, 1, 1));
	ПараметрыСоздания.Вставить("ВебКлиент", Ложь);
	ПараметрыСоздания.Вставить("ЯвляетсяСканКопией", Ложь);
	ПараметрыСоздания.Вставить("Владелец", Неопределено);
	
	Возврат ПараметрыСоздания;
	
КонецФункции

// Создает в файл в Документообороте по данным из файла-шаблона.
//
// Параметры:
//   ПараметрыСоздания - Структура - описание создаваемого файла.
//   ID - Строка - идентификатор владельца файла в Документообороте.
//   Тип - Строка - тип владельца файла в Документообороте.
//   Представление - Строка - представление владельца файла в Документообороте.
//
// Возвращаемое значение:
//   Строка - идентификатор созданного файла.
//
Функция СоздатьФайлИзШаблона(ПараметрыСоздания, ID, Тип, Представление = "") Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMAddFileRequest");
	
	ОбъектВладелец = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObject");
	ОбъектВладелец.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, ID, Тип);
	ОбъектВладелец.name = Представление;
	
	Запрос.owner = ОбъектВладелец;
	
	Запрос.file = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMFile");
	Запрос.file.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, "", "DMFile");
	
	Запрос.file.extension = ПараметрыСоздания.Расширение;
	Запрос.file.name = ПараметрыСоздания.Имя;
	
	ФайлШаблон = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMFile");
	ФайлШаблон.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси,
		ПараметрыСоздания.ШаблонID, "DMFile");
	ФайлШаблон.name = ПараметрыСоздания.Имя;
	
	Запрос.file.template = ФайлШаблон;
	
	Ответ = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
	Возврат Ответ.file.objectId.id; 

КонецФункции

// Получает из ДО сведения о редактировании активной версии файла или указанной версии.
//
// Параметры:
//   Идентификатор - Строка - идентификатор файла или версии.
//   Тип - Строка - DMFile или DMFileVersion.
//
// Возвращаемое значение:
//   Структура со свойствами:
//     ИдентификаторВерсии - Строка - идентификатор сохраненной версии файла (тот же, если версия указана явно).
//     Сохранен - Булево - Истина, если файл сохранен на диске.
//     ДатаСохранения - Булево - дата сохранения файла на диске, если он сохранен.
//     ПолныйПуть - Строка - полный путь к файлу на диске, если он сохранен.
//     РекомендуемаяПапка - Строка - рекомендуемая папка для сохранения файла, если он не сохранен.
//     НаЧтение - Булево - Истина, если файл сохранен только для чтения.
//     ВРабочемКаталогеВладельца - Истина, если файл сохранен в рабочем каталоге папки-владельца.
//     ДатаМодификацииУниверсальная - Дата - дата и время модификации сохраненной версии файла.
//     Размер - Число - размер сохраненной версии файла.
//     МаксимальныйРазмерЛокальногоКэшаФайлов - Число - максимальный размер каталога.
//
Функция ПолучитьСведенияОРедактированииФайла(Идентификатор, Тип) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси,"DMGetFileEditingInfoRequest");
	Запрос.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, Идентификатор, Тип);
	
	Если Запрос.Свойства().Получить("clientIdentifier") <> Неопределено Тогда
		СисИнфо = Новый СистемнаяИнформация;
		Запрос.clientIdentifier = Строка(СисИнфо.ИдентификаторКлиента);
	КонецЕсли;
	
	Результат = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	Сведения = Новый Структура;
	Сведения.Вставить("ИдентификаторВерсии", Результат.objectId.id);
	Сведения.Вставить("Сохранен", Результат.info.saved);
	Если Сведения.Сохранен Тогда
		Сведения.Вставить("ДатаСохранения", Результат.info.saveDate);
		Сведения.Вставить("ПолныйПуть", Результат.info.fullPath);
		Сведения.Вставить("НаЧтение", Результат.info.readOnly);
		Сведения.Вставить("ДатаМодификацииУниверсальная", Результат.info.modificationDateUniversal);
		Сведения.Вставить("Размер", Результат.info.size);
	КонецЕсли;
	Сведения.Вставить("РекомендуемаяПапка", Результат.info.folder);
	Сведения.Вставить("МаксимальныйРазмерЛокальногоКэшаФайлов", 0);
	Сведения.Вставить("ВРабочемКаталогеВладельца", Результат.info.inOwnersFolder);
	
	Если ДоступенФункционалВерсииСервиса("2.1.14.2") Тогда
		Сведения.МаксимальныйРазмерЛокальногоКэшаФайлов = Результат.info.maxFolderSize;
	КонецЕсли;
	
	Возврат Сведения;
	
КонецФункции

// Сохраняет в ДО сведения о редактировании версии файла.
//
// Параметры:
//   Сведения - Структура со свойствами:
//     ИдентификаторВерсии - Строка - идентификатор версии файла.
//     Сохранен - Булево - Истина, если файл сохранен на диске.
//     ДатаСохранения - Булево - дата сохранения файла на диске.
//     ПолныйПуть - Строка - полный путь к файлу на диске.
//     НаЧтение - Булево - Истина, если файл сохранен только для чтения.
//     ВРабочемКаталогеВладельца - Истина, если файл сохранен в рабочем каталоге папки-владельца.
//     Размер - размер сохраненной версии файла.
//
Процедура СохранитьСведенияОРедактированииФайла(Сведения) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси,"DMUpdateFileEditingInfoRequest");
	
	Запрос.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, 
		Сведения.ИдентификаторВерсии, "DMFileVersion");
		
	Если Запрос.Свойства().Получить("clientIdentifier") <> Неопределено Тогда
		СисИнфо = Новый СистемнаяИнформация;
		Запрос.clientIdentifier = Строка(СисИнфо.ИдентификаторКлиента);
	КонецЕсли;
	
	Запрос.info = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси,"DMFileEditingInfo");
	
	Запрос.info.saved = Сведения.Сохранен;
	Если Сведения.Сохранен Тогда
		Запрос.info.saveDate = Сведения.ДатаСохранения;
		Запрос.info.fullPath = Сведения.ПолныйПуть;
		Запрос.info.readOnly = Сведения.НаЧтение;
		Запрос.info.modificationDateUniversal = Сведения.ДатаМодификацииУниверсальная;
		Запрос.info.size = Сведения.Размер;
		Запрос.info.inOwnersFolder = Сведения.ВРабочемКаталогеВладельца;
	КонецЕсли;
	
	Результат = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
КонецПроцедуры

// Заполняет поля указанного файла данными его владельца на стороне ДО.
//
// Параметры:
//   ID - Строка - идентификатор файла ДО.
//
Процедура ЗаполнитьПоляФайлаДаннымиВладельца(ID) Экспорт
	
	Если Не ДоступенФункционалВерсииСервиса("2.0.15.1") Тогда
		ТекстСообщения = 
			НСтр("ru = 'Для заполнения файлов нужен 1С:Документооборот версии не ниже 2.0.15.1'");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси,"DMAutoFillRequest");
	
	Файл = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMFile");
	Файл.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси, ID, "DMFile");
	Файл.name = "";
	
	Запрос.file = Файл;
	
	Результат = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
КонецПроцедуры

#КонецОбласти

#Область ЭлектронныеПодписи

//Заполняет структуру данных подписи по объекту XDTO.
//Параметры:
//	Подпись - структура подписи
//	ПодписьXDTO - объект XDTO подписи
Процедура ЗаполнитьПоляПодписи(Подпись, ПодписьXDTO)
	
	Подпись.Вставить("КомуВыданСертификат", ПодписьXDTO.author);
	Подпись.Вставить("ДатаПодписи", ПодписьXDTO.date);
	Подпись.Вставить("Комментарий", ПодписьXDTO.comment);
	Подпись.Вставить("Подпись", ПодписьXDTO.signature);
	Подпись.Вставить("Отпечаток", ПодписьXDTO.thumbprint);
	Подпись.Вставить("Сертификат", ПодписьXDTO.certificate);
	Подпись.Вставить("ИмяФайлаПодписи", ПодписьXDTO.signatureFileName);
	Подпись.Вставить("УстановившийПодпись", ПодписьXDTO.signer.name);
	Подпись.Вставить("УстановившийПодписьИд", ПодписьXDTO.signer.objectId.id);
	
КонецПроцедуры

// Обновляет дерево подписей по данным коллекций XDTO.
//
// Параметры:
//   ФайлыXDTO - СписокXDTO - файлы документа Документооборота.
//   Подписи - ДанныеФормыДерево - подписи в форме.
//   УникальныйИдентификаторФормы - УникальныйИдентификатор - идентификатор формы-владельца.
//
Процедура ОбновитьСписокПодписейФайлов(ФайлыXDTO, Подписи, УникальныйИдентификаторФормы) Экспорт
	
	// Заполним соответствие, где идентификатору владельца подписи 
	// сопоставлен его тип и массив подписей.
	Соответствие = Новый Соответствие;
	
	Для Каждого Файл Из ФайлыXDTO Цикл
		
		Счетчик = 0;
		Для Каждого ПодписьXDTO Из Файл.signatures Цикл
			
			Подпись = Новый Структура;
			ЗаполнитьПоляПодписи(Подпись, ПодписьXDTO);
			
			Подпись.Вставить("НомерСтроки", Счетчик);
			Подпись.Вставить("ОбъектИмя", Файл.name);
			Подпись.Вставить("ОбъектИд", Файл.objectId.id);
			Подпись.Вставить("ОбъектТип", Файл.objectId.type);
			
			ТипИПодписи = Соответствие.Получить(Подпись.ОбъектИд);
			
			Если ТипИПодписи = Неопределено Тогда
				ТипИПодписи = Новый Структура;
				ТипИПодписи.Вставить("Тип", Подпись.ОбъектТип);
				ТипИПодписи.Вставить("Подписи", Новый Массив);
				Соответствие.Вставить(Подпись.ОбъектИд, ТипИПодписи);
			КонецЕсли;
			
			ТипИПодписи.Подписи.Добавить(Подпись);
			Счетчик = Счетчик + 1;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ЭлементыДерева = Подписи.ПолучитьЭлементы();
	ЭлементыДерева.Очистить();
	
	Для Каждого КлючЗначение Из Соответствие Цикл
		
		ТипИПодписи = КлючЗначение.Значение;
		
		Если ТипИПодписи.Тип = "DMFile"
			И ТипИПодписи.Подписи.Количество() <> 0 Тогда
			
			НоваяСтрока = ЭлементыДерева.Добавить();
			
			// для ветки дерева используем КомуВыданСертификат как Представление.
			ПредставлениеТипа = ПредставлениеТипа(ТипИПодписи.Подписи[0].ОбъектТип);
			НоваяСтрока.КомуВыданСертификат = ПредставлениеТипа + " """ + Подпись.ОбъектИмя + """"; 
			Если НоваяСтрока.Свойство("КомуВыданСертификатИСтатус") Тогда
				НоваяСтрока.КомуВыданСертификатИСтатус = НоваяСтрока.КомуВыданСертификат;
			КонецЕсли;
			
			ЭлементыПодписи = НоваяСтрока.ПолучитьЭлементы();
			Для Каждого Подпись Из ТипИПодписи.Подписи Цикл
				ЭлементПодпись = ЭлементыПодписи.Добавить();
				ЗаполнитьСтрокуДереваПодписей(ЭлементПодпись,
					Подпись,
					УникальныйИдентификаторФормы);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

//Заполняет список подписей Входящего Исходящего Внутреннего документа и его подчиненных файлов.
//Параметры:
//	ПодписиXDTO - список XDTO подписей документа Документооборота
//	ФайлыXDTO - список XDTO файлов документа Документооборота
//	Форма - форма документа Документооборота
Процедура ЗаполнитьСписокПодписейСервер(ПодписиXDTO, ФайлыXDTO, Форма, 
	ЭлементДляИзмененияЗаголовка = Неопределено) Экспорт
	
	Соответствие = Новый Соответствие;
	
	Счетчик = 0;
	Для Каждого ПодписьXDTO Из ПодписиXDTO Цикл
		
		Подпись = Новый Структура;
		ЗаполнитьПоляПодписи(Подпись, ПодписьXDTO);
		
		Подпись.Вставить("НомерСтроки", Счетчик);
		Подпись.Вставить("ОбъектИмя", Форма.Представление);
		Подпись.Вставить("ОбъектИд",  Форма.ID);
		Подпись.Вставить("ОбъектТип", Форма.Тип);
		
		Ид = Подпись.ОбъектИд;
		ДанныеВладельца = Соответствие.Получить(Ид);
		
		Если ДанныеВладельца = Неопределено Тогда
			ДанныеВладельца = Новый Структура("Тип, МассивПодписей", Подпись.ОбъектТип, Новый Массив);
			Соответствие.Вставить(Ид, ДанныеВладельца);
		КонецЕсли;
		
		ДанныеВладельца.МассивПодписей.Добавить(Подпись);
		
		Счетчик = Счетчик + 1;
		
	КонецЦикла;
	
	Для Каждого Файл Из ФайлыXDTO Цикл
		Счетчик = 0;
		Для Каждого ПодписьXDTO Из Файл.signatures Цикл
			Подпись = Новый Структура;
			ЗаполнитьПоляПодписи(Подпись, ПодписьXDTO);
			
			Подпись.Вставить("НомерСтроки", Счетчик);
			Подпись.Вставить("ОбъектИмя", Файл.name);
			Подпись.Вставить("ОбъектИд", Файл.objectId.id);
			Подпись.Вставить("ОбъектТип", Файл.objectId.type);
			
			Ид = Подпись.ОбъектИд;
			ДанныеВладельца = Соответствие.Получить(Ид);
			
			Если ДанныеВладельца = Неопределено Тогда
				ДанныеВладельца = Новый Структура("Тип, МассивПодписей", Подпись.ОбъектТип, Новый Массив);
				Соответствие.Вставить(Ид, ДанныеВладельца);
			КонецЕсли;
			
			ДанныеВладельца.МассивПодписей.Добавить(Подпись);
			
			Счетчик = Счетчик + 1;
		КонецЦикла;
	КонецЦикла;
	
	ВсегоПодписей = 0;
	
	Форма.ТаблицаПодписей.ПолучитьЭлементы().Очистить();
	
	ЭлементыДерева = Форма. ТаблицаПодписей.ПолучитьЭлементы();
	
	КоличествоПодписей = 0;
	
	Для Каждого ПараКлючЗначение Из Соответствие Цикл
		
		ДанныеВладельца = ПараКлючЗначение.Значение;
		
		ТипОбъекта = ДанныеВладельца.Тип;
		
		Если ТипОбъекта <> "DMFile" И ДанныеВладельца.МассивПодписей.Количество() <> 0 Тогда
			НоваяСтрока = ЭлементыДерева.Добавить();
			// для ветки дерева используем КомуВыданСертификат как Представление.
			ПредставлениеТипа = ПредставлениеТипа(ДанныеВладельца.МассивПодписей[0].ОбъектТип);
			НоваяСтрока.КомуВыданСертификат = ПредставлениеТипа + " """ + Форма.Представление + """"; 
			Если НоваяСтрока.Свойство("КомуВыданСертификатИСтатус") Тогда
				НоваяСтрока.КомуВыданСертификатИСтатус = НоваяСтрока.КомуВыданСертификат;
			КонецЕсли;
			НоваяСтрока.ИндексКартинки = 0;  // стандартная иконка справочника
			Для Каждого Подпись Из ДанныеВладельца.МассивПодписей Цикл
				НоваяСтрокаДерева = НоваяСтрока.ПолучитьЭлементы().Добавить();
				ЗаполнитьСтрокуДереваПодписей(НоваяСтрокаДерева, Подпись, Форма.УникальныйИдентификатор);
				КоличествоПодписей = КоличествоПодписей + 1;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	ВсегоПодписей = ВсегоПодписей + КоличествоПодписей;
	
	Для Каждого ПараКлючЗначение Из Соответствие Цикл
		
		ДанныеВладельца = ПараКлючЗначение.Значение;
		
		ТипОбъекта = ДанныеВладельца.Тип;
		
		Если ТипОбъекта = "DMFile" И ДанныеВладельца.МассивПодписей.Количество() <> 0 Тогда
			
			НоваяСтрока = ЭлементыДерева.Добавить();
			// для ветки дерева используем КомуВыданСертификат как Представление.
			ПредставлениеТипа = ПредставлениеТипа(ДанныеВладельца.МассивПодписей[0].ОбъектТип);
			НоваяСтрока.КомуВыданСертификат = ПредставлениеТипа + " """ + Подпись.ОбъектИмя + """"; 
			Если НоваяСтрока.Свойство("КомуВыданСертификатИСтатус") Тогда
				НоваяСтрока.КомуВыданСертификатИСтатус = НоваяСтрока.КомуВыданСертификат;
			КонецЕсли;
			
			Отбор = Новый Структура("ID", ДанныеВладельца.МассивПодписей[0].ОбъектИд);
			СтрокаФайлов = Форма.Файлы.НайтиСтроки(Отбор);
			
			Если СтрокаФайлов.Количество() <> 0 Тогда
				НоваяСтрока.ИндексКартинки = СтрокаФайлов[0].ИндексКартинки;
			КонецЕсли;
			
			КоличествоПодписей = 0;
			
			Для Каждого Подпись Из ДанныеВладельца.МассивПодписей Цикл
				НоваяСтрокаДерева = НоваяСтрока.ПолучитьЭлементы().Добавить();
				ЗаполнитьСтрокуДереваПодписей(НоваяСтрокаДерева, Подпись, Форма.УникальныйИдентификатор);
				КоличествоПодписей = КоличествоПодписей + 1;
			КонецЦикла;
			
			ВсегоПодписей = ВсегоПодписей + КоличествоПодписей;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЭлементДляИзмененияЗаголовка <> Неопределено Тогда
		ТекстЗаголовка = НСтр("ru = 'ЭП'");
		Если ВсегоПодписей <> 0 Тогда
			ТекстЗаголовка = ТекстЗаголовка + " (" + Строка(ВсегоПодписей) + ")";
		КонецЕсли;
		ЭлементДляИзмененияЗаголовка.Заголовок = ТекстЗаголовка;
	КонецЕсли;
	
КонецПроцедуры

//Заполняет строку дерева на закладке ЭП в документе.
//Параметры:
//	НоваяСтока - строка дерева значений списка подписей 
//	Подпись - структура данных с информацией о подписи
//	ИдентификаторФормы - идентификатор управляемой формы, из которой вызывается процедура
Процедура ЗаполнитьСтрокуДереваПодписей(НоваяСтрока, Подпись, ИдентификаторФормы) Экспорт
	
	ЗаполнитьЗначенияСвойств(НоваяСтрока, Подпись);
	
	Если НоваяСтрока.Свойство("КомуВыданСертификатИСтатус") Тогда
		НоваяСтрока.КомуВыданСертификатИСтатус = НоваяСтрока.КомуВыданСертификат
			+ Символы.ПС + НоваяСтрока.Статус;
	КонецЕсли;
	Если НоваяСтрока.Свойство("ДатаПодписиИКомментарий") Тогда
		НоваяСтрока.ДатаПодписиИКомментарий = Формат(НоваяСтрока.ДатаПодписи, "ДФ='dd.MM.yyyy HH:mm'")
			+ Символы.ПС + НоваяСтрока.Комментарий;
	КонецЕсли;
	
	НоваяСтрока.Объект = Подпись.ОбъектИд;
	
	НоваяСтрока.Неверна = Ложь;
	НоваяСтрока.ИндексКартинки = -1;
	
	ДвоичныеДанные = Подпись.Подпись;
	НоваяСтрока.АдресПодписи = ПоместитьВоВременноеХранилище(ДвоичныеДанные, ИдентификаторФормы);
	
	ДвоичныеДанныеСертификата = Подпись.Сертификат;
	Если ДвоичныеДанныеСертификата <> Неопределено Тогда 
		НоваяСтрока.АдресСертификата = ПоместитьВоВременноеХранилище(ДвоичныеДанныеСертификата, ИдентификаторФормы);
	КонецЕсли;
	
КонецПроцедуры

//Проверяет все подписи на сервере из массива ВыделенныеСтроки.
// Параметры:
//	ВыделенныеСтроки - массив идентификаторов выделенных строк таблицы подписей 
//	ТаблицаПодписей - реквизит таблицы подписей формы документа
//	АдресСлепкаДокумента - адрес временного хранилища двоичных данных документа Документооборота
Процедура ПроверитьПодписиНаСервере(Знач ВыделенныеСтроки, ТаблицаПодписей, ID, АдресСлепкаДокумента) Экспорт
	
	МенеджерКриптографии = ПолучитьМенеджерКриптографии("ПроверкаПодписи");
	
	СоответствиеИдОбъектаИДвоичныхДанных = Новый Соответствие;
	
	Для Каждого Элемент Из ВыделенныеСтроки Цикл
		ДанныеСтроки = ТаблицаПодписей.НайтиПоИдентификатору(Элемент);
		
		Если НЕ ПустаяСтрока(ДанныеСтроки.Объект) Тогда
			ПроверитьОднуПодписьНаСервере(ДанныеСтроки, МенеджерКриптографии, ID, 
				СоответствиеИдОбъектаИДвоичныхДанных, АдресСлепкаДокумента);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

//Проверяет одну электронно-цифровую подпись.
//Параметры:
//	ДанныеСтроки - строка таблицы файлов или структура, содержащая идентификатор объекта в свойстве ОбъектИД
//	МенеджерКриптографии - объект Менеджера криптографии
//	УникальныйИдентификатор - идентификатор управляемой формы объекта Документооборота
//	СоответствиеИдОбъектаИДвоичныхДанных - соответствие идентификаторов объектов и их двоичных данных 
//	АдресСлепкаДокумента - адрес временного хранения двоичных данных файла
Процедура ПроверитьОднуПодписьНаСервере(ДанныеСтроки, МенеджерКриптографии, УникальныйИдентификатор, 
	СоответствиеИдОбъектаИДвоичныхДанных, АдресСлепкаДокумента) Экспорт
	
	АдресПодписи = ДанныеСтроки.АдресПодписи;
	ДвоичныеДанныеПодписи = ПолучитьИзВременногоХранилища(АдресПодписи);
	
	ДвоичныеДанныеФайла = СоответствиеИдОбъектаИДвоичныхДанных[ДанныеСтроки.ОбъектИд];
	
	Если ДвоичныеДанныеФайла = Неопределено Тогда
		
		Если ДанныеСтроки.ОбъектТип = "DMFile" Тогда
			ДвоичныеДанныеФайла = ДвоичныеДанныеФайла(ДанныеСтроки.ОбъектИд);
		Иначе	
			ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(АдресСлепкаДокумента);
		КонецЕсли;
		
		СоответствиеИдОбъектаИДвоичныхДанных[ДанныеСтроки.ОбъектИд] = ДвоичныеДанныеФайла;
	КонецЕсли;
	
	ОписаниеОшибки = "";
	Попытка
		ДанныеСтроки.Неверна = НЕ ЭлектроннаяПодпись.ПроверитьПодпись(
			МенеджерКриптографии, ДвоичныеДанныеФайла, ДвоичныеДанныеПодписи, ОписаниеОшибки);
		ДанныеСтроки.Статус = ?(ДанныеСтроки.Неверна,
			ДанныеСтроки.Статус = НСтр("ru = 'Не верна'") + ": " + ОписаниеОшибки,
			НСтр("ru = 'Верна'"));
	Исключение
		ДанныеСтроки.Статус = НСтр("ru = 'Не верна: не указан менеджер криптографии'");
		ДанныеСтроки.Неверна = Истина;
	КонецПопытки;
	
	Если ДанныеСтроки.Свойство("КомуВыданСертификатИСтатус") Тогда
		ДанныеСтроки.КомуВыданСертификатИСтатус = 
			ДанныеСтроки.КомуВыданСертификат
			+ Символы.ПС
			+ ДанныеСтроки.Статус;
	КонецЕсли;
	
КонецПроцедуры

//Проверяет все подписи на сервере из массива ВыделенныеСтроки.
//Параметры:
//	ТаблицаПодписей - таблица формы, содержащая список подписей
//	УникальныйИдентификатор - идентификатор управляемой формы объекта Документооборота
//	АдресСлепкаДокумента - адрес временного хранения двоичных данных объекта Документооборота
Процедура ПроверитьВсеПодписиНаСервере(ТаблицаПодписей, УникальныйИдентификатор, АдресСлепкаДокумента) Экспорт
	
	МенеджерКриптографии = ПолучитьМенеджерКриптографии("ПроверкаПодписи");
	СоответствиеИдОбъектаИДвоичныхДанных = Новый Соответствие;
	
	НомераСтрок = ДанныеПодписейСервер(ТаблицаПодписей);
	Для Каждого Элемент Из НомераСтрок Цикл
		ДанныеСтроки = ТаблицаПодписей.НайтиПоИдентификатору(Элемент);
		Если НЕ ПустаяСтрока(ДанныеСтроки.Объект) Тогда
			ПроверитьОднуПодписьНаСервере(ДанныеСтроки, МенеджерКриптографии, УникальныйИдентификатор, 
				СоответствиеИдОбъектаИДвоичныхДанных, АдресСлепкаДокумента);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

//Получает объекты, которые требуется подписать ЭП, заполняет массив для подписей.
//Параметры:
//	ОбъектИД - идентификатор документа Документооборота
//	ОбъектТип - тип объекта XDTO документа Документообороте
//	МассивОбъектовДляПодписи - заполняемый массив структур объектов для подписи
//	Файлы - массив идентификаторов файлов в Документообороте
//	ДвоичныеДанные - двоичные данные документа Документооборота
Процедура ПолучитьОбъектыДляПодписи(ОбъектИд, ОбъектТип, МассивОбъектовДляПодписи, Файлы, ДвоичныеДанные) Экспорт 
	
	//добавим в массив документ.
	ОбъектДляПодписи = Новый Структура("ДвоичныеДанные, ОбъектСсылкаДляПодписи, ОбъектТип", 
		ДвоичныеДанные, ОбъектИд, ОбъектТип);
	МассивОбъектовДляПодписи.Добавить(ОбъектДляПодписи);
	
	//добавим все подчиненные файлы.
	ДанныеФайлов = ДвоичныеДанныеФайлов(Файлы);
	
	Для Каждого Файл Из ДанныеФайлов.objects Цикл
		
		ОбъектДляПодписи = Новый Структура("ДвоичныеДанные, ОбъектСсылкаДляПодписи, ОбъектТип", 
			Файл.binaryData, Файл.objectId.id, Файл.objectId.type);
		МассивОбъектовДляПодписи.Добавить(ОбъектДляПодписи);
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает Истина, если есть хранимые файлы к объекту ВнешнийОбъект.
//
// Параметры:
//   ВнешнийОбъект - Произвольный - объект-владелец.
//
// Возвращаемое значение:
//   Булево - Истина, если есть хранимые файлы.
//
Функция ЕстьХранимыеФайлы(ВнешнийОбъект) Экспорт
	
	ЕстьХранимыеФайлы = Ложь;
	
	ИнтеграцияС1СДокументооборотПереопределяемый.ПриОпределенииНаличияПрисоединенныхФайлов(
		ВнешнийОбъект, ЕстьХранимыеФайлы);
	
	Возврат ЕстьХранимыеФайлы;
	
КонецФункции

// Возвращает хранимые файлы к объекту ВнешнийОбъект.
//
// Параметры:
//   ВнешнийОбъект - Произвольный - объект-владелец.
//
// Возвращаемое значение:
//   Массив - массив ссылок на хранимые файлы.
//
Функция ПолучитьХранимыеФайлы(ВнешнийОбъект) Экспорт
	
	ХранимыеФайлы = Новый Массив;
	
	ИнтеграцияС1СДокументооборотПереопределяемый.ПриПолученииПрисоединенныхФайлов(ВнешнийОбъект, ХранимыеФайлы);
	
	Возврат ХранимыеФайлы;
	
КонецФункции

//Заполняет список файлов копированием присоединенных файлов или Файлов из внешнего объекта.
// Параметры:
//   ВнешнийОбъект - ссылка на объект потребитель
//	ID - идентификатор объекта Документооборота
//	Тип - тип XDTO объекта Документооборота
//	Представление - представление объекта Документооборота
//	УникальныйИдентификатор - идентификатор управляемой формы объекта Документооборота
Процедура ЗаполнитьФайлыКопированием(ВнешнийОбъект, ID, Тип, Представление, УникальныйИдентификатор) Экспорт
	
	МассивФайлов = ПолучитьХранимыеФайлы(ВнешнийОбъект);
	
	Для Каждого ДанныеФайла Из МассивФайлов Цикл
	
		ВремяИзменения = МестноеВремя(ДанныеФайла.ДатаМодификацииУниверсальная);
		ВремяИзмененияУниверсальное = ДанныеФайла.ДатаМодификацииУниверсальная;
		Размер = ДанныеФайла.Размер;
		ИмяБезРасширения = ДанныеФайла.Наименование;
		Расширение = ДанныеФайла.Расширение;
		
		// Поместим Файл в ВременноеХранилище.
		Если ДанныеФайла.Свойство("ДвоичныеДанныеФайла") Тогда
			Если ТипЗнч(ДанныеФайла.ДвоичныеДанныеФайла) = Тип("Строка")
					И ЭтоАдресВременногоХранилища(ДанныеФайла.ДвоичныеДанныеФайла) Тогда
				АдресВременногоХранилищаФайла = ДанныеФайла.ДвоичныеДанныеФайла;
			Иначе // двоичные данные
				АдресВременногоХранилищаФайла =
					ПоместитьВоВременноеХранилище(ДанныеФайла.ДвоичныеДанныеФайла, УникальныйИдентификатор);
			КонецЕсли;
		ИначеЕсли ДанныеФайла.Свойство("СсылкаНаДвоичныеДанныеФайла")
				И ЭтоАдресВременногоХранилища(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла) Тогда
			АдресВременногоХранилищаФайла = ДанныеФайла.СсылкаНаДвоичныеДанныеФайла;
		Иначе
			ВызватьИсключение НСтр("ru = 'Не удалось определить адрес временного хранилища присоединенного файла'");
		КонецЕсли;
		
		ПараметрыСоздания = НовыеПараметрыСозданияФайла();
		ПараметрыСоздания.Имя = ИмяБезРасширения;
		ПараметрыСоздания.Расширение = Расширение;
		ПараметрыСоздания.Размер = Размер;
		ПараметрыСоздания.АдресВременногоХранилищаФайла = АдресВременногоХранилищаФайла;
		ПараметрыСоздания.ВремяИзменения = ВремяИзменения;
		ПараметрыСоздания.ВремяИзмененияУниверсальное = ВремяИзмененияУниверсальное;
		ПараметрыСоздания.Текст = "";
		Если ДанныеФайла.СтатусИзвлеченияТекста = "Извлечен" Тогда
			ТекстХранилище = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеФайла.Ссылка, "ТекстХранилище");
			Если ТипЗнч(ТекстХранилище) = Тип("ХранилищеЗначения") Тогда
				ПараметрыСоздания.Текст = ТекстХранилище.Получить();
			КонецЕсли;
		КонецЕсли;
		ПараметрыСоздания.ЯвляетсяСканКопией = Ложь;
		
		СоздатьИзФайлаНаДискеСервер(ПараметрыСоздания, ID, Тип, Представление);
		
	КонецЦикла;
	
КонецПроцедуры

//Получает менеджер криптографии.
//
Функция ПолучитьМенеджерКриптографии(Операция = "") Экспорт
	
	Возврат ЭлектроннаяПодпись.МенеджерКриптографии(Операция);
	
КонецФункции

//Возвращает значение константы "ИспользоватьЭлектронныеЦифровыеПодписи".
//
Функция ИспользоватьЭлектронныеЦифровыеПодписи() Экспорт
	
	Возврат ЭлектроннаяПодпись.ИспользоватьЭлектронныеПодписи();
	
КонецФункции

//Подписывает документ электронной подписью.
//Параметры:
//	ДобавленныеПодписи - массив структур новых подписей файла
//	ТаблицаПодписей - таблица подписей формы документа Документооборота
//Возвращает:
//	Соответствие строк объектов новым подписям
Функция ПодписатьДокумент(ДобавленныеПодписи, ТаблицаПодписей) Экспорт
	
	// получаем массив всех подписей.
	НомераСтрок = ДанныеПодписейСервер(ТаблицаПодписей);
	
	МассивДанныхПодписей = Новый Массив; // подписи, оставшиеся после удаления 
	
	// формируем массив данных подписи.
	Для Каждого Элемент Из НомераСтрок Цикл
		
		ДанныеСтроки = ТаблицаПодписей.НайтиПоИдентификатору(Элемент);
		Если НЕ ПустаяСтрока(ДанныеСтроки.Объект) Тогда
			
			ПодписьДвоичныеДанные = ПолучитьИзВременногоХранилища(ДанныеСтроки.АдресПодписи);
			Если ЗначениеЗаполнено(ДанныеСтроки.АдресСертификата) Тогда
				ДвоичныеДанныеСертификата = ПолучитьИзВременногоХранилища(ДанныеСтроки.АдресСертификата);
			Иначе
				ДвоичныеДанныеСертификата = Неопределено;
			КонецЕсли;
			
			ДанныеПодписи = Новый Структура;
			ДанныеПодписи.Вставить("ОбъектСсылка", ДанныеСтроки.ОбъектИд);
			ДанныеПодписи.Вставить("НоваяПодписьДвоичныеДанные", ПодписьДвоичныеДанные);
			ДанныеПодписи.Вставить("Отпечаток", ДанныеСтроки.Отпечаток);
			ДанныеПодписи.Вставить("ДатаПодписи", ДанныеСтроки.ДатаПодписи);
			ДанныеПодписи.Вставить("Комментарий", ДанныеСтроки.Комментарий);
			ДанныеПодписи.Вставить("ИмяФайлаПодписи", ДанныеСтроки.ИмяФайлаПодписи);
			ДанныеПодписи.Вставить("КомуВыданСертификат", ДанныеСтроки.КомуВыданСертификат);
			ДанныеПодписи.Вставить("ДвоичныеДанныеСертификата", ДвоичныеДанныеСертификата);
			ДанныеПодписи.Вставить("мОбъектТип", ДанныеСтроки.ОбъектТип);
			ДанныеПодписи.Вставить("УстановившийПодпись", ДанныеСтроки.УстановившийПодпись); 
			ДанныеПодписи.Вставить("УстановившийПодписьИд", ДанныеСтроки.УстановившийПодписьИд);
			
			МассивДанныхПодписей.Добавить(ДанныеПодписи);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ДанныеПодписи Из ДобавленныеПодписи Цикл
		МассивДанныхПодписей.Добавить(ДанныеПодписи);
	КонецЦикла;
	
	Соответствие = Новый Соответствие;
	
	// Из массива удаленных строим массив объектов, где изменились подписи, 
	// в том числе тех, где не осталось ни одной подписи.
	Для Каждого СтрокаДобавленнойПодписи Из ДобавленныеПодписи Цикл
		
		Ид = СтрокаДобавленнойПодписи.ОбъектСсылка;
		
		ДанныеВладельца = Соответствие.Получить(Ид);
		
		Если ДанныеВладельца = Неопределено Тогда
			ДанныеВладельца = Новый Структура("Тип, МассивПодписей", СтрокаДобавленнойПодписи.ОбъектТип, Новый Массив);
			Соответствие.Вставить(Ид, ДанныеВладельца);
		КонецЕсли;
		
	КонецЦикла;
	
	// Распределяем массив всех оставшихся подписей по объектам.
	Для Каждого Подпись Из МассивДанныхПодписей Цикл
		
		Ид = Подпись.ОбъектСсылка;
		ДанныеВладельца = Соответствие.Получить(Ид);
		
		Если ДанныеВладельца <> Неопределено Тогда
			ДанныеВладельца.МассивПодписей.Добавить(Подпись);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Соответствие;
	
КонецФункции

//Удаляет подписи документа и его файлов.
//Параметры:
//	УдаляемыеПодписи - массив структур новых подписей файла
//	ТаблицаПодписей - таблица подписей формы документа Документооборота
//Возвращает:
//	Соответствие объектов и удаленных подписей
Функция УдалитьПодписиДокумента(УдаляемыеПодписи, ТаблицаПодписей) Экспорт
	
	// получаем массив всех подписей.
	НомераСтрок = ДанныеПодписейСервер(ТаблицаПодписей);
	
	МассивДанныхПодписей = Новый Массив; // подписи, оставшиеся после удаления
	
	// формируем массив данных подписи.
	Для Каждого Элемент Из НомераСтрок Цикл
		ДанныеСтроки = ТаблицаПодписей.НайтиПоИдентификатору(Элемент);
		Если НЕ ПустаяСтрока(ДанныеСтроки.Объект) Тогда
			
			// по GUID объекта + номер строки - удаляем из массива всех те, что надо удалить.
			ПодписьУдалена = Ложь;
			Для Каждого СтрокаУдаленнойПодписи Из УдаляемыеПодписи Цикл
				Если СтрокаУдаленнойПодписи.ОбъектСсылка = ДанныеСтроки.ОбъектИд 
					И СтрокаУдаленнойПодписи.НомерСтроки = ДанныеСтроки.НомерСтроки Тогда
					ПодписьУдалена = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если НЕ ПодписьУдалена Тогда 
				
				ПодписьДвоичныеДанные = ПолучитьИзВременногоХранилища(ДанныеСтроки.АдресПодписи);
				Если ЗначениеЗаполнено(ДанныеСтроки.АдресСертификата) Тогда
					ДвоичныеДанныеСертификата = ПолучитьИзВременногоХранилища(ДанныеСтроки.АдресСертификата);
				Иначе
					ДвоичныеДанныеСертификата = Неопределено;
				КонецЕсли;
				
				ДанныеПодписи = Новый Структура;
				ДанныеПодписи.Вставить("ОбъектСсылка", ДанныеСтроки.ОбъектИд);
				ДанныеПодписи.Вставить("НоваяПодписьДвоичныеДанные", ПодписьДвоичныеДанные);
				ДанныеПодписи.Вставить("Отпечаток", ДанныеСтроки.Отпечаток);
				ДанныеПодписи.Вставить("ДатаПодписи", ДанныеСтроки.ДатаПодписи);
				ДанныеПодписи.Вставить("Комментарий", ДанныеСтроки.Комментарий);
				ДанныеПодписи.Вставить("ИмяФайлаПодписи", ДанныеСтроки.ИмяФайлаПодписи);
				ДанныеПодписи.Вставить("КомуВыданСертификат", ДанныеСтроки.КомуВыданСертификат);
				ДанныеПодписи.Вставить("ДвоичныеДанныеСертификата", ДвоичныеДанныеСертификата);
				ДанныеПодписи.Вставить("ОбъектТип", ДанныеСтроки.ОбъектТип);
				ДанныеПодписи.Вставить("УстановившийПодпись", ДанныеСтроки.УстановившийПодпись);
				ДанныеПодписи.Вставить("УстановившийПодписьИд", ДанныеСтроки.УстановившийПодписьИд);
				
				МассивДанныхПодписей.Добавить(ДанныеПодписи);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Соответствие = Новый Соответствие;
	
	// Из массива удаленных строим массив объектов, где изменились подписи - в том числе тех, где не осталось ни одной подписи.
	Для Каждого СтрокаУдаленнойПодписи Из УдаляемыеПодписи Цикл
		Ид = СтрокаУдаленнойПодписи.ОбъектСсылка;
		ДанныеВладельца = Соответствие.Получить(Ид);
		Если ДанныеВладельца = Неопределено Тогда
			ДанныеВладельца = Новый Структура("Тип, МассивПодписей", СтрокаУдаленнойПодписи.ОбъектТип, Новый Массив);
			Соответствие.Вставить(Ид, ДанныеВладельца);
		КонецЕсли;
	КонецЦикла;
	
	// Распределяем массив всех оставшихся подписей по объектам.
	Для Каждого Подпись Из МассивДанныхПодписей Цикл
		Ид = Подпись.ОбъектСсылка;
		ДанныеВладельца = Соответствие.Получить(Ид);
		Если ДанныеВладельца <> Неопределено Тогда
			ДанныеВладельца.МассивПодписей.Добавить(Подпись);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Соответствие;
	
КонецФункции

//Преобразует 2-уровневое дерево в массив.
//Параметры:
//	ТаблицаПодписей - дерево значений формы, содержащая список подписей
Функция ДанныеПодписейСервер(ТаблицаПодписей) Экспорт
	
	ДанныеСтрок = Новый Массив;
	
	ЭлементыПервогоУровня = ТаблицаПодписей.ПолучитьЭлементы();
	
	Для Каждого СтрокаУровняОдин Из ЭлементыПервогоУровня Цикл
		ЭлементыВторогоУровня = СтрокаУровняОдин.ПолучитьЭлементы();
		
		Для Каждого Строка Из ЭлементыВторогоУровня Цикл
			ДанныеСтрок.Добавить(Строка.ПолучитьИдентификатор());
		КонецЦикла;
	КонецЦикла;
	
	Возврат ДанныеСтрок;
	
КонецФункции

//Получает таблицу значений выделенных строк подписей по выделенным срокам.
//Параметры:
//	ВыделенныеСтроки - массив индексов выделенных строк подписей
//	ТаблицаПодписей - таблица формы, содержащая список подписей
//Возвращает:
//	Таблица значения с номерами строк, типами и идентификаторами подписей 
Функция ВыделенныеПодписи(ВыделенныеСтроки, ТаблицаПодписей) Экспорт
	
	ТаблицаВыделенныеСтроки = Новый ТаблицаЗначений;
	
	ТаблицаВыделенныеСтроки.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
	ТаблицаВыделенныеСтроки.Колонки.Добавить("ОбъектСсылка", Новый ОписаниеТипов("Строка"));
	ТаблицаВыделенныеСтроки.Колонки.Добавить("ОбъектТип", Новый ОписаниеТипов("Строка"));
	
	Для Каждого Элемент Из ВыделенныеСтроки Цикл
		
		ДанныеСтроки = ТаблицаПодписей.НайтиПоИдентификатору(Элемент);
		
		Если НЕ ПустаяСтрока(ДанныеСтроки.Объект) Тогда
			
			НоваяСтрока = ТаблицаВыделенныеСтроки.Добавить();
			НоваяСтрока.НомерСтроки = ДанныеСтроки.НомерСтроки;
			НоваяСтрока.ОбъектСсылка = ДанныеСтроки.ОбъектИд;
			НоваяСтрока.ОбъектТип = ДанныеСтроки.ОбъектТип;
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицаВыделенныеСтроки;
	
КонецФункции

//Возвращает массив подписей файла.
//Параметры:
//	ИдентификаторФайла - идентификатор объекта Документооборота
//	ТаблицаПодписей - таблица подписей объекта
//Возвращает:
//	Массив структур существующих подписей объекта
Функция ДанныеПодписейФайла(ИдентификаторФайла, ТаблицаПодписей) Экспорт
	
	// получаем массив всех подписей.
	НомераСтрок = ДанныеПодписейСервер(ТаблицаПодписей);
	
	МассивДанныхПодписей = Новый Массив; // подписи, оставшиеся после удаления
	
	// формируем массив данных подписи.
	Для Каждого Элемент Из НомераСтрок Цикл
		
		ДанныеСтроки = ТаблицаПодписей.НайтиПоИдентификатору(Элемент);
		
		Если НЕ ПустаяСтрока(ДанныеСтроки.Объект) Тогда
			Если ДанныеСтроки.ОбъектИд = ИдентификаторФайла Тогда 
				
				ПодписьДвоичныеДанные = ПолучитьИзВременногоХранилища(ДанныеСтроки.АдресПодписи);
				Если ЗначениеЗаполнено(ДанныеСтроки.АдресСертификата) Тогда
					ДвоичныеДанныеСертификата = ПолучитьИзВременногоХранилища(ДанныеСтроки.АдресСертификата);
				Иначе
					ДвоичныеДанныеСертификата = Неопределено;
				КонецЕсли;
				
				ДанныеПодписи = Новый Структура;
				ДанныеПодписи.Вставить("ОбъектСсылка", ДанныеСтроки.ОбъектИд);
				ДанныеПодписи.Вставить("НоваяПодписьДвоичныеДанные", ПодписьДвоичныеДанные);
				ДанныеПодписи.Вставить("Отпечаток", ДанныеСтроки.Отпечаток);
				ДанныеПодписи.Вставить("ДатаПодписи", ДанныеСтроки.ДатаПодписи);
				ДанныеПодписи.Вставить("Комментарий", ДанныеСтроки.Комментарий);
				ДанныеПодписи.Вставить("ИмяФайлаПодписи", ДанныеСтроки.ИмяФайлаПодписи);
				ДанныеПодписи.Вставить("КомуВыданСертификат", ДанныеСтроки.КомуВыданСертификат);
				ДанныеПодписи.Вставить("ДвоичныеДанныеСертификата", ДвоичныеДанныеСертификата);
				ДанныеПодписи.Вставить("ОбъектТип", ДанныеСтроки.ОбъектТип);
				ДанныеПодписи.Вставить("УстановившийПодпись", ДанныеСтроки.УстановившийПодпись);
				ДанныеПодписи.Вставить("УстановившийПодписьИд", ДанныеСтроки.УстановившийПодписьИд);
				
				МассивДанныхПодписей.Добавить(ДанныеПодписи);
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат МассивДанныхПодписей;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Дополняет набор колонок условий выбора DMObjectListQuery.query колонками для отдельных типов объектов.
//
Процедура ДополнитьНаборКолонокУсловийВыбора(НаборКолонок, ТипXDTO)
	
	Если ТипXDTO = "DMOrganization" Тогда
		// НДС и статьи ДДС.
		Если ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("2.1.8.1.CORP") Тогда
			НаборКолонок.Добавить("VATpayer"); 
		КонецЕсли;
	ИначеЕсли ТипXDTO = "DMProduct" Тогда
		НаборКолонок.Добавить("VATRate"); 
		НаборКолонок.Добавить("measurementUnit"); 
		НаборКолонок.Добавить("price"); 
	КонецЕсли;
	
КонецПроцедуры

// Возвращает хеш-сумму данных по алгоритму CRC32.
//
// Параметры:
//   Данные - Строка, ДвоичныеДанные - данные для расчета.
//
// Возвращаемое значение:
//   Число - хеш-сумма, рассчитанная по алгоритму CRC32.
//
Функция ХешСуммаCRC32(Данные)

	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.CRC32);
	ХешированиеДанных.Добавить(Данные);
	
	Возврат ХешированиеДанных.ХешСумма;

КонецФункции

// Возвращает структуру Вида документа ДО для заполнения по переданному имени типа.
//
// Параметры:
//   ТипОбъектаДО - Строка - тип объекта: DMInternalDocumentType или DMIncomingDocumentType или DMOutgoingDocumentType.
//
// Возвращаемое значение:
//   Структура - реквизиты объекта указанного типа, подлежащие заполнению.
//
Функция СтруктураРеквизитовЗаполняемогоВидаДокументовДО(ТипОбъектаДО) Экспорт
	
	СтруктураРеквизитов = Новый Структура;
	
	// Реквизиты, общие для всех типов.
	СтруктураРеквизитов.Вставить("Тип", ТипОбъектаДО);
	СтруктураРеквизитов.Вставить("Имя");
	СтруктураРеквизитов.Вставить("ИспользоватьСрокИсполнения");
	СтруктураРеквизитов.Вставить("УчитыватьСуммуДокумента");
	СтруктураРеквизитов.Вставить("АвтоНумерация");
	СтруктураРеквизитов.Вставить("ЗапретитьСозданиеДокументовНеПоШаблону");
	
	Если ТипОбъектаДО = "DMInternalDocumentType" Тогда
		СтруктураРеквизитов.Вставить("ВестиУчетПоАдресатам");
		СтруктураРеквизитов.Вставить("ВестиУчетПоСтатьямДДС");
		СтруктураРеквизитов.Вставить("ВестиУчетПоКонтрагентам");
		СтруктураРеквизитов.Вставить("УчитыватьСрокДействия");
		СтруктураРеквизитов.Вставить("ВестиУчетПоОрганизациям");
		СтруктураРеквизитов.Вставить("ВестиУчетТоваровИУслуг");
		СтруктураРеквизитов.Вставить("ВариантПодписания");
		СтруктураРеквизитов.Вставить("ВариантПодписанияID");
		СтруктураРеквизитов.Вставить("ВариантПодписанияТип");
		СтруктураРеквизитов.Вставить("ИспользоватьУтверждение");
		СтруктураРеквизитов.Вставить("ИспользоватьПодписание");
	ИначеЕсли ТипОбъектаДО = "DMIncomingDocumentType" Тогда
		
	ИначеЕсли ТипОбъектаДО = "DMOutgoingDocumentType" Тогда
		СтруктураРеквизитов.Вставить("УчитыватьВходящийНомерИДатуПолучателя");
	Иначе
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Тип ""%1"" не относится к видам документов ДО'"),
			ТипОбъектаДО);
	КонецЕсли;
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

// Возвращает структуру Папки внутренних документов ДО для заполнения.
//
// Возвращаемое значение:
//   Структура - реквизиты объекта указанного типа, подлежащие заполнению.
//
Функция СтруктураРеквизитовЗаполняемойПапкиВнутреннихДокументов() Экспорт
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("Имя");
	СтруктураРеквизитов.Вставить("Описание");
	СтруктураРеквизитов.Вставить("Ответственный");
	СтруктураРеквизитов.Вставить("ОтветственныйID");
	СтруктураРеквизитов.Вставить("ОтветственныйТип");
	СтруктураРеквизитов.Вставить("ДатаСоздания");
	СтруктураРеквизитов.Вставить("Родитель");
	СтруктураРеквизитов.Вставить("РодительID");
	СтруктураРеквизитов.Вставить("РодительТип");
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

#КонецОбласти