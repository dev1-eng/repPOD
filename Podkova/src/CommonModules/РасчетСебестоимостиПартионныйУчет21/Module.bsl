
#Область СлужебныеПроцедурыИФункции

#Область ПереходНаПартионныйУчет22

// При необходимости заполним служебный реквизит РасчетПартий:
// если расчет текущего периода ранее был выполнен в партионном учете версии 2.1, то этот реквизит не заполнялся.
//
Процедура ЗаполнитьРеквизитРасчетПартийВДвижениях(ПараметрыРасчета) Экспорт
	
	Если Не ПроверитьНеобходимостьЗаполненияРеквизитаРасчетПартий(ПараметрыРасчета) Тогда
		Возврат;
	КонецЕсли;
	
		
	// ПартииПрочихРасходов
	ОписаниеРегистра    = ПараметрыРасчета.Движения[Метаданные.РегистрыНакопления.ПартииПрочихРасходов.Имя];
	ТекстРасчетПартий   =
		"ВЫБОР КОГДА Т.РасчетПартий ТОГДА ИСТИНА
		|	КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	 И ЕСТЬNULL(СтатьиРасходов.ВариантРаспределенияРасходовУпр, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|		ТОГДА ИСТИНА
		|	КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	 И ЕСТЬNULL(СтатьиРасходов.ВариантРаспределенияРасходовРегл, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|		ТОГДА ИСТИНА
		|	КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 
		|	 И Т.АналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
		|	 И НЕ СтатьиРасходов.Ссылка ЕСТЬ NULL
		|		ТОГДА ИСТИНА
		|	ИНАЧЕ ЛОЖЬ
		|КОНЕЦ";
	ТекстСоединения     =
		"ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
		|	ПО Т.СтатьяРасходов = СтатьиРасходов.Ссылка";
	ТекстУпорядочивание = "Т.Подразделение, Т.СтатьяРасходов, Т.АналитикаРасходов";
	ЗаполнитьРеквизитРасчетПартий(ПараметрыРасчета, ОписаниеРегистра, ТекстРасчетПартий, ТекстСоединения, ТекстУпорядочивание);
	
	// СебестоимостьТоваров
	ОписаниеРегистра    = ПараметрыРасчета.Движения[Метаданные.РегистрыНакопления.СебестоимостьТоваров.Имя];
	ТекстРасчетПартий   =
		"ВЫБОР КОГДА Т.РасчетПартий ТОГДА ИСТИНА
		|	КОГДА НЕ Т.РасчетСебестоимости
		|		И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ПустаяСсылка)
		// Прочие расходы на себестоимость товаров
		|		И ((Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			И Т.СтатьяРасходовСписания <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
		|			И Т.СтатьяРасходовСписания <> НЕОПРЕДЕЛЕНО
		|			И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
		|			И (Т.Количество = 0 И Т.Стоимость = 0 И Т.СтоимостьБезНДС = 0)
		|			И (Т.ДопРасходы <> 0 ИЛИ Т.ДопРасходыБезНДС <> 0))
		|		 )
		|	ТОГДА ИСТИНА
		|	ИНАЧЕ ЛОЖЬ
		|КОНЕЦ";
	ТекстСоединения     = "";
	ТекстУпорядочивание = "Т.АналитикаУчетаНоменклатуры, Т.ВидЗапасов";
	ЗаполнитьРеквизитРасчетПартий(ПараметрыРасчета, ОписаниеРегистра, ТекстРасчетПартий, ТекстСоединения, ТекстУпорядочивание);
	
	// ДвиженияНоменклатураДоходыРасходы
	ОписаниеРегистра    = ПараметрыРасчета.Движения[Метаданные.РегистрыНакопления.ДвиженияНоменклатураДоходыРасходы.Имя];
	ТекстРасчетПартий   =
		"ВЫБОР КОГДА Т.РасчетПартий ТОГДА ИСТИНА
		|	КОГДА НЕ Т.РасчетСебестоимости
		|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовНаСебестоимость)
		|		И Т.ДокументДвижения <> НЕОПРЕДЕЛЕНО
		|		И Т.Количество = 0
		|	ТОГДА ИСТИНА
		|	ИНАЧЕ ЛОЖЬ
		|КОНЕЦ";
	ТекстСоединения     = "";
	ТекстУпорядочивание = "Т.АналитикаУчетаНоменклатуры, Т.ВидЗапасов";
	ЗаполнитьРеквизитРасчетПартий(ПараметрыРасчета, ОписаниеРегистра, ТекстРасчетПартий, ТекстСоединения, ТекстУпорядочивание);
	
КонецПроцедуры

// Проверяет, был ли выполнен расчет текущего периода в партионном учете версии 2.2.
// Если был выполнен, то значит выполнялись процедуры
//	- ОчиститьНеиспользуемыеДанныеПартионногоУчетаВерсии21()
//	- ИсправитьПроблемыВДвиженияхИДокументах() (в части корректировки полей партионного учета версии 2.2)
// и в регистрах нет устаревших и некорректных движений.
//
Функция ПроверитьНеобходимостьЗаполненияРеквизитаРасчетПартий(ПараметрыРасчета)
	
	// Проверим наличие движений по партионным регистрам версии 2.1
	ШаблонТекстаЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	""%1"" КАК РегистрСоСтарымиДвижениями
	|ИЗ
	|	%1 КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И НЕ Т.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
	|";
	
	ТекстЗапроса = "";
	
	Для Каждого МетаданныеРегистра Из НеиспользуемыеДанныеМеханизмаВерсии21() Цикл
		ТекстЗапроса = ТекстЗапроса
			+ ?(ТекстЗапроса = "", "", ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении())
			+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекстаЗапроса, МетаданныеРегистра.Ключ.ПолноеИмя());
	КонецЦикла;
	
	// Проверим наличие первичных движений себестоимости с незаполненным типом записи
	ТекстЗапроса = ТекстЗапроса
	+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	""%1"" КАК РегистрСоСтарымиДвижениями
	|ИЗ
	|	%1 КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И НЕ Т.РасчетСебестоимости
	|	И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ПустаяСсылка)
	|	И НЕ Т.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|";
	
	ТекстЗапроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗапроса,
		Метаданные.РегистрыНакопления.СебестоимостьТоваров.ПолноеИмя());
	
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	Запрос.Текст = ТекстЗапроса;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат НЕ РезультатЗапроса.Пустой(); // есть "старые" движения - расчет в версии 2.2 еще не выполнялся
	
КонецФункции

// Заполняет реквизит РасчетПартий, если период был рассчитан в партионном учете версии 2.1.
//
Процедура ЗаполнитьРеквизитРасчетПартий(ПараметрыРасчета, ОписаниеРегистра,
			ТекстРасчетПартий, ТекстСоединения, ТекстУпорядочивание) Экспорт
	
	// В регистре могут быть движения с некорректно незаполненным полем РасчетПартий.
	// Такие движения будут, если месяц был рассчитан ранее в партионном учете версии 2.1.
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "ЗаполнитьРеквизитРасчетПартий" + ОписаниеРегистра.ИмяРегистра);
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	// Получим перечень регистраторов с некорректными движениями
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	%1 КАК Т
	|		%2
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И НЕ Т.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|	И %3 <> Т.РасчетПартий";
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Запрос.Текст,
	    ОписаниеРегистра.ПолноеИмяРегистра,
		ТекстСоединения,
		ТекстРасчетПартий); 
		
	РасчетСебестоимостиПротоколРасчета.НачалоФормированиеВременнойТаблицы(ПараметрыРасчета, "ВТРегистраторыСНекорректнымиДвижениями");
	Запрос.Выполнить();
	РасчетСебестоимостиПротоколРасчета.ОкончаниеФормированиеВременнойТаблицы(ПараметрыРасчета, Истина);
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, "ВТРегистраторыСНекорректнымиДвижениями") > 0 Тогда
		
		// Выберем движения, правильно заполним служебный реквизит и перезапишем эти движения в ИБ.
		Запрос.Текст =
		"ВЫБРАТЬ
		|	%1
		|ИЗ
		|	%2 КАК Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистраторыСНекорректнымиДвижениями КАК Регистраторы
		|		ПО Т.Регистратор = Регистраторы.Регистратор
		|		%3
		|
		|УПОРЯДОЧИТЬ ПО
		|	%4";
		
		// Подставим шаблоны в текст запроса
		ПоляОсновнойТаблицыРегистра = СтрЗаменить(ОписаниеРегистра.ПоляОсновнойТаблицыРегистра, "%1", "Т.");
		ПоляОсновнойТаблицыРегистра = СтрЗаменить(ПоляОсновнойТаблицыРегистра, "Т.РасчетПартий", ТекстРасчетПартий + " КАК РасчетПартий");
		
		ПоляУпорядочивания = "Т.Регистратор, Т.РасчетПартий, Т.Период"
			+ ?(ОписаниеРегистра.ЕстьОрганизация, 		  ", Т.Организация", "")
			+ ?(ОписаниеРегистра.ЕстьАналитикаПартнеров,  ", Т.АналитикаУчетаПоПартнерам", "")
			+ ?(ОписаниеРегистра.ЕстьСвойствоВидДвижения, ", Т.ВидДвижения", "")
			+ ?(ПустаяСтрока(ТекстУпорядочивание), "", ", ") + ТекстУпорядочивание;
		
		Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Запрос.Текст,
			ПоляОсновнойТаблицыРегистра,
			ОписаниеРегистра.ПолноеИмяРегистра,
			ТекстСоединения,
			ПоляУпорядочивания); 
			
		// Перезапишем движения с правильно заполненным служебным реквизитом
		ЗаписатьДвиженияПоРегистру(
			ПараметрыРасчета,
			Запрос,
			ОписаниеРегистра.МенеджерРегистра);
		
		ОписаниеРегистра.НадоОбновитьРасчетныйКэш = Истина;
		
	КонецЕсли;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета, "ВТРегистраторыСНекорректнымиДвижениями");
	
КонецПроцедуры


// При расчете в партионном учете версии 2.2 очищает начальные остатки и движения партионного учета версии 2.1.
//
Процедура ОчиститьНеиспользуемыеДанныеПартионногоУчетаВерсии21(ПараметрыРасчета) Экспорт
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "ОчиститьНеиспользуемыеДанныеПартионногоУчетаВерсии21");
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);

#Область ОчисткаОстатков21

	// Очистим остатки партионных регистров версии 2.1, которые больше не используются в партионном учете версии 2.2.
	Для Каждого КлючИЗначение Из НеиспользуемыеДанныеМеханизмаВерсии21() Цикл
		Запрос.Текст = ТекстЗапросаОчисткаОстатковРегистраПУВерсии21(КлючИЗначение);
		ОчиститьОстаткиРегистраПУВерсии21(ПараметрыРасчета, Запрос, КлючИЗначение.Ключ.Имя);
	КонецЦикла;
	
	
#КонецОбласти

#Область ОчисткаДвижений21

	// Очистим движения партионных регистров версии 2.1, которые больше не используются в партионном учете версии 2.2.
	ШаблонТекстаЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор
	|ИЗ
	|	РегистрНакопления.%1 КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И ((&НачалоПериода = &ДатаПереходаНаПартионныйУчетВерсии22
	|		И НЕ Т.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров)
	|	  ИЛИ (&НачалоПериода > &ДатаПереходаНаПартионныйУчетВерсии22))";
	
	Для Каждого КлючИЗначение Из НеиспользуемыеДанныеМеханизмаВерсии21() Цикл
		
		ИмяРегистра = КлючИЗначение.Ключ.Имя;
		
		Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекстаЗапроса, ИмяРегистра);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Количество() > 0 Тогда
			
			// Очистим движения
			ЗаписатьДвиженияПоРегистру(
				ПараметрыРасчета,
				Выборка,
				РегистрыНакопления[ИмяРегистра]);
			
			РасчетСебестоимостиПротоколРасчета.ДополнительнаяИнформация(
				ПараметрыРасчета,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Очищено устаревших движений по регистру %1: %2'"),
					ИмяРегистра,
					РасчетСебестоимостиПротоколРасчета.ПредставлениеЗначения(Выборка.Количество())));
			
		КонецЕсли;
		
	КонецЦикла;
	
#КонецОбласти

КонецПроцедуры

// Формирует текст запроса получения остатков регистра,
// которые необходимо закрыть при переходе на партионный учет версии 2.2
//
Функция ТекстЗапросаОчисткаОстатковРегистраПУВерсии21(ОписаниеРегистра, ТекстОтборОстатков = "ИСТИНА")
	
	ШаблонТекстаЗапроса =
	"ВЫБРАТЬ
	|	ДокументыВводаОстатков.Ссылка КАК Регистратор,
	|	%2,
	|	%5
	|ПОМЕСТИТЬ ВТОстаткиПартий
	|ИЗ
	|	РегистрНакопления.%1.Остатки(&ГраницаКонецПредыдущегоПериода, Организация В (&МассивОрганизаций) И %7) КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДокументыРасчетаСебестоимости КАК ДокументыВводаОстатков
	|		ПО Т.Организация = ДокументыВводаОстатков.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор
	|ПОМЕСТИТЬ ВТИзменившиесяДокументы
	|ИЗ
	|	(ВЫБРАТЬ
	|		Т.Регистратор КАК Регистратор,
	|		%2,
	|		%3
	|	ИЗ
	|		ВТОстаткиПартий КАК Т
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Т.Регистратор,
	|		%2,
	|		%4
	|	ИЗ
	|		РегистрНакопления.%1 КАК Т
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыРасчетаСебестоимости КАК ДокументыВводаОстатков
	|			ПО Т.Регистратор = ДокументыВводаОстатков.Ссылка
	|	ГДЕ
	|		ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.РасчетСебестоимостиТоваров)) КАК Т
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Регистратор,
	|	%2
	|
	|ИМЕЮЩИЕ
	|	%6
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&НачалоПериода КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	Т.Регистратор КАК Регистратор,
	|	%2,
	|	%3
	|ИЗ
	|	ВТОстаткиПартий КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИзменившиесяДокументы КАК ВТИзменившиесяДокументы
	|		ПО Т.Регистратор = ВТИзменившиесяДокументы.Регистратор
	|
	|УПОРЯДОЧИТЬ ПО
	|	Т.Регистратор,
	|	%2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТИзменившиесяДокументы.Регистратор КАК Регистратор
	|ИЗ
	|	ВТИзменившиесяДокументы КАК ВТИзменившиесяДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстаткиПартий КАК ОстаткиПартий
	|		ПО ОстаткиПартий.Регистратор = ВТИзменившиесяДокументы.Регистратор
	|
	|ГДЕ
	|	ОстаткиПартий.Регистратор ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТИзменившиесяДокументы.Регистратор
	|";
	
	// Сформируем текст запроса для выборки остатков
	МетаданныеРегистра = ОписаниеРегистра.Ключ;
	ИмяРегистра = МетаданныеРегистра.Имя; // %1
	
	ТекстИзмеренияРегистра = "";
	ТекстРесурсыРегистра = "";
	ТекстРесурсыРегистраСМинусом = "";
	ТекстОстаткиРесурсовРегистра = "";
	ТекстОтборРесурсыРегистра = "";
	
	Для Каждого ПолеРегистра Из МетаданныеРегистра.Измерения Цикл
		ТекстИзмеренияРегистра = ТекстИзмеренияРегистра + ?(ТекстИзмеренияРегистра = "", "", ",
			|	") + "Т." + ПолеРегистра.Имя;
	КонецЦикла;
	
	Для Каждого ПолеРегистра Из МетаданныеРегистра.Ресурсы Цикл
		ТекстРесурсыРегистра = ТекстРесурсыРегистра + ?(ТекстРесурсыРегистра = "", "", ",
			|	") + "Т." + ПолеРегистра.Имя;
		ТекстРесурсыРегистраСМинусом = ТекстРесурсыРегистраСМинусом + ?(ТекстРесурсыРегистраСМинусом = "", "", ",
			|	") + "-Т." + ПолеРегистра.Имя;
		ТекстОстаткиРесурсовРегистра = ТекстОстаткиРесурсовРегистра + ?(ТекстОстаткиРесурсовРегистра = "", "", ",
			|	") + "Т." + ПолеРегистра.Имя + "Остаток КАК " + ПолеРегистра.Имя;
		ТекстОтборРесурсыРегистра = ТекстОтборРесурсыРегистра + ?(ТекстОтборРесурсыРегистра = "", "", "
			|	ИЛИ ") + "СУММА(Т." + ПолеРегистра.Имя + ") <> 0";
	КонецЦикла;
	
	ТекстЗапроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонТекстаЗапроса,
		ИмяРегистра, // 1
		ТекстИзмеренияРегистра, // 2
		ТекстРесурсыРегистра, // 3
		ТекстРесурсыРегистраСМинусом, // 4
		ТекстОстаткиРесурсовРегистра, // 5
		ТекстОтборРесурсыРегистра, // 6
		ТекстОтборОстатков); // 7
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Получает данные движений по очистке остатков и выполняет их запись
//
Процедура ОчиститьОстаткиРегистраПУВерсии21(ПараметрыРасчета, Запрос, ИмяРегистра)
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВГраница = РезультатЗапроса.ВГраница();
	ДвиженияПоРегистру		= РезультатЗапроса[ВГраница - 1];
	РегистраторыКОчистке	= РезультатЗапроса[ВГраница];
	
	Если НЕ ДвиженияПоРегистру.Пустой() Тогда
		
		// Очистим остатки регистра
		Выборка = ДвиженияПоРегистру.Выбрать();
		
		ЗаписатьДвиженияПоРегистру(
			ПараметрыРасчета,
			Выборка,
			РегистрыНакопления[ИмяРегистра]);
		
		РасчетСебестоимостиПротоколРасчета.ДополнительнаяИнформация(
			ПараметрыРасчета,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Сформировано движений сторнирования остатков по регистру %1: %2'"),
				ИмяРегистра,
				РасчетСебестоимостиПротоколРасчета.ПредставлениеЗначения(Выборка.Количество())));
		
	ИначеЕсли Не РегистраторыКОчистке.Пустой() Тогда
		
		// Очистим остатки регистра
		Выборка = РегистраторыКОчистке.Выбрать();
		
		ЗаписатьДвиженияПоРегистру(
			ПараметрыРасчета,
			Выборка,
			РегистрыНакопления[ИмяРегистра]);
		
		РасчетСебестоимостиПротоколРасчета.ДополнительнаяИнформация(
			ПараметрыРасчета,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Очищено движений сторнирования остатков по регистру %1: %2'"),
				ИмяРегистра,
				РасчетСебестоимостиПротоколРасчета.ПредставлениеЗначения(Выборка.Количество())));
		
	КонецЕсли;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета,
		"ВТОстаткиПартий, ВТИзменившиесяДокументы");
	
КонецПроцедуры

// Выполняет проверку возможности понижения версии партионного учета с 2.2 до 2.1.
//
// Параметры:
//	ЕстьДвиженияПУ22 - Булево - признак наличия в ИБ движений, сформированных партионным учетом версии 2.2
//
// Возвращаемое значение:
//	Строка - Текст предупреждения.
//
Функция ПартионныйУчетВерсии22НельзяПонизитьДоВерсии21(ЕстьДвиженияПУ22) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЕстьДвиженияПУ22", ЕстьДвиженияПУ22);
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	1 КАК КодПричины
	|ГДЕ
	|	&ЕстьДвиженияПУ22 = ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3 КАК КодПричины
	|ИЗ
	|	(ВЫБРАТЬ
	|		0 КАК ЧислоЗаписей
	|
	|	 ОБЪЕДИНИТЬ ВСЕ
	|
	|	 ВЫБРАТЬ ПЕРВЫЕ 1
	|		1 КАК ЧислоЗаписей
	|	 ИЗ
	|		РегистрНакопления.ПартииТоваровОрганизаций КАК Т
	|	) КАК Т
	|ИМЕЮЩИЕ
	|	СУММА(Т.ЧислоЗаписей) = 0
	|
	|
	|УПОРЯДОЧИТЬ ПО
	|	КодПричины
	|";
	
	ТекстПредупреждения = "";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.КодПричины = 1 Тогда
			ТекстПредупреждения = ТекстПредупреждения + "
				|    - " + НСтр("ru='есть движения по себестоимости товаров, сформированные партионным учетом версии 2.2'");
		ИначеЕсли Выборка.КодПричины = 3 Тогда
			ТекстПредупреждения = ТекстПредупреждения + "
				|    - " + НСтр("ru='в данной информационной базе партионный учет версии 2.1 не использовался'");
		Иначе
			ВызватьИсключение НСтр("ru='Неизвестный код причины.'");
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда
		ТекстПредупреждения = НСтр("ru='Невозможен возврат к партионному учету версии 2.1:'")
			+ ТекстПредупреждения;;
	КонецЕсли;
	
	Возврат ТекстПредупреждения;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыЭтапа_ЗаполнениеПартийВРегистреСебестоимостьТоваров

Функция ТекстЗапросаДляПартийТоваров() Экспорт
	
	ТекстЗапроса = ""
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстРеализацииПрошлыхМесяцев_ПУ21() // использует ПрошлыеРеализации
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстНачальныеОстаткиДляКорректировкиПриобретения_ПУ21() 
		+ "";
	Возврат ТекстЗапроса;	
	
КонецФункции

Функция ТекстРеализацииПрошлыхМесяцев_ПУ21() // использует ПрошлыеРеализации 
	Возврат 
		"ВЫБРАТЬ
		|	""ТекстРеализацииПрошлыхМесяцев_ПУ21"" 	  		 КАК ЗапросИсточник,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Прошлое) КАК ТипЗаписи,
		|	ЛОЖЬ											 КАК ЭтоТочноРасчетноеДвижение,
		|	ЛОЖЬ											 КАК Сторно,
		|	0 										  		 КАК Приоритет,
		|	0								  				 КАК Знаменатель,
		|	ИСТИНА 											 КАК РасчетЗавершен,
		|	ДД.АналитикаУчетаНоменклатуры.Номенклатура 		 КАК Номенклатура,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС 		 КАК НалогообложениеПартии,
		|
		|	ДД.Период,
		|	ДД.Период КАК ПериодПоступления,
		|	ДД.Регистратор,
		|	ДД.ВидДвижения,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.РазделУчета,
		|	(ВЫБОР
		|		КОГДА ДД.ВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
		|			ТОГДА Прошлые.ВидЗапасов
		|		ИНАЧЕ ДД.ВидЗапасов КОНЕЦ) КАК ВидЗапасов,
		|	ДД.Организация,
		|	Партии.ДокументПоступления КАК Партия,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	Партии.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаФинансовогоУчета,
		|	(ВЫБОР
		|		КОГДА Партии.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|			ТОГДА Партии.АналитикаУчетаПартий.НалогообложениеНДС
		|		ИНАЧЕ Партии.НалогообложениеНДС КОНЕЦ) КАК ВидДеятельностиНДС,
		|
		|	СУММА(Партии.Количество) КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьЗабалансовая,
		|	0 КАК СтоимостьУпр,
		|	0 КАК ДопРасходыУпр,
		|	0 КАК ПостатейныеПеременныеСНДС,
		|	0 КАК ПостатейныеПеременныеБезНДС,
		|	0 КАК ТрудозатратыУпр,
		|	0 КАК ПостатейныеПостоянныеУпр,
		|	0 КАК ПостатейныеПеременныеУпр,
		|	0 КАК ДопРасходы,
		|	0 КАК ДопРасходыБезНДС,
		|	0 КАК Трудозатраты,
		|	0 КАК ПостатейныеПостоянныеСНДС,
		|	0 КАК ПостатейныеПостоянныеБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК СтоимостьЗабалансоваяРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	0 КАК ДопРасходыРегл,
		|	0 КАК ТрудозатратыРегл,
		|	0 КАК ПостатейныеПостоянныеРегл,
		|	0 КАК ПостатейныеПеременныеРегл,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) 				КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО															КАК ПриоритетнаяХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) 		КАК КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) 	КАК КорРазделУчета,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) 							КАК КорВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) 							КАК КорОрганизация,
		|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(31,2)) 											КАК КорСтоимость,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка) 		КАК АналитикаУчетаПоПартнерам,
		|	НЕОПРЕДЕЛЕНО 															КАК ЗаказКлиента,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) 					КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО 															КАК АналитикаРасходов,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) 			КАК СтатьяРасходовСписания,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка) 			КАК СтатьяДоходов,
		|	НЕОПРЕДЕЛЕНО 															КАК АналитикаДоходов,
		|	ДАТАВРЕМЯ(1, 1, 1) 														КАК ПериодПродажи,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка) 	КАК СтатьяАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО 															КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО 															КАК ДокументДвижения,
		|	ВЫРАЗИТЬ("""" КАК СТРОКА(36)) 											КАК ИдентификаторСтроки,
		|	ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка) КАК ГруппаПродукции,
		|	НЕОПРЕДЕЛЕНО 															КАК КорПартия,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) 			КАК КорАналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО 															КАК КорАналитикаФинансовогоУчета,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) 				КАК КорВидДеятельностиНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) 				КАК ВидДеятельностиНДСДокумента,
		|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(31,2)) 											КАК НДСУпр,
		|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(31,2)) 											КАК НДСРегл,
		|	НЕОПРЕДЕЛЕНО															КАК СтатьяКалькуляции,
		|	Прошлые.ДокументИсточник												КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК КорНаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПрошлыеРеализации КАК Прошлые
		|		ПО ДД.Регистратор = Прошлые.ДокументРеализации
		|		И ДД.АналитикаУчетаНоменклатуры = Прошлые.АналитикаУчетаНоменклатуры
		|		И (ДД.ВидЗапасов = Прошлые.ВидЗапасов
		|			ИЛИ ДД.ВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка))
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровОрганизаций КАК Партии
		|		ПО Партии.Регистратор = ДД.Регистратор
		|		И Партии.Период = ДД.Период
		|		И Партии.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|		И (Партии.ВидЗапасов = ДД.ВидЗапасов
		|			ИЛИ ДД.ВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка))
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК АналитикаПартий
		|		ПО АналитикаПартий.Ссылка = Партии.АналитикаУчетаПартий
		|ГДЕ
		|	ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И НЕ ДД.РасчетСебестоимости
		|	И ДД.Организация В (&ОрганизацииСФИФОСкользящая)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.АналитикаУчетаНоменклатуры.Номенклатура,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.ВидДвижения,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.РазделУчета,
		|	(ВЫБОР
		|		КОГДА ДД.ВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
		|			ТОГДА Прошлые.ВидЗапасов
		|		ИНАЧЕ ДД.ВидЗапасов КОНЕЦ),
		|	ДД.Организация,
		|	Партии.ДокументПоступления,
		|	Партии.АналитикаУчетаПартий,
		|	(ВЫБОР
		|		КОГДА Партии.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|			ТОГДА Партии.АналитикаУчетаПартий.НалогообложениеНДС
		|		ИНАЧЕ Партии.НалогообложениеНДС КОНЕЦ),
		|	Прошлые.ДокументИсточник
		|";
КонецФункции

Функция ТекстНачальныеОстаткиДляКорректировкиПриобретения_ПУ21()
	Возврат 
		"ВЫБРАТЬ
		|	""ТекстНачальныеОстаткиДляКорректировкиПриобретения"" КАК ЗапросИсточник,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Прошлое) 	  КАК ТипЗаписи,
		|	ЛОЖЬ												  КАК ЭтоТочноРасчетноеДвижение,
		|	ЛОЖЬ												  КАК Сторно,
		|	0 										  			  КАК Приоритет,
		|	0								  					  КАК Знаменатель,
		|	ИСТИНА 												  КАК РасчетЗавершен,
		|	ДД.АналитикаУчетаНоменклатуры.Номенклатура 			  КАК Номенклатура,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС 			  КАК НалогообложениеПартии,
		|
		|	&КонецПредыдущегоПериода 	 						  КАК Период,
		|	&КонецПредыдущегоПериода 	 						  КАК ПериодПоступления,
		|	ДД.Регистратор			 	 						  КАК Регистратор,
		|	НЕОПРЕДЕЛЕНО 			 	 						  КАК ВидДвижения,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.РазделУчета,
		|	ДД.ВидЗапасов,
		|	ДД.Организация,
		|	(ВЫБОР
		|		КОГДА РасчетСебестоимостиТоваров.МетодОценки = ЗНАЧЕНИЕ(Перечисление.МетодыОценкиСтоимостиТоваров.ФИФОСкользящаяОценка)
		|			ТОГДА Партии.ДокументПоступления
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ) КАК Партия,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	(ВЫБОР
		|		КОГДА РасчетСебестоимостиТоваров.МетодОценки = ЗНАЧЕНИЕ(Перечисление.МетодыОценкиСтоимостиТоваров.ФИФОСкользящаяОценка)
		|			ТОГДА Партии.АналитикаУчетаПартий
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ) КАК АналитикаУчетаПартий,
		|	ВЫБОР
		|		КОГДА Партии.ВидЗапасов ЕСТЬ NULL 
		|			ТОГДА ДД.АналитикаФинансовогоУчета
		|		КОГДА Партии.ВидЗапасов.ГруппаПродукции ЕСТЬ NULL 
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		КОГДА Партии.ВидЗапасов.ГруппаПродукции <> ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
		|			ТОГДА Партии.ВидЗапасов.ГруппаПродукции
		|		КОГДА Партии.ВидЗапасов.УстарелоПредназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляСделки)
		|			ТОГДА Партии.ВидЗапасов.УстарелоСделка
		|		КОГДА Партии.ВидЗапасов.УстарелоПредназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляПодразделения)
		|			ТОГДА Партии.ВидЗапасов.УстарелоПодразделение
		|		КОГДА Партии.ВидЗапасов.УстарелоПредназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляМенеджера)
		|			ТОГДА Партии.ВидЗапасов.УстарелоМенеджер
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК АналитикаФинансовогоУчета,
		|	ВЫБОР
		|		КОГДА Партии.НалогообложениеНДС ЕСТЬ NULL
		|			ТОГДА ДД.ВидДеятельностиНДС
		|		КОГДА Партии.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|			ТОГДА Партии.АналитикаУчетаПартий.НалогообложениеНДС
		|		ИНАЧЕ Партии.НалогообложениеНДС
		|	КОНЕЦ КАК ВидДеятельностиНДС,
		|
		|	ДД.Количество КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьЗабалансовая,
		|	0 КАК СтоимостьУпр,
		|	0 КАК ДопРасходыУпр,
		|	0 КАК ПостатейныеПеременныеСНДС,
		|	0 КАК ПостатейныеПеременныеБезНДС,
		|	0 КАК ТрудозатратыУпр,
		|	0 КАК ПостатейныеПостоянныеУпр,
		|	0 КАК ПостатейныеПеременныеУпр,
		|	0 КАК ДопРасходы,
		|	0 КАК ДопРасходыБезНДС,
		|	0 КАК Трудозатраты,
		|	0 КАК ПостатейныеПостоянныеСНДС,
		|	0 КАК ПостатейныеПостоянныеБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК СтоимостьЗабалансоваяРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	0 КАК ДопРасходыРегл,
		|	0 КАК ТрудозатратыРегл,
		|	0 КАК ПостатейныеПостоянныеРегл,
		|	0 КАК ПостатейныеПеременныеРегл,
		|
		|	ДД.ХозяйственнаяОперация 												КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО															КАК ПриоритетнаяХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) 		КАК КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) 	КАК КорРазделУчета,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) 							КАК КорВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) 							КАК КорОрганизация,
		|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(31,2)) 											КАК КорСтоимость,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка) 		КАК АналитикаУчетаПоПартнерам,
		|	НЕОПРЕДЕЛЕНО 															КАК ЗаказКлиента,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) 					КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО 															КАК АналитикаРасходов,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) 			КАК СтатьяРасходовСписания,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка) 			КАК СтатьяДоходов,
		|	НЕОПРЕДЕЛЕНО 															КАК АналитикаДоходов,
		|	ДАТАВРЕМЯ(1, 1, 1) 														КАК ПериодПродажи,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка) 	КАК СтатьяАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО 															КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО 															КАК ДокументДвижения,
		|	ВЫРАЗИТЬ("""" КАК СТРОКА(36)) 											КАК ИдентификаторСтроки,
		|	ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка) КАК ГруппаПродукции,
		|	НЕОПРЕДЕЛЕНО 															КАК КорПартия,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) 			КАК КорАналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО 															КАК КорАналитикаФинансовогоУчета,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) 				КАК КорВидДеятельностиНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) 				КАК ВидДеятельностиНДСДокумента,
		|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(31,2)) 											КАК НДСУпр,
		|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(31,2)) 											КАК НДСРегл,
		|	НЕОПРЕДЕЛЕНО															КАК СтатьяКалькуляции,
		|	НЕОПРЕДЕЛЕНО 															КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО															КАК КорНаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыПоступленияИмпортаПрошлогоПериода КАК Поступление
		|		ПО Поступление.Ссылка = ДД.Регистратор
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровОрганизаций КАК Партии
		|		ПО Партии.Регистратор = ДД.Регистратор
		|		И Партии.Период = ДД.Период
		|		И Партии.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|		И (Партии.ВидЗапасов = ДД.ВидЗапасов
		|			ИЛИ ДД.ВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка))
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РасчетСебестоимостиТоваров КАК РасчетСебестоимостиТоваров
		|		ПО НАЧАЛОПЕРИОДА(РасчетСебестоимостиТоваров.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ДД.Период, МЕСЯЦ)
		|		И РасчетСебестоимостиТоваров.Организация = ДД.Организация
		|		И НЕ РасчетСебестоимостиТоваров.ПредварительныйРасчет
		|ГДЕ
		|	ДД.Количество <> 0
		|";
КонецФункции

#КонецОбласти

#Область ПроцедурыЭтапа10_РаспределениеДопРасходовМеждуПартиямиИТоварами

Функция ТекстТаможенныеДекларацииИПоступления() Экспорт // вт Декларации, КоличествоДеклараций
	ТекстЗапроса = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Партия КАК Ссылка,
		|	Партии.ДокументПоступления КАК Поступление
		|ПОМЕСТИТЬ
		|	Декларации
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровОрганизаций КАК Партии
		|		ПО Партии.Регистратор = ДД.Партия
		|		И Партии.Организация = ДД.Организация
		|		И Партии.АналитикаУчетаНоменклатуры.Номенклатура = Аналитика.Номенклатура
		|		И Партии.АналитикаУчетаНоменклатуры.Характеристика = Аналитика.Характеристика
		|ГДЕ
		|	ДД.Партия ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
		|	И Партии.Период < &ДатаПереходаНаПартионныйУчетВерсии22
		|	И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|ИНДЕКСИРОВАТЬ ПО
		|	Поступление
		|;
		|ВЫБРАТЬ
		|	ДД.Поступление,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.Ссылка) КАК Количество
		|ПОМЕСТИТЬ
		|	КоличествоДеклараций
		|ИЗ
		|	Декларации КАК ДД
		|СГРУППИРОВАТЬ ПО
		|	ДД.Поступление
		|ИНДЕКСИРОВАТЬ ПО
		|	Поступление
		|";
	Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти

#Область РасчетСебестоимости_ФИФОСкользящаяОценкаВерсии21

Процедура РасчетСебестоимости_ФИФОСкользящаяОценкаВерсии21(ПараметрыРасчета) Экспорт
	
	// Этап 2.1
	// Формирует движения по регистрам:
	// - СебестоимостьТоваров
	// - ПрочиеРасходы
	// - ПрочиеДоходы
	// - ДвиженияНоменклатураДоходыРасходы
	// - ДвиженияНоменклатураНоменклатура
	// - ПрочиеАктивыПассивы
	СкорректироватьСтоимостьСписанияЗапасовПоФИФОСкользящая(ПараметрыРасчета);
	
	// Этап 2.2
	// Формирует движения по регистрам:
	// - ВыручкаИСебестоимостьПродаж
	// - Закупки
	СкорректироватьСтоимостьПродажПоФИФОСкользящая(ПараметрыРасчета);
	
	
	// Этап 2.5
	// Формирует движения по регистрам:
	// - СтоимостьТоваров
	ЗарегистрироватьСтоимостьФИФО(ПараметрыРасчета);
	
	Если ПараметрыРасчета.ФО.ФормироватьФинансовыйРезультат Тогда
		// Этап 4.1
		// Формирует движения по регистрам:
		// - ФинансовыеРезультаты
		РасчетСебестоимостиКорректировкаСтоимости.РаспределитьВыручкуПоНаправлениямДеятельности(ПараметрыРасчета);
	КонецЕсли;
	
	// Этап 4.3
	// Формирует движения по регистрам:
	// - ПрочиеАктивыПассивы
	РасчетСебестоимостиКорректировкаСтоимости.ОтразитьПрибылиИУбытки(ПараметрыРасчета);
	
	Если ПараметрыРасчета.ФО.ФормироватьУправленческийБаланс Тогда
		// Этап 4.4
		// Формирует движения управленческого баланса
		РасчетСебестоимостиКорректировкаСтоимости.ОтразитьВУправленческомБалансе(ПараметрыРасчета);
	КонецЕсли;
	
КонецПроцедуры

// Этап 2.1
Процедура СкорректироватьСтоимостьСписанияЗапасовПоФИФОСкользящая(ПараметрыРасчета)

	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "СкорректироватьСтоимостьСписанияЗапасовПоФИФОСкользящая");
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиКорректировкаСтоимости.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР КОГДА УчетСебестоимости.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента
	|	 ИЛИ УчетСебестоимости.Регистратор ССЫЛКА Документ.КорректировкаРеализации
	|		И УчетСебестоимости.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента) ТОГДА
	|		(ВЫБОР
	|			КОГДА УчетСебестоимости.Количество < 0 ТОГДА 2
	|			КОГДА УчетСебестоимости.ХозяйственнаяОперация =
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиентаПрошлыхПериодов) ТОГДА 2
	|			ИНАЧЕ 3 КОНЕЦ)
	|		ИНАЧЕ 2 КОНЕЦ КАК Порядок,
	|	УчетСебестоимости.Период						КАК Период,
	|	УчетСебестоимости.Регистратор					КАК Регистратор,
	|	УчетСебестоимости.Организация					КАК Организация,
	|	УчетСебестоимости.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	УчетСебестоимости.ВидЗапасов					КАК ВидЗапасов,
	|	ВЫБОР КОГДА Аналитика.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
	|	КОГДА УчетСебестоимости.ВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) ТОГДА
	|		УчетСебестоимости.ВидЗапасов.ТипЗапасов
	|	ИНАЧЕ
	|		 (ВЫБОР УчетСебестоимости.РазделУчета
	|			КОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			КОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар) КОНЕЦ)
	|	КОНЕЦ КАК ТипЗапасов,
	|	ВЫБОР КОГДА УчетСебестоимости.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтчетДавальцу))
	|	ТОГДА
	|		УчетСебестоимости.КорАналитикаУчетаНоменклатуры
	|	КОГДА УчетСебестоимости.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваров),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваровСПереоценкой),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваров),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваровСПереоценкой))
	|	 И НЕ УчетСебестоимости.СлужебноеВидДвиженияПриход
	|	ТОГДА
	|		УчетСебестоимости.КорАналитикаУчетаНоменклатуры
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК АналитикаУчетаПродукции,
	|	УчетСебестоимости.ПериодПродажи					КАК ПериодПродажи,
	|
	|	ВЫБОР
	|		КОГДА УчетСебестоимости.СлужебноеВидДвиженияПриход
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ                   						КАК ВидДвижения,
	|	УчетСебестоимости.РазделУчета					КАК РазделУчета,
	|	УчетСебестоимости.АналитикаУчетаПоПартнерам		КАК АналитикаУчетаПоПартнерам,
	|	УчетСебестоимости.ЗаказКлиента					КАК ЗаказКлиента,
	|	УчетСебестоимости.ХозяйственнаяОперация			КАК ХозяйственнаяОперация,
	|	УчетСебестоимости.КорАналитикаУчетаНоменклатуры	КАК КорАналитикаУчетаНоменклатуры,
	|	УчетСебестоимости.КорРазделУчета				КАК КорРазделУчета,
	|	УчетСебестоимости.КорВидЗапасов					КАК КорВидЗапасов,
	|	КорАналитикаНоменклатуры.МестоХранения			КАК КорСклад,
	|	УчетСебестоимости.КорОрганизация				КАК КорОрганизация,
	|	УчетСебестоимости.Подразделение					КАК Подразделение,
	|	УчетСебестоимости.АналитикаРасходов				КАК АналитикаРасходов,
	|	УчетСебестоимости.СтатьяРасходовСписания		КАК СтатьяРасходовСписания,
	|	УчетСебестоимости.СтатьяРасходовСписания.ВариантРаспределенияРасходовУпр КАК ВариантРаспределенияРасходовУпр,
	|	УчетСебестоимости.СтатьяРасходовСписания.ВариантРаспределенияРасходовРегл КАК ВариантРаспределенияРасходовРегл,
	|	ЕСТЬNULL(УчетСебестоимости.СтатьяРасходовСписания.ПринятиеКНалоговомуУчету, ЛОЖЬ) КАК ПринятиеКНалоговомуУчету,
	|	УчетСебестоимости.АналитикаАктивовПассивов		КАК АналитикаАктивовПассивов,
	|	УчетСебестоимости.СтатьяАктивовПассивов			КАК СтатьяАктивовПассивов,
	|	УчетСебестоимости.СтатьяДоходов					КАК СтатьяДоходов,
	|	УчетСебестоимости.АналитикаДоходов				КАК АналитикаДоходов,
	|	УчетСебестоимости.Регистратор					КАК ДокументДвижения,
	|	УчетСебестоимости.ИдентификаторСтроки			КАК ИдентификаторСтроки,
	|	УчетСебестоимости.ГруппаПродукции				КАК ГруппаПродукции,
	|	ЕСТЬNULL(Назначения.НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
	|	УчетСебестоимости.КорНаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	ВЫБОР КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|		УчетСебестоимости.ВидЗапасов
	|	ИНАЧЕ
	|		Аналитика.Номенклатура
	|	КОНЕЦ КАК ИсточникГФУНоменклатуры,
	|	ВЫБОР КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|		УчетСебестоимости.КорВидЗапасов
	|	ИНАЧЕ
	|		КорАналитикаНоменклатуры.Номенклатура
	|	КОНЕЦ КАК КорИсточникГФУНоменклатуры,
	|	Аналитика.МестоХранения КАК Склад,
	|
	|	ВЫБОР КОГДА КорАналитикаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
	|	КОГДА УчетСебестоимости.ВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) ТОГДА
	|		УчетСебестоимости.ВидЗапасов.ТипЗапасов
	|	ИНАЧЕ
	|		 (ВЫБОР УчетСебестоимости.РазделУчета
	|			КОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			КОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар) КОНЕЦ)
	|	КОНЕЦ КАК КорТипЗапасов,
	|	ВЫБОР КОГДА УчетСебестоимости.ХозяйственнаяОперация В (&МассивОперацийРеализации) ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК Продажа,
	|	ВЫБОР КОГДА УчетСебестоимости.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию))
	|	ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ЭтоПередачаМеждуОрганизациями,
	|	ВЫБОР КОГДА УчетСебестоимости.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента
	|	 ИЛИ УчетСебестоимости.Регистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах И УчетСебестоимости.Количество < 0
	|	 ИЛИ УчетСебестоимости.Регистратор ССЫЛКА Документ.КорректировкаРеализации
	|		И УчетСебестоимости.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента) ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ЭтоВозвратОтКлиента,
	|
	|	СУММА(ВЫБОР КОГДА УчетСебестоимости.Количество < 0 ТОГДА
	|		0 - УчетСебестоимости.Количество
	|	ИНАЧЕ
	|		УчетСебестоимости.Количество
	|	КОНЕЦ) КАК Количество,
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьБезНДС,
	|	0 КАК СтоимостьЗабалансовая,
	|	0 КАК ДопРасходы,
	|	0 КАК ДопРасходыБезНДС,
	|	0 КАК СтоимостьРегл,
	|	0 КАК СтоимостьЗабалансоваяРегл,
	|	0 КАК НДСРегл,
	|	0 КАК НДСУпр,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|
	|	СУММА(
	|		ВЫБОР КОГДА УчетСебестоимости.ХозяйственнаяОперация
	|			= ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиентаПрошлыхПериодов)
	|		ТОГДА
	|			- УчетСебестоимости.Количество
	|		КОГДА УчетСебестоимости.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции)
	|			И УчетСебестоимости.Количество < 0
	|		ТОГДА
	|			- УчетСебестоимости.Количество
	|		ИНАЧЕ
	|			УчетСебестоимости.Количество
	|		КОНЕЦ) КАК ИсходноеКоличество,
	|	СУММА(УчетСебестоимости.КорСтоимость) КАК КорСтоимость,
	|	СУММА(УчетСебестоимости.Стоимость) КАК ИсходнаяСтоимость,
	|	СУММА(УчетСебестоимости.СтоимостьРегл) КАК ИсходнаяСтоимостьРегл,
	|	СУММА(УчетСебестоимости.СтоимостьЗабалансовая) КАК ИсходнаяСтоимостьЗабалансовая,
	|	СУММА(УчетСебестоимости.СтоимостьЗабалансоваяРегл) КАК ИсходнаяСтоимостьЗабалансоваяРегл,
	|	СУММА(УчетСебестоимости.СтоимостьБезНДС) КАК ИсходнаяСтоимостьБезНДС
	|ИЗ
	|	ВТКэшРасчетныеОборотыСебестоимостьТоваров КАК УчетСебестоимости
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		УчетСебестоимости.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.КлючиАналитикиУчетаНоменклатуры КАК КорАналитикаНоменклатуры
	|	ПО
	|		УчетСебестоимости.КорАналитикаУчетаНоменклатуры = КорАналитикаНоменклатуры.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК Назначения
	|		ПО Назначения.Ссылка = Аналитика.Назначение
	|	
	|ГДЕ
	|	(НЕ УчетСебестоимости.СлужебноеВидДвиженияПриход
	|		ИЛИ УчетСебестоимости.ХозяйственнаяОперация 
	|				= ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиентаПрошлыхПериодов)
	|		ИЛИ УчетСебестоимости.ХозяйственнаяОперация 
	|				= ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТарыОтКлиентаПрошлыхПериодов)
	|		ИЛИ УчетСебестоимости.ХозяйственнаяОперация
	|				= ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноСписанияНаРасходы)
	|		ИЛИ УчетСебестоимости.ХозяйственнаяОперация 
	|				= ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров)
	|		ИЛИ УчетСебестоимости.ХозяйственнаяОперация 
	|				= ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазборкаТоваров)
	|		ИЛИ УчетСебестоимости.ХозяйственнаяОперация 
	|				= ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет)
	|		ИЛИ УчетСебестоимости.ХозяйственнаяОперация 
	|				= ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиентуРеглУчет)
	|		ИЛИ (УчетСебестоимости.ХозяйственнаяОперация 
	|				= ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции) И УчетСебестоимости.Количество <> 0)
	|		ИЛИ (УчетСебестоимости.ХозяйственнаяОперация 
	|				= ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение) И УчетСебестоимости.Количество <> 0)
	|		ИЛИ (УчетСебестоимости.ХозяйственнаяОперация 
	|				= ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтчетДавальцу) И УчетСебестоимости.Количество <> 0)
	|		ИЛИ (УчетСебестоимости.ХозяйственнаяОперация 
	|				= ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОтПереработчика) И УчетСебестоимости.Количество <> 0)
	|		ИЛИ (УчетСебестоимости.ХозяйственнаяОперация 
	|				= ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию) И УчетСебестоимости.Количество <> 0)
	|		ИЛИ УчетСебестоимости.Регистратор ССЫЛКА Документ.ВозвратТоваровМеждуОрганизациями
	|		ИЛИ УчетСебестоимости.ХозяйственнаяОперация
	|				= ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеТоваров)
	|		ИЛИ УчетСебестоимости.ХозяйственнаяОперация
	|				= ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваров)
	|		ИЛИ УчетСебестоимости.ХозяйственнаяОперация
	|				= ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваров)
	|	)
	|	
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР КОГДА УчетСебестоимости.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента
	|	 ИЛИ УчетСебестоимости.Регистратор ССЫЛКА Документ.КорректировкаРеализации
	|		И УчетСебестоимости.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента) ТОГДА
	|		(ВЫБОР
	|			КОГДА УчетСебестоимости.Количество < 0 ТОГДА 2
	|			КОГДА УчетСебестоимости.ХозяйственнаяОперация =
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиентаПрошлыхПериодов) ТОГДА 2
	|			ИНАЧЕ 3 КОНЕЦ)
	|		ИНАЧЕ 2 КОНЕЦ,
	|	УчетСебестоимости.СтатьяДоходов,
	|	УчетСебестоимости.КорАналитикаУчетаНоменклатуры,
	|	УчетСебестоимости.АналитикаДоходов,
	|	УчетСебестоимости.СтатьяРасходовСписания,
	|	УчетСебестоимости.СтатьяРасходовСписания.ВариантРаспределенияРасходовУпр,
	|	УчетСебестоимости.СтатьяРасходовСписания.ВариантРаспределенияРасходовРегл,
	|	ЕСТЬNULL(УчетСебестоимости.СтатьяРасходовСписания.ПринятиеКНалоговомуУчету, ЛОЖЬ),
	|	УчетСебестоимости.КорВидЗапасов,
	|	УчетСебестоимости.АналитикаРасходов,
	|	УчетСебестоимости.СтатьяАктивовПассивов,
	|	УчетСебестоимости.АналитикаАктивовПассивов,
	|	УчетСебестоимости.ИдентификаторСтроки,
	|	ВЫБОР КОГДА УчетСебестоимости.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтчетДавальцу))
	|	ТОГДА
	|		УчетСебестоимости.КорАналитикаУчетаНоменклатуры
	|	КОГДА УчетСебестоимости.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваров),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваровСПереоценкой),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваров),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваровСПереоценкой))
	|	 И НЕ УчетСебестоимости.СлужебноеВидДвиженияПриход
	|	ТОГДА
	|		УчетСебестоимости.КорАналитикаУчетаНоменклатуры
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ,
	|	УчетСебестоимости.ПериодПродажи,
	|	УчетСебестоимости.ГруппаПродукции,
	|	ЕСТЬNULL(Назначения.НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
	|	УчетСебестоимости.КорНаправлениеДеятельности,
	|	УчетСебестоимости.КорРазделУчета,
	|	УчетСебестоимости.КорОрганизация,
	|	УчетСебестоимости.Подразделение,
	|	ВЫБОР
	|		КОГДА УчетСебестоимости.СлужебноеВидДвиженияПриход
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ,
	|	УчетСебестоимости.Период,
	|	УчетСебестоимости.ВидЗапасов,
	|	ВЫБОР КОГДА Аналитика.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
	|	КОГДА УчетСебестоимости.ВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) ТОГДА
	|		УчетСебестоимости.ВидЗапасов.ТипЗапасов
	|	ИНАЧЕ
	|		 (ВЫБОР УчетСебестоимости.РазделУчета
	|			КОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			КОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар) КОНЕЦ)
	|	КОНЕЦ,
	|	УчетСебестоимости.ХозяйственнаяОперация,
	|	УчетСебестоимости.РазделУчета,
	|	УчетСебестоимости.АналитикаУчетаНоменклатуры,
	|	УчетСебестоимости.АналитикаУчетаПоПартнерам,
	|	УчетСебестоимости.ЗаказКлиента,
	|	УчетСебестоимости.Организация,
	|	УчетСебестоимости.Регистратор,
	|	ВЫБОР КОГДА КорАналитикаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
	|	КОГДА УчетСебестоимости.ВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) ТОГДА
	|		УчетСебестоимости.ВидЗапасов.ТипЗапасов
	|	ИНАЧЕ
	|		 (ВЫБОР УчетСебестоимости.РазделУчета
	|			КОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			КОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар) КОНЕЦ)
	|	КОНЕЦ,
	|	ВЫБОР КОГДА УчетСебестоимости.ХозяйственнаяОперация В (&МассивОперацийРеализации) ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ,
	|	ВЫБОР КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|		УчетСебестоимости.ВидЗапасов
	|	ИНАЧЕ
	|		Аналитика.Номенклатура
	|	КОНЕЦ,
	|	ВЫБОР КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|		УчетСебестоимости.КорВидЗапасов
	|	ИНАЧЕ
	|		КорАналитикаНоменклатуры.Номенклатура
	|	КОНЕЦ,
	|	Аналитика.МестоХранения,
	|	КорАналитикаНоменклатуры.МестоХранения,
	|	ВЫБОР КОГДА УчетСебестоимости.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции) ТОГДА
	|		УчетСебестоимости.КорАналитикаУчетаНоменклатуры
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР КОГДА УчетСебестоимости.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента
	|	 ИЛИ УчетСебестоимости.Регистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах И УчетСебестоимости.Количество < 0
	|	 ИЛИ УчетСебестоимости.Регистратор ССЫЛКА Документ.КорректировкаРеализации
	|		И УчетСебестоимости.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента) ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1 КАК Порядок,
	|	Партии.Период КАК Период,
	|	Партии.Регистратор КАК Регистратор,
	|	Партии.Организация КАК Организация,
	|	Партии.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Партии.ВидЗапасов КАК ВидЗапасов,
	|	Партии.ТипЗапасов КАК ТипЗапасов,
	|	Партии.АналитикаУчетаПродукции,
	|	Партии.ПериодПродажи,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидДвижения,
	|	НЕОПРЕДЕЛЕНО КАК РазделУчета,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПоПартнерам,
	|	НЕОПРЕДЕЛЕНО КАК ЗаказКлиента,
	|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО КАК КорСклад,
	|	НЕОПРЕДЕЛЕНО КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО КАК ВариантРаспределенияРасходовУпр,
	|	НЕОПРЕДЕЛЕНО КАК ВариантРаспределенияРасходовРегл,
	|	НЕОПРЕДЕЛЕНО КАК ПринятиеКНалоговомуУчету,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяДоходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО КАК ДокументДвижения,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК ИсточникГФУНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК КорИсточникГФУНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК Склад,
	|
	|	НЕОПРЕДЕЛЕНО КАК КорТипЗапасов,
	|	НЕОПРЕДЕЛЕНО КАК Продажа,
	|	НЕОПРЕДЕЛЕНО КАК ЭтоПередачаМеждуОрганизациями,
	|	ВЫБОР КОГДА Партии.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента
	|	 ИЛИ Партии.Регистратор ССЫЛКА Документ.КорректировкаРеализации ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ЭтоВозвратОтКлиента,
	|
	|	СУММА(Партии.Количество) КАК Количество,
	|	СУММА(ВЫБОР
	|		КОГДА Партии.ТипЗапасов В (ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар), ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца))
	|			ТОГДА 0
	|		ИНАЧЕ Партии.Стоимость КОНЕЦ) КАК Стоимость,
	|	СУММА(ВЫБОР
	|		КОГДА Партии.ТипЗапасов В (ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар), ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца))
	|			ТОГДА 0
	|		ИНАЧЕ Партии.СтоимостьБезНДС КОНЕЦ) КАК СтоимостьБезНДС,
	|	СУММА(ВЫБОР
	|		КОГДА Партии.ТипЗапасов В (ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар), ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца))
	|			ТОГДА Партии.Стоимость
	|		ИНАЧЕ 0 КОНЕЦ) КАК СтоимостьЗабалансовая,
	|	СУММА(Партии.ДопРасходы) КАК ДопРасходы,
	|	СУММА(Партии.ДопРасходыБезНДС) КАК ДопРасходыБезНДС,
	|	СУММА(ВЫБОР
	|		КОГДА Партии.ТипЗапасов В (ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар), ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца))
	|			ТОГДА 0
	|		ИНАЧЕ Партии.СтоимостьРегл КОНЕЦ) КАК СтоимостьРегл,
	|	СУММА(ВЫБОР
	|		КОГДА Партии.ТипЗапасов В (ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар), ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца))
	|			ТОГДА Партии.СтоимостьРегл
	|		ИНАЧЕ 0 КОНЕЦ) КАК СтоимостьЗабалансоваяРегл,
	|	СУММА(ВЫБОР КОГДА Партии.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента ТОГДА
	|		0
	|	ИНАЧЕ
	|		Партии.НДСРегл
	|	КОНЕЦ) КАК НДСРегл,
	|	0 КАК НДСУпр,
	|	СУММА(Партии.ПостояннаяРазница) КАК ПостояннаяРазница,
	|	СУММА(Партии.ВременнаяРазница) КАК ВременнаяРазница,
	|
	|	0 КАК ИсходноеКоличество,
	|	0 КАК КорСтоимость,
	|	0 КАК ИсходнаяСтоимость,
	|	0 КАК ИсходнаяСтоимостьРегл,
	|	0 КАК ИсходнаяСтоимостьЗабалансовая,
	|	0 КАК ИсходнаяСтоимостьЗабалансоваяРегл,
	|	0 КАК ИсходнаяСтоимостьБезНДС
	|ИЗ
	|	ВтПартии КАК Партии
	|
	|СГРУППИРОВАТЬ ПО
	|	Партии.Период,
	|	Партии.Регистратор,
	|	Партии.Организация,
	|	Партии.АналитикаУчетаНоменклатуры,
	|	Партии.ВидЗапасов,
	|	Партии.ТипЗапасов,
	|	Партии.АналитикаУчетаПродукции,
	|	Партии.ПериодПродажи,
	|	ВЫБОР КОГДА Партии.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента
	|	 ИЛИ Партии.Регистратор ССЫЛКА Документ.КорректировкаРеализации ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	Организация,
	|	АналитикаУчетаНоменклатуры,
	|	ВидЗапасов,
	|	ТипЗапасов,
	|	АналитикаУчетаПродукции,
	|	ПериодПродажи УБЫВ,
	|	Подразделение,
	|	СтатьяРасходовСписания,
	|	АналитикаРасходов,
	|	КорВидЗапасов,
	|	Порядок
	|";
	
	ЗаписьРаспределения = Новый Структура("
	|Период, ВидДвижения, АналитикаУчетаНоменклатуры, РазделУчета, ВидЗапасов, Организация, Склад,
	|ХозяйственнаяОперация, ЗаказКлиента, ТипЗапасов, АналитикаУчетаПоПартнерам, ЭтоПередачаМеждуОрганизациями,
	|КорАналитикаУчетаНоменклатуры, КорРазделУчета, КорВидЗапасов, КорТипЗапасов,КорОрганизация,
	|АналитикаУчетаНоменклатурыВО,АналитикаФинансовогоУчетаВО,АналитикаАктивовПассивов, КорСклад,
	|СтатьяАктивовПассивов, Подразделение, АналитикаРасходов, СтатьяРасходовСписания, СтатьяДоходов, АналитикаДоходов,
	|ВариантРаспределенияРасходовУпр, ВариантРаспределенияРасходовРегл,
	|ДокументДвижения, ИдентификаторСтроки, КодСтроки, ПринятиеКНалоговомуУчету, ИсточникГФУНоменклатуры, КорИсточникГФУНоменклатуры,
	|ПериодПродажи, ГруппаПродукции, СписаниеПриПередачеНУ, НаправлениеДеятельности, КорНаправлениеДеятельности,
	|Стоимость, СтоимостьБезНДС, СтоимостьЗабалансовая, Количество, КорСтоимость, ПостояннаяРазница, ВременнаяРазница,
	|ДопРасходы, ДопРасходыБезНДС, СтоимостьРегл, СтоимостьЗабалансоваяРегл, НДСРегл, НДСУпр
	|");
	
	СуммыРаспределения = Новый Структура("Количество, Стоимость, СтоимостьБезНДС, СтоимостьЗабалансовая, СтоимостьРегл, СтоимостьЗабалансоваяРегл, НДСРегл,
	|ДопРасходы, ДопРасходыБезНДС, ПостояннаяРазница, ВременнаяРазница
	|");
	
	СтрокаОстатка = Новый Структура("Регистратор, Организация, АналитикаУчетаНоменклатуры, ВидЗапасов, ТипЗапасов, АналитикаУчетаПродукции, ПериодПродажи,
	|Количество, Стоимость, СтоимостьБезНДС, СтоимостьЗабалансовая, СтоимостьРегл, СтоимостьЗабалансоваяРегл, НДСРегл,
	|ДопРасходы, ДопРасходыБезНДС, ПостояннаяРазница, ВременнаяРазница
	|");
	
	СуммыВозврата = Новый Структура("Регистратор, Организация, АналитикаУчетаНоменклатуры, ВидЗапасов, ТипЗапасов, АналитикаУчетаПродукции, ПериодПродажи,
	|Количество, Стоимость, СтоимостьБезНДС, СтоимостьЗабалансовая, СтоимостьРегл, СтоимостьЗабалансоваяРегл, НДСРегл,
	|ДопРасходы, ДопРасходыБезНДС, ПостояннаяРазница, ВременнаяРазница
	|");
	
	// Сформируем ВТПартии
	ПолучитьСебестоимостьПартийТоваров(ПараметрыРасчета);
	
	Выборка = РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос, Истина);
	
	// Формируются движения по регистрам:
	// - СебестоимостьТоваров
	// - ПрочиеРасходы
	// - ПрочиеДоходы
	// - ДвиженияНоменклатураДоходыРасходы
	// - ДвиженияНоменклатураНоменклатура
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Порядок = 1 Тогда
			
			ЗаполнитьЗначенияСвойств(СтрокаОстатка, Выборка);
			Если Выборка.ЭтоВозвратОтКлиента Тогда
				Если СуммыВозврата.Регистратор = Выборка.Регистратор
				 И СуммыВозврата.Организация = Выборка.Организация
				 И СуммыВозврата.АналитикаУчетаНоменклатуры = Выборка.АналитикаУчетаНоменклатуры
				 И СуммыВозврата.ВидЗапасов = Выборка.ВидЗапасов
				 И СуммыВозврата.ТипЗапасов = Выборка.ТипЗапасов
				 И СуммыВозврата.ПериодПродажи = Выборка.ПериодПродажи
				 И СуммыВозврата.АналитикаУчетаПродукции = Выборка.АналитикаУчетаПродукции Тогда
					СуммыВозврата.Количество = СуммыВозврата.Количество + Выборка.Количество;
					РасчетСебестоимостиКорректировкаСтоимости.ДополнитьСуммыРаспределения(СуммыВозврата, Выборка);
				Иначе
					ЗаполнитьЗначенияСвойств(СуммыВозврата, Выборка);
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли СтрокаОстатка.Регистратор = Выборка.Регистратор
			И СтрокаОстатка.Организация = Выборка.Организация
			И СтрокаОстатка.АналитикаУчетаНоменклатуры = Выборка.АналитикаУчетаНоменклатуры
			И СтрокаОстатка.ВидЗапасов = Выборка.ВидЗапасов
			И СтрокаОстатка.ТипЗапасов = Выборка.ТипЗапасов
			И СтрокаОстатка.АналитикаУчетаПродукции = Выборка.АналитикаУчетаПродукции
			И (СтрокаОстатка.ПериодПродажи = Выборка.ПериодПродажи
				ИЛИ Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратТоваровОтКлиента
				ИЛИ Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОтРозничногоПокупателя) Тогда
			
			ЗаполнитьЗначенияСвойств(ЗаписьРаспределения, Выборка);
			
			Если Выборка.Порядок = 2 И СтрокаОстатка.Количество > 0 Тогда
				РасчетСебестоимостиКорректировкаСтоимости.РаспределитьСтоимость(СуммыРаспределения, СтрокаОстатка, Выборка, Выборка.Количество);	
			ИначеЕсли Выборка.Порядок = 3 Тогда
				РасчетСебестоимостиКорректировкаСтоимости.РаспределитьСтоимость(СуммыРаспределения, СуммыВозврата, Выборка, Выборка.Количество);
			КонецЕсли;
			
			Если НЕ (Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыпускПродукции
			 И Выборка.ВидДвижения = ВидДвиженияНакопления.Приход) Тогда
				РасчетСебестоимостиКорректировкаСтоимости.ДополнитьСуммыРаспределения(ЗаписьРаспределения, СуммыРаспределения);
			КонецЕсли;
			
					
			// Корректировка списания товаров
			Если Выборка.ИсходнаяСтоимость <> 0 Тогда
				ЗаписьРаспределения.Стоимость = ЗаписьРаспределения.Стоимость - Выборка.ИсходнаяСтоимость;
			КонецЕсли;
			Если Выборка.ИсходнаяСтоимостьРегл <> 0 Тогда
				ЗаписьРаспределения.СтоимостьРегл = ЗаписьРаспределения.СтоимостьРегл - Выборка.ИсходнаяСтоимостьРегл;
			КонецЕсли;
			Если Выборка.ИсходнаяСтоимостьЗабалансовая <> 0 Тогда
				ЗаписьРаспределения.СтоимостьЗабалансовая = ЗаписьРаспределения.СтоимостьЗабалансовая - Выборка.ИсходнаяСтоимостьЗабалансовая;
			КонецЕсли;
			Если Выборка.ИсходнаяСтоимостьЗабалансоваяРегл <> 0 Тогда
				ЗаписьРаспределения.СтоимостьЗабалансоваяРегл = ЗаписьРаспределения.СтоимостьЗабалансоваяРегл - Выборка.ИсходнаяСтоимостьЗабалансоваяРегл;
			КонецЕсли;
			Если Выборка.ИсходнаяСтоимостьБезНДС <> 0 Тогда
				ЗаписьРаспределения.СтоимостьБезНДС = ЗаписьРаспределения.СтоимостьБезНДС - Выборка.ИсходнаяСтоимостьБезНДС;
			КонецЕсли;
			
			Если Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратТоваровОтКлиентаПрошлыхПериодов Тогда
				
				ЗаписьРаспределения.Стоимость = - ЗаписьРаспределения.Стоимость;
				ЗаписьРаспределения.СтоимостьБезНДС = - ЗаписьРаспределения.СтоимостьБезНДС;
				ЗаписьРаспределения.СтоимостьЗабалансовая = - ЗаписьРаспределения.СтоимостьЗабалансовая;
				ЗаписьРаспределения.ДопРасходы = - ЗаписьРаспределения.ДопРасходы;
				ЗаписьРаспределения.ДопРасходыБезНДС = - ЗаписьРаспределения.ДопРасходыБезНДС;
				ЗаписьРаспределения.СтоимостьРегл = - ЗаписьРаспределения.СтоимостьРегл;
				ЗаписьРаспределения.СтоимостьЗабалансоваяРегл = - ЗаписьРаспределения.СтоимостьЗабалансоваяРегл;
				ЗаписьРаспределения.ПостояннаяРазница = - ЗаписьРаспределения.ПостояннаяРазница;
				ЗаписьРаспределения.ВременнаяРазница = - ЗаписьРаспределения.ВременнаяРазница;
				
				СформироватьДвиженияСебестоимостьТоваров(ПараметрыРасчета, ЗаписьРаспределения,	ВидДвиженияНакопления.Приход);
				
			Иначе
				
				СформироватьДвиженияСебестоимостьТоваров(ПараметрыРасчета, ЗаписьРаспределения, Выборка.ВидДвижения);
				
			КонецЕсли;

			// Если есть кор. раздел - необходимо скорректировать стоимость в кор. части.
			Если ЗначениеЗаполнено(ЗаписьРаспределения.КорРазделУчета)
			 И Выборка.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.СборкаТоваров
			 И Выборка.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.РазборкаТоваров
			 И НЕ (ТипЗнч(Выборка.Регистратор) = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями") 
			 	И Выборка.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПередачаНаКомиссиюВДругуюОрганизацию)
			 И ТипЗнч(Выборка.Регистратор) <> Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями")
			 И Выборка.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПересортицаТоваров
			 И Выборка.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПорчаТоваров
			 И Выборка.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПеремещениеТоваров Тогда
				
				СформироватьКорДвиженияСебестоимостьТоваров(ПараметрыРасчета, ЗаписьРаспределения);
						
			КонецЕсли;
				
			// Корректировка списания товаров на затраты в регистре учет прочих расходов.
			Если (Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеТоваров
					Или Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеТоваровПоТребованию
					Или Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СторноСписанияНаРасходы
					Или Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаПрочиеЦели
					Или Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаВЭксплуатацию
					Или Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаВЭксплуатациюБУНУ
					Или Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтчетКомиссионераОСписании)
				 И Выборка.РазделУчета <> Перечисления.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию
				 И Выборка.РазделУчета <> Перечисления.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку Тогда
				 
					ЭтоВыпускПродукции = Ложь;
					
					Если ЗначениеЗаполнено(ЗаписьРаспределения.СтатьяАктивовПассивов) Тогда
						РасчетСебестоимостиКорректировкаСтоимости.СформироватьДвиженияПрочиеАктивыПассивы(
							ПараметрыРасчета,
							ЗаписьРаспределения,
							ЗаписьРаспределения.Стоимость + ЗаписьРаспределения.ДопРасходы + ?(ЭтоВыпускПродукции, Выборка.ИсходнаяСтоимость, 0));
					Иначе
						СуммовыеПоказатели = Новый Структура;
						СуммовыеПоказатели.Вставить("СуммаСНДС", ЗаписьРаспределения.Стоимость + ЗаписьРаспределения.ДопРасходы
							+ ?(ЭтоВыпускПродукции, Выборка.ИсходнаяСтоимость, 0));
						СуммовыеПоказатели.Вставить("СуммаБезНДС", ЗаписьРаспределения.СтоимостьБезНДС + ЗаписьРаспределения.ДопРасходыБезНДС
							+ ?(ЭтоВыпускПродукции, Выборка.ИсходнаяСтоимостьБезНДС, 0));
						СуммовыеПоказатели.Вставить("СуммаРегл", ЗаписьРаспределения.СтоимостьРегл + ЗаписьРаспределения.НДСРегл
							+ ?(ЭтоВыпускПродукции, Выборка.ИсходнаяСтоимостьРегл, 0));
						СуммовыеПоказатели.Вставить("СуммаУпр", 0);
						СуммовыеПоказатели.Вставить("ПостояннаяРазница", ЗаписьРаспределения.ПостояннаяРазница);
						СуммовыеПоказатели.Вставить("ВременнаяРазница", ЗаписьРаспределения.ВременнаяРазница);
						
						РасчетСебестоимостиКорректировкаСтоимости.СформироватьДвиженияПрочиеРасходы(
							ПараметрыРасчета,
							ЗаписьРаспределения,
							СуммовыеПоказатели);
					КонецЕсли;

			КонецЕсли;
					
			// Формирование прочих доходов\расходов при пересортице
			Если Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПересортицаТоваровСПереоценкой
			 И Выборка.РазделУчета <> Перечисления.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию
			 И Выборка.РазделУчета <> Перечисления.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку Тогда
				 
			 СуммаПереоценки = ЗаписьРаспределения.Стоимость + ЗаписьРаспределения.ДопРасходы - Выборка.КорСтоимость;
				Если СуммаПереоценки > 0 Тогда
					
					СуммовыеПоказатели = Новый Структура;
					СуммовыеПоказатели.Вставить("СуммаСНДС", СуммаПереоценки);
					СуммовыеПоказатели.Вставить("СуммаБезНДС", ЗаписьРаспределения.СтоимостьБезНДС + ЗаписьРаспределения.ДопРасходыБезНДС);
					СуммовыеПоказатели.Вставить("СуммаРегл", ЗаписьРаспределения.СтоимостьРегл + ЗаписьРаспределения.НДСРегл);
					СуммовыеПоказатели.Вставить("СуммаУпр", 0);
					СуммовыеПоказатели.Вставить("ПостояннаяРазница", ЗаписьРаспределения.ПостояннаяРазница);
					СуммовыеПоказатели.Вставить("ВременнаяРазница", ЗаписьРаспределения.ВременнаяРазница); 

					РасчетСебестоимостиКорректировкаСтоимости.СформироватьДвиженияПрочиеРасходы(
						ПараметрыРасчета,
						ЗаписьРаспределения,
						СуммовыеПоказатели);

				ИначеЕсли СуммаПереоценки < 0 Тогда

					РасчетСебестоимостиКорректировкаСтоимости.СформироватьДвиженияПрочиеДоходы(
						ПараметрыРасчета,
						ЗаписьРаспределения,
						- СуммаПереоценки,
						- (ЗаписьРаспределения.СтоимостьРегл + ЗаписьРаспределения.НДСРегл),
						0);

				КонецЕсли;

			КонецЕсли;
		
			// Формирование прочих расходов при порче
			Если Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПорчаТоваровСПереоценкой
				 И Выборка.РазделУчета <> Перечисления.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию
				 И Выборка.РазделУчета <> Перечисления.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку Тогда
			
				СуммаПереоценки = ЗаписьРаспределения.Стоимость + ЗаписьРаспределения.ДопРасходы - Выборка.КорСтоимость;
				Если СуммаПереоценки <> 0 Тогда
					
					СуммовыеПоказатели = Новый Структура;
					СуммовыеПоказатели.Вставить("СуммаСНДС", СуммаПереоценки);
					СуммовыеПоказатели.Вставить("СуммаБезНДС", ЗаписьРаспределения.СтоимостьБезНДС + ЗаписьРаспределения.ДопРасходыБезНДС);
					СуммовыеПоказатели.Вставить("СуммаРегл", ЗаписьРаспределения.СтоимостьРегл + ЗаписьРаспределения.НДСРегл);
					СуммовыеПоказатели.Вставить("СуммаУпр", 0);
					СуммовыеПоказатели.Вставить("ПостояннаяРазница", ЗаписьРаспределения.ПостояннаяРазница);
					СуммовыеПоказатели.Вставить("ВременнаяРазница", ЗаписьРаспределения.ВременнаяРазница);
					
					РасчетСебестоимостиКорректировкаСтоимости.СформироватьДвиженияПрочиеРасходы(
						ПараметрыРасчета,
						ЗаписьРаспределения,
						СуммовыеПоказатели);
					
				КонецЕсли;

			КонецЕсли;

			// Формирование прочих доходов/расходов при возврате товаров поставщику.
			Если ((Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратТоваровПоставщику
				И ЗаписьРаспределения.КорСтоимость <> 0)
				ИЛИ Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияПереданнойВозвратнойТары
				ИЛИ (Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратТоваровМеждуОрганизациями
					И Выборка.КорСтоимость <> 0)
				ИЛИ Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СторноПереданнойТары)
				И ПараметрыРасчета.ФО.ИспользоватьУчетПрочихДоходовРасходов
				И Выборка.РазделУчета <> Перечисления.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию
				И Выборка.РазделУчета <> Перечисления.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку Тогда
				
				СуммаПереоценки = ЗаписьРаспределения.Стоимость + ЗаписьРаспределения.ДопРасходы;
				Если СуммаПереоценки > 0 ИЛИ ЗаписьРаспределения.СтоимостьРегл > 0 Тогда
					
					СуммовыеПоказатели = Новый Структура;
					СуммовыеПоказатели.Вставить("СуммаСНДС", СуммаПереоценки);
					СуммовыеПоказатели.Вставить("СуммаБезНДС", ЗаписьРаспределения.СтоимостьБезНДС + ЗаписьРаспределения.ДопРасходыБезНДС);
					СуммовыеПоказатели.Вставить("СуммаРегл", ЗаписьРаспределения.СтоимостьРегл + ЗаписьРаспределения.НДСРегл);
					СуммовыеПоказатели.Вставить("СуммаУпр", 0);
					СуммовыеПоказатели.Вставить("ПостояннаяРазница", ЗаписьРаспределения.ПостояннаяРазница);
					СуммовыеПоказатели.Вставить("ВременнаяРазница", ЗаписьРаспределения.ВременнаяРазница);

					РасчетСебестоимостиКорректировкаСтоимости.СформироватьДвиженияПрочиеРасходы(
						ПараметрыРасчета,
						ЗаписьРаспределения,
						СуммовыеПоказатели);

				ИначеЕсли СуммаПереоценки < 0 Тогда

					РасчетСебестоимостиКорректировкаСтоимости.СформироватьДвиженияПрочиеДоходы(
						ПараметрыРасчета,
						ЗаписьРаспределения,
						- СуммаПереоценки,
						- (ЗаписьРаспределения.СтоимостьРегл),
						0);

				КонецЕсли;
				
			КонецЕсли;
		
			// Корректировка движений по оборотным регистрам управленческого учета.
			РасчетСебестоимостиКорректировкаСтоимости.СформироватьДвиженияПоОборотнымРегистрамУпрУчета(ПараметрыРасчета, ЗаписьРаспределения);
			
		КонецЕсли;

	КонецЦикла;
	
	// Формируются движения по регистрам:
	// - ПрочиеРасходы
	РасчетСебестоимостиКорректировкаСтоимости.СформироватьДвиженияПрочиеРасходыСписаниеНДС(ПараметрыРасчета);
	
	// Формируются движения по регистрам:
	// - ДвиженияНоменклатураДоходыРасходы
	РасчетСебестоимостиКорректировкаСтоимости.СформироватьДвиженияНоменклатураДоходыРасходыСписаниеНДС(ПараметрыРасчета);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета,	"ВТПартии");
	
	РасчетСебестоимостиПрикладныеАлгоритмы.КэшироватьСформированныеДвижения(ПараметрыРасчета);
	
КонецПроцедуры

// Этап 2.2
Процедура СкорректироватьСтоимостьПродажПоФИФОСкользящая(ПараметрыРасчета)

	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "СкорректироватьСтоимостьПродажПоФИФОСкользящая");
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиКорректировкаСтоимости.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	2 КАК Порядок,
	|	Продажи.Период                     КАК Период,
	|	Продажи.Регистратор                КАК Регистратор,
	|	Продажи.Регистратор                КАК ДокументДвижения,
	|	АналитикаПартнеров.Организация     КАК Организация,
	|	Продажи.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Продажи.ВидЗапасов                 КАК ВидЗапасов,
	|	Продажи.ТипЗапасов                 КАК ТипЗапасов,
	|
	|	Продажи.РазделУчета                КАК РазделУчета,
	|	Продажи.ЗаказКлиента               КАК ЗаказКлиента,
	|	Продажи.АналитикаУчетаПоПартнерам  КАК АналитикаУчетаПоПартнерам,
	|	АналитикаПартнеров.Партнер         КАК Партнер,
	|	АналитикаПартнеров.Контрагент      КАК Контрагент,
	|	Продажи.Подразделение              КАК Подразделение,
	|	Продажи.Менеджер                   КАК Менеджер,
	|	Продажи.Склад                      КАК Склад,
	|	Продажи.Соглашение                 КАК Соглашение,
	|	Продажи.Договор                    КАК Договор,
	|	Продажи.ХозяйственнаяОперация      КАК ХозяйственнаяОперация,
	|	Продажи.НалогообложениеНДС         КАК НалогообложениеНДС,
	|	Продажи.ВалютаВзаиморасчетов       КАК ВалютаВзаиморасчетов,
	|	Продажи.ВалютаДокумента            КАК ВалютаДокумента,
	|	Продажи.ИсточникГФУНоменклатуры    КАК ИсточникГФУНоменклатуры,
	|	Продажи.ИсточникГФУРасчетов        КАК ИсточникГФУРасчетов,
	|	Продажи.НаправлениеДеятельностиКонтрагента КАК НаправлениеДеятельностиКонтрагента,
	|	Продажи.НаправлениеДеятельностиНоменклатуры КАК НаправлениеДеятельностиНоменклатуры,
	|	СУММА(
	|		ВЫБОР КОГДА Продажи.Количество < 0 ТОГДА
	|			0 - Продажи.Количество
	|		ИНАЧЕ
	|			Продажи.Количество
	|		КОНЕЦ
	|	) КАК Количество,
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьБезНДС,
	|	0 КАК СтоимостьЗабалансовая,
	|	0 КАК ДопРасходы,
	|	0 КАК ДопРасходыБезНДС,
	|	0 КАК СтоимостьРегл,
	|	0 КАК СтоимостьЗабалансоваяРегл,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|
	|	СУММА(Продажи.Количество)          КАК ИсходноеКоличество,
	|	0                                  КАК КорСтоимость
	|ИЗ
	|	ВТКэшРасчетныеОборотыВыручкаИСебестоимостьПродаж КАК Продажи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОтборАналитикаПоПартнерам КАК АналитикаПартнеров
	|			ПО Продажи.АналитикаУчетаПоПартнерам = АналитикаПартнеров.КлючАналитики
	|
	|ГДЕ
	|	Продажи.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|	И (Продажи.ХозяйственнаяОперация В (&МассивОперацийРеализации)
	|		ИЛИ Продажи.ХозяйственнаяОперация В (&МассивОперацийВозвратыПрошлыхПериодов))
	|	
	|СГРУППИРОВАТЬ ПО
	|	Продажи.Период,
	|	Продажи.Регистратор,
	|	Продажи.АналитикаУчетаНоменклатуры,
	|	Продажи.ЗаказКлиента,
	|	Продажи.АналитикаУчетаПоПартнерам,
	|	АналитикаПартнеров.Партнер,
	|	АналитикаПартнеров.Контрагент,
	|	Продажи.Подразделение,
	|	Продажи.РазделУчета,
	|	Продажи.ТипЗапасов,
	|	Продажи.ВидЗапасов,
	|	Продажи.Менеджер,
	|	Продажи.Склад,
	|	Продажи.Соглашение,
	|	Продажи.Договор,
	|	Продажи.ХозяйственнаяОперация,
	|	Продажи.НалогообложениеНДС,
	|	Продажи.ВалютаВзаиморасчетов,
	|	Продажи.ВалютаДокумента,
	|	Продажи.ИсточникГФУНоменклатуры,
	|	Продажи.ИсточникГФУРасчетов,
	|	Продажи.НаправлениеДеятельностиКонтрагента,
	|	Продажи.НаправлениеДеятельностиНоменклатуры,
	|	АналитикаПартнеров.Организация
	|
	|ИМЕЮЩИЕ
	|	СУММА(Продажи.Количество) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1 КАК Порядок,
	|	&НачалоПериода КАК Период,
	|	Партии.Регистратор КАК Регистратор,
	|	Партии.Регистратор КАК ДокументДвижения,
	|	Партии.Организация КАК Организация,
	|	Партии.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Партии.ВидЗапасов КАК ВидЗапасов,
	|	Партии.ТипЗапасов КАК ТипЗапасов,
	|
	|	НЕОПРЕДЕЛЕНО КАК РазделУчета,
	|	НЕОПРЕДЕЛЕНО КАК ЗаказКлиента,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПоПартнерам,
	|	НЕОПРЕДЕЛЕНО КАК Партнер,
	|	НЕОПРЕДЕЛЕНО КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК Менеджер,
	|	НЕОПРЕДЕЛЕНО КАК Склад,
	|	НЕОПРЕДЕЛЕНО КАК Соглашение,
	|	НЕОПРЕДЕЛЕНО КАК Договор,
	|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК НалогообложениеНДС,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаВзаиморасчетов,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДокумента,
	|	НЕОПРЕДЕЛЕНО КАК ИсточникГФУНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК ИсточникГФУРасчетов,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиКонтрагента,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиНоменклатуры,
	|
	|	Партии.Количество КАК Количество,
	|	Партии.Стоимость КАК Стоимость,
	|	Партии.СтоимостьБезНДС КАК СтоимостьБезНДС,
	|	0 КАК СтоимостьЗабалансовая,
	|	Партии.ДопРасходы КАК ДопРасходы,
	|	Партии.ДопРасходыБезНДС КАК ДопРасходыБезНДС,
	|	Партии.СтоимостьРегл КАК СтоимостьРегл,
	|	0 КАК СтоимостьЗабалансоваяРегл,
	|	Партии.НДСРегл КАК НДСРегл,
	|	Партии.ПостояннаяРазница КАК ПостояннаяРазница,
	|	Партии.ВременнаяРазница КАК ВременнаяРазница,
	|
	|	0 КАК ИсходноеКоличество,
	|	0 КАК КорСтоимость
	|ИЗ
	|	ВтПартии КАК Партии
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	Организация,
	|	АналитикаУчетаНоменклатуры,
	|	ВидЗапасов,
	|	ТипЗапасов,
	|	Порядок
	|";
	
	ЗаписьРаспределения = Новый Структура("
	|Период, АналитикаУчетаНоменклатуры, ВидЗапасов, Организация, Менеджер,
	|ХозяйственнаяОперация, ЗаказКлиента, ТипЗапасов, РазделУчета,
	|Подразделение, АналитикаУчетаПоПартнерам, Партнер, Контрагент,
	|Склад, Соглашение, Договор, ВалютаВзаиморасчетов, ВалютаДокумента,
	|ИсточникГФУНоменклатуры, ИсточникГФУРасчетов,
	|НаправлениеДеятельностиКонтрагента, НаправлениеДеятельностиНоменклатуры, 
	|НалогообложениеНДС, ДокументДвижения,
	|Стоимость, СтоимостьБезНДС, СтоимостьЗабалансовая, Количество,
	|ДопРасходы, ДопРасходыБезНДС,
	|СтоимостьРегл, СтоимостьЗабалансоваяРегл, НДСРегл, ПостояннаяРазница, ВременнаяРазница
	|");
	
	СтрокаОстатка = Новый Структура("
	|Регистратор, Организация, АналитикаУчетаНоменклатуры, ВидЗапасов, ТипЗапасов,
	|Количество, Стоимость, СтоимостьБезНДС, СтоимостьЗабалансовая, СтоимостьРегл, СтоимостьЗабалансоваяРегл, НДСРегл,
	|ДопРасходы, ДопРасходыБезНДС, ПостояннаяРазница, ВременнаяРазница
	|");

	// Сформируем ВТПартии
	ПолучитьСебестоимостьПартийТоваров(ПараметрыРасчета, Истина);
	
	Выборка = РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос, Истина);
	
	// Формируются движения по регистрам:
	// - ВыручкаИСебестоимостьПродаж
	// - Закупки
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Порядок = 1 Тогда
			
			ЗаполнитьЗначенияСвойств(СтрокаОстатка, Выборка);
			
		ИначеЕсли СтрокаОстатка.Регистратор = Выборка.Регистратор
		 И СтрокаОстатка.Организация = Выборка.Организация
		 И СтрокаОстатка.АналитикаУчетаНоменклатуры = Выборка.АналитикаУчетаНоменклатуры
		 И СтрокаОстатка.ВидЗапасов = Выборка.ВидЗапасов
		 И СтрокаОстатка.ТипЗапасов = Выборка.ТипЗапасов
		 И СтрокаОстатка.Количество > 0 Тогда
		
			ЗаполнитьЗначенияСвойств(ЗаписьРаспределения, Выборка);
			РасчетСебестоимостиКорректировкаСтоимости.РаспределитьСтоимость(ЗаписьРаспределения, СтрокаОстатка, Выборка, Выборка.Количество);
			
			СформироватьДвиженияВыручкаИСебестоимостьПродаж(ПараметрыРасчета, ЗаписьРаспределения);
			
			Если Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию Тогда	
				
				СформироватьДвиженияЗакупки(ПараметрыРасчета, ЗаписьРаспределения);
				
			КонецЕсли;

		КонецЕсли;
		
	КонецЦикла;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета,	"ВТПартии");
	
	РасчетСебестоимостиПрикладныеАлгоритмы.КэшироватьСформированныеДвижения(ПараметрыРасчета);

КонецПроцедуры

// Служебная, этап 2.1, 3.3, 3.14
Процедура СформироватьДвиженияСебестоимостьТоваров(ПараметрыРасчета, ДанныеДвижения, ВидДвижения)
	
	ИмяРегистра = Метаданные.РегистрыНакопления.СебестоимостьТоваров.Имя;
	
	// Кор. стоимость не включается в список полей.
	КопируемыеПоля = "
	|Период, АналитикаУчетаНоменклатуры, РазделУчета, ВидЗапасов, Организация,
	|ХозяйственнаяОперация,
	|КорАналитикаУчетаНоменклатуры, КорРазделУчета, КорВидЗапасов,КорОрганизация,
	|АналитикаУчетаПоПартнерам, ЗаказКлиента,
	|Подразделение, АналитикаРасходов, СтатьяРасходовСписания, СтатьяДоходов, АналитикаДоходов,
	|СтатьяАктивовПассивов, АналитикаАктивовПассивов,
	|ИдентификаторСтроки, КодСтроки, ДокументДвижения, ПериодПродажи, ГруппаПродукции,
	|КорНаправлениеДеятельности,
	|Стоимость, СтоимостьБезНДС, СтоимостьЗабалансовая, ДопРасходы, ДопРасходыБезНДС,
	|ПостояннаяРазница, ВременнаяРазница, СтоимостьРегл, СтоимостьЗабалансоваяРегл";
	
	// Добавим движение и заполним его основные свойства
	Запись = РасчетСебестоимостиКорректировкаСтоимости.ДобавитьЗаписьВТаблицуДвижений(ПараметрыРасчета, ИмяРегистра, ДанныеДвижения, КопируемыеПоля);
	
	// Заполним дополнительные свойства движения
	Запись.ВидДвижения = ВидДвижения;
	Запись.Количество  = 0;

КонецПроцедуры

// Служебная, этап 2.1, 3.1, 3.14
Процедура СформироватьКорДвиженияСебестоимостьТоваров(ПараметрыРасчета, ДанныеДвижения, ПромежуточнаяСебестоимость = Ложь)
	
	Если ПромежуточнаяСебестоимость Тогда
		ТаблицаПриемник = "ВТПромежуточнаяСебестоимостьТоваров";
	Иначе
		ТаблицаПриемник = Метаданные.РегистрыНакопления.СебестоимостьТоваров.Имя;
	КонецЕсли;
	
	КопируемыеПоля = "
	|Период, ХозяйственнаяОперация, ДокументДвижения, ИдентификаторСтроки,
	|Подразделение, ГруппаПродукции,
	|Стоимость, СтоимостьЗабалансовая, СтоимостьБезНДС, ДопРасходы, ДопРасходыБезНДС,
	|СтоимостьРегл, СтоимостьЗабалансоваяРегл, ПостояннаяРазница, ВременнаяРазница";

	// Добавим движение и заполним его основные свойства
	Запись = РасчетСебестоимостиКорректировкаСтоимости.ДобавитьЗаписьВТаблицуДвижений(ПараметрыРасчета, ТаблицаПриемник, ДанныеДвижения, КопируемыеПоля);
	
	// Заполним дополнительные свойства движения
	Запись.ВидДвижения                = ВидДвиженияНакопления.Приход;
	Запись.АналитикаУчетаНоменклатуры = ДанныеДвижения.КорАналитикаУчетаНоменклатуры;
	Запись.РазделУчета                = ДанныеДвижения.КорРазделУчета;
	Запись.ВидЗапасов                 = ДанныеДвижения.КорВидЗапасов;
	Запись.Организация                = ?(ЗначениеЗаполнено(ДанныеДвижения.КорОрганизация), ДанныеДвижения.КорОрганизация, ДанныеДвижения.Организация);
	Запись.Количество 				  = 0;
	Если Запись.РазделУчета = Перечисления.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение Тогда
		Запись.КорВидЗапасов             = ДанныеДвижения.КорВидЗапасов;
	КонецЕсли;

	Если ДанныеДвижения.ЭтоПередачаМеждуОрганизациями Тогда
		
		Запись.СтоимостьРегл = 0;
		Запись.СтоимостьЗабалансоваяРегл = 0;
		Запись.ПостояннаяРазница = 0;
		Запись.ВременнаяРазница = 0;
		
	ИначеЕсли ДанныеДвижения.НДСРегл <> 0 ИЛИ ДанныеДвижения.НДСУпр <> 0 Тогда // это не передача
		
		КопируемыеПоля = " Период, ХозяйственнаяОперация, ДокументДвижения, ИдентификаторСтроки, Подразделение, Организация";
		
		// Добавим движение и заполним его основные свойства
		Запись = РасчетСебестоимостиКорректировкаСтоимости.ДобавитьЗаписьВТаблицуДвижений(ПараметрыРасчета, ТаблицаПриемник, ДанныеДвижения, КопируемыеПоля);
		
		// Заполним дополнительные свойства движения
		Запись.ВидДвижения 				  	 = ВидДвиженияНакопления.Приход;
		Запись.АналитикаУчетаНоменклатуры 	 = ДанныеДвижения.КорАналитикаУчетаНоменклатуры;
		Запись.РазделУчета 				  	 = ДанныеДвижения.КорРазделУчета;
		Запись.ВидЗапасов 				  	 = ДанныеДвижения.КорВидЗапасов;
		Запись.Организация                	 = ?(ЗначениеЗаполнено(ДанныеДвижения.КорОрганизация), ДанныеДвижения.КорОрганизация, ДанныеДвижения.Организация);
		Если ПараметрыРасчета.ПартионныйУчетВерсии22 Тогда
			Запись.АналитикаФинансовогоУчета = ДанныеДвижения.КорАналитикаФинансовогоУчета;
			Запись.ВидДеятельностиНДС   	 = ДанныеДвижения.КорВидДеятельностиНДС;
			Если ДанныеДвижения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыпускПродукции Тогда
				Если ТипЗнч(ДанныеДвижения.АналитикаФинансовогоУчета) = Тип("СправочникСсылка.ГруппыАналитическогоУчетаНоменклатуры") Тогда
					Запись.КорАналитикаФинансовогоУчета = ДанныеДвижения.АналитикаФинансовогоУчета;
				Иначе
					Запись.КорАналитикаФинансовогоУчета = Справочники.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка();
				КонецЕсли;
				Запись.ГруппаПродукции = ДанныеДвижения.АналитикаФинансовогоУчета;
			КонецЕсли;
		КонецЕсли;
		
		Запись.СтоимостьРегл 			  	 = ДанныеДвижения.НДСРегл;
		Запись.СтоимостьУпр 			  	 = ДанныеДвижения.НДСУпр;
		
	КонецЕсли;

КонецПроцедуры

// Служебная, этап 2.2, 3.17, 3.18
Процедура СформироватьДвиженияВыручкаИСебестоимостьПродаж(ПараметрыРасчета, ДанныеДвижения)
	
	ИмяРегистра = Метаданные.РегистрыНакопления.ВыручкаИСебестоимостьПродаж.Имя;
	
	КопируемыеПоля = "
	|Период, Подразделение, АналитикаУчетаНоменклатуры, Менеджер, РазделУчета,
	|АналитикаУчетаПоПартнерам, ТипЗапасов, ВидЗапасов, ЗаказКлиента, ХозяйственнаяОперация,
	|Склад, Соглашение, Договор, ВалютаВзаиморасчетов, ВалютаДокумента,
	|ИсточникГФУНоменклатуры, ИсточникГФУРасчетов, НаправлениеДеятельностиНоменклатуры,
	|НаправлениеДеятельностиКонтрагента, 
	|НалогообложениеНДС, ДокументДвижения";
	
	// Добавим движение и заполним его основные свойства
	Запись = РасчетСебестоимостиКорректировкаСтоимости.ДобавитьЗаписьВТаблицуДвижений(ПараметрыРасчета, ИмяРегистра, ДанныеДвижения, КопируемыеПоля);
	
	// Заполним дополнительные свойства движения
	ПоляРесурсов = "
		|Стоимость, СтоимостьБезНДС, ПостояннаяРазница, ВременнаяРазница,
		|СтоимостьРегл, ДопРасходы, ДопРасходыБезНДС";
	
	ЗаполнитьЗначенияСвойств(Запись, ДанныеДвижения, ПоляРесурсов);
	
	Если ДанныеДвижения.НДСРегл <> 0 Тогда
		
		// Добавим движение и заполним его основные свойства
		Запись = РасчетСебестоимостиКорректировкаСтоимости.ДобавитьЗаписьВТаблицуДвижений(ПараметрыРасчета, ИмяРегистра, ДанныеДвижения, КопируемыеПоля);
		
		// Заполним дополнительные свойства движения
		Запись.СтоимостьРегл = ДанныеДвижения.НДСРегл;
		
	КонецЕсли;
	
КонецПроцедуры

// Служебная, этап 2.2, 3.17
Процедура СформироватьДвиженияЗакупки(ПараметрыРасчета, ДанныеДвижения)
	
	ИмяРегистра = Метаданные.РегистрыНакопления.Закупки.Имя;
	
	КопируемыеПоля = "
	|Период, Подразделение, Менеджер, АналитикаУчетаНоменклатуры,
	|Склад, ТипЗапасов, ВидЗапасов, Партнер, Соглашение, Договор,
	|ВалютаДокумента, ВалютаВзаиморасчетов, ИсточникГФУНоменклатуры, ИсточникГФУРасчетов, ДокументДвижения,
	|Стоимость, СтоимостьБезНДС";
	
	// Добавим движение и заполним его основные свойства
	Запись = РасчетСебестоимостиКорректировкаСтоимости.ДобавитьЗаписьВТаблицуДвижений(ПараметрыРасчета, ИмяРегистра, ДанныеДвижения, КопируемыеПоля);
	Запись.Стоимость = Запись.Стоимость + ДанныеДвижения.ДопРасходы;
	Запись.СтоимостьБезНДС = Запись.СтоимостьБезНДС + ДанныеДвижения.ДопРасходыБезНДС;
	
	// Заполним дополнительные свойства движения
	Запись.Организация 			 = ДанныеДвижения.Контрагент;
	Запись.Контрагент 			 = ДанныеДвижения.Организация;
	Запись.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУДругойОрганизации;
	
КонецПроцедуры


// Служебная, этап 2.1, 2.2
// Формирует ВтПартии
//
Процедура ПолучитьСебестоимостьПартийТоваров(ПараметрыРасчета, ДляСтоимостиПродаж = Ложь)
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиКорректировкаСтоимости.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	Запрос.УстановитьПараметр("ДляСтоимостиПродаж", ДляСтоимостиПродаж);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Партии.Организация КАК Организация,
	|	(ВЫБОР
	|		КОГДА Возврат.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера)
	|			ТОГДА ДАТАВРЕМЯ(1,1,1)
	|		КОГДА НЕ Возврат.Ссылка ЕСТЬ NULL ИЛИ НЕ Корректировка.Ссылка ЕСТЬ NULL
	|			ТОГДА НАЧАЛОПЕРИОДА(ЕСТЬNULL(Реализация.Дата, ЕСТЬNULL(Розница.Дата, ДАТАВРЕМЯ(1,1,1))), ДЕНЬ)
	|		КОГДА НЕ Потребление.Ссылка ЕСТЬ NULL
	|			ТОГДА НАЧАЛОПЕРИОДА(Потребление.Дата, ДЕНЬ)
	|		КОГДА НЕ Передача.Ссылка ЕСТЬ NULL
	|			ТОГДА НАЧАЛОПЕРИОДА(Передача.Дата, ДЕНЬ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1,1,1) КОНЕЦ) КАК ПериодПродажи,
	|	ВЫБОР КОГДА (НЕ Возврат.Ссылка ЕСТЬ NULL ИЛИ НЕ Корректировка.Ссылка ЕСТЬ NULL)
	|		И Партии.КорВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
	|		И (НАЧАЛОПЕРИОДА(Возврат.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Реализация.Дата, МЕСЯЦ)
	|			ИЛИ НАЧАЛОПЕРИОДА(Возврат.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Розница.Дата, МЕСЯЦ)
	|			ИЛИ НАЧАЛОПЕРИОДА(Корректировка.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Реализация.Дата, МЕСЯЦ)
	|			ИЛИ &ДляСтоимостиПродаж)
	|	ТОГДА
	|		Партии.КорВидЗапасов
	|	ИНАЧЕ
	|		Партии.ВидЗапасов
	|	КОНЕЦ КАК ВидЗапасов,
	|	Партии.Регистратор КАК Регистратор,
	|	ВЫБОР КОГДА НЕ Возврат.Ссылка ЕСТЬ NULL
	|		И Партии.КорАналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|		И (НАЧАЛОПЕРИОДА(Возврат.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Реализация.Дата, МЕСЯЦ)
	|			ИЛИ НАЧАЛОПЕРИОДА(Возврат.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Розница.Дата, МЕСЯЦ)
	|			ИЛИ Возврат.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера)
	|			ИЛИ &ДляСтоимостиПродаж)
	|	ТОГДА
	|		Партии.КорАналитикаУчетаНоменклатуры
	|	КОГДА Партии.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтПереработчика)
	|		ТОГДА Партии.КорАналитикаУчетаНоменклатуры
	|	ИНАЧЕ
	|		Партии.АналитикаУчетаНоменклатуры
	|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
	|	(ВЫБОР
	|		КОГДА Партии.ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваров),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваровСПереоценкой),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваров),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваровСПереоценкой))
	|		 И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА Партии.КорАналитикаУчетаНоменклатуры
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КОНЕЦ) КАК КорАналитикаУчетаНоменклатуры,
	|	СУММА(
	|		ВЫБОР
	|			КОГДА Партии.Количество < 0 
	|			ТОГДА 0 - Партии.Количество
	|			ИНАЧЕ Партии.Количество
	|		КОНЕЦ
	|	)										КАК Количество,
	|	СУММА(
	|		ВЫБОР
	|			КОГДА Партии.Количество < 0 
	|			ТОГДА 0 - Партии.Стоимость
	|			ИНАЧЕ Партии.Стоимость
	|		КОНЕЦ
	|	)										КАК Стоимость,
	|	СУММА(
	|		ВЫБОР
	|			КОГДА Партии.Количество < 0 
	|			ТОГДА 0 - Партии.СтоимостьБезНДС
	|			ИНАЧЕ Партии.СтоимостьБезНДС
	|		КОНЕЦ
	|	)										КАК СтоимостьБезНДС,
	|	СУММА(
	|		ВЫБОР
	|			КОГДА Партии.Количество < 0 
	|			ТОГДА 0 - Партии.ПостояннаяРазница
	|			ИНАЧЕ Партии.ПостояннаяРазница
	|		КОНЕЦ
	|	)										КАК ПостояннаяРазница,
	|	СУММА(
	|		ВЫБОР
	|			КОГДА Партии.Количество < 0 
	|			ТОГДА 0 - Партии.ВременнаяРазница
	|			ИНАЧЕ Партии.ВременнаяРазница
	|		КОНЕЦ
	|	)										КАК ВременнаяРазница,
	|	СУММА(
	|		ВЫБОР
	|			КОГДА Партии.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА Партии.Количество < 0 
	|					ТОГДА 0 - Партии.СтоимостьРегл - Партии.НДСРегл 
	|					ИНАЧЕ Партии.СтоимостьРегл + Партии.НДСРегл 
	|				КОНЕЦ
	|			ИНАЧЕ
	|				ВЫБОР
	|					КОГДА Партии.Количество < 0 
	|					ТОГДА 0 - Партии.СтоимостьРегл 
	|					ИНАЧЕ Партии.СтоимостьРегл
	|				КОНЕЦ
	|		КОНЕЦ
	|	)										КАК СтоимостьРегл,
	|
	|	СУММА(
	|		ВЫБОР КОГДА Партии.СтатьяСписанияНДС <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА 0
	|		КОГДА Партии.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСНеУчитываетсяВСтоимости)
	|		 И Партии.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|		ТОГДА
	|			ВЫБОР КОГДА Партии.Количество < 0 ТОГДА
	|				0 - Партии.НДСРегл
	|			ИНАЧЕ
	|				Партии.НДСРегл
	|			КОНЕЦ
	|
	|		КОГДА Партии.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|		 И Партии.НалогообложениеНДС В (&ТипыНалогообложенияНДСНеУчитываетсяВСтоимости)
	|		ТОГДА
	|			ВЫБОР КОГДА Партии.Количество < 0 ТОГДА
	|				Партии.НДСРегл
	|			ИНАЧЕ
	|				- Партии.НДСРегл
	|			КОНЕЦ
	|		ИНАЧЕ
	|			0
	|		КОНЕЦ
	|	) КАК НДСРегл,
	|	СУММА(
	|		ВЫБОР КОГДА Партии.СтатьяСписанияНДС <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА ВЫБОР КОГДА Партии.Количество < 0 ТОГДА -Партии.НДСРегл ИНАЧЕ Партии.НДСРегл КОНЕЦ
	|		ИНАЧЕ
	|			0
	|		КОНЕЦ
	|	) КАК СписаниеНДС,
	|	
	|	Партии.Номенклатура						КАК Номенклатура,
	|	Партии.Характеристика					КАК Характеристика,
	|	Партии.Период,
	|	Партии.СтатьяСписанияНДС,
	|	Партии.АналитикаСписанияНДС
	|
	|ПОМЕСТИТЬ ТаблицаПартий
	|ИЗ
	|	РегистрНакопления.ПартииТоваровОрганизаций КАК Партии
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента КАК Возврат
	|		ПО Возврат.Ссылка = Партии.Регистратор
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК Корректировка
	|		ПО Корректировка.Ссылка = Партии.Регистратор
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК Реализация
	|		ПО Реализация.Ссылка = Партии.ДокументИсточник
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах КАК Розница
	|		ПО Розница.Ссылка = Партии.ДокументИсточник
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВнутреннееПотреблениеТоваров КАК Потребление
	|		ПО Потребление.Ссылка = Партии.ДокументИсточник
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаТоваровМеждуОрганизациями КАК Передача
	|		ПО Передача.Ссылка = Партии.ДокументИсточник
	|		И Партии.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровМеждуОрганизациями)
	|		И Партии.Количество < 0
	|ГДЕ
	|	Партии.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Партии.Активность
	|	И Партии.Организация В (&МассивОрганизаций)
	|	И (
	|		(Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И НЕ Партии.Регистратор Ссылка Документ.ВозвратТоваровМеждуОрганизациями
	|		ИЛИ Партии.ХозяйственнаяОперация В(
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеТоваров),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваров),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваров),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазборкаТоваров),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссиюВДругуюОрганизацию),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОтПереработчика),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиентуРеглУчет)
	|			)
	|		) ИЛИ Партии.Регистратор Ссылка Документ.ВозвратТоваровМеждуОрганизациями
	|		)
	|	И НЕ (Партии.Регистратор Ссылка Документ.ВозвратТоваровОтКлиента И Партии.Первичное)
	|
	|СГРУППИРОВАТЬ ПО
	|	Партии.Организация,
	|	(ВЫБОР
	|		КОГДА Возврат.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера)
	|			ТОГДА ДАТАВРЕМЯ(1,1,1)
	|		КОГДА НЕ Возврат.Ссылка ЕСТЬ NULL ИЛИ НЕ Корректировка.Ссылка ЕСТЬ NULL
	|			ТОГДА НАЧАЛОПЕРИОДА(ЕСТЬNULL(Реализация.Дата, ЕСТЬNULL(Розница.Дата, ДАТАВРЕМЯ(1,1,1))), ДЕНЬ)
	|		КОГДА НЕ Потребление.Ссылка ЕСТЬ NULL
	|			ТОГДА НАЧАЛОПЕРИОДА(Потребление.Дата, ДЕНЬ)
	|		КОГДА НЕ Передача.Ссылка ЕСТЬ NULL
	|			ТОГДА НАЧАЛОПЕРИОДА(Передача.Дата, ДЕНЬ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1,1,1) КОНЕЦ),
	|	ВЫБОР КОГДА (НЕ Возврат.Ссылка ЕСТЬ NULL ИЛИ НЕ Корректировка.Ссылка ЕСТЬ NULL)
	|		И Партии.КорВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
	|		И (НАЧАЛОПЕРИОДА(Возврат.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Реализация.Дата, МЕСЯЦ)
	|			ИЛИ НАЧАЛОПЕРИОДА(Возврат.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Розница.Дата, МЕСЯЦ)
	|			ИЛИ НАЧАЛОПЕРИОДА(Корректировка.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Реализация.Дата, МЕСЯЦ)
	|			ИЛИ &ДляСтоимостиПродаж)
	|	ТОГДА
	|		Партии.КорВидЗапасов
	|	ИНАЧЕ
	|		Партии.ВидЗапасов
	|	КОНЕЦ,
	|	Партии.Регистратор,
	|	ВЫБОР КОГДА НЕ Возврат.Ссылка ЕСТЬ NULL
	|		И Партии.КорАналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|		И (НАЧАЛОПЕРИОДА(Возврат.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Реализация.Дата, МЕСЯЦ)
	|			ИЛИ НАЧАЛОПЕРИОДА(Возврат.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Розница.Дата, МЕСЯЦ)
	|			ИЛИ Возврат.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера)
	|			ИЛИ &ДляСтоимостиПродаж)
	|	ТОГДА
	|		Партии.КорАналитикаУчетаНоменклатуры 
	|	КОГДА Партии.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтПереработчика)
	|		ТОГДА Партии.КорАналитикаУчетаНоменклатуры
	|	ИНАЧЕ
	|		Партии.АналитикаУчетаНоменклатуры
	|	КОНЕЦ,
	|	(ВЫБОР
	|		КОГДА Партии.ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваров),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваровСПереоценкой),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваров),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваровСПереоценкой))
	|		 И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА Партии.КорАналитикаУчетаНоменклатуры
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КОНЕЦ),
	|	Партии.Номенклатура,
	|	Партии.Характеристика,
	|	Партии.Период,
	|	Партии.СтатьяСписанияНДС,
	|	Партии.АналитикаСписанияНДС
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Партии.Организация,
	|	Партии.АналитикаУчетаПродукции,
	|	Партии.ДокументВыпуска,
	|	Партии.Регистратор
	|ПОМЕСТИТЬ ЗатратыНаВыпуск
	|ИЗ
	|	РегистрНакопления.ПартииЗатратНаВыпуск КАК Партии
	|ГДЕ
	|	Партии.Период МЕЖДУ &НачалоПериода И &КонецПериода	
	|	И Партии.Активность
	|	И Партии.Организация В (&МассивОрганизаций)
	|ИНДЕКСИРОВАТЬ ПО
	|	Партии.Регистратор,
	|	Партии.Организация,
	|	Партии.АналитикаУчетаПродукции,
	|	Партии.ДокументВыпуска
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Партии.Организация КАК Организация,
	|	Партии.ВидЗапасов КАК ВидЗапасов,
	|	Партии.Регистратор КАК Регистратор,
	|	Партии.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Партии.ДокументПоступления КАК ДокументПоступления
	|
	|ПОМЕСТИТЬ СборкиТоваровВидыЗапасов
	|ИЗ
	|	РегистрНакопления.ПартииТоваровОрганизаций КАК Партии
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ЗатратыНаВыпуск КАК ПартииЗатратНаВыпуск
	|	ПО
	|		Партии.Регистратор = ПартииЗатратНаВыпуск.Регистратор
	|		И Партии.Организация = ПартииЗатратНаВыпуск.Организация
	|		И Партии.АналитикаУчетаНоменклатуры = ПартииЗатратНаВыпуск.АналитикаУчетаПродукции
	|		И Партии.ДокументПоступления = ПартииЗатратНаВыпуск.ДокументВыпуска
	|ГДЕ
	|	Партии.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Партии.Активность
	|	И Партии.Организация В (&МассивОрганизаций)
	|	И (
	|		Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИЛИ Партии.ХозяйственнаяОперация В(
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеТоваров),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазборкаТоваров),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваров),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваров)
	|			)
	|		)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Партии.Организация КАК Организация,
	|	Партии.ВидЗапасов КАК ВидЗапасов,
	|	Партии.Регистратор КАК Регистратор,
	|	Партии.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Партии.ДокументПоступления КАК ДокументПоступления
	|ИЗ
	|	РегистрНакопления.ПартииТоваровПереданныеНаКомиссию КАК Партии
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ЗатратыНаВыпуск КАК ПартииЗатратНаВыпуск
	|	ПО
	|		Партии.Организация = ПартииЗатратНаВыпуск.Организация
	|		И Партии.Регистратор = ПартииЗатратНаВыпуск.Регистратор
	|		И Партии.АналитикаУчетаНоменклатуры = ПартииЗатратНаВыпуск.АналитикаУчетаПродукции
	|		И Партии.ДокументПоступления = ПартииЗатратНаВыпуск.ДокументВыпуска
	|ГДЕ
	|	Партии.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Партии.Активность
	|	И Партии.Организация В (&МассивОрганизаций)
	|	И (
	|		Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИЛИ Партии.ХозяйственнаяОперация В(
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию)
	|			)
	|		)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Партии.Организация КАК Организация,
	|	Партии.ВидЗапасов КАК ВидЗапасов,
	|	Партии.Регистратор КАК Регистратор,
	|	Партии.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Партии.ДокументПоступления КАК ДокументПоступления
	|ИЗ
	|	РегистрНакопления.ПартииПроизводственныхЗатрат КАК Партии
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ЗатратыНаВыпуск КАК ПартииЗатратНаВыпуск
	|	ПО
	|		Партии.Организация = ПартииЗатратНаВыпуск.Организация
	|		И Партии.Регистратор = ПартииЗатратНаВыпуск.Регистратор
	|		И Партии.АналитикаУчетаНоменклатуры = ПартииЗатратНаВыпуск.АналитикаУчетаПродукции
	|		И Партии.ДокументПоступления = ПартииЗатратНаВыпуск.ДокументВыпуска
	|ГДЕ
	|	Партии.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Партии.Активность
	|	И Партии.Организация В (&МассивОрганизаций)
	|	И (Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	Партии.Организация 						КАК Организация,
	|	(ВЫБОР
	|		КОГДА Возврат.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера)
	|			ТОГДА ДАТАВРЕМЯ(1,1,1)
	|		КОГДА НЕ Возврат.Ссылка ЕСТЬ NULL ИЛИ НЕ Корректировка.Ссылка ЕСТЬ NULL
	|			ТОГДА НАЧАЛОПЕРИОДА(ЕСТЬNULL(Реализация.Дата, ЕСТЬNULL(Розница.Дата, ДАТАВРЕМЯ(1,1,1))), ДЕНЬ)
	|		КОГДА НЕ Потребление.Ссылка ЕСТЬ NULL
	|			ТОГДА НАЧАЛОПЕРИОДА(Потребление.Дата, ДЕНЬ)
	|		КОГДА НЕ Передача.Ссылка ЕСТЬ NULL
	|			ТОГДА НАЧАЛОПЕРИОДА(Передача.Дата, ДЕНЬ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1,1,1) КОНЕЦ) КАК ПериодПродажи,
	|	Партии.Регистратор 						КАК Регистратор,
	|	ВЫБОР
	|		КОГДА НЕ Возврат.Ссылка ЕСТЬ NULL
	|			И Партии.КорАналитикаУчетаПродукции <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|			И (НАЧАЛОПЕРИОДА(Партии.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Реализация.Дата, МЕСЯЦ)
	|				ИЛИ НАЧАЛОПЕРИОДА(Партии.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Розница.Дата, МЕСЯЦ)
	|				ИЛИ &ДляСтоимостиПродаж)
	|		ТОГДА Партии.КорАналитикаУчетаПродукции
	|		ИНАЧЕ Партии.АналитикаУчетаПродукции
	|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА (НЕ Возврат.Ссылка ЕСТЬ NULL ИЛИ НЕ Корректировка.Ссылка ЕСТЬ NULL)
	|			И (НАЧАЛОПЕРИОДА(Партии.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Реализация.Дата, МЕСЯЦ)
	|				ИЛИ НАЧАЛОПЕРИОДА(Партии.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Розница.Дата, МЕСЯЦ)
	|				ИЛИ &ДляСтоимостиПродаж)
	|			ТОГДА Партии.КорВидЗапасов
	|		КОГДА Партии.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту)
	|		 ИЛИ Партии.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию)
	|			И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Партии.КорВидЗапасов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК КорВидЗапасов,
	|	(ВЫБОР
	|		КОГДА Партии.ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваров),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваровСПереоценкой),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваров),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваровСПереоценкой))
	|		 И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА Партии.КорАналитикаУчетаПродукции
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КОНЕЦ) КАК КорАналитикаУчетаПродукции,
	|	0 КАК Количество,
	|	СУММА(
	|		ВЫБОР КОГДА Партии.Знаменатель < 0 ТОГДА
	|			0 - Партии.Стоимость
	|		ИНАЧЕ
	|			Партии.Стоимость
	|		КОНЕЦ) КАК Стоимость,
	|	СУММА(
	|		ВЫБОР КОГДА Партии.Знаменатель < 0 ТОГДА
	|			0 - Партии.СтоимостьБезНДС
	|		ИНАЧЕ
	|			Партии.СтоимостьБезНДС
	|		КОНЕЦ) КАК СтоимостьБезНДС,
	|	СУММА(
	|		ВЫБОР КОГДА Партии.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|		ТОГДА
	|			ВЫБОР КОГДА Партии.Знаменатель < 0 ТОГДА
	|				0 - Партии.СтоимостьРегл - Партии.НДСРегл 
	|			ИНАЧЕ
	|				Партии.СтоимостьРегл + Партии.НДСРегл 
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫБОР КОГДА Партии.Знаменатель < 0 ТОГДА
	|				0 - Партии.СтоимостьРегл 
	|			ИНАЧЕ
	|				Партии.СтоимостьРегл
	|			КОНЕЦ
	|		КОНЕЦ
	|	)										КАК СтоимостьРегл,
	|	СУММА(
	|		ВЫБОР КОГДА Партии.СтатьяСписанияНДС <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА 0
	|		КОГДА Партии.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСНеУчитываетсяВСтоимости)
	|		 И Партии.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|		ТОГДА
	|			ВЫБОР КОГДА Партии.Знаменатель < 0 ТОГДА
	|				0 - Партии.НДСРегл
	|			ИНАЧЕ
	|				Партии.НДСРегл
	|			КОНЕЦ
	|
	|		КОГДА Партии.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|		 И Партии.НалогообложениеНДС В (&ТипыНалогообложенияНДСНеУчитываетсяВСтоимости)
	|		ТОГДА
	|			ВЫБОР КОГДА Партии.Знаменатель < 0 ТОГДА
	|				Партии.НДСРегл
	|			ИНАЧЕ
	|				- Партии.НДСРегл
	|			КОНЕЦ
	|		ИНАЧЕ
	|			0
	|		КОНЕЦ
	|	) КАК НДСРегл,
	|	СУММА(
	|		ВЫБОР КОГДА Партии.СтатьяСписанияНДС <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА ВЫБОР КОГДА Партии.Знаменатель < 0 ТОГДА -Партии.НДСРегл ИНАЧЕ Партии.НДСРегл КОНЕЦ
	|		ИНАЧЕ
	|			0
	|		КОНЕЦ
	|	) КАК СписаниеНДС,
	|
	|	СУММА(
	|		ВЫБОР КОГДА Партии.Знаменатель < 0 ТОГДА
	|			0 - Партии.ПостояннаяРазница
	|		ИНАЧЕ
	|			Партии.ПостояннаяРазница
	|		КОНЕЦ) КАК ПостояннаяРазница,
	|	СУММА(
	|		ВЫБОР КОГДА Партии.Знаменатель < 0 ТОГДА
	|			0 - Партии.ВременнаяРазница
	|		ИНАЧЕ
	|			Партии.ВременнаяРазница
	|		КОНЕЦ) КАК ВременнаяРазница,
	|	ВЫБОР КОГДА НЕ Возврат.Ссылка ЕСТЬ NULL
	|		И Партии.КорАналитикаУчетаПродукции <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|	ТОГДА
	|		Партии.КорАналитикаУчетаПродукции 
	|	ИНАЧЕ
	|		Партии.АналитикаУчетаПродукции
	|	КОНЕЦ КАК АналитикаУчетаПродукции,
	|	Партии.АналитикаУчетаПродукции КАК ИсходнаяАналитикаУчетаПродукции,
	|	Партии.ДокументВыпуска,
	|	Партии.Период,
	|	Партии.СтатьяСписанияНДС,
	|	Партии.АналитикаСписанияНДС
	|ПОМЕСТИТЬ СборкиТоваровПартии
	|ИЗ
	|	РегистрНакопления.ПартииЗатратНаВыпуск КАК Партии
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента КАК Возврат
	|		ПО Возврат.Ссылка = Партии.Регистратор
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК Корректировка
	|		ПО Корректировка.Ссылка = Партии.Регистратор
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК Реализация
	|		ПО Реализация.Ссылка = Партии.ДокументИсточник
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах КАК Розница
	|		ПО Розница.Ссылка = Партии.ДокументИсточник
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВнутреннееПотреблениеТоваров КАК Потребление
	|		ПО Потребление.Ссылка = Партии.ДокументИсточник
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаТоваровМеждуОрганизациями КАК Передача
	|		ПО Передача.Ссылка = Партии.ДокументИсточник
	|		И Партии.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровМеждуОрганизациями)
	|		И Партии.Знаменатель < 0
	|ГДЕ
	|	Партии.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Партии.Активность
	|	И Партии.Организация В (&МассивОрганизаций)
	|	И НЕ (Партии.Регистратор ССЫЛКА Документ.КорректировкаНазначенияТоваров И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|	И НЕ (Партии.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
	|		И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|	И НЕ (Партии.Регистратор ССЫЛКА Документ.КорректировкаВидаДеятельностиНДС И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|
	|СГРУППИРОВАТЬ ПО
	|	Партии.Организация,
	|	(ВЫБОР
	|		КОГДА Возврат.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера)
	|			ТОГДА ДАТАВРЕМЯ(1,1,1)
	|		КОГДА НЕ Возврат.Ссылка ЕСТЬ NULL ИЛИ НЕ Корректировка.Ссылка ЕСТЬ NULL
	|			ТОГДА НАЧАЛОПЕРИОДА(ЕСТЬNULL(Реализация.Дата, ЕСТЬNULL(Розница.Дата, ДАТАВРЕМЯ(1,1,1))), ДЕНЬ)
	|		КОГДА НЕ Потребление.Ссылка ЕСТЬ NULL
	|			ТОГДА НАЧАЛОПЕРИОДА(Потребление.Дата, ДЕНЬ)
	|		КОГДА НЕ Передача.Ссылка ЕСТЬ NULL
	|			ТОГДА НАЧАЛОПЕРИОДА(Передача.Дата, ДЕНЬ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1,1,1) КОНЕЦ),
	|	Партии.Регистратор,
	|	ВЫБОР
	|		КОГДА НЕ Возврат.Ссылка ЕСТЬ NULL
	|			И Партии.КорАналитикаУчетаПродукции <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|			И (НАЧАЛОПЕРИОДА(Партии.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Реализация.Дата, МЕСЯЦ)
	|				ИЛИ НАЧАЛОПЕРИОДА(Партии.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Розница.Дата, МЕСЯЦ)
	|				ИЛИ &ДляСтоимостиПродаж)
	|		ТОГДА Партии.КорАналитикаУчетаПродукции
	|		ИНАЧЕ Партии.АналитикаУчетаПродукции
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА (НЕ Возврат.Ссылка ЕСТЬ NULL ИЛИ НЕ Корректировка.Ссылка ЕСТЬ NULL)
	|			И (НАЧАЛОПЕРИОДА(Партии.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Реализация.Дата, МЕСЯЦ)
	|				ИЛИ НАЧАЛОПЕРИОДА(Партии.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Розница.Дата, МЕСЯЦ)
	|				ИЛИ &ДляСтоимостиПродаж)
	|			ТОГДА Партии.КорВидЗапасов
	|		КОГДА Партии.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту)
	|		 ИЛИ Партии.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию)
	|			И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Партии.КорВидЗапасов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР КОГДА НЕ Возврат.Ссылка ЕСТЬ NULL
	|		И Партии.КорАналитикаУчетаПродукции <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|	ТОГДА
	|		Партии.КорАналитикаУчетаПродукции 
	|	ИНАЧЕ
	|		Партии.АналитикаУчетаПродукции
	|	КОНЕЦ,
	|	Партии.АналитикаУчетаПродукции,
	|	(ВЫБОР
	|		КОГДА Партии.ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваров),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваровСПереоценкой),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваров),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваровСПереоценкой))
	|		 И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА Партии.КорАналитикаУчетаПродукции
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КОНЕЦ),
	|	Партии.ДокументВыпуска,
	|	Партии.Период,
	|	Партии.СтатьяСписанияНДС,
	|	Партии.АналитикаСписанияНДС
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	Партии.Организация,
	|	(ВЫБОР
	|		КОГДА Партии.КорВидЗапасов <> НЕОПРЕДЕЛЕНО ТОГДА Партии.КорВидЗапасов
	|		ИНАЧЕ ТаблицаПартий.ВидЗапасов КОНЕЦ) КАК ВидЗапасов,
	|	Партии.Регистратор,
	|	Партии.АналитикаУчетаНоменклатуры,
	|	Партии.ПериодПродажи,
	|	Партии.Количество,
	|	Партии.Стоимость,
	|	Партии.СтоимостьБезНДС,
	|	Партии.СтоимостьРегл,
	|	Партии.НДСРегл,
	|	Партии.СписаниеНДС,
	|	Партии.ПостояннаяРазница,
	|	Партии.ВременнаяРазница,
	|	Партии.АналитикаУчетаПродукции,
	|	Партии.КорАналитикаУчетаПродукции,
	|	Партии.Период,
	|	Партии.СтатьяСписанияНДС,
	|	Партии.АналитикаСписанияНДС
	|ПОМЕСТИТЬ СборкиТоваров
	|ИЗ
	|	СборкиТоваровПартии КАК Партии
	|	ЛЕВОЕ СОЕДИНЕНИЕ СборкиТоваровВидыЗапасов КАК ТаблицаПартий
	|		ПО ТаблицаПартий.Организация = Партии.Организация
	|		И ТаблицаПартий.АналитикаУчетаНоменклатуры = Партии.ИсходнаяАналитикаУчетаПродукции
	|		И ТаблицаПартий.ДокументПоступления = Партии.ДокументВыпуска
	|		И ТаблицаПартий.Регистратор = Партии.Регистратор
	|		И Партии.КорВидЗапасов = НЕОПРЕДЕЛЕНО
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Партии.Регистратор КАК Ссылка,
	|	Партии.Период КАК Дата,
	|	Партии.ДокументПоступления,
	|	Партии.АналитикаУчетаНоменклатуры,
	|	Партии.КорАналитикаУчетаНоменклатуры,
	|	Партии.ВидЗапасов,
	|	Партии.КорВидЗапасов
	|ПОМЕСТИТЬ Возвраты
	|ИЗ
	|	РегистрНакопления.ПартииТоваровОрганизаций КАК Партии
	|ГДЕ
	|	Партии.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Партии.Активность
	|	И Партии.Организация В (&МассивОрганизаций)
	|	И (Партии.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента
	|		ИЛИ Партии.Регистратор ССЫЛКА Документ.КорректировкаРеализации)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	ПартииРасходов.Организация,
	|	(ВЫБОР
	|		КОГДА Возврат.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера)
	|			ТОГДА ДАТАВРЕМЯ(1,1,1)
	|		КОГДА НЕ Возврат.Ссылка ЕСТЬ NULL ИЛИ НЕ Корректировка.Ссылка ЕСТЬ NULL
	|			ТОГДА НАЧАЛОПЕРИОДА(ЕСТЬNULL(Реализация.Дата, ЕСТЬNULL(Розница.Дата, ДАТАВРЕМЯ(1,1,1))), ДЕНЬ)
	|		КОГДА НЕ Потребление.Ссылка ЕСТЬ NULL
	|			ТОГДА НАЧАЛОПЕРИОДА(Потребление.Дата, ДЕНЬ)
	|		КОГДА НЕ Передача.Ссылка ЕСТЬ NULL
	|			ТОГДА НАЧАЛОПЕРИОДА(Передача.Дата, ДЕНЬ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1,1,1) КОНЕЦ) КАК ПериодПродажи,
	|	ПартииРасходов.Регистратор,
	|	ВЫБОР
	|		КОГДА НЕ Партии.Ссылка ЕСТЬ NULL
	|			И ПартииРасходов.КорАналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|			И (НАЧАЛОПЕРИОДА(Партии.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Реализация.Дата, МЕСЯЦ)
	|				ИЛИ НАЧАЛОПЕРИОДА(Партии.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Розница.Дата, МЕСЯЦ)
	|				ИЛИ &ДляСтоимостиПродаж)
	|		ТОГДА ПартииРасходов.КорАналитикаУчетаНоменклатуры
	|		ИНАЧЕ ПартииРасходов.АналитикаУчетаНоменклатуры
	|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
	|	
	|	ВЫБОР
	|		КОГДА НЕ Партии.Ссылка ЕСТЬ NULL
	|			И ПартииРасходов.КорАналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|			И (НАЧАЛОПЕРИОДА(Партии.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Реализация.Дата, МЕСЯЦ)
	|				ИЛИ НАЧАЛОПЕРИОДА(Партии.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Розница.Дата, МЕСЯЦ)
	|				ИЛИ &ДляСтоимостиПродаж)
	|		ТОГДА Партии.КорВидЗапасов
	|		ИНАЧЕ ПартииРасходов.ВидЗапасов
	|	КОНЕЦ КАК ВидЗапасов,
	|	
	|	(ВЫБОР
	|		КОГДА ПартииРасходов.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции)
	|			ТОГДА ПартииРасходов.КорАналитикаУчетаНоменклатуры
	|		КОГДА ПартииРасходов.ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваров),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваровСПереоценкой),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваров),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваровСПереоценкой))
	|		 И ПартииРасходов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА ПартииРасходов.КорАналитикаУчетаНоменклатуры
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КОНЕЦ) КАК АналитикаУчетаПродукции,
	|	СУММА(
	|		ВЫБОР КОГДА ПартииРасходов.Количество < 0 ТОГДА
	|			0 - ПартииРасходов.Стоимость
	|		ИНАЧЕ
	|			ПартииРасходов.Стоимость
	|		КОНЕЦ
	|	) КАК Стоимость,
	|	СУММА(
	|		ВЫБОР КОГДА ПартииРасходов.Количество < 0 ТОГДА
	|			0 - ПартииРасходов.СтоимостьБезНДС
	|		ИНАЧЕ
	|			ПартииРасходов.СтоимостьБезНДС
	|		КОНЕЦ
	|	) КАК СтоимостьБезНДС,
	|	СУММА(
	|		ВЫБОР КОГДА ПартииРасходов.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|		ТОГДА
	|			ВЫБОР КОГДА ПартииРасходов.Количество < 0 ТОГДА
	|				0 - ПартииРасходов.СтоимостьРегл - ПартииРасходов.НДСРегл 
	|			ИНАЧЕ
	|				ПартииРасходов.СтоимостьРегл + ПартииРасходов.НДСРегл 
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫБОР КОГДА ПартииРасходов.Количество < 0 ТОГДА
	|				0 - ПартииРасходов.СтоимостьРегл 
	|			ИНАЧЕ
	|				ПартииРасходов.СтоимостьРегл
	|			КОНЕЦ
	|		КОНЕЦ
	|		) КАК СтоимостьРегл,
	|	СУММА(
	|		ВЫБОР КОГДА ПартииРасходов.СтатьяСписанияНДС <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА 0
	|		КОГДА ПартииРасходов.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСНеУчитываетсяВСтоимости)
	|		 И ПартииРасходов.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|		ТОГДА
	|			ВЫБОР КОГДА ПартииРасходов.Количество < 0 ТОГДА
	|				0 - ПартииРасходов.НДСРегл
	|			ИНАЧЕ
	|				ПартииРасходов.НДСРегл
	|			КОНЕЦ
	|
	|		КОГДА ПартииРасходов.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|		 И ПартииРасходов.НалогообложениеНДС В (&ТипыНалогообложенияНДСНеУчитываетсяВСтоимости)
	|		ТОГДА
	|			ВЫБОР КОГДА ПартииРасходов.Количество < 0 ТОГДА
	|				ПартииРасходов.НДСРегл
	|			ИНАЧЕ
	|				- ПартииРасходов.НДСРегл
	|			КОНЕЦ
	|		ИНАЧЕ
	|			0
	|		КОНЕЦ
	|	) КАК НДСРегл,
	|	СУММА(
	|		ВЫБОР КОГДА ПартииРасходов.СтатьяСписанияНДС <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА ВЫБОР КОГДА ПартииРасходов.Количество < 0 ТОГДА -ПартииРасходов.НДСРегл ИНАЧЕ ПартииРасходов.НДСРегл КОНЕЦ
	|		ИНАЧЕ
	|			0
	|		КОНЕЦ
	|	) КАК СписаниеНДС,
	|
	|	СУММА(
	|		ВЫБОР КОГДА ПартииРасходов.Количество < 0 ТОГДА
	|			0 - ПартииРасходов.ПостояннаяРазница
	|		ИНАЧЕ
	|			ПартииРасходов.ПостояннаяРазница
	|		КОНЕЦ
	|	) КАК ПостояннаяРазница,
	|	СУММА(
	|		ВЫБОР КОГДА ПартииРасходов.Количество < 0 ТОГДА
	|			0 - ПартииРасходов.ВременнаяРазница
	|		ИНАЧЕ
	|			ПартииРасходов.ВременнаяРазница
	|		КОНЕЦ
	|	) КАК ВременнаяРазница,
	|	ПартииРасходов.Период,
	|	ПартииРасходов.СтатьяСписанияНДС,
	|	ПартииРасходов.АналитикаСписанияНДС
	|ПОМЕСТИТЬ ПартииРасходов
	|ИЗ
	|	РегистрНакопления.ПартииРасходовНаСебестоимостьТоваров КАК ПартииРасходов
	|	ЛЕВОЕ СОЕДИНЕНИЕ Возвраты КАК Партии
	|		ПО Партии.Ссылка = ПартииРасходов.Регистратор
	|		И Партии.ДокументПоступления = ПартииРасходов.ДокументПоступления
	|		И Партии.АналитикаУчетаНоменклатуры = ПартииРасходов.АналитикаУчетаНоменклатуры
	|		И Партии.КорАналитикаУчетаНоменклатуры = ПартииРасходов.КорАналитикаУчетаНоменклатуры
	|		И Партии.ВидЗапасов = ПартииРасходов.ВидЗапасов
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента КАК Возврат
	|		ПО Возврат.Ссылка = ПартииРасходов.Регистратор
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК Корректировка
	|		ПО Корректировка.Ссылка = ПартииРасходов.Регистратор
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК Реализация
	|		ПО Реализация.Ссылка = ПартииРасходов.ДокументИсточник
	|		И Реализация.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссию)
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах КАК Розница
	|		ПО Розница.Ссылка = ПартииРасходов.ДокументИсточник
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВнутреннееПотреблениеТоваров КАК Потребление
	|		ПО Потребление.Ссылка = ПартииРасходов.ДокументИсточник
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаТоваровМеждуОрганизациями КАК Передача
	|		ПО Передача.Ссылка = ПартииРасходов.ДокументИсточник
	|		И ПартииРасходов.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровМеждуОрганизациями)
	|		И ПартииРасходов.Количество < 0
	|ГДЕ
	|	ПартииРасходов.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ПартииРасходов.Активность
	|	И ПартииРасходов.Организация В (&МассивОрганизаций)
	|	И НЕ (ПартииРасходов.Регистратор ССЫЛКА Документ.КорректировкаНазначенияТоваров И ПартииРасходов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|	И НЕ (ПартииРасходов.Регистратор ССЫЛКА Документ.КорректировкаПриобретения И ПартииРасходов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|	И НЕ (ПартииРасходов.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
	|		И ПартииРасходов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|	И НЕ (ПартииРасходов.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссиюВДругуюОрганизацию)
	|		И ПартииРасходов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|
	|СГРУППИРОВАТЬ ПО
	|	ПартииРасходов.Организация,
	|	(ВЫБОР
	|		КОГДА Возврат.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера)
	|			ТОГДА ДАТАВРЕМЯ(1,1,1)
	|		КОГДА НЕ Возврат.Ссылка ЕСТЬ NULL ИЛИ НЕ Корректировка.Ссылка ЕСТЬ NULL
	|			ТОГДА НАЧАЛОПЕРИОДА(ЕСТЬNULL(Реализация.Дата, ЕСТЬNULL(Розница.Дата, ДАТАВРЕМЯ(1,1,1))), ДЕНЬ)
	|		КОГДА НЕ Потребление.Ссылка ЕСТЬ NULL
	|			ТОГДА НАЧАЛОПЕРИОДА(Потребление.Дата, ДЕНЬ)
	|		КОГДА НЕ Передача.Ссылка ЕСТЬ NULL
	|			ТОГДА НАЧАЛОПЕРИОДА(Передача.Дата, ДЕНЬ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1,1,1) КОНЕЦ),
	|	ПартииРасходов.Регистратор,
	|	ВЫБОР
	|		КОГДА НЕ Партии.Ссылка ЕСТЬ NULL
	|			И ПартииРасходов.КорАналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|			И (НАЧАЛОПЕРИОДА(Партии.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Реализация.Дата, МЕСЯЦ)
	|				ИЛИ НАЧАЛОПЕРИОДА(Партии.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Розница.Дата, МЕСЯЦ)
	|				ИЛИ &ДляСтоимостиПродаж)
	|		ТОГДА ПартииРасходов.КорАналитикаУчетаНоменклатуры
	|		ИНАЧЕ ПартииРасходов.АналитикаУчетаНоменклатуры
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ Партии.Ссылка ЕСТЬ NULL
	|			И ПартииРасходов.КорАналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|			И (НАЧАЛОПЕРИОДА(Партии.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Реализация.Дата, МЕСЯЦ)
	|				ИЛИ НАЧАЛОПЕРИОДА(Партии.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Розница.Дата, МЕСЯЦ)
	|				ИЛИ &ДляСтоимостиПродаж)
	|		ТОГДА Партии.КорВидЗапасов
	|		ИНАЧЕ ПартииРасходов.ВидЗапасов
	|	КОНЕЦ,
	|	(ВЫБОР
	|		КОГДА ПартииРасходов.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции)
	|			ТОГДА ПартииРасходов.КорАналитикаУчетаНоменклатуры
	|		КОГДА ПартииРасходов.ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваров),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваровСПереоценкой),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваров),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваровСПереоценкой))
	|		 И ПартииРасходов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА ПартииРасходов.КорАналитикаУчетаНоменклатуры
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КОНЕЦ),
	|	ПартииРасходов.Период,
	|	ПартииРасходов.СтатьяСписанияНДС,
	|	ПартииРасходов.АналитикаСписанияНДС
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	Партии.Организация,
	|	Партии.ВидЗапасов КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА Партии.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
	|		ИНАЧЕ ЕСТЬNULL(Партии.ВидЗапасов.ТипЗапасов, ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар))
	|	КОНЕЦ КАК ТипЗапасов,
	|	Партии.Регистратор,
	|	(ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям ТОГДА Партии.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ АналитикаБезНазначения.КлючАналитики КОНЕЦ) КАК АналитикаУчетаНоменклатуры,
	|	(ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям ТОГДА Партии.АналитикаУчетаПродукции
	|		КОГДА АналитикаПродукцииБезНазначения.КлючАналитики ЕСТЬ NULL ТОГДА Партии.АналитикаУчетаПродукции
	|		ИНАЧЕ АналитикаПродукцииБезНазначения.КлючАналитики КОНЕЦ) КАК АналитикаУчетаПродукции,
	|	(ВЫБОР
	|		КОГДА &ДляСтоимостиПродаж ТОГДА ДАТАВРЕМЯ(1,1,1)
	|		ИНАЧЕ Партии.ПериодПродажи КОНЕЦ) КАК ПериодПродажи,
	|	Партии.Период,
	|	Партии.СтатьяСписанияНДС,
	|	Партии.АналитикаСписанияНДС,
	|	СУММА(
	|		ВЫБОР
	|			КОГДА Партии.Количество < 0 ТОГДА 0 - Партии.Количество
	|			ИНАЧЕ Партии.Количество КОНЕЦ) КАК Количество,
	|	СУММА(Партии.Стоимость) КАК Стоимость,
	|	СУММА(Партии.СтоимостьБезНДС) КАК СтоимостьБезНДС,
	|	СУММА(Партии.ДопРасходы) КАК ДопРасходы,
	|	СУММА(Партии.ДопРасходыБезНДС) КАК ДопРасходыБезНДС,
	|	СУММА(Партии.СтоимостьРегл) КАК СтоимостьРегл,
	|	СУММА(Партии.НДСРегл) КАК НДСРегл,
	|	0 КАК НДСУпр,
	|	СУММА(Партии.СписаниеНДС) КАК СписаниеНДС,
	|	0 КАК СписаниеНДСУпр,
	|	СУММА(Партии.ПостояннаяРазница) КАК ПостояннаяРазница,
	|	СУММА(Партии.ВременнаяРазница) КАК ВременнаяРазница
	|
	|ПОМЕСТИТЬ ВтПартии
	|ИЗ (
	|	ВЫБРАТЬ
	|		Таблица.Организация КАК Организация,
	|		Таблица.ВидЗапасов КАК ВидЗапасов,
	|		Таблица.Регистратор КАК Регистратор,
	|		Таблица.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		Таблица.КорАналитикаУчетаНоменклатуры КАК АналитикаУчетаПродукции,
	|		Таблица.ПериодПродажи КАК ПериодПродажи,
	|		Таблица.Количество КАК Количество,
	|		Таблица.Стоимость КАК Стоимость,
	|		Таблица.СтоимостьБезНДС КАК СтоимостьБезНДС,
	|		0 КАК ДопРасходы,
	|		0 КАК ДопРасходыБезНДС,
	|		Таблица.СтоимостьРегл КАК СтоимостьРегл,
	|		Таблица.НДСРегл КАК НДСРегл,
	|		Таблица.ПостояннаяРазница КАК ПостояннаяРазница,
	|		Таблица.ВременнаяРазница КАК ВременнаяРазница,
	|		Таблица.СписаниеНДС КАК СписаниеНДС,
	|		Таблица.Период,
	|		Таблица.СтатьяСписанияНДС,
	|		Таблица.АналитикаСписанияНДС
	|	ИЗ
	|		ТаблицаПартий КАК Таблица
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ПартииРасходов.Организация КАК Организация,
	|		ПартииРасходов.ВидЗапасов КАК ВидЗапасов,
	|		ПартииРасходов.Регистратор КАК Регистратор,
	|		ПартииРасходов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ПартииРасходов.АналитикаУчетаПродукции,
	|		ПартииРасходов.ПериодПродажи КАК ПериодПродажи,
	|		0 КАК Количество,
	|		0 КАК Стоимость,
	|		0 КАК СтоимостьБезНДС,
	|		ПартииРасходов.Стоимость КАК ДопРасходы,
	|		ПартииРасходов.СтоимостьБезНДС КАК ДопРасходыБезНДС,
	|		ПартииРасходов.СтоимостьРегл КАК СтоимостьРегл,
	|		ПартииРасходов.НДСРегл КАК НДСРегл,
	|		ПартииРасходов.ПостояннаяРазница КАК ПостояннаяРазница,
	|		ПартииРасходов.ВременнаяРазница КАК ВременнаяРазница,
	|		ПартииРасходов.СписаниеНДС КАК СписаниеНДС,
	|		ПартииРасходов.Период,
	|		ПартииРасходов.СтатьяСписанияНДС,
	|		ПартииРасходов.АналитикаСписанияНДС
	|	ИЗ
	|		ПартииРасходов КАК ПартииРасходов
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Таблица.Организация КАК Организация,
	|		Таблица.ВидЗапасов КАК ВидЗапасов,
	|		Таблица.Регистратор КАК Регистратор,
	|		Таблица.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		Таблица.КорАналитикаУчетаПродукции КАК АналитикаУчетаПродукции,
	|		Таблица.ПериодПродажи КАК ПериодПродажи,
	|		Таблица.Количество КАК Количество,
	|		Таблица.Стоимость КАК Стоимость,
	|		Таблица.СтоимостьБезНДС КАК СтоимостьБезНДС,
	|		0 КАК ДопРасходы,
	|		0 КАК ДопРасходыБезНДС,
	|		Таблица.СтоимостьРегл КАК СтоимостьРегл,
	|		Таблица.НДСРегл КАК НДСРегл,
	|		Таблица.ПостояннаяРазница КАК ПостояннаяРазница,
	|		Таблица.ВременнаяРазница КАК ВременнаяРазница,
	|		Таблица.СписаниеНДС КАК СписаниеНДС,
	|		Таблица.Период,
	|		Таблица.СтатьяСписанияНДС,
	|		Таблица.АналитикаСписанияНДС
	|	ИЗ
	|		СборкиТоваров КАК Таблица
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ПартииНаКомиссии.Организация,
	|		ПартииНаКомиссии.ВидЗапасов,
	|		ПартииНаКомиссии.Регистратор,
	|		ПартииНаКомиссии.АналитикаУчетаНоменклатуры,
	|		ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаПродукции,
	|		ДАТАВРЕМЯ(1,1,1) КАК ПериодПродажи,
	|		ВЫБОР
	|			КОГДА ПартииНаКомиссии.Количество < 0 
	|				ТОГДА 0 - ПартииНаКомиссии.Количество
	|			ИНАЧЕ ПартииНаКомиссии.Количество
	|		КОНЕЦ КАК Количество,
	|		ВЫБОР
	|			КОГДА ПартииНаКомиссии.Количество < 0 
	|				ТОГДА 0 - ПартииНаКомиссии.Стоимость
	|			ИНАЧЕ ПартииНаКомиссии.Стоимость
	|		КОНЕЦ КАК Стоимость,
	|		ВЫБОР
	|			КОГДА ПартииНаКомиссии.Количество < 0 
	|				ТОГДА 0 - ПартииНаКомиссии.СтоимостьБезНДС
	|			ИНАЧЕ ПартииНаКомиссии.СтоимостьБезНДС
	|		КОНЕЦ КАК СтоимостьБезНДС,
	|		0,
	|		0,
	|		ВЫБОР
	|			КОГДА ПартииНаКомиссии.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|				ТОГДА
	|					ВЫБОР КОГДА ПартииНаКомиссии.Количество < 0 ТОГДА
	|						0 - (ПартииНаКомиссии.СтоимостьРегл + ПартииНаКомиссии.НДСРегл)
	|					ИНАЧЕ
	|						ПартииНаКомиссии.СтоимостьРегл + ПартииНаКомиссии.НДСРегл
	|					КОНЕЦ
	|			ИНАЧЕ
	|				ВЫБОР КОГДА ПартииНаКомиссии.Количество < 0 ТОГДА
	|					0 - ПартииНаКомиссии.СтоимостьРегл
	|				ИНАЧЕ
	|					ПартииНаКомиссии.СтоимостьРегл
	|				КОНЕЦ
	|		КОНЕЦ КАК СтоимостьРегл,
	|		ВЫБОР КОГДА ПартииНаКомиссии.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСНеУчитываетсяВСтоимости)
	|		 И ПартииНаКомиссии.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|		ТОГДА
	|			ВЫБОР КОГДА ПартииНаКомиссии.Количество < 0 ТОГДА
	|				0 - ПартииНаКомиссии.НДСРегл
	|			ИНАЧЕ
	|				ПартииНаКомиссии.НДСРегл
	|			КОНЕЦ
	|
	|		КОГДА ПартииНаКомиссии.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|		 И ПартииНаКомиссии.НалогообложениеНДС В (&ТипыНалогообложенияНДСНеУчитываетсяВСтоимости)
	|		ТОГДА
	|			ВЫБОР КОГДА ПартииНаКомиссии.Количество < 0 ТОГДА
	|				ПартииНаКомиссии.НДСРегл
	|			ИНАЧЕ
	|				- ПартииНаКомиссии.НДСРегл
	|			КОНЕЦ
	|		ИНАЧЕ
	|			0
	|		КОНЕЦ КАК НДСРегл,
	|
	|		ВЫБОР
	|			КОГДА ПартииНаКомиссии.Количество < 0 
	|				ТОГДА 0 - ПартииНаКомиссии.ПостояннаяРазница
	|			ИНАЧЕ ПартииНаКомиссии.ПостояннаяРазница
	|		КОНЕЦ КАК ПостояннаяРазница,
	|		ВЫБОР
	|			КОГДА ПартииНаКомиссии.Количество < 0 
	|				ТОГДА 0 - ПартииНаКомиссии.ВременнаяРазница
	|			ИНАЧЕ ПартииНаКомиссии.ВременнаяРазница
	|		КОНЕЦ КАК ВременнаяРазница,
	|		0 КАК СписаниеНДС,
	|		ПартииНаКомиссии.Период,
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка),
	|		НЕОПРЕДЕЛЕНО
	|	ИЗ
	|		РегистрНакопления.ПартииТоваровПереданныеНаКомиссию КАК ПартииНаКомиссии
	|	ГДЕ
	|		ПартииНаКомиссии.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И ПартииНаКомиссии.Активность
	|		И ПартииНаКомиссии.Организация В(&МассивОрганизаций)
	|		И ПартииНаКомиссии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И НЕ ПартииНаКомиссии.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Затраты.Организация,
	|		Затраты.ВидЗапасов,
	|		Затраты.Регистратор,
	|		Затраты.АналитикаУчетаНоменклатуры,
	|		ВЫБОР
	|			КОГДА Затраты.СтатьяРасходовСписания = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|				ТОГДА ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КОНЕЦ КАК АналитикаУчетаПродукции,
	|		ЕСТЬNULL(НАЧАЛОПЕРИОДА(Корректировка.ДокументОснование.Дата, ДЕНЬ), ДАТАВРЕМЯ(1,1,1)) КАК ПериодПродажи,
	|		ВЫБОР КОГДА Затраты.Количество < 0 ТОГДА
	|			0 - Затраты.Количество
	|		ИНАЧЕ
	|			Затраты.Количество
	|		КОНЕЦ КАК Количество,
	|		ВЫБОР КОГДА Затраты.Количество < 0 ТОГДА
	|			0 - Затраты.Стоимость
	|		ИНАЧЕ
	|			Затраты.Стоимость
	|		КОНЕЦ КАК Стоимость,
	|		ВЫБОР КОГДА Затраты.Количество < 0 ТОГДА
	|			0 - Затраты.СтоимостьБезНДС
	|		ИНАЧЕ
	|			Затраты.СтоимостьБезНДС
	|		КОНЕЦ КАК СтоимостьБезНДС,
	|		0 КАК ДопРасходы,
	|		0 КАК ДопРасходыБезНДС,
	|		ВЫБОР КОГДА Затраты.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|		ТОГДА
	|			ВЫБОР КОГДА Затраты.Количество < 0 ТОГДА
	|				0 - (Затраты.СтоимостьРегл + Затраты.НДСРегл )
	|			ИНАЧЕ
	|				Затраты.СтоимостьРегл + Затраты.НДСРегл
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫБОР КОГДА Затраты.Количество < 0 ТОГДА
	|				0 - Затраты.СтоимостьРегл
	|			ИНАЧЕ
	|				Затраты.СтоимостьРегл
	|			КОНЕЦ
	|		КОНЕЦ КАК СтоимостьРегл,
	|		0 КАК НДСРегл,
	|		ВЫБОР КОГДА Затраты.Количество < 0 ТОГДА
	|			0 - Затраты.ПостояннаяРазница
	|		ИНАЧЕ
	|			Затраты.ПостояннаяРазница
	|		КОНЕЦ КАК ПостояннаяРазница,
	|		ВЫБОР КОГДА Затраты.Количество < 0 ТОГДА
	|			0 - Затраты.ВременнаяРазница
	|		ИНАЧЕ
	|			Затраты.ВременнаяРазница
	|		КОНЕЦ КАК ВременнаяРазница,
	|		ВЫБОР КОГДА Затраты.СтатьяСписанияНДС <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА ВЫБОР КОГДА Затраты.Количество < 0 ТОГДА -Затраты.НДСРегл ИНАЧЕ Затраты.НДСРегл КОНЕЦ
	|		ИНАЧЕ
	|			0
	|		КОНЕЦ КАК СписаниеНДС,
	|		Затраты.Период,
	|		Затраты.СтатьяСписанияНДС,
	|		Затраты.АналитикаСписанияНДС
	|	ИЗ
	|		РегистрНакопления.ПартииПроизводственныхЗатрат КАК Затраты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК Корректировка
	|			ПО Корректировка.Ссылка = Затраты.Регистратор
	|	ГДЕ
	|		Затраты.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Затраты.Активность
	|		И Затраты.Организация В(&МассивОрганизаций)
	|		И (Затраты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ИЛИ Затраты.Регистратор ССЫЛКА Документ.ПрочееОприходованиеТоваров
	|			ИЛИ Затраты.Регистратор ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями
	| 			)
	|
	|	) КАК Партии
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО Аналитика.Ссылка = Партии.АналитикаУчетаНоменклатуры
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|		ПО АналитикаБезНазначения.Номенклатура = Аналитика.Номенклатура
	|		И АналитикаБезНазначения.Характеристика = Аналитика.Характеристика
	|		И АналитикаБезНазначения.Серия = Аналитика.Серия
	|		И АналитикаБезНазначения.МестоХранения = Аналитика.МестоХранения
	|		И АналитикаБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаПродукции
	|		ПО АналитикаПродукции.Ссылка = Партии.АналитикаУчетаПродукции
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПродукцииБезНазначения
	|		ПО АналитикаПродукцииБезНазначения.Номенклатура = АналитикаПродукции.Номенклатура
	|		И АналитикаПродукцииБезНазначения.Характеристика = АналитикаПродукции.Характеристика
	|		И АналитикаПродукцииБезНазначения.Серия = АналитикаПродукции.Серия
	|		И АналитикаПродукцииБезНазначения.МестоХранения = АналитикаПродукции.МестоХранения
	|		И АналитикаПродукцииБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Партии.Организация,
	|	Партии.ВидЗапасов,
	|	ВЫБОР
	|		КОГДА Партии.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
	|		ИНАЧЕ ЕСТЬNULL(Партии.ВидЗапасов.ТипЗапасов, ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар))
	|	КОНЕЦ,
	|	Партии.Регистратор,
	|	(ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям ТОГДА Партии.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ АналитикаБезНазначения.КлючАналитики КОНЕЦ),
	|	(ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям ТОГДА Партии.АналитикаУчетаПродукции
	|		КОГДА АналитикаПродукцииБезНазначения.КлючАналитики ЕСТЬ NULL ТОГДА Партии.АналитикаУчетаПродукции
	|		ИНАЧЕ АналитикаПродукцииБезНазначения.КлючАналитики КОНЕЦ),
	|	(ВЫБОР
	|		КОГДА &ДляСтоимостиПродаж ТОГДА ДАТАВРЕМЯ(1,1,1)
	|		ИНАЧЕ Партии.ПериодПродажи КОНЕЦ),
	|	Партии.Период,
	|	Партии.СтатьяСписанияНДС,
	|	Партии.АналитикаСписанияНДС
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета,
		"ТаблицаПартий, ЗатратыНаВыпуск, СборкиТоваровВидыЗапасов,
		|СборкиТоваровПартии, СборкиТоваров, Возвраты, ПартииРасходов");
	
КонецПроцедуры

// Этап 2.5
Процедура ЗарегистрироватьСтоимостьФИФО(ПараметрыРасчета)

	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "ЗарегистрироватьСтоимостьФИФО");
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиКорректировкаСтоимости.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Себестоимость.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	Себестоимость.ВидЗапасов						КАК ВидЗапасов,
	|	Себестоимость.Организация						КАК Организация,
	|	Себестоимость.РазделУчета						КАК РазделУчета,
	|	СУММА(Себестоимость.Количество)					КАК Количество,
	|	СУММА(Себестоимость.Стоимость)					КАК Стоимость,
	|	СУММА(Себестоимость.СтоимостьБезНДС)			КАК СтоимостьБезНДС,
	|	СУММА(Себестоимость.ДопРасходы)			КАК СтоимостьДопРасходы,
	|	СУММА(Себестоимость.ДопРасходыБезНДС) 	КАК СтоимостьДопРасходыБезНДС,
	|	СУММА(Себестоимость.СтоимостьРегл)				КАК СтоимостьРегл
	|ПОМЕСТИТЬ ВТСебестоимостьОборот
	|ИЗ
	|	ВТКэшРасчетныеОборотыСебестоимостьТоваров КАК Себестоимость
	|ГДЕ
	|	НЕ Себестоимость.СлужебноеВидДвиженияПриход
	|
	|СГРУППИРОВАТЬ ПО
	|	Себестоимость.АналитикаУчетаНоменклатуры,
	|	Себестоимость.ВидЗапасов,
	|	Себестоимость.Организация,
	|	Себестоимость.РазделУчета,
	|	Себестоимость.Партия,
	|	Себестоимость.АналитикаУчетаПартий,
	|	Себестоимость.АналитикаФинансовогоУчета,
	|	Себестоимость.ВидДеятельностиНДС
	|
	|ИМЕЮЩИЕ
	|	СУММА(Себестоимость.Количество) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	ДокументыРасчетаСебестоимости.Ссылка		КАК ДокументДвижения,
	|	&НачалоПериода								КАК Период,
	|
	|	Себестоимость.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	Себестоимость.ВидЗапасов					КАК ВидЗапасов,
	|	Себестоимость.Организация					КАК Организация,
	|	Себестоимость.РазделУчета					КАК РазделУчета,
	|
	|	ВЫРАЗИТЬ((Себестоимость.Стоимость / Себестоимость.Количество) КАК ЧИСЛО(31,10)) 				КАК Стоимость,
	|	ВЫРАЗИТЬ((Себестоимость.СтоимостьБезНДС / Себестоимость.Количество) КАК ЧИСЛО(31,10)) 			КАК СтоимостьБезНДС,
	|	ВЫРАЗИТЬ((Себестоимость.СтоимостьДопРасходы / Себестоимость.Количество) КАК ЧИСЛО(31,10)) 		КАК СтоимостьДопРасходы,
	|	ВЫРАЗИТЬ((Себестоимость.СтоимостьДопРасходыБезНДС / Себестоимость.Количество) КАК ЧИСЛО(31,10)) КАК СтоимостьДопРасходыБезНДС,
	|	ВЫРАЗИТЬ((Себестоимость.СтоимостьРегл / Себестоимость.Количество) КАК ЧИСЛО(31,10)) 			КАК СтоимостьРегл
	|ИЗ
	|	ВТСебестоимостьОборот КАК Себестоимость
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДокументыРасчетаСебестоимости КАК ДокументыРасчетаСебестоимости
	|			ПО Себестоимость.Организация = ДокументыРасчетаСебестоимости.Организация
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.СформироватьДвиженияПоРегиструПоДаннымЗапроса(
		ПараметрыРасчета,
		Метаданные.РегистрыСведений.СтоимостьТоваров.Имя,
		Запрос);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета,	"ВТСебестоимостьОборот");
	
	РасчетСебестоимостиПрикладныеАлгоритмы.КэшироватьСформированныеДвижения(ПараметрыРасчета);
	
КонецПроцедуры

#КонецОбласти

#Область УниверсальныеПроцедурыРаботыСДвижениями

// Обертка для универсальной процедуры записи движений.
//
Процедура ЗаписатьДвиженияПоРегистру(ПараметрыРасчета, ИсходныеДанные, МенеджерРегистра)
	
	ПараметрыЗаписи = РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьПараметрыЗаписи(
		ПараметрыРасчета.РасчетныйПериод.НачалоПериода,
		ПараметрыРасчета.ОграниченияВыборки.КоличествоЗаписейВНЗ,
		ПараметрыРасчета.НомерЗаданияДоРасчета);
	
	Попытка
		РасчетСебестоимостиПрикладныеАлгоритмы.ЗаписатьДвиженияПоРегистру(
			ИсходныеДанные,
			МенеджерРегистра,
			ПараметрыЗаписи);
	Исключение
		
		// Информацию об ошибке добавим в протокол расчета, в механизме закрытия месяца ошибки уже должны быть зарегистрированы.
		ТекстДляПротокола = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		РасчетСебестоимостиПротоколРасчета.ЗафиксироватьОшибкуРасчета(
			ПараметрыРасчета,
			Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаЗаписиДвиженийПоРегистрам,
			ТекстДляПротокола);
			
		ВызватьИсключение ТекстДляПротокола;
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область УниверсальныеПроцедурыОписанияДанныхМеханизма

// Возвращает перечень объектов метаданных, на основании данных которых выполняется расчет партий.
// В перечень не включаются объекты, которые являются одновременно и исходящими данными механизмов расчета партий и себестоимости.
//
// Параметры:
//	ВходящиеДанные - Соответствие - уже инициализированное хранилище для описания входящих данных
//	ТолькоТребующиеПерерасчета - Булево - если установлен, то будет возвращен перечень только тех данных,
//		изменение которых влечет за собой необходимость перерасчета партий и себестоимости
//		При изменении этих данных должна создаваться запись в регистре сведений ЗаданияКРасчетуСебестоимости.
//
// Возвращаемое значение:
//	Соответствие - Ключ - ОбъектМетаданных.
//
Процедура ВходящиеДанныеМеханизма(ВходящиеДанные, ТолькоТребующиеПерерасчета) Экспорт
	
	Если ТолькоТребующиеПерерасчета Тогда
		Значение = Истина; // чтобы можно было проверить вхождение объекта метаданных в это соответствие
	Иначе
		Значение = Неопределено;
	КонецЕсли;
	
	
КонецПроцедуры

// Возвращает перечень регистров партионного учета версии 2.1, неиспользуемых в версии 2.2.
//
Функция НеиспользуемыеДанныеМеханизмаВерсии21(НеиспользуемыеДанные = Неопределено, Значение = Истина) Экспорт
	
	Если НеиспользуемыеДанные = Неопределено Тогда
		НеиспользуемыеДанные = Новый Соответствие;
	КонецЕсли;
	
	НеиспользуемыеДанные.Вставить(Метаданные.РегистрыНакопления.ПартииЗатратНаВыпуск, 					Значение);
	НеиспользуемыеДанные.Вставить(Метаданные.РегистрыНакопления.ПартииПроизводственныхЗатрат, 			Значение);
	НеиспользуемыеДанные.Вставить(Метаданные.РегистрыНакопления.ПартииРасходовНаСебестоимостьТоваров, 	Значение);
	НеиспользуемыеДанные.Вставить(Метаданные.РегистрыНакопления.ПартииТоваровОрганизаций, 				Значение);
	НеиспользуемыеДанные.Вставить(Метаданные.РегистрыНакопления.ПартииТоваровПереданныеНаКомиссию, 		Значение);
	
	Возврат НеиспользуемыеДанные;
	
КонецФункции

#КонецОбласти

#Область ЗаданияКРасчетуСебестоимости

// Добавляет описания регистров для их подключения к механизму дат запрета изменения.
//
Процедура ОписаниеРегистровДляКонтроляДатЗапретаИзменения(ИсточникиДанных) Экспорт
	
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "РегистрНакопления.ТоварыОрганизаций", "Период", "РегламентныеОперации", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "РегистрНакопления.ТоварыОрганизацийКПередаче", "Период", "РегламентныеОперации", "ОрганизацияВладелец");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "РегистрНакопления.ТоварыПереданныеНаКомиссию", "Период", "РегламентныеОперации", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "РегистрНакопления.ТоварыКОформлениюОтчетовКомитенту", "Период", "РегламентныеОперации");
	
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "РегистрНакопления.ПартииПроизводственныхЗатрат", "Период", "РегламентныеОперации", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "РегистрНакопления.ПартииРасходовНаСебестоимостьТоваров", "Период", "РегламентныеОперации", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "РегистрНакопления.ПартииТоваровОрганизаций", "Период", "РегламентныеОперации", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "РегистрНакопления.ПартииТоваровПереданныеНаКомиссию", "Период", "РегламентныеОперации", "Организация");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти