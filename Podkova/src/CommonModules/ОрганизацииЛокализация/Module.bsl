
#Область СлужебныйПрограммныйИнтерфейс

Процедура УстановитьПараметрыЗапросаПриОбработкеПолученияДанныхВыбора(Параметры, Запрос) Экспорт

	//++ Локализация
	
	
	//-- Локализация
	
КонецПроцедуры


//++ Локализация

// Процедура заполняет данные по патентам в форме организации.
//
// Параметры:
//  Форма                      - ФормаКлиентскогоПриложения - форма, для которой выполняется обработчик.
//
Процедура НастроитьДанныеПримененияПСН(Форма) Экспорт
	
	Организация = Форма.Объект.Ссылка;
	УчетнаяПолитикаОрганизации = Форма.УчетнаяПолитикаОрганизаций.УчетнаяПолитика;
	
	ПрименяетсяПСН =
		ЗначениеЗаполнено(УчетнаяПолитикаОрганизации)
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяПолитикаОрганизации, "ПрименяетсяПСН")
		И Форма.ВидОрганизации = "ИндивидуальныйПредприниматель";
	
	Форма.Элементы.ГруппаОтступПримененияПСН.Видимость = ПрименяетсяПСН;
		
	Если ПрименяетсяПСН Тогда
		
		СписокПатентовОрганизации = Справочники.Патенты.ПолучитьПатентыПоОрганизации(Организация, Неопределено);
		СписокДействующихПатентовОрганизации = Справочники.Патенты.ПолучитьДействующиеПатентыНаДату(Организация, Неопределено);
		СтрокаПСН = СтрШаблон(НСтр("ru='Патенты (%1 из %2)'"), СписокДействующихПатентовОрганизации.Количество(), СписокПатентовОрганизации.Количество());
		Форма.СтрокаПСН = Новый ФорматированнаяСтрока(СтрокаПСН, , , , "НастроитьТаблицуПримененияПСН");
		
	Иначе
		Форма.СтрокаПСН = Новый ФорматированнаяСтрока(НСтр("ru='Нет информации о применении ПСН.'"));
	КонецЕсли;
	
КонецПроцедуры

// Возвращает признак применения патента организацией на указанную дату по учетной политике
//
// Параметры:
//  Организация  - СправочникСсылка.Организации - Организация, по которой требуется определение
//  ДатаДействия - Дата - Дата, на которую требуется определение
// 
// Возвращаемое значение:
//  Булево - использует организация патенты на указанную дату
//
Функция ОрганизацияПрименяетПатенты(Организация, ДатаДействия = Неопределено) Экспорт
	
	ПрименяетсяПСН = Ложь;
	Если ТипЗнч(Организация) = Тип("Массив") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаДействия) Тогда
		ДатаДействия = ТекущаяДатаСеанса();
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаСрезПоследних.ПрименяетсяПСН КАК ПрименяетсяПСН
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(&Период,
	|		Организация = ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).ГоловнаяОрганизация) КАК ТаблицаСрезПоследних
	|";
	Запрос.УстановитьПараметр("Период", ДатаДействия);
	Запрос.УстановитьПараметр("Организация", Организация);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ПрименяетсяПСН = Выборка.ПрименяетсяПСН;
	КонецЕсли;
	
	Возврат ПрименяетсяПСН;
	
КонецФункции

//-- Локализация

#КонецОбласти
