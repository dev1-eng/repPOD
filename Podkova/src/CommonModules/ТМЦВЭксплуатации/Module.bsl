////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции подсистемы ТМЦ в эксплуатации.
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Процедура ВыполнитьКонтрольРезультатовПроведения(Объект, Отказ) Экспорт

	ДанныеТаблиц = Объект.ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	МассивКонтролей = Новый Массив;
	СписокЗапросов  = Новый Массив;
	
	// Контроль отрицательных оборотов ТМЦВЭксплуатации.
	Если ПроведениеСерверУТ.ЕстьИзмененияВТаблице(ДанныеТаблиц,"ДвиженияТМЦВЭксплуатацииИзменение") Тогда
		
		МассивКонтролей.Добавить(ВРег("ТМЦВЭксплуатации"));
		СписокЗапросов.Добавить(
		"ВЫБРАТЬ
		|	ТаблицаОборотов.Организация  КАК Организация,
		|	ТаблицаОборотов.Подразделение КАК Подразделение,
		|	ТаблицаОборотов.Номенклатура КАК Номенклатура,
		|	ТаблицаОборотов.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ТаблицаОборотов.Характеристика КАК Характеристика,
		|	ТаблицаОборотов.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ТаблицаОборотов.Партия КАК Партия,
		|	ТаблицаОборотов.КоличествоОборот КАК Количество
		|ИЗ
		|	РегистрНакопления.ТМЦВЭксплуатации.Обороты(,,,
		|			(Организация, Подразделение, ФизическоеЛицо, Партия) В
		|				(ВЫБРАТЬ
		|					Таблица.Организация,
		|					Таблица.Подразделение,
		|					Таблица.ФизическоеЛицо,
		|					Таблица.Партия
		|				ИЗ
		|					ДвиженияТМЦВЭксплуатацииИзменение КАК Таблица)
		|	) КАК ТаблицаОборотов
		|ГДЕ
		|	ТаблицаОборотов.КоличествоОборот < 0");
		
	КонецЕсли;
	
	Если МассивКонтролей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	ПакетЗапросов = Новый Запрос(ТекстЗапроса);
	ПакетЗапросов.МенеджерВременныхТаблиц = ДанныеТаблиц.МенеджерВременныхТаблиц;
	МассивРезультатов = ПакетЗапросов.ВыполнитьПакет();

	Итератор = -1;
	Для Каждого Результат Из МассивРезультатов Цикл

		Итератор = Итератор + 1;
		Если Результат.Пустой() Тогда
			Продолжить;
		КонецЕсли;

		ИмяКонтроля = МассивКонтролей[Итератор];

		Если ИмяКонтроля = ВРег("ТМЦВЭксплуатации") Тогда
			
			СообщитьОбОшибкахПроведенияПоРегиструТМЦВЭксплуатации(Объект, Отказ, Результат);
	
		Иначе

			ВызватьИсключение НСтр("ru = 'Ошибка контроля проведения'");

		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СообщитьОбОшибкахПроведенияПоРегиструТМЦВЭксплуатации(Объект, Отказ, РезультатЗапроса)
	
	Если ТипЗнч(Объект) = Тип("ДокументОбъект.ВнутреннееПотреблениеТоваров")
		Или ТипЗнч(Объект) = Тип("ДокументОбъект.ВводОстатков") Тогда
		
		ШаблонСообщения = НСтр("ru = 'Номенклатура %НоменклатураХарактеристика%, %Партия%
			|По ТМЦ уже оформлено списание, перемещение или возврат в количестве большем, чем указано в документе, на %Количество% %Единица%'");
	Иначе
		ШаблонСообщения = НСтр("ru = 'Номенклатура %НоменклатураХарактеристика%, %Партия%
			|Превышен оперативный остаток в подразделении %Подразделение%, на %Количество% %Единица%'");
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ТекстСообщения = СтрЗаменить(ШаблонСообщения,"%НоменклатураХарактеристика%", НоменклатураКлиентСервер.ПредставлениеНоменклатуры(Выборка.Номенклатура, Выборка.Характеристика));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Партия%", Строка(Выборка.Партия));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Подразделение%", Строка(Выборка.Подразделение));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Количество%", Строка(-Выборка.Количество));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Единица%", Строка(Выборка.ЕдиницаИзмерения));
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Объект,,, Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "ТМЦВЭксплуатации.ИспользоватьТМЦВЭксплуатации_ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.4.8.8";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("4ef67970-2b74-4971-b50a-e3eaa3e9c099");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "ТМЦВЭксплуатации.ИспользоватьТМЦВЭксплуатации_ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "";
	Обработчик.Комментарий = НСтр("ru = 'Устанавливает константу ""Использовать ТМЦ в эксплуатации""'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Константы.ИспользоватьТМЦВЭксплуатации.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Константы.ИспользоватьТМЦВЭксплуатации.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");

КонецПроцедуры

#Область УстановкаКонстанты_ИспользоватьТМЦВЭксплуатации

Процедура ИспользоватьТМЦВЭксплуатации_ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	// Регистрация не требуется
	Возврат;
	
КонецПроцедуры

Процедура ИспользоватьТМЦВЭксплуатации_ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Если НЕ Константы.ИспользоватьТМЦВЭксплуатации.Получить() Тогда
		МенеджерЗначения = Константы.ИспользоватьТМЦВЭксплуатации.СоздатьМенеджерЗначения();
		МенеджерЗначения.Значение = Истина;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерЗначения);
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Истина;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти