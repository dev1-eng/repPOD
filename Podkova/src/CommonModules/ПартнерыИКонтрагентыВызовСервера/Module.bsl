
#Область ПрограммныйИнтерфейс

#Область ОбщиеПроцедурыИФункцииФормСпискаИВыбораСправочникаПартнеры

// При перетаскивании в форме списка обновляет значение реквизита, на которое было выполнено перетаскивание для массива
// перетаскиваемых партнеров.
//
// Параметры:
//  Значение              - Произвольный - значение, на которое было выполнено перетаскивание.
//  МассивПартнеров       - Массив - массив перетаскиваемых партнеров.
//  КоличествоЗаписанных  - Число - количество записанных партнеров.
//
Процедура ОбновитьЗначениеРеквизитаУПеретаскиваемыхПартнеров(Значение, МассивПартнеров, КоличествоЗаписанных) Экспорт

	КоличествоЗаписанных = 0;
	МассивГруппДоступныхДляИзменения =
		УправлениеДоступом.ГруппыЗначенийДоступаРазрешающиеИзменениеЗначенийДоступа(Тип("СправочникСсылка.Партнеры"), Истина);
	
	ИмяРеквизита = "";
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.БизнесРегионы") Тогда
		ИмяРеквизита = "БизнесРегион";
	ИначеЕсли ТипЗнч(Значение) = Тип("СправочникСсылка.ГруппыДоступаПартнеров") Тогда
		ИмяРеквизита = "ГруппаДоступа";
		Если МассивГруппДоступныхДляИзменения.Найти(Значение) = Неопределено Тогда
			Возврат;
		КонецЕсли;
	ИначеЕсли ТипЗнч(Значение) = Тип("СправочникСсылка.Пользователи") Тогда
		ИмяРеквизита = "ОсновнойМенеджер";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Партнеры.Ссылка
	|ИЗ
	|	Справочник.Партнеры КАК Партнеры
	|ГДЕ
	|	Партнеры.Ссылка В(&МассивПартнеров)
	|	И Партнеры.ГруппаДоступа В (&МассивГруппДоступныхДляИзменения)
	|	И Партнеры.%ИмяРеквизита% <> &Значение";
	
	Запрос.УстановитьПараметр("МассивПартнеров",МассивПартнеров);
	Запрос.УстановитьПараметр("Значение",Значение);
	Запрос.УстановитьПараметр("МассивГруппДоступныхДляИзменения", МассивГруппДоступныхДляИзменения);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИмяРеквизита%", ИмяРеквизита);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
	
		ПартнерОбъект =  Выборка.Ссылка.ПолучитьОбъект();
		ПартнерОбъект[ИмяРеквизита] = Значение;
		ПартнерОбъект.Записать();
		КоличествоЗаписанных = КоличествоЗаписанных + 1;
	
	КонецЦикла;

КонецПроцедуры

// Получает основного менеджера бизнес-региона.
//
// Параметры:
//  БизнесРегион  - СправочникСсылка.БизнесРегионы - бизнес-регион, для которого получается основной менеджер.
//
// Возвращаемое значение:
//   СправочникСсылка.Пользователи   - основной менеджер бизнес-региона.
//
Функция ОсновнойМенеджерБизнесРегиона(БизнесРегион) Экспорт

	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БизнесРегион,"ОсновнойМенеджер");

КонецФункции

// Получает партнера - объекта авторизации внешнего пользователя
//
// Возвращаемое значение:
//  СправочникСсылка.Партнеры - партнер,если объект авторизации партнер, неопределено в обратном случае.
//
Функция ПолучитьАвторизовавшегосяПартнера() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВнешнийПользователь = ВнешниеПользователи.ТекущийВнешнийПользователь();
	Если НЕ ЗначениеЗаполнено(ВнешнийПользователь) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(ВнешнийПользователь.ОбъектАвторизации) = Тип("СправочникСсылка.Партнеры") Тогда
		Возврат ВнешнийПользователь.ОбъектАвторизации;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Формирует представление справочника Партнеры в зависимости от настроек раздельного или совместного использования
// партнеров и контрагентов.
//
// Возвращаемое значение:
//   Строка   - сформированное представление справочника Партнеры.
//
Функция ПредставлениеСправочникаПартнеры() Экспорт
	
	Возврат ?(ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов"), НСтр("ru = 'Контрагенты'"), НСтр("ru = 'Партнеры'"));
	
КонецФункции

// Получает структуру данных о авторизовавшемся внешнем пользователе
//
// Возвращаемое значение:
//  Структура - содержит информацию о авторизовавшемся внешнем пользователе.
//
Функция ДанныеАвторизовавшегосяВнешнегоПользователя() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВнешнийПользователь = ВнешниеПользователи.ТекущийВнешнийПользователь();
	Если НЕ ЗначениеЗаполнено(ВнешнийПользователь) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	АвторизованПартнер = Истина;
	Партнер            = Справочники.Партнеры.ПустаяСсылка();
	КонтактноеЛицо     = Справочники.КонтактныеЛицаПартнеров.ПустаяСсылка();
	
	ОбъектАвторизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВнешнийПользователь, "ОбъектАвторизации");
	
	Если ТипЗнч(ОбъектАвторизации) = Тип("СправочникСсылка.Партнеры") Тогда
		Партнер        = ОбъектАвторизации;
	ИначеЕсли ТипЗнч(ОбъектАвторизации) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") Тогда
		АвторизованПартнер = Ложь;
		КонтактноеЛицо = ОбъектАвторизации;
		Партнер        = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектАвторизации, "Владелец");
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураКВозврату = Новый Структура;
	СтруктураКВозврату.Вставить("АвторизованПартнер", АвторизованПартнер);
	СтруктураКВозврату.Вставить("Партнер", Партнер);
	СтруктураКВозврату.Вставить("КонтактноеЛицо", КонтактноеЛицо);
	
	Возврат СтруктураКВозврату;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Устанавливает подчиненному контрагенту его головного контрагенту
//
// Параметры
//  Контрагент  - СправочникСсылка.Контрагенты - контрагент, которому задается головной
//  ГоловнойКонтрагент  - СправочникСсылка.Контрагенты - устанавливаемое значение головного контрагента
//  ОписаниеОшибки  - Строка - параметр, в который записываются возникшие ошибки.
//
Процедура ИзменитьГоловногоКонтрагента(Контрагент, ГоловнойКонтрагент, ОписаниеОшибки) Экспорт
	
	ИНН = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ГоловнойКонтрагент, "ИНН");
	
	Если ЗначениеЗаполнено(ИНН) Тогда
		
		КПП = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "КПП");
		
		ВыборкаКонтрагент = ПартнерыИКонтрагенты.ИННКППУжеИспользуетсяВИнформационнойБазе(ИНН, КПП, Контрагент);
		Если ВыборкаКонтрагент <> Неопределено Тогда
			ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Не удалось изменить головного контрагента для %1.
				|Данные ИНН и КПП уже указаны для контрагента с кодом %2, ответственный - %3.
				|Попробуйте указать правильный КПП, и лишь затем выбрать головного контрагента.'"),
				Контрагент,
				ВыборкаКонтрагент.Код,
				ВыборкаКонтрагент.ОсновнойМенеджер);
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	КонтрагентОбъект = Контрагент.ПолучитьОбъект();
	
	Попытка
		КонтрагентОбъект.Заблокировать();
	Исключение
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось изменить головного контрагента для %1.
				|Возможно, контрагент в настоящий момент редактируется.'"),
			Контрагент);
		Возврат;
	КонецПопытки;
	
	КонтрагентОбъект.ГоловнойКонтрагент = ГоловнойКонтрагент;
	КонтрагентОбъект.ИНН = ИНН;
	КонтрагентОбъект.Записать();
	
	КонтрагентОбъект.Разблокировать();
	
КонецПроцедуры

Функция ЗначенияПолейКонтактнойИнформации(Знач Представление, Знач ВидКонтактнойИнформации, Знач Комментарий = Неопределено) Экспорт
	
	// Создаем новый экземпляр по представлению
	Результат = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(Представление, ВидКонтактнойИнформации);
	
	// Добавляем в него комментарий, если есть
	Если Комментарий <> Неопределено Тогда
		Комментарий = УправлениеКонтактнойИнформацией.КомментарийКонтактнойИнформации(Результат);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции 

// Получает данные контрагента по ИНН в зависимости от типа контрагента.
//
// Параметры:
//  ЭтоЮридическоеЛицо  - Булево - Истина, если юридическое лицо, Ложь если индивидуальный предприниматель.
//  ИНН                 - Строка - ИНН, по которому будут получатся данные.
//
// Возвращаемое значение:
//   Структура   - полученные данные. Описание структур см. в 
//                  РаботаСКонтрагентами.НовыеРеквизитыЮридическогоЛица() и 
//                  РаботаСКонтрагентами.НовыеРеквизитыПредпринимателя().
//
Функция ДанныеКонтрагентаПоИНН(ЭтоЮридическоеЛицо, ИНН, ИдентификаторЗадания) Экспорт
	
	Если ТипЗнч(ИдентификаторЗадания) = Тип("УникальныйИдентификатор") Тогда
		Задание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗадания);
		
		Если Задание <> Неопределено
			И Задание.Состояние = СостояниеФоновогоЗадания.Активно Тогда
			Задание.Отменить();
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтоЮридическоеЛицо Тогда
		РеквизитыЮридическогоЛица = РаботаСКонтрагентами.РеквизитыЮридическогоЛицаПоИНН(ИНН);
		ИсторияРеквизитовВСтруктуру(РеквизитыЮридическогоЛица);
		Возврат РеквизитыЮридическогоЛица;
	Иначе
		РеквизитыПредпринимателя = РаботаСКонтрагентами.РеквизитыПредпринимателяПоИНН(ИНН);
		ИсторияРеквизитовВСтруктуру(РеквизитыПредпринимателя);
		Возврат РеквизитыПредпринимателя;
	КонецЕсли;
	
КонецФункции

// Запускает фоновое задание по получению данных контрагента с сервиса, согласно указанному ИНН.
//
// Параметры:
//  ИНН  - Строка - ИНН контрагента, согласно которому будет выполнен поиск данных
//  ЮрФизЛицо  - Перечисления.ЮрФизЛицо - в зависимости от значения поиск будет выполнен либо в ЕГРЮЛ ЛИБО ЕГРИП.
//  УникальныйИдентификатор  - УникальныйИдентификатор - уникальный идентификатор запускаемого регламентного задания.
//
// Возвращаемое значение:
//   РезультатЗапуска   - структура, содержит следующие параметры:
//        ЗаданиеЗапущено      - Булево - Истина, если задание удалось выполнить сразу.
//        РеквизитыКонтрагента - Структура - данные контрагента, если они были получены сразу.
//        ИдентификаторЗадания - Строка - идентификатор запущенного фонового задания.
//        АдресХранилища       - Строка - адрес хранилища, в которое будет помещен результат выполнения.
Функция ФоновоеЗаданиеДанныеПартнераПоИННЗапустить(ИНН, ЮрФизЛицо, УникальныйИдентификатор, ИдентификаторЗадания) Экспорт
	
	РезультатЗапуска = Новый Структура("ЗаданиеЗапущено, РеквизитыКонтрагента", Ложь, Неопределено);
	РезультатЗапуска.Вставить("ИдентификаторЗадания","");
	РезультатЗапуска.Вставить("АдресХранилища","");
	
	Если ТипЗнч(ИдентификаторЗадания) = Тип("УникальныйИдентификатор") Тогда
		Задание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗадания);
		
		Если Задание <> Неопределено
			И Задание.Состояние = СостояниеФоновогоЗадания.Активно Тогда
			Задание.Отменить();
		КонецЕсли;
	КонецЕсли;
	
	ИнформацияОбОшибке = Неопределено;
	ПараметрыФормирования = Новый Структура;
	ПараметрыФормирования.Вставить("ИНН", ИНН);
	ПараметрыФормирования.Вставить("ЭтоЮридическоеЛицо", ОбщегоНазначенияУТКлиентСервер.ЭтоЮрЛицо(ЮрФизЛицо));
	
	Попытка
		РезультатФоновогоЗадания = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
			УникальныйИдентификатор,
			"ПартнерыИКонтрагенты.ДанныеКонтрагентаПоИННФоновоеЗадание",
			ПараметрыФормирования,
			НСтр("ru = 'Работа с контрагентами: получение реквизитов по ИНН.'"));
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		Возврат РезультатЗапуска;
	КонецПопытки;
	
	РезультатЗапуска.ИдентификаторЗадания  = РезультатФоновогоЗадания.ИдентификаторЗадания;
	РезультатЗапуска.АдресХранилища        = РезультатФоновогоЗадания.АдресХранилища;
	
	Если РезультатФоновогоЗадания.ЗаданиеВыполнено Тогда
		РезультатЗапуска.РеквизитыКонтрагента = ПолучитьИзВременногоХранилища(РезультатФоновогоЗадания.АдресХранилища);
	Иначе
		РезультатЗапуска.ЗаданиеЗапущено      = Истина;
	КонецЕсли;
	
	ИсторияРеквизитовВСтруктуру(РезультатЗапуска.РеквизитыКонтрагента);
	
	Возврат РезультатЗапуска;
	
КонецФункции

// Проверяет, выполнено ли ранее запущенное фоновое задание по получению данных контрагента по ИНН.
//
// Параметры:
//  ЗаданиеИдентификатор  - Строка - идентификатор фонового задания.
//  АдресХранилища        - Строка - адрес хранилища, в которое будет помещен результат выполнения.
//
// Возвращаемое значение:
//   РезультатВыполнения   - структура, содержит следующие параметры:
//        ЗаданиеВыполнено      - Булево - Истина, если задание выполнено.
//        РеквизитыКонтрагента  - Структура - полученные данные контрагента.
//
Функция ФоновоеЗаданиеВыполнено(ЗаданиеИдентификатор, АдресХранилища) Экспорт
	
	РезультатВыполнения = Новый Структура("ЗаданиеВыполнено, РеквизитыКонтрагента", Ложь, Неопределено);
	
	Попытка
		РезультатВыполнения.ЗаданиеВыполнено = ДлительныеОперации.ЗаданиеВыполнено(ЗаданиеИдентификатор);
	Исключение
		РезультатВыполнения.ЗаданиеВыполнено = Ложь;
	КонецПопытки;
	
	Если РезультатВыполнения.ЗаданиеВыполнено Тогда
		РезультатВыполнения.РеквизитыКонтрагента = ПолучитьИзВременногоХранилища(АдресХранилища);
		ИсторияРеквизитовВСтруктуру(РезультатВыполнения.РеквизитыКонтрагента);
	КонецЕсли;
	
	Возврат РезультатВыполнения;
	
КонецФункции

Функция ИмяФормыСозданияКонтрагента() Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов") Тогда
		Возврат "Справочник.Партнеры.Форма.ПомощникНового";
	Иначе
		Возврат "Справочник.Контрагенты.Форма.ФормаЭлемента";
	КонецЕсли;
	
КонецФункции

Функция МассивУстановленныхПараметровОтбораПоТипуПартнера(Параметры) Экспорт
	
	МассивУстановленныхОтборов = Новый Массив;
	Если Параметры.Отбор.Свойство("Клиент") Тогда
		МассивУстановленныхОтборов.Добавить("Клиент");
	КонецЕсли;
	Если Параметры.Отбор.Свойство("Поставщик") Тогда
		МассивУстановленныхОтборов.Добавить("Поставщик");
	КонецЕсли;
	Если Параметры.Отбор.Свойство("Конкурент") Тогда
		МассивУстановленныхОтборов.Добавить("Конкурент");
	КонецЕсли;
	Если Параметры.Отбор.Свойство("ПрочиеОтношения") Тогда
		МассивУстановленныхОтборов.Добавить("ПрочиеОтношения");
	КонецЕсли;
	Если Параметры.Отбор.Свойство("ОбслуживаетсяТорговымиПредставителями") Тогда
		МассивУстановленныхОтборов.Добавить("ОбслуживаетсяТорговымиПредставителями");
	КонецЕсли;
	Если Параметры.Отбор.Свойство("НашеПредприятие") Тогда
		МассивУстановленныхОтборов.Добавить("НашеПредприятие");
	КонецЕсли;
	
	Возврат МассивУстановленныхОтборов;
	
КонецФункции

Функция СтрокаОтбораЗапросаПоТипуПартнера(Параметры, МассивУстановленныхОтборов) Экспорт
	
	УстанавливатьОтборПоТипуПартнераКакИЛИ = Параметры.Свойство("УстанавливатьОтборПоТипуПартнераКакИЛИ") И Параметры.УстанавливатьОтборПоТипуПартнераКакИЛИ;
		
	СтрокаОтбора = "";
	СтрокаУсловия = ?(УстанавливатьОтборПоТипуПартнераКакИЛИ," ИЛИ "," И ");
	
	Для каждого ЭлементМассива Из МассивУстановленныхОтборов Цикл
		Если ЭлементМассива = "НашеПредприятие" Тогда
			Условие = "Партнеры.Ссылка = ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)"; 
		Иначе
			Условие = "Партнеры." + ЭлементМассива; 
		КонецЕсли; 
		СтрокаОтбора = СтрокаОтбора + СтрокаУсловия + Условие;
	КонецЦикла;
	
	СтрокаОтбора = Прав(СтрокаОтбора,СтрДлина(СтрокаОтбора) - ?(УстанавливатьОтборПоТипуПартнераКакИЛИ,4,2));
	Возврат ?(ПустаяСтрока(СтрокаОтбора),""," И (" + СтрокаОтбора + ")");
	
КонецФункции

Процедура ЗаполнитьКонтрагентаПартнераПоУмолчанию(Знач Партнер, Контрагент, ПерезаполнятьКонтрагента) Экспорт
	
	ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Партнер, Контрагент, ПерезаполнятьКонтрагента);
	
КонецПроцедуры

Процедура ЗаполнитьДанныеКонтрагентаПартнера(Приемник, Партнер, СИсторическимиДанными = Истина) Экспорт
	
	ПартнерыИКонтрагенты.ЗаполнитьДанныеКонтрагентаПартнера(Приемник, Партнер, СИсторическимиДанными);
	
КонецПроцедуры

Функция КонтрагентПартнера(Партнер) Экспорт
	
	Возврат ПартнерыИКонтрагенты.ПолучитьКонтрагентаПартнераПоУмолчанию(Партнер);
	
КонецФункции

#Область ИсторияРеквизитов

// Вызывается для преобразования таблицы значения в массив структур.
// Параметры:
//  Реквизиты    - Структура        - реквизиты контрагента.
//
Процедура ИсторияРеквизитовВСтруктуру(Реквизиты) Экспорт
	
	Если НЕ (ТипЗнч(Реквизиты) = Тип("Структура") И Реквизиты.Свойство("ИсторияРеквизитов")) Тогда
		Возврат;
	КонецЕсли;
	
	ИсторияРеквизитов = Новый Структура;
	
	Для каждого Элемент Из Реквизиты.ИсторияРеквизитов Цикл
		Элемент.Значение.Сортировать("Дата");
		ИсторияРеквизитов.Вставить(Элемент.Ключ, ОбщегоНазначения.ТаблицаЗначенийВМассив(Элемент.Значение));
	КонецЦикла; 
	
	Реквизиты.Вставить("ИсторияРеквизитов", Новый ФиксированнаяСтруктура(ИсторияРеквизитов));

КонецПроцедуры

// Вызывается для включения ведения истории для вида контактной информации.
// Параметры:
//  ИмяВидаКонтактнойИнформации - Строка - имя вида контактной информации.
//
Процедура ВключитьХранениеИсторииИзменений(ИмяВидаКонтактнойИнформации) Экспорт
	
	ПартнерыИКонтрагенты.ВключитьХранениеИсторииИзменений(ИмяВидаКонтактнойИнформации);
	
КонецПроцедуры

#КонецОбласти

#Область ДанныеВыбора

Функция ВидыСделокДанныхВыбора(СтрокаПоиска) Экспорт

	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьБизнесПроцессыИЗадачи") Тогда

		ДанныеВыбора  = Новый СписокЗначений;
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ВидыСделокСКлиентами.Ссылка
		|ИЗ
		|	Справочник.ВидыСделокСКлиентами КАК ВидыСделокСКлиентами
		|ГДЕ
		|	ВидыСделокСКлиентами.ИспользованиеРазрешено
		|	И ВидыСделокСКлиентами.ТипСделки В(&ДоступныеТипыСделок)
		|	И ВидыСделокСКлиентами.Наименование ПОДОБНО &СтрокаПоиска";
		
		ДоступныеТипыСделок = Новый Массив;
		ДоступныеТипыСделок.Добавить(Перечисления.ТипыСделокСКлиентами.ПрочиеНепроцессныеСделки);
		ДоступныеТипыСделок.Добавить(Перечисления.ТипыСделокСКлиентами.СделкиСРучнымПереходомПоЭтапам);
		
		Запрос.УстановитьПараметр("СтрокаПоиска",        СтрокаПоиска+"%");
		Запрос.УстановитьПараметр("ДоступныеТипыСделок", ДоступныеТипыСделок);
		
		ДанныеВыбора = Новый СписокЗначений;
		ДанныеВыбора.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
		
		Возврат ДанныеВыбора;
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;

КонецФункции

Функция КонтрагентыДанныеВыбора(Параметры) Экспорт
	
	ДанныеВыбора = Новый СписокЗначений;
	Партнер = Неопределено;
	
	Если Параметры.Отбор.Свойство("Партнер") Тогда
		Партнер = ?(ЗначениеЗаполнено(Параметры.Отбор.Партнер),Параметры.Отбор.Партнер,Неопределено);
		Параметры.Отбор.Удалить("Партнер");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	МассивУстановленныхОтборов = ПартнерыИКонтрагентыВызовСервера.МассивУстановленныхПараметровОтбораПоТипуПартнера(Параметры);
	ИспользоватьПартнеровКакКонтрагентов = ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов");
	
	Если МассивУстановленныхОтборов.Количество() > 0 И ИспользоватьПартнеровКакКонтрагентов Тогда
		
		УстанавливатьОтборПоТипуПартнераКакИЛИ = Ложь;
		
		Если Параметры.Свойство("УстанавливатьОтборПоТипуПартнераКакИЛИ") И Параметры.УстанавливатьОтборПоТипуПартнераКакИЛИ Тогда
			УстанавливатьОтборПоТипуПартнераКакИЛИ = Истина;
		КонецЕсли;
		
		СтрокаОтбора = ПартнерыИКонтрагентыВызовСервера.СтрокаОтбораЗапросаПоТипуПартнера(Параметры, МассивУстановленныхОтборов);
		
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ ПЕРВЫЕ 51
		|	Контрагенты.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Контрагенты.Ссылка) КАК Представление,
		|	Партнеры.Наименование КАК ПартнерНаименование,
		|	Контрагенты.Наименование,
		|	Контрагенты.ИНН,
		|	&НайденоПоНаименованию КАК НайденоПо,
		|	Контрагенты.ПометкаУдаления
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
		|		ПО Контрагенты.Партнер = Партнеры.Ссылка
		|ГДЕ
		|	Контрагенты.Наименование ПОДОБНО &СтрокаВвода 
		|	И Партнеры.Ссылка = &УсловиеОтбора
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 51
		|	Контрагенты.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Контрагенты.Ссылка),
		|	Партнер.Наименование,
		|	Контрагенты.Наименование,
		|	Контрагенты.ИНН,
		|	&НайденоПоИНН,
		|	Контрагенты.ПометкаУдаления
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
		|		ПО Контрагенты.Партнер = Партнеры.Ссылка
		|ГДЕ
		|	Контрагенты.ИНН ПОДОБНО &СтрокаВвода
		|	И Партнеры.Ссылка = &УсловиеОтбора
		|
		|УПОРЯДОЧИТЬ ПО
		|	Контрагенты.ПометкаУдаления";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"И Партнеры.Ссылка = &УсловиеОтбора" , СтрокаОтбора);
		
	ИначеЕсли Параметры.Свойство("ВыборКонтрагентовИОрганизаций") И ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями") Тогда
		
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ ПЕРВЫЕ 41
		|	Контрагенты.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Контрагенты.Ссылка) КАК Представление,
		|	ВЫБОР
		|		КОГДА ИерархияПартнеров.Партнер = &Партнер
		|			ТОГДА ИерархияПартнеров.Родитель.Наименование
		|		ИНАЧЕ Контрагенты.Партнер.Наименование
		|	КОНЕЦ КАК ПартнерНаименование,
		|	Контрагенты.Наименование,
		|	ВЫБОР
		|		КОГДА ИерархияПартнеров.Партнер = &Партнер
		|			ТОГДА ИерархияПартнеров.Уровень
		|		ИНАЧЕ -1
		|	КОНЕЦ КАК ПорядокВывода,
		|	Контрагенты.ИНН,
		|	&НайденоПоНаименованию КАК НайденоПо,
		|	Контрагенты.ПометкаУдаления
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИерархияПартнеров КАК ИерархияПартнеров
		|		ПО Контрагенты.Партнер = ИерархияПартнеров.Родитель
		|ГДЕ
		|	Контрагенты.Наименование ПОДОБНО &СтрокаВвода
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 41
		|	Контрагенты.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Контрагенты.Ссылка),
		|	ВЫБОР
		|		КОГДА ИерархияПартнеров.Партнер = &Партнер
		|			ТОГДА ИерархияПартнеров.Родитель.Наименование
		|		ИНАЧЕ Контрагенты.Партнер.Наименование
		|	КОНЕЦ,
		|	Контрагенты.Наименование,
		|	ВЫБОР
		|		КОГДА ИерархияПартнеров.Партнер = &Партнер
		|			ТОГДА ИерархияПартнеров.Уровень
		|		ИНАЧЕ -1
		|	КОНЕЦ,
		|	Контрагенты.ИНН,
		|	&НайденоПоИНН,
		|	Контрагенты.ПометкаУдаления
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИерархияПартнеров КАК ИерархияПартнеров
		|		ПО Контрагенты.Партнер = ИерархияПартнеров.Родитель
		|ГДЕ
		|	Контрагенты.ИНН ПОДОБНО &СтрокаВвода
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 10
		|	Контрагенты.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Контрагенты.Ссылка),
		|	&НашеПредприятие,
		|	Контрагенты.Наименование,
		|	 -1,
		|	Контрагенты.ИНН,
		|	&НайденоПоНаименованию,
		|	Контрагенты.ПометкаУдаления
		|ИЗ
		|	Справочник.Организации КАК Контрагенты
		|ГДЕ
		|	Контрагенты.Наименование ПОДОБНО &СтрокаВвода
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 10
		|	Контрагенты.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Контрагенты.Ссылка),
		|	&НашеПредприятие,
		|	Контрагенты.Наименование,
		|	 -1,
		|	Контрагенты.ИНН,
		|	&НайденоПоИНН,
		|	Контрагенты.ПометкаУдаления
		|ИЗ
		|	Справочник.Организации КАК Контрагенты
		|ГДЕ
		|	Контрагенты.ИНН ПОДОБНО &СтрокаВвода
		|
		|УПОРЯДОЧИТЬ ПО
		|	Контрагенты.ПометкаУдаления,
		|	ПорядокВывода УБЫВ";
		
	Иначе
		
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ ПЕРВЫЕ 51
		|	Контрагенты.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Контрагенты.Ссылка) КАК Представление,
		|	ВЫБОР
		|		КОГДА ИерархияПартнеров.Партнер = &Партнер
		|			ТОГДА ИерархияПартнеров.Родитель.Наименование
		|		ИНАЧЕ Контрагенты.Партнер.Наименование
		|	КОНЕЦ КАК ПартнерНаименование,
		|	Контрагенты.Наименование,
		|	ВЫБОР
		|		КОГДА ИерархияПартнеров.Партнер = &Партнер
		|			ТОГДА ИерархияПартнеров.Уровень
		|		ИНАЧЕ -1
		|	КОНЕЦ КАК ПорядокВывода,
		|	Контрагенты.ИНН,
		|	&НайденоПоНаименованию КАК НайденоПо,
		|	Контрагенты.ПометкаУдаления
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИерархияПартнеров КАК ИерархияПартнеров
		|		ПО Контрагенты.Партнер = ИерархияПартнеров.Родитель
		|ГДЕ
		|	Контрагенты.Наименование ПОДОБНО &СтрокаВвода
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 51
		|	Контрагенты.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Контрагенты.Ссылка),
		|	ВЫБОР
		|		КОГДА ИерархияПартнеров.Партнер = &Партнер
		|			ТОГДА ИерархияПартнеров.Родитель.Наименование
		|		ИНАЧЕ Контрагенты.Партнер.Наименование
		|	КОНЕЦ,
		|	Контрагенты.Наименование,
		|	ВЫБОР
		|		КОГДА ИерархияПартнеров.Партнер = &Партнер
		|			ТОГДА ИерархияПартнеров.Уровень
		|		ИНАЧЕ -1
		|	КОНЕЦ,
		|	Контрагенты.ИНН,
		|	&НайденоПоИНН,
		|	Контрагенты.ПометкаУдаления
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИерархияПартнеров КАК ИерархияПартнеров
		|		ПО Контрагенты.Партнер = ИерархияПартнеров.Родитель
		|ГДЕ
		|	Контрагенты.ИНН ПОДОБНО &СтрокаВвода
		|
		|УПОРЯДОЧИТЬ ПО
		|	Контрагенты.ПометкаУдаления,
		|	ПорядокВывода УБЫВ";
		
	КонецЕсли;
	СтрокаПоиска = ?(Параметры.СтрокаПоиска = Неопределено, "", Параметры.СтрокаПоиска);
	
	Запрос.УстановитьПараметр("СтрокаВвода", СтрокаПоиска + "%" );
	Запрос.УстановитьПараметр("Партнер", Партнер);
	Запрос.УстановитьПараметр("НайденоПоНаименованию", "ПоНаименованию");
	Запрос.УстановитьПараметр("НайденоПоИНН", "ПоИНН");
	Запрос.УстановитьПараметр("НашеПредприятие", НСтр("ru = 'Наше предприятие'"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если ДанныеВыбора.НайтиПоЗначению(Выборка.Ссылка) = Неопределено Тогда
			
			Если Выборка.НайденоПо = "ПоНаименованию"  Тогда
				Представление = Выборка.Наименование;
			Иначе
				Представление = Выборка.ИНН + " (" +Выборка.Наименование + ")";
			КонецЕсли;
			
			Если Не ИспользоватьПартнеровКакКонтрагентов Тогда
				Представление = Представление + " (" + Выборка.ПартнерНаименование + ")";
			КонецЕсли;
			
			Если Выборка.ПометкаУдаления Тогда
				СтруктураЗначение = Новый Структура("Значение,ПометкаУдаления", Выборка.Ссылка, Выборка.ПометкаУдаления);
				ДанныеВыбора.Добавить(СтруктураЗначение,Представление,,БиблиотекаКартинок.ПомеченныйНаУдалениеЭлемент);
			Иначе
				ДанныеВыбора.Добавить(Выборка.Ссылка,Представление);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДанныеВыбора;
	
КонецФункции

Функция ПартнерыДанныеВыбора(Параметры) Экспорт

	МассивУстановленныхОтборов = ПартнерыИКонтрагентыВызовСервера.МассивУстановленныхПараметровОтбораПоТипуПартнера(Параметры);
	
	ИспользоватьПартнеровКакКонтрагентов = ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов");
	
	Если МассивУстановленныхОтборов.Количество() > 0 ИЛИ ИспользоватьПартнеровКакКонтрагентов Тогда
		
		Запрос = Новый Запрос;
	
		Если Параметры.Свойство("УстанавливатьОтборПоТипуПартнераКакИЛИ") И Параметры.УстанавливатьОтборПоТипуПартнераКакИЛИ Тогда
			УстанавливатьОтборПоТипуПартнераКакИЛИ = Истина;
		КонецЕсли;
		
		СтрокаОтбора = ПартнерыИКонтрагентыВызовСервера.СтрокаОтбораЗапросаПоТипуПартнера(Параметры, МассивУстановленныхОтборов);
		
		Если Параметры.Отбор.Свойство("БизнесРегион") Тогда
			СтрокаОтбора = " И Партнеры.БизнесРегион = &БизнесРегион";
			Запрос.УстановитьПараметр("БизнесРегион", Параметры.Отбор.БизнесРегион);
		КонецЕсли;
		
		Если ИспользоватьПартнеровКакКонтрагентов Тогда
			СтрокаОтбора = СтрокаОтбора + " И (НЕ Партнеры.Ссылка = Значение(Справочник.Партнеры.НеизвестныйПартнер)) ";
		КонецЕсли;
		
		Если Не ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями") Тогда
			СтрокаОтбора = СтрокаОтбора + " И (НЕ Партнеры.Ссылка = Значение(Справочник.Партнеры.НашеПредприятие)) ";
		КонецЕсли;
		
		Запрос.УстановитьПараметр("СтрокаВвода",Параметры.СтрокаПоиска + "%" );
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 51
		|	Партнеры.Ссылка КАК Партнер,
		|	ПРЕДСТАВЛЕНИЕ(Партнеры.Ссылка) КАК Представление,
		|	Партнеры.ПометкаУдаления КАК ПометкаУдаления,
		|	"""" КАК ИНН,
		|	""НайденоПоНаименованию"" КАК НайденоПо
		|ИЗ
		|	Справочник.Партнеры КАК Партнеры
		|ГДЕ
		|	Партнеры.Наименование ПОДОБНО &СтрокаВвода " + СтрокаОтбора + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 51
		|	Партнеры.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Партнеры.Ссылка),
		|	Партнеры.ПометкаУдаления,
		|	Контрагенты.ИНН КАК ИНН,
		|	""НайденоПоИНН""
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
		|		ПО Контрагенты.Партнер = Партнеры.Ссылка
		|ГДЕ
		|	Контрагенты.ИНН ПОДОБНО &СтрокаВвода " + СтрокаОтбора;
		
		Выборка = Запрос.Выполнить().Выбрать();
		ДанныеВыбора = Новый СписокЗначений;
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.НайденоПо = "НайденоПоНаименованию" Тогда
				ТекстПредставление = Выборка.Представление;
			Иначе
				ТекстПредставление = Выборка.ИНН + " (" +Выборка.Представление + ")";
			КонецЕсли;
			
			Если Выборка.ПометкаУдаления Тогда
				СтруктураЗначение = Новый Структура("Значение,ПометкаУдаления", Выборка.Партнер, Выборка.ПометкаУдаления);
				ДанныеВыбора.Добавить(СтруктураЗначение,ТекстПредставление,, БиблиотекаКартинок.ПомеченныйНаУдалениеЭлемент);
			Иначе
				ДанныеВыбора.Добавить(Выборка.Партнер, ТекстПредставление);
			КонецЕсли;
		КонецЦикла;
		
		Возврат ДанныеВыбора;
	
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

Функция РолиПартнеровВСделкахИПроектахДанныеВыбора(Параметры) Экспорт
	
	Если Параметры.Свойство("Клиент") Тогда

		ДанныеВыбора = Новый СписокЗначений;
		
		Если Параметры.Клиент = Параметры.Участник Тогда
			
			ДанныеВыбора = Новый СписокЗначений;
			Роль = Справочники.РолиПартнеровВСделкахИПроектах.Клиент;
			ДанныеВыбора.Добавить(Роль, Роль.Наименование);
			
		Иначе
			
			Запрос = Новый Запрос(
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	РолиПартнеровВСделкахИПроектах.Ссылка,
				|	РолиПартнеровВСделкахИПроектах.Наименование
				|ИЗ
				|	Справочник.РолиПартнеровВСделкахИПроектах КАК РолиПартнеровВСделкахИПроектах
				|ГДЕ
				|	РолиПартнеровВСделкахИПроектах.Ссылка <> ЗНАЧЕНИЕ(Справочник.РолиПартнеровВСделкахИПроектах.Клиент)");
			РезультатЗапроса = Запрос.Выполнить();
			Если Не РезультатЗапроса.Пустой() Тогда
				
				Выборка = РезультатЗапроса.Выбрать();
				Пока Выборка.Следующий() Цикл
					ДанныеВыбора.Добавить(Выборка.Ссылка, Выборка.Наименование);
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Возврат ДанныеВыбора;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#КонецОбласти
