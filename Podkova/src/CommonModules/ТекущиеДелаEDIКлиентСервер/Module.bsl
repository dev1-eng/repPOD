///////////////////////////////////////////////////////////////////////////////////////////////////////
// В модуле реализованы клиент-серверные процедуры и функции, предназначенные для работы с текущими делами EDI
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

#Область Разделы

// Возвращает разделы виджета
// 
// Параметры:
// 	Виджет - ПеречислениеСсылка.ДоступныеВиджетыТекущихДелEDI - виджет, для которого требуется получить разделы
// Возвращаемое значение:
// 	Неопределено, Массив - массив разделов виджета.
//
Функция РазделыВиджета(Виджет) Экспорт
	
	Разделы = Неопределено;
	
	Если Виджет = ПредопределенноеЗначение("Перечисление.ДоступныеВиджетыТекущихДелEDI.Закупки") Тогда
		
		Разделы = ТекущиеДелаEDIИнтеграцияКлиентСервер.РазделыВиджетаЗакупки();
		
	ИначеЕсли Виджет = ПредопределенноеЗначение("Перечисление.ДоступныеВиджетыТекущихДелEDI.Продажи") Тогда
		
		Разделы = ТекущиеДелаEDIИнтеграцияКлиентСервер.РазделыВиджетаПродажи();
		
	ИначеЕсли Виджет = ПредопределенноеЗначение("Перечисление.ДоступныеВиджетыТекущихДелEDI.КонтрольПоступлений") Тогда
		
		Разделы = ТекущиеДелаEDIИнтеграцияКлиентСервер.РазделыВиджетаКонтрольПоступлений();
	
	ИначеЕсли Виджет = ПредопределенноеЗначение("Перечисление.ДоступныеВиджетыТекущихДелEDI.КонтрольОтгрузок") Тогда
		
		Разделы = ТекущиеДелаEDIИнтеграцияКлиентСервер.РазделыВиджетаКонтрольОтгрузок();
		
	ИначеЕсли Виджет = ПредопределенноеЗначение("Перечисление.ДоступныеВиджетыТекущихДелEDI.Купить") Тогда
		
		Разделы = ТекущиеДелаEDIИнтеграцияКлиентСервер.РазделыВиджетаКупить();
	
	ИначеЕсли Виджет = ПредопределенноеЗначение("Перечисление.ДоступныеВиджетыТекущихДелEDI.НастройкиСправочники") Тогда
		
		Разделы = ТекущиеДелаEDIИнтеграцияКлиентСервер.РазделыВиджетаНастройкиСправочники();
		
	КонецЕсли;
	
	Возврат Разделы;
	
КонецФункции

#КонецОбласти

#Область ДекорацииВиджетов

// Определяет имя следующей декорации при выводе виджета
// 
// Параметры:
// 	ИмяТекущейДекорации - Строка - определеяемое имя декорации
// 	ИмяДекорации        - Строка - имя выводимой декорации
// 	ТекущийПостфикс     - Число - постфикс декорации
//
Процедура ОпределитьИмяСледующейДекорации(ИмяТекущейДекорации, ИмяДекорации, ТекущийПостфикс) Экспорт
	
	ИнкрементироватьПостфиксДекорации(ТекущийПостфикс);
	ИмяТекущейДекорации = ИмяДекорации + ТекущийПостфикс;
	
КонецПроцедуры

// Отключает видимость декорации группы начиная с определенного номера
// 
// Параметры:
// 	Группа       - ГруппаФормы - группа элементов формы, для подчиненных элементов которой определяется видиомсть.
// 	ИмяДекорации - Строка      - имя декорации формы.
// 	Постфикс     - Число       - постикс последней видимой декорации.
// 	Сдвиг        - Число       - сдвиг постфикса.
//
Процедура ОтключитьВидимостьДекорацийГруппыНачинаяСПостфикса(Группа, ИмяДекорации, Постфикс, Сдвиг = 0) Экспорт
	
	ПодчиненныеЭлементы = Группа.ПодчиненныеЭлементы;
	КоличествоЭлементовГруппы = ПодчиненныеЭлементы.Количество() + Сдвиг;
	Пока ПостфиксВЧисло(Постфикс) <= КоличествоЭлементовГруппы Цикл
		ПодчиненныеЭлементы[ИмяДекорации + Постфикс].Видимость = Ложь;
		ИнкрементироватьПостфиксДекорации(Постфикс);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнкрементироватьПостфиксДекорации(Постфикс)
	
	Если Постфикс = "Первая" Тогда
		Постфикс = "Вторая";
	ИначеЕсли Постфикс = "Вторая" Тогда
		Постфикс = "Третья";
	ИначеЕсли Постфикс = "Третья" Тогда
		Постфикс = "Четвертая";
	ИначеЕсли Постфикс = "Четвертая" Тогда
		Постфикс = "Пятая";
	ИначеЕсли Постфикс = "Пятая" Тогда
		Постфикс = "Шестая";
	КонецЕсли;
	
КонецПроцедуры

Функция ПостфиксВЧисло(Постфикс)
	
	Если Постфикс = "Первая" Тогда
		Возврат 1;
	ИначеЕсли Постфикс = "Вторая" Тогда
		Возврат 2;
	ИначеЕсли Постфикс = "Третья" Тогда
		Возврат 3;
	ИначеЕсли Постфикс = "Четвертая" Тогда
		Возврат 4;
	ИначеЕсли Постфикс = "Пятая" Тогда
		Возврат 5;
	ИначеЕсли Постфикс = "Шестая" Тогда
		Возврат 6;
	КонецЕсли; 
	
КонецФункции

#КонецОбласти

