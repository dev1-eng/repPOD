#Область ПрограммныйИнтерфейс

#Область ПроцедурыПроверкиКорректностиНомераТаможеннойДекларации

// Проверяет корректность ввода номера таможенной декларации
//
// Параметры:
//  НомерТаможеннойДекларации - Строка
//  НачалоКорректногоПериода - Дата - Дата начала периода проверки
//  КонецКорректногоПериода - Дата - Дата окончания периода проверки
// 
// Возвращаемое значение:
//  Число - код ошибки, расшифровка в ТекстОшибкиВНомереТаможеннойДекларации()
//
Функция ПроверитьКорректностьНомераТаможеннойДекларации(НомерТаможеннойДекларации, НачалоКорректногоПериода, КонецКорректногоПериода) Экспорт
	
	НомерДекларацииНаТовары = СокрЛП(НомерТаможеннойДекларации);
	
	Если НЕ ЗначениеЗаполнено(НомерДекларацииНаТовары) Тогда
		// Пользователь еще ничего не ввел.
		Возврат 0;
	КонецЕсли;
	
	МассивТД = СтрРазделить(НомерДекларацииНаТовары, "/");
	
	Если МассивТД.Количество() > 4
	 ИЛИ МассивТД.Количество() < 3 Тогда
		// Ошибочное количество элементов.
		Возврат 1;
	КонецЕсли;
	
	КодТаможенногоОргана = СокрЛП(МассивТД[0]);
	
	Если СтрДлина(КодТаможенногоОргана) <> 2
		И СтрДлина(КодТаможенногоОргана) <> 5
		И СтрДлина(КодТаможенногоОргана) <> 8 Тогда
		// Ошибочная длина кода таможенного органа.
		Возврат 2;
	КонецЕсли;
	
	ДатаПринятияДекларацииНаТовары = СокрЛП(МассивТД[1]);
	
	Если СтрДлина(ДатаПринятияДекларацииНаТовары) <> 6 Тогда
		// Ошибочная длина поля дата.
		Возврат 3;
	Иначе
		// Проверим корректность указания даты.
		Если НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ДатаПринятияДекларацииНаТовары) Тогда
			// Длина поля верная, но дата указана ошибочно.
			Возврат 3;
		КонецЕсли; 
		
		СтрокаВДату = СтроковыеФункцииКлиентСервер.СтрокаВДату(ДатаПринятияДекларацииНаТовары);
		Если НЕ ЗначениеЗаполнено(СтрокаВДату) Тогда
			// Длина поля верная, но дата указана ошибочно.
			Возврат 3;
		Иначе
			// Проверим год на корректность указания.
			
			Если СтрокаВДату < НачалоКорректногоПериода 
			 ИЛИ СтрокаВДату > КонецКорректногоПериода Тогда
				Возврат 4;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	ПорядковыйНомерДекларацииНаТовары = СокрЛП(МассивТД[2]);
	
	Если СтрДлина(ПорядковыйНомерДекларацииНаТовары) < 7
	 ИЛИ СтрДлина(ПорядковыйНомерДекларацииНаТовары) > 8 Тогда
		// Ошибочная длина поля порядковый номер.
		Возврат 5;
	КонецЕсли;
	
	Если СтрДлина(ПорядковыйНомерДекларацииНаТовары) = 7 Тогда
		ПервыйСимвол = ВРег(Лев(ПорядковыйНомерДекларацииНаТовары, 1));
		Если (ПервыйСимвол = "В" ИЛИ ПервыйСимвол = "B") Тогда
			Если НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Прав(ПорядковыйНомерДекларацииНаТовары,6)) Тогда
				// Ошибочный формат поля порядковый номер.
				Возврат 5;
			КонецЕсли;
		Иначе
			Если НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ПорядковыйНомерДекларацииНаТовары) Тогда
				// Ошибочный формат поля порядковый номер.
				Возврат 5;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если СтрДлина(ПорядковыйНомерДекларацииНаТовары) = 8 Тогда
		ПервыеДваСимвола = ВРег(Лев(ПорядковыйНомерДекларацииНаТовары, 2));
		Если ПервыеДваСимвола = "0Б" Тогда 
			// Вместо буквы "О" указана цифра ноль.
			Возврат 6;
		ИначеЕсли ПервыеДваСимвола = "3В"
				ИЛИ ПервыеДваСимвола = "3B" Тогда
			// Вместо буквы "З" указана цифра три.
			Возврат 7;
		ИначеЕсли ПервыеДваСимвола <> "ОБ"
				И ПервыеДваСимвола <> "OБ"
				И ПервыеДваСимвола <> "ЗВ"
				И ПервыеДваСимвола <> "ЗB" Тогда 
			// Ошибочный формат поля порядковый номер.
			Возврат 5;
		КонецЕсли;
		ПоследниеШестьСимволов = ВРег(Прав(ПорядковыйНомерДекларацииНаТовары, 6));
		Если НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ПоследниеШестьСимволов) Тогда
			// Ошибочный формат поля порядковый номер.
			Возврат 5;
		КонецЕсли;
	КонецЕсли;
	
	Если МассивТД.Количество() = 4 Тогда
		ПорядковыйНомерТовара = МассивТД[3];
		Если СтрДлина(ПорядковыйНомерТовара) > 3
		 ИЛИ СтрДлина(ПорядковыйНомерТовара) < 1 Тогда
			// Ошибочная длина поля порядковый номер товара.
			Возврат 8;
		КонецЕсли;
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции

// Возвращает текстовое описание ошибки при вводе номера таможенной декларации
// 
// Параметры:
//  КодОшибки - Число
//
// Возвращаемое значение:
//  Строка
//
Функция ТекстОшибкиВНомереТаможеннойДекларации(КодОшибки) Экспорт
	
	ТекстОшибки = "";
	
	Если КодОшибки = 1 Тогда
		ТекстОшибки = НСтр("ru='Номер должен состоять из трех или четырех блоков, разделенных дробью ""/""'");
	ИначеЕсли КодОшибки = 2 Тогда
		ТекстОшибки = НСтр("ru='Первый блок (код таможенного органа) в зависимости от страны ввоза должен состоять из 2, 5 или 8 цифр'");
	ИначеЕсли КодОшибки = 3 Тогда
		ТекстОшибки = НСтр("ru='Второй блок (дата регистрации декларации) должен быть в формате ДДММГГ'");
	ИначеЕсли КодОшибки = 4 Тогда
		ТекстОшибки = НСтр("ru='Во втором блоке (дата регистрации декларации) указанный год лежит за пределами допустимого периода'");
	ИначеЕсли КодОшибки = 5 Тогда
		ТекстОшибки = НСтр("ru='Третий блок (порядковый номер декларации) может состоять из (один из вариантов):
									|  1) 7 цифр
									|  2) двух букв (""ОБ"" или ""ЗВ"") и 6 цифр
									|  3) одной буквы (""В"") и 6 цифр'");
	ИначеЕсли КодОшибки = 6 Тогда
		ТекстОшибки = НСтр("ru='В третьем блоке вместо буквы ""О"" указана цифра ноль'");
	ИначеЕсли КодОшибки = 7 Тогда
		ТекстОшибки = НСтр("ru='В третьем блоке вместо буквы ""З"" указана цифра три'");
	ИначеЕсли КодОшибки = 8 Тогда
		ТекстОшибки = НСтр("ru='Четвертый блок (порядковый номер товара) должен содержать от 1 до 3 цифр'");
	КонецЕсли;
	
	Возврат ТекстОшибки;
	
КонецФункции

#КонецОбласти

#КонецОбласти
