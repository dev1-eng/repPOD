#Область ПрограммныйИнтерфейс

// Этап подготовки к расчету
//
// Параметры:
//	ПараметрыРасчета - Структура - параметры расчета себестоимости
//
Процедура ПодготовкаИсходныхДанныхКРасчету(ПараметрыРасчета) Экспорт
	
	РасчетСебестоимостиПодготовкаДанныхЛокализация.ПодготовкаИсходныхДанныхКРасчету(ПараметрыРасчета);
	
	ПерепровестиДокументыПоРегистрам(ПараметрыРасчета);
	
	// Исправим проблемы в движениях и в самих документах в рассчитываемом периоде.
	ИсправитьПроблемыВДвиженияхИДокументах(ПараметрыРасчета);
	
	ОчиститьДанныеУпрРегистров(ПараметрыРасчета);
		
	// Проверим корректность исходных данных для расчета.
	РасчетСебестоимостиПрикладныеАлгоритмы.ПроверитьКорректностьИсходныхДанныхДоРасчета(ПараметрыРасчета);
	
	// Обновим кэши регистров, данные которых были изменены в этой процедуре.
	РасчетСебестоимостиПрикладныеАлгоритмы.ОбновитьРасчетныеКэшиРегистров(ПараметрыРасчета, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПерепровестиДокументыПоРегистрам(ПараметрыРасчета)
	
	// Перепроведение документов выполняется в автотестах.
	Если НЕ ПараметрыРасчета.АвтоматическоеТестирование Тогда
		Возврат;
	КонецЕсли;
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "ПерепровестиДокументыПоРегистрам");
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	#Область СебестоимостьТоваров
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Регистратор КАК Ссылка,
	|	ДД.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК ДД
	|ГДЕ
	|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДД.Организация В (&МассивОрганизаций)
	|	И НЕ ДД.РасчетПартий
	|	И НЕ ДД.РасчетСебестоимости
	|";
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	ОписаниеРегистра = ПараметрыРасчета.Движения[Метаданные.РегистрыНакопления.СебестоимостьТоваров.Имя];
	ОписаниеРегистра.НадоОбновитьРасчетныйКэш = Истина;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВосстановитьДвиженияДокументовПоРегиструНакопления(
		ПараметрыРасчета, 
		ВыборкаДокументов, 
		Метаданные.РегистрыНакопления.СебестоимостьТоваров.Имя);
		
	#КонецОбласти
	
	#Область ВыручкаИСебестоимостьПродаж
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Регистратор КАК Ссылка,
	|	ДД.АналитикаУчетаПоПартнерам.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ДД
	|ГДЕ
	|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДД.АналитикаУчетаПоПартнерам.Организация В (&МассивОрганизаций)
	|	И НЕ ДД.РасчетПартий
	|	И НЕ ДД.РасчетСебестоимости
	|";
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	ОписаниеРегистра = ПараметрыРасчета.Движения[Метаданные.РегистрыНакопления.ВыручкаИСебестоимостьПродаж.Имя];
	ОписаниеРегистра.НадоОбновитьРасчетныйКэш = Истина;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВосстановитьДвиженияДокументовПоРегиструНакопления(
		ПараметрыРасчета, 
		ВыборкаДокументов, 
		Метаданные.РегистрыНакопления.ВыручкаИСебестоимостьПродаж.Имя);
		
	#КонецОбласти
	
	#Область ПрочиеРасходы
	
	Запрос.УстановитьПараметр("ТипыДокументовОСиНМА", РасчетСебестоимостиПрикладныеАлгоритмы.ТипыДокументовОСиНМА());
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Регистратор КАК Ссылка,
	|	ДД.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК ДД
	|ГДЕ
	|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДД.Организация В (&МассивОрганизаций)
	|	И НЕ ДД.РасчетПартий
	|	И НЕ ДД.РасчетСебестоимости
	|	И НЕ ТИПЗНАЧЕНИЯ(ДД.Регистратор) В (&ТипыДокументовОСиНМА)
	|";
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	ОписаниеРегистра = ПараметрыРасчета.Движения[Метаданные.РегистрыНакопления.ПрочиеРасходы.Имя];
	ОписаниеРегистра.НадоОбновитьРасчетныйКэш = Истина;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВосстановитьДвиженияДокументовПоРегиструНакопления(
		ПараметрыРасчета, 
		ВыборкаДокументов, 
		Метаданные.РегистрыНакопления.ПрочиеРасходы.Имя);
		
	#КонецОбласти
	
	#Область ПартииПрочихРасходов
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Регистратор КАК Ссылка,
	|	ДД.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК ДД
	|ГДЕ
	|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДД.Организация В (&МассивОрганизаций)
	|	И НЕ ДД.РасчетПартий
	|	И НЕ ТИПЗНАЧЕНИЯ(ДД.Регистратор) В (&ТипыДокументовОСиНМА)
	|";
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	ОписаниеРегистра = ПараметрыРасчета.Движения[Метаданные.РегистрыНакопления.ПартииПрочихРасходов.Имя];
	ОписаниеРегистра.НадоОбновитьРасчетныйКэш = Истина;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВосстановитьДвиженияДокументовПоРегиструНакопления(
		ПараметрыРасчета, 
		ВыборкаДокументов, 
		Метаданные.РегистрыНакопления.ПартииПрочихРасходов.Имя);
		
	#КонецОбласти
	
КонецПроцедуры

// Механизм, аналогичный по своему назначению механизму обновления ИБ.
// Выполняется при расчете конкретного периода и исправляет документы только этого периода.
//
// Исправляет проблемы в движениях документов и в самих документах в рассчитываемом периоде.
//
// Есть ошибки, которые "портят" исходные данные для расчета,
// но для исправления их последствий не пишется обработчик обновления.
// Например, из-за какой-то старой ошибки в каких-то случаях движения документа были некорректны.
// - если для исправления этой ошибки написать обработчик обновления,
// то он изменит данные прошлых, "закрытых" периодов, из-за чего эти периоды перестанут быть "закрытыми".
// Придется повторно закрывать эти периоды, из-за чего в них может измениться себестоимость и, как следствие,
// регламентированная отчетность. Это плохо, т.к. по этим периодам отчетность уже может быть сдана.
// - с другой стороны, в тех периодах, которые пользователь будет пересчитывать, эту ошибку в движениях надо бы исправить,
// чтобы в этих пересчитанных периодах себестоимость основывалась на правильных исходных данных.
//
// Для исправления таких ошибок и предназначена данная процедура.
// По сути, она является аналогом обработчика обновления, исправляющего движения документов в периоде, указанном пользователем.
//
// Также эта процедура используется для корректировки движений периода при его перерасчете в партионном учете версии 2.2.
//
Процедура ИсправитьПроблемыВДвиженияхИДокументах(ПараметрыРасчета)
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "ИсправитьПроблемыВДвиженияхИДокументах");
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	#Область ПоляПартионногоУчетаВерсии22
	
	// Проверим в первичных движениях документов корректность заполнения полей:
	// Партия, КорПартия, ТипЗаписи, аналитика учета партий, вид деятельности НДС.
	// Если поля заполнены некорректно, то, видимо, документ был проведен до перехода на партионный учет версии 2.2 -
	// надо перепровести документ по регистру, содержащему некорректные движения.
	// Если после перепроведения документа поля все равно заполнены некорректно, значит ошибка кроется
	// в процедурах формирования движений документа или в макете описания движений (для регистра себестоимости товаров).
	
	// Подготовим вспомогательные данные.
	ОписаниеЦепочек 		= РасчетСебестоимостиЗаполнениеПартий.ОписаниеЦепочекПартийТоваров(ПараметрыРасчета);
	ИспользуемыеТипыЗаписей = РасчетСебестоимостиПрикладныеАлгоритмы.ИспользуемыеТипыЗаписейВЦепочках(ОписаниеЦепочек);
	
	Запрос.УстановитьПараметр("ИспользуемыеТипыЗаписей", ИспользуемыеТипыЗаписей);
	
	#Область ТекстЗапроса
	
	// Подготовим запрос для формирования ВТРегистраторыСНекорректнымиДвижениями.
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.ВидДвижения				 КАК ВидДвижения,
	|	Т.Регистратор 				 КАК Регистратор,
	|	Т.Организация 				 КАК Организация,
	|	Т.КорОрганизация 			 КАК КорОрганизация,
	|	Т.Партия 	  				 КАК Партия,
	|	Т.КорПартия 	  			 КАК КорПартия,
	|	Т.ХозяйственнаяОперация 	 КАК ХозяйственнаяОперация,
	|	Т.ТипЗаписи					 КАК ТипЗаписи,
	|	Т.АналитикаУчетаПартий		 КАК АналитикаУчетаПартий,
	|	Т.КорАналитикаУчетаПартий	 КАК КорАналитикаУчетаПартий,
	|	Т.РазделУчета				 КАК РазделУчета,
	|	Т.ВидЗапасов				 КАК ВидЗапасов,
	|	Т.КорВидЗапасов				 КАК КорВидЗапасов,
	|	Т.КорРазделУчета			 КАК КорРазделУчета,
	|	Т.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	Т.КорАналитикаУчетаНоменклатуры	КАК КорАналитикаУчетаНоменклатуры,
	|	Т.АналитикаФинансовогоУчета	КАК АналитикаФинансовогоУчета,
	|	Т.КорАналитикаФинансовогоУчета	КАК КорАналитикаФинансовогоУчета,
	|	Т.ВидДеятельностиНДС		 КАК ВидДеятельностиНДС,
	|	Т.КорВидДеятельностиНДС		 КАК КорВидДеятельностиНДС,
	|	Т.АналитикаУчетаПоПартнерам	 КАК АналитикаУчетаПоПартнерам,
	|	Т.ГруппаПродукции			 КАК ГруппаПродукции,
	|	Т.ДокументИсточник			 КАК ДокументИсточник,
	|	Т.ПериодПродажи				 КАК ПериодПродажи,
	|	ВЫБОР КОГДА Т.Количество > 0
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ 						 КАК ПоложительноеКоличество,
	|	Т.ДокументДвижения			 КАК ДокументДвижения,
	|	Т.ИдентификаторСтроки		 КАК ИдентификаторСтроки,
	|	Т.Стоимость					 КАК Стоимость,
	|	Т.СтоимостьРегл				 КАК СтоимостьРегл,
	|	Т.СтоимостьЗабалансоваяРегл	 КАК СтоимостьЗабалансоваяРегл
	|ПОМЕСТИТЬ ВТДвиженияСебестоимостьТоваров
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (ТИП(Документ.РасчетСебестоимостиТоваров),
	|									ТИП(Документ.КорректировкаРегистров))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор 			 КАК Регистратор,
	|	Т.Организация 			 КАК Организация,
	|	Т.АналитикаУчетаПартий	 КАК АналитикаУчетаПартий,
	|	Т.ВидДеятельностиНДС	 КАК ВидДеятельностиНДС
	|ПОМЕСТИТЬ ВТДвиженияПартииПрочихРасходов
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.Регистратор ССЫЛКА Документ.РаспределениеНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор КАК Регистратор,
	|	Т.Организация КАК Организация,
	|	Т.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|ПОМЕСТИТЬ ВозвратыТоваровНаДругойСклад
	|ИЗ
	|	ВТДвиженияСебестоимостьТоваров КАК Т
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Т.Регистратор) В (ТИП(Документ.ВозвратТоваровОтКлиента), ТИП(Документ.ВозвратТоваровМеждуОрганизациями))
	|	И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ВозвратНаДругойСклад)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.ИмяРегистра КАК ИмяРегистра,
	|	Т.Ссылка 	  КАК Ссылка,
	|	Т.Организация КАК Организация,
	|	Т.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	Т.КодОшибки   КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"" КАК ИмяРегистра,
	|		Т.Регистратор КАК Ссылка,
	|		Т.Организация КАК Организация,
	|		Т.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|		ВЫБОР
	|			КОГДА Правила.ПустоеЗначениеРегистратора ЕСТЬ NULL 
	|				ТОГДА 1
	|			КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ВЫБОР
	|						КОГДА Т.ТипЗаписи В
	|							(ВЫБРАТЬ
	|								ТипыЗаписей.ТипЗаписиПриход
	|							 ИЗ
	|								ВТПравилаЗаполненияПоляТипЗаписи КАК ТипыЗаписей
	|							 ГДЕ
	|								ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИПЗНАЧЕНИЯ(ТипыЗаписей.ПустоеЗначениеРегистратора)
	|								И (ТипыЗаписей.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|									ИЛИ Т.ХозяйственнаяОперация = ТипыЗаписей.ХозяйственнаяОперация)
	|								И (ТипыЗаписей.ПоложительноеКоличество = НЕОПРЕДЕЛЕНО
	|									ИЛИ Т.ПоложительноеКоличество = ТипыЗаписей.ПоложительноеКоличество))
	|							ТОГДА 0
	|						ИНАЧЕ 2
	|					КОНЕЦ
	|			ИНАЧЕ ВЫБОР
	|						КОГДА Т.ТипЗаписи В
	|							(ВЫБРАТЬ
	|								ТипыЗаписей.ТипЗаписиРасход
	|							 ИЗ
	|								ВТПравилаЗаполненияПоляТипЗаписи КАК ТипыЗаписей
	|							 ГДЕ
	|								ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИПЗНАЧЕНИЯ(ТипыЗаписей.ПустоеЗначениеРегистратора)
	|								И (ТипыЗаписей.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|									ИЛИ Т.ХозяйственнаяОперация = ТипыЗаписей.ХозяйственнаяОперация)
	|								И (ТипыЗаписей.ПоложительноеКоличество = НЕОПРЕДЕЛЕНО
	|									ИЛИ Т.ПоложительноеКоличество = ТипыЗаписей.ПоложительноеКоличество))
	|						ТОГДА 0
	|					ИНАЧЕ 3
	|				КОНЕЦ
	|		КОНЕЦ КАК КодОшибки
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТПравилаЗаполненияПоляТипЗаписи КАК Правила
	|			ПО ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИПЗНАЧЕНИЯ(Правила.ПустоеЗначениеРегистратора)
	|				И (Правила.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|					ИЛИ Т.ХозяйственнаяОперация = Правила.ХозяйственнаяОперация)
	|				И (Правила.ПоложительноеКоличество = НЕОПРЕДЕЛЕНО
	|					ИЛИ Т.ПоложительноеКоличество = Правила.ПоложительноеКоличество)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		ВЫБОР
	|			КОГДА Т.Организация В (&ОрганизацииСФИФОСкользящая) И Т.Партия = НЕОПРЕДЕЛЕНО
	|				ТОГДА 4
	|			КОГДА НЕ (Т.Организация В (&ОрганизацииСФИФОСкользящая)) И Т.Партия <> НЕОПРЕДЕЛЕНО
	|			  И Т.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)
	|			  И Т.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|			  И Т.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|				ТОГДА 5
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ТипЗаписи В (&ТипыЗаписейПервичныхПартий)
	|		ИЛИ Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаПриобретения)
	|		ИЛИ Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыВПути)
	|			И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
	|		ИЛИ Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Дополнение)
	|			И Т.ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		ВЫБОР
	// Проверка кор. партии в расходных движениях при заполненной кор. организации (схема Интеркампани).
	|			КОГДА Т.Организация В (&ОрганизацииСФИФОСкользящая)
	|				И Т.КорОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|				И НЕ Т.КорОрганизация В (&ОрганизацииСФИФОСкользящая)
	|				И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА
	|					ВЫБОР
	|						КОГДА Т.КорПартия = НЕОПРЕДЕЛЕНО
	|							ТОГДА 0
	|						ИНАЧЕ 5
	|					КОНЕЦ
	|			КОГДА НЕ Т.Организация В (&ОрганизацииСФИФОСкользящая)
	|				И Т.КорОрганизация В (&ОрганизацииСФИФОСкользящая)
	|				И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				И Т.КорРазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка)
	|				ТОГДА
	|					ВЫБОР
	|						КОГДА Т.КорПартия = Т.Регистратор
	|							ТОГДА 0
	|						ИНАЧЕ 6
	|					КОНЕЦ
	// Проверка кор. партии в расходных движениях без кор. организации (изменение документа партии внутри одной организации).
	|			КОГДА Т.Организация В (&ОрганизацииСФИФОСкользящая)
	|				И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				И НЕ Т.КорРазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку),
	|										ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение),
	|										ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка))
	|				И (
	// При списании на партии производства кор. партия должна быть заполнена.
	|					(
	|						(ЕСТЬNULL(Правила.КорПартияВРасходе, ЛОЖЬ)
	|							И Т.КорПартия = НЕОПРЕДЕЛЕНО
	|						ИЛИ НЕ ЕСТЬNULL(Правила.КорПартияВРасходе, ЛОЖЬ)
	|							И НЕ Т.КорПартия = НЕОПРЕДЕЛЕНО)
	|					)
	|				)
	|				ТОГДА 6
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТПравилаЗаполненияПоляТипЗаписи КАК Правила
	|			ПО ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИПЗНАЧЕНИЯ(Правила.ПустоеЗначениеРегистратора)
	|				И (Правила.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|					ИЛИ Т.ХозяйственнаяОперация = Правила.ХозяйственнаяОперация)
	|				И (Правила.ПоложительноеКоличество = НЕОПРЕДЕЛЕНО
	|					ИЛИ Т.ПоложительноеКоличество = Правила.ПоложительноеКоличество)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		7
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		НЕ (Т.ТипЗаписи В (&ИспользуемыеТипыЗаписей))
	|		И НЕ (Т.ТипЗаписи В (&НепересчитываемыеТипыЗаписей))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		8
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК Аналитики
	|			ПО Т.АналитикаУчетаПартий = Аналитики.Ссылка
	|	ГДЕ
	|		Аналитики.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		9
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК Аналитики
	|			ПО Т.КорАналитикаУчетаПартий = Аналитики.Ссылка
	|	ГДЕ
	|		Аналитики.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""ПартииПрочихРасходов"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		8
	|	ИЗ
	|		ВТДвиженияПартииПрочихРасходов КАК Т
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК Аналитики
	|			ПО Т.АналитикаУчетаПартий = Аналитики.Ссылка
	|	ГДЕ
	|		Аналитики.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""ПартииПрочихРасходов"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		10
	|	ИЗ
	|		ВТДвиженияПартииПрочихРасходов КАК Т
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК Аналитики
	|			ПО Т.АналитикаУчетаПартий = Аналитики.Ссылка
	|	ГДЕ
	|		Т.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		12
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Т.КорАналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Возвраты.Регистратор,
	|		Возвраты.Организация,
	|		Возвраты.ХозяйственнаяОперация,
	|		13
	|	ИЗ
	|		ВозвратыТоваровНаДругойСклад КАК Возвраты
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТДвиженияСебестоимостьТоваров КАК Т
	|			ПО Т.Регистратор = Возвраты.Регистратор
	|			И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СторноВозвратНаДругойСклад)
	|	ГДЕ
	|		Т.Регистратор ЕСТЬ NULL
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		14
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)
	|		И НЕ Т.РазделУчета В (
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи))
	|
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		17
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ДополнительныеРасходыСПартией)
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыСПартией)
	|		И Т.ДокументИсточник = НЕОПРЕДЕЛЕНО
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		ВЫБОР
	|			КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|					И Правила.ДокументИсточникВПриходе И Т.ДокументИсточник = НЕОПРЕДЕЛЕНО
	|			  ТОГДА 18
	|			КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И Правила.ДокументИсточникВРасходе И Т.ДокументИсточник = НЕОПРЕДЕЛЕНО
	|			  ТОГДА 19
	|			  ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТПравилаЗаполненияПоляТипЗаписи КАК Правила
	|			ПО ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИПЗНАЧЕНИЯ(Правила.ПустоеЗначениеРегистратора)
	|				И (Правила.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|					ИЛИ Т.ХозяйственнаяОперация = Правила.ХозяйственнаяОперация)
	|				И (Правила.ПоложительноеКоличество = НЕОПРЕДЕЛЕНО
	|					ИЛИ Т.ПоложительноеКоличество = Правила.ПоложительноеКоличество)
	|				И (Правила.ДокументИсточникВПриходе И Т.ТипЗаписи = Правила.ТипЗаписиПриход
	|					ИЛИ Правила.ДокументИсточникВРасходе И Т.ТипЗаписи = Правила.ТипЗаписиРасход)
	|	ГДЕ
	|		ВЫБОР
	|			КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|					И Правила.ДокументИсточникВПриходе И Т.ДокументИсточник = НЕОПРЕДЕЛЕНО
	|			  ТОГДА 18
	|			КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И Правила.ДокументИсточникВРасходе И Т.ДокументИсточник = НЕОПРЕДЕЛЕНО
	|			  ТОГДА 19
	|			  ИНАЧЕ 0
	|		КОНЕЦ <> 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		20
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		НЕ Т.Организация В (&ОрганизацииСФИФОСкользящая)
	|		И Т.КорАналитикаУчетаПартий = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)
	|		И Т.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Т.ТипЗаписи В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаПриобретения))
	|		И Т.РазделУчета В (
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты))
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		21
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиентаПрошлыхПериодов)
	|		И Т.ПериодПродажи = ДАТАВРЕМЯ(1,1,1)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		22
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет)
	|		И Т.КорОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		24
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
	|			ПО ВыручкаИСебестоимостьПродаж.Регистратор = Т.Регистратор
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Т.Организация = ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|		И Т.АналитикаУчетаПоПартнерам = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)
	|		И Т.ТипЗаписи <> ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ВозвратНаДругойСклад)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		25
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		(Т.КорАналитикаУчетаПартий = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)
	|			ИЛИ Т.КорАналитикаУчетаПартий = Т.АналитикаУчетаПартий) // По ошибке перезаполнился реквизит КорАналитикаУчетаПартий
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Т.ТипЗаписи В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыБезПартии),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыСПартией))
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""ВыручкаИСебестоимостьПродаж"",
	|		Т.Регистратор,
	|		Аналитика.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		28
	|	ИЗ
	|		РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
	|			ПО Аналитика.Ссылка = Т.АналитикаУчетаПоПартнерам
	|	ГДЕ
	|		Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Аналитика.Организация В (&МассивОрганизаций)
	|		И Т.Активность
	|		И НЕ Т.РасчетПартий
	|		И НЕ Т.РасчетСебестоимости
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ОтчетПоКомиссииМеждуОрганизациями)
	|		И Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		30
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТаможеннаяДекларацияИмпорт КАК Декларация
	|			ПО Декларация.Ссылка = Т.Регистратор
	|	ГДЕ
	|		Т.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		31
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		&УчитыватьСебестоимостьТоваровПоНазначениям
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) В (
	|			ТИП(Документ.СборкаТоваров))
	|		И ЕСТЬNULL(Т.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС, ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка))
	|			<> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|		И ЕСТЬNULL(Т.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС, ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка))
	|			<> Т.ВидДеятельностиНДС
	|	
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		36
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения КАК КорректировкаПриобретения
	|			ПО КорректировкаПриобретения.Ссылка = Т.Регистратор
	|	ГДЕ
	|		Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаПриобретенияПрошлогоПериода)
	|		И Т.Стоимость <> 0
	|	
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""ПартииПрочихРасходов"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		39
	|	ИЗ
	|		РегистрНакопления.ПартииПрочихРасходов КАК Т
	|	ГДЕ
	|		Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Т.Организация В (&МассивОрганизаций)
	|		И Т.Активность
	|		И НЕ Т.РасчетПартий
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ПрочиеДоходыРасходы)
	|		И Т.ДокументПоступленияРасходов = НЕОПРЕДЕЛЕНО
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		40
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		И Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		41
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.КорректировкаНазначенияТоваров)
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Т.АналитикаУчетаНоменклатуры = Т.КорАналитикаУчетаНоменклатуры
	|		И Т.ВидЗапасов = Т.КорВидЗапасов
	|	
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		43
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ПередачаТоваровМеждуОрганизациями)
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию)
	|		И Т.ИдентификаторСтроки = """"
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		44
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи)
	|		И Т.ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|
	|	) КАК Т
	|
	|ГДЕ
	|	Т.КодОшибки <> 0
	|";
	
	#КонецОбласти
	
	// Сформируем описание возможных кодов ошибок в движениях.
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Для движения не найдено соответствующее ему правило заполнения типа записи'"));
	РасшифровкаКодовОшибок.Вставить(2, НСтр("ru='Тип записи приходного движения не соответствует правилу'"));
	РасшифровкаКодовОшибок.Вставить(3, НСтр("ru='Тип записи расходного движения не соответствует правилу'"));
	РасшифровкаКодовОшибок.Вставить(4, НСтр("ru='Для организаций с методом оценки ""ФИФО (скользящая)"" не заполнена первичная партия'"));
	РасшифровкаКодовОшибок.Вставить(5, НСтр("ru='Для организаций с методом оценки, отличным от ""ФИФО (скользящая)"", заполнена первичная партия или кор. партия'"));
	РасшифровкаКодовОшибок.Вставить(6, НСтр("ru='Для организаций с методом оценки ""ФИФО (скользящая)"" некорректно заполнена кор. партия в расходном движении'"));
	РасшифровкаКодовОшибок.Вставить(7, НСтр("ru='Тип записи, указанный в движении, не используется при распределении партий'"));
	РасшифровкаКодовОшибок.Вставить(8, НСтр("ru='Аналитика учета партий не содержит вида ценности'"));
	РасшифровкаКодовОшибок.Вставить(9, НСтр("ru='Кор. аналитика учета партий не содержит вида ценности'"));
	РасшифровкаКодовОшибок.Вставить(10, НСтр("ru='Не заполнен вид деятельности НДС при заполненной аналитике учета партий'"));
	РасшифровкаКодовОшибок.Вставить(11, НСтр("ru='Для аналитики учета партий выпуска изделий не заполнен код строки (продукции)'"));
	РасшифровкаКодовОшибок.Вставить(12, НСтр("ru='Не заполнена кор. аналитика учета номенклатуры для операции ""Реализация (товары в пути)""'"));
	РасшифровкаКодовОшибок.Вставить(13, НСтр("ru='Для документов возврата товаров от клиента/организации на склад, отличный от склада реализации, используются неправильные хозяйственные операции и типы записей'"));
	РасшифровкаКодовОшибок.Вставить(14, НСтр("ru='Не заполнен вид деятельности НДС у приходных движений с типом записи ""Партия""'"));
	РасшифровкаКодовОшибок.Вставить(17, НСтр("ru='Не заполнен документ - источник для операции ""Дополнительные расходы с указанием документа партии"".'"));
	РасшифровкаКодовОшибок.Вставить(18, НСтр("ru='Заполнение документа-источника в приходном движении не соответствует правилу.'"));
	РасшифровкаКодовОшибок.Вставить(19, НСтр("ru='Заполнение документа-источника в расходном движении не соответствует правилу.'"));
	РасшифровкаКодовОшибок.Вставить(20, НСтр("ru='Не заполнена кор. аналитика учета партий приходных движений с типами записи ""Партия"" и ""Корректировка приобретения"" для организаций с методом оценки, отличным от ""ФИФО (скользящая)""'"));
	РасшифровкаКодовОшибок.Вставить(21, НСтр("ru='Не заполнен период продажи для операции ""Возврат товаров от клиента прошлых периодов""'"));
	РасшифровкаКодовОшибок.Вставить(22, НСтр("ru='Не заполнены кор. реквизиты для операции ""Закупка по регл. учету""'"));
	РасшифровкаКодовОшибок.Вставить(24, НСтр("ru='Не заполнена аналитика учета по партнерам в движениях документов продажи'"));
	РасшифровкаКодовОшибок.Вставить(25, НСтр("ru='Не заполнена кор. аналитика учета партий в приходных движениях с типом записей ""Доп. расходы (не указана партия)"" и ""Доп. расходы (указана партия)""'"));
	РасшифровкаКодовОшибок.Вставить(28, НСтр("ru='Не корректно заполнен раздел учета для операции ""Отчет по комиссии между организациями""'"));
	РасшифровкаКодовОшибок.Вставить(30, НСтр("ru='Не корректно заполнен кор. вид деятельности НДС для документа ""Таможенная декларация на импорт"".'"));
	РасшифровкаКодовОшибок.Вставить(31, НСтр("ru='Не корректно заполнен вид деятельности НДС в приходном движении документа.'"));
	РасшифровкаКодовОшибок.Вставить(32, НСтр("ru='Не корректно заполнен тип записи в приходном движении для документа ""Производство без заказа"".'"));
	РасшифровкаКодовОшибок.Вставить(33, НСтр("ru='Не корректные движения по изменению назначения работ для документа ""Этап производства"".'"));
	РасшифровкаКодовОшибок.Вставить(34, НСтр("ru='Некорректные движения по разделу учета ""Производственные затраты"".'"));
	РасшифровкаКодовОшибок.Вставить(36, НСтр("ru='Некорректные движения документа ""Корректировка приобретения"", сформированного в предыдущих версиях партионного учета.'"));
	РасшифровкаКодовОшибок.Вставить(39, НСтр("ru='Не заполнен документ поступления расходов в движениях документа ""Отражение прочих доходов и расходов"".'"));
	РасшифровкаКодовОшибок.Вставить(40, НСтр("ru='Не правильный раздел учета для товаров на хранении с правом продажи.'"));
	РасшифровкаКодовОшибок.Вставить(41, НСтр("ru='Некорректные движения документа ""Корректировка назначения товаров"" при отключенном учете себестоимости по назначениям.'"));
	РасшифровкаКодовОшибок.Вставить(43, НСтр("ru='Не заполнен идентификатор строки в расходном движении для операции ""Реализация товаров в другую организацию"".'"));
	РасшифровкаКодовОшибок.Вставить(44, НСтр("ru='Не правильный раздел учета для собственных товарах.'"));

	ДополнительныеПоля = Новый Структура("ХозяйственнаяОперация", НСтр("ru = 'операция'"));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='переход на партионный учет версии 2.2'"),
		ДополнительныеПоля);
	
	#КонецОбласти
	

	#Область ВыручкаИСебестоимостьПродаж
	
	// Исправление некорректных движений в регистре "Выручка и себестоимость продаж".
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Дублирование расчетной себестоимости в первичных движениях'"));
	
	СуществующиеВТ = РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьПереченьСуществующихВременныхТаблиц(Запрос);
	
	Запрос.Текст = ТекстЗапросаДокументыСДублямиДвиженийПоВыручкеИСебестоимостиПродаж();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='ошибка 00-00071689'"));
	
	НовыеВТ = РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьПереченьСуществующихВременныхТаблиц(Запрос, СуществующиеВТ);
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета, НовыеВТ);
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Пропавшие первичные движения по корректировке выручки'"));
	
	СуществующиеВТ = РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьПереченьСуществующихВременныхТаблиц(Запрос);
	
	Запрос.Текст = ТекстЗапросаДокументыСПропавшимиДвижениямиПоВыручкеИСебестоимостиПродаж();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='ошибка 00-00302463'"));
	
	НовыеВТ = РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьПереченьСуществующихВременныхТаблиц(Запрос, СуществующиеВТ);
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета, НовыеВТ);
	
	#КонецОбласти

	#Область НекорректнаяАналитикаФинансовогоУчета
	
	// Исправление некорректной пустой аналитики финансового учета в регистре "Себестоимость товаров".
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Пустая аналитика финансового учет в первичных движениях имеет значение отличное от НЕОПРЕДЕЛЕНО'"));
	РасшифровкаКодовОшибок.Вставить(2, НСтр("ru='Пустая кор аналитика финансового учет в первичных движениях имеет значение отличное от НЕОПРЕДЕЛЕНО'"));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	1 						 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И Т.АналитикаФинансовогоУчета В
	|		(ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка),
	|		 ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка),
	|		 ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка),
	|		 ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	2 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И Т.КорАналитикаФинансовогоУчета В
	|		(ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка),
	|		 ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка),
	|		 ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка),
	|		 ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка))
	|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) В (
	|		ТИП(Документ.РеализацияТоваровУслуг),
	|		ТИП(Документ.ПеремещениеТоваров))
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='ошибка 00-00098117'"));
	
	#КонецОбласти

	#Область СуммыВДвижениях
	
	// Проверка соответствия заполненности суммы упр./регл. значениям функциональных опций.
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Заполнение упр. сумм в движениях не соответствует значению функциональной опции ""ВестиУправленческийУчетОрганизаций""'"));
	РасшифровкаКодовОшибок.Вставить(2, НСтр("ru='Заполнение регл. сумм в движениях не соответствует значению функциональной опции ""ИспользоватьУчетПрочихДоходовРасходовРегл""'"));
	РасшифровкаКодовОшибок.Вставить(3, НСтр("ru='Заполнение суммы без НДС в движениях не соответствует варианту распределения статьи расходов'"));
	
	#Область ИсключаемыеТипыДокументов
	
	Запрос.УстановитьПараметр("ТипыДокументовОСиНМА", РасчетСебестоимостиПрикладныеАлгоритмы.ТипыДокументовОСиНМА());
	
	ТипыРегистраторов = Новый Массив;
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.РаспределениеНДС"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.РаспределениеДоходовПоНаправлениямДеятельности"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.РаспределениеПрочихЗатрат"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.РаспределениеРасходовБудущихПериодов"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.РасчетКурсовыхРазниц"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.РегистраторРасчетов"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.ПрочиеДоходыРасходы"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.ДвижениеПрочихАктивовПассивов"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.КорректировкаПриобретения"));
	
	Запрос.УстановитьПараметр("ТипыРегламентныхДокументов", ТипыРегистраторов);
	
	#КонецОбласти
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	1 						 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И (Т.ТипЗаписи В (&НепересчитываемыеТипыЗаписей)
	|		ИЛИ Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление)
	|		  И Т.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию))
	|	И (Т.Стоимость <> 0 ИЛИ Т.КорСтоимость <> 0 ИЛИ Т.ДопРасходы <> 0 ИЛИ Т.Трудозатраты <> 0
	|		ИЛИ Т.ПостатейныеПостоянныеСНДС <> 0 ИЛИ Т.ПостатейныеПеременныеСНДС <> 0
	|		ИЛИ Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию))
	|	И НЕ Т.РазделУчета В (
	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию),
	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку),
	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи))
	|	И ((&УправленческийУчетОрганизаций И (Т.СтоимостьБезНДС <> 0 ИЛИ Т.КорСтоимость <> 0
	|			ИЛИ Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию)
	|			 И Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		И Т.СтоимостьУпр = 0 И Т.ДопРасходыУпр = 0 И Т.ТрудозатратыУпр = 0
	|		И Т.ПостатейныеПостоянныеУпр = 0 И Т.ПостатейныеПеременныеУпр = 0)
	|	ИЛИ (НЕ &УправленческийУчетОрганизаций
	|		И (Т.СтоимостьУпр <> 0 ИЛИ Т.ДопРасходыУпр <> 0 ИЛИ Т.ТрудозатратыУпр <> 0
	|		ИЛИ Т.ПостатейныеПостоянныеУпр <> 0 ИЛИ Т.ПостатейныеПеременныеУпр <> 0)))
	|	И НЕ (Т.СтоимостьБезНДС = 0	И Т.ДопРасходы <> 0 И Т.ДопРасходыУпр = 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПрочиеДоходы"" 		 КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	ВЫБОР
	|		КОГДА Т.Сумма <> 0
	|			И Т.СуммаРегл = 0
	|			И &ИспользоватьУчетПрочихДоходовРасходовРегл
	|			И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ					 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ПрочиеДоходы КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетСебестоимости
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
	|	И ((&УправленческийУчетОрганизаций
	|			И Т.СуммаУпр = 0
	|			И Т.Сумма <> 0
	|			И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА))
	|		ИЛИ (НЕ &УправленческийУчетОрганизаций
	|			И Т.СуммаУпр <> 0)
	|		ИЛИ (Т.Сумма <> 0
	|			И Т.СуммаРегл = 0 И ВЫРАЗИТЬ(Т.Сумма * &КоэффициентВалютыРегл КАК ЧИСЛО(31,2)) <> 0
	|			И &ИспользоватьУчетПрочихДоходовРасходовРегл
	|			И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПрочиеРасходы"" 		 КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	1 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
	|	И ((&УправленческийУчетОрганизаций И Т.СуммаУпр = 0 И Т.Сумма <> 0 И Т.СуммаРегл <> 0
	|			И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА))
	|		ИЛИ (НЕ &УправленческийУчетОрганизаций И Т.СуммаУпр <> 0
	|			И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПартииПрочихРасходов"" КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	1 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
	|	И ((&УправленческийУчетОрганизаций И Т.Стоимость <> 0 И Т.НДСРегл <> 0 И Т.НДСУпр = 0 И ВЫРАЗИТЬ(Т.НДСРегл * &КоэффициентВалютыУпр КАК ЧИСЛО(31,2)) <> 0
	|			И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА))
	|		ИЛИ (НЕ &УправленческийУчетОрганизаций И Т.НДСУпр <> 0))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ВыручкаИСебестоимостьПродаж"" КАК ИмяРегистра,
	|	Т.Регистратор 			        КАК Ссылка,
	|	Аналитика.Организация 			КАК Организация,
	|	1 						        КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
	|		ПО Аналитика.Ссылка = Т.АналитикаУчетаПоПартнерам
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Аналитика.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
	|	И ((&УправленческийУчетОрганизаций И Т.Стоимость <> 0 И Т.СтоимостьУпр = 0)
	|		ИЛИ (НЕ &УправленческийУчетОрганизаций И Т.СтоимостьУпр <> 0))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПрочиеРасходы"" 		 КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	3 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
	|	И Т.СуммаБезНДС <> 0
	|	И Т.СтатьяРасходов.ВариантРаспределенияРасходовУпр <>
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='некорректное заполнение сумм упр. учета'"));
	
	#КонецОбласти


	#Область ДвиженияНоменклатураДоходыРасходы
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Сформированы некорректные движения по регистру ""Движения Номенклатура - Доходы/Расходы"".'"));
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор				КАК Ссылка,
	|	Т.Период					КАК Период,
	|	Т.Организация				КАК Организация
	|ПОМЕСТИТЬ ВТДвиженияСебестоимостиСписаниеВПроизводстве
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноСписанияНаРасходы)
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Период
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор 				КАК Ссылка,
	|	Т.Период	  				КАК Период
	|ПОМЕСТИТЬ ВТДвиженияНоменклатураСписаниеВПроизводстве
	|ИЗ
	|	РегистрНакопления.ДвиженияНоменклатураДоходыРасходы КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И НЕ Т.РасчетСебестоимости
	|	И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноСписанияНаРасходы)
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Период
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ДвиженияНоменклатураДоходыРасходы""	КАК ИмяРегистра,
	|	Себестоимость.Ссылка					КАК Ссылка,
	|	Себестоимость.Организация				КАК Организация,
	|	1 										КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	ВТДвиженияСебестоимостиСписаниеВПроизводстве КАК Себестоимость
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТДвиженияНоменклатураСписаниеВПроизводстве КАК Движения
	|		ПО Движения.Ссылка = Себестоимость.Ссылка
	|		 И Движения.Период = Себестоимость.Период
	|ГДЕ
	|	Движения.Ссылка ЕСТЬ NULL
	|";

	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='исправление движений документов по регистру ""Движения номенклатура доходы-расходы""'"));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(
		ПараметрыРасчета,
		"ВТДвиженияСебестоимостиСписаниеВПроизводстве, ВТДвиженияНоменклатураСписаниеВПроизводстве");
	
	#КонецОбласти

	#Область ПартииПрочихРасходов
	
	// В регистре "Партии прочих расходов" не должно быть движений с видом деятельности НДС "Определяется распределением".
	// Такие движения должны формироваться в регистре "Партии НДС к распределению".
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Некорректные движения с видом деятельности НДС ""Определяется распределениям"" в регистре ПартииПрочихРасходов'"));
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПартииПрочихРасходов"" КАК ИмяРегистра,
	|	Т.Регистратор			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	1 						 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И Т.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ПередачаТоваровМеждуОрганизациями)
	|";
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='некорректные движения по регистру ""Партии прочих расходов""'"));
	
	#КонецОбласти
	
	#Область НаправлениеДеятельностиВПрочихРасходах
		
	// В регистрах "Прочие расходы" и "Партии прочих расходов" направление деятельности заполняется только при включенном
	// учете затрат по направлению деятельности. 
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Заполнено направление деятельности с выключенным учетом затрат'"));
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПрочиеРасходы""		 КАК ИмяРегистра,
	|	Т.Регистратор			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	1 						 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И Т.НаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	И НЕ Т.НаправлениеДеятельности.УчетЗатрат
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)
	|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) <> ТИП(Документ.РаспределениеДоходовПоНаправлениямДеятельности) 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПартииПрочихРасходов"" КАК ИмяРегистра,
	|	Т.Регистратор			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	1 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И Т.НаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	И НЕ Т.НаправлениеДеятельности.УчетЗатрат
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)
	|";
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='некорректное заполнение направления деятельности в прочих расходах'"));
	
	#КонецОбласти

	#Область ПартнерВКлючеАналитикиУчетаНоменклатуры
	
	// Замена ключей аналитики учета номенклатуры с партнером на ключи с договором контрагента.
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='В ключе аналитики учета номенклатуры в регистре ""Себестоимость товаров"" указан партнер, а не договор контрагента.'"));
	РасшифровкаКодовОшибок.Вставить(2, НСтр("ru='В ключе аналитики учета номенклатуры в регистре ""Закупки"" указан партнер, а не договор контрагента.'"));
	РасшифровкаКодовОшибок.Вставить(3, НСтр("ru='В ключе аналитики учета номенклатуры в регистре ""Движения Номенклатура - Номенклатура"" указан партнер, а не договор контрагента.'"));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров""	КАК ИмяРегистра,
	|	Т.Регистратор				КАК Ссылка,
	|	Т.Организация				КАК Организация,
	|	1							КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И Т.РазделУчета В (
	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути),
	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки))
	|	И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""Закупки""	  КАК ИмяРегистра,
	|	Т.Регистратор КАК Ссылка,
	|	Т.Организация КАК Организация,
	|	2			  КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.Закупки КАК Т
	|
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И Т.ТипЗапасов В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке))
	|	И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
	|	И НЕ Т.РасчетСебестоимости
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ДвиженияНоменклатураНоменклатура"" КАК ИмяРегистра,
	|	Т.Регистратор						 КАК Ссылка,
	|	Т.Организация						 КАК Организация,
	|	3									 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ДвиженияНоменклатураНоменклатура КАК Т
	|
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И Т.ТипЗапасов В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке))
	|	И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
	|	И НЕ Т.РасчетСебестоимости
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='исправление ключей аналитики учета номенклатуры в регистрах (замена партнера на договор)'"));
	
	#КонецОбласти
	
	#Область НетТаможеннойПошлиныВДвиженияхДокументаТаможеннаяДекларацияИмпорт
	
	// Исправление движений по регистру "Себестоимость товаров" для документов "Таможенная декларация на импорт".
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='В движения таможенной декларации на импорт по регистру ""Себестоимость товаров"" отсутствует сумма пошлины. Требуется перепровести документ по регистру ""Себестоимость товаров""'"));
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	Декларация.Ссылка 		 КАК Ссылка,
	|	Декларация.Организация 	 КАК Организация,
	|	1 						 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	Документ.ТаможеннаяДекларацияИмпорт КАК Декларация
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Движения
	|		ПО Декларация.Ссылка = Движения.Регистратор
	|		И Движения.ТипЗаписи В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыСПартией),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыБезПартии))
	|ГДЕ
	|	Декларация.Проведен
	|	И Декларация.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Декларация.Организация В (&МассивОрганизаций)
	|	И Декларация.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыТаможенныхДеклараций.ТаможенноеОформление)
	|	И (Декларация.СуммаДокумента - Декларация.ТаможенныйСбор - Декларация.ТаможенныйШтраф) <> 0
	|	И Движения.Регистратор ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ ПЕРВЫЕ 0
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	Движения.Регистратор	 КАК Ссылка,
	|	Движения.Организация 	 КАК Организация,
	|	1 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Движения
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='не отражена таможенная пошлина в движениях по регистру ""Себестоимость товаров""'"));

	
	#КонецОбласти
	
	#Область АналитикаОтгрузкиВВозвратеТоваровМеждуОрганизациями
	
	// Заполнение нового реквизита табличной части ВидыЗапасов
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Не заполнена аналитика номенклатуры отгрузки в документе возврата товаров между организациями.'"));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Ссылка				КАК Ссылка,
	|	Т.Ссылка.Организация	КАК Организация,
	|	1						КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыДляПерепроведения
	|ИЗ
	|	Документ.ВозвратТоваровМеждуОрганизациями.ВидыЗапасов КАК Т
	|
	|ГДЕ
	|	Т.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Ссылка.Организация В(&МассивОрганизаций)
	|	И Т.Ссылка.Проведен
	|	И Т.СпособОпределенияСебестоимости = ЗНАЧЕНИЕ(Перечисление.СпособыОпределенияСебестоимости.ИзДокументаПередачи)
	|	И Т.АналитикаУчетаНоменклатурыОтгрузки = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументы(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='исправление ключей аналитики учета номенклатуры отгрузки в документе возврата товаров между организациями'"));
	
	#КонецОбласти

	#Область ВключениеУчетаСебестоимостиПоНазначениям
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Включение учета себестоимости по назначениям'"));
	
	Запрос.Текст = ТекстЗапросаВключениеУчетаСебестоимостиПоНазначениям();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='включение учета себестоимости по назначениям'"));
		
	Запрос.Текст = ТекстЗапросаВключениеУчетаВыручкиИСебестоимостиПродажПоНазначениям();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='включение учета выручки и себестоимости продаж по назначениям'"));
	
	#КонецОбласти
	
	#Область ВыключениеУчетаСебестоимостиПоНазначениям
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Выключение учета себестоимости по назначениям'"));
	
	Запрос.Текст = ТекстЗапросаВыключениеУчетаСебестоимостиПоНазначениям();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='выключение учета себестоимости по назначениям'"));
		
	Запрос.Текст = ТекстЗапросаВыключениеУчетаВыручкиИСебестоимостиПродажПоНазначениям();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='выключение учета выручки и себестоимости продаж по назначениям'"));
		
	#КонецОбласти
		
	#Область ВключениеУчетаСебестоимостиПоВидамЗапасов
	
	РасшифровкаКодовОшибок = Новый Соответствие;	
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Включение учета себестоимости по видам запасов'"));
	
	Запрос.Текст = ТекстЗапросаВключениеУчетаСебестоимостиПоВидамЗапасов();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='включение учета себестоимости по видам запасов'"));
		
	Запрос.Текст = ТекстЗапросаВключениеУчетаВыручкиИСебестоимостиПродажПоВидамЗапасов();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='включение учета выручки и себестоимости продаж по видам запасов'"));
	
	#КонецОбласти
	
	
	#Область КорректировкаПриобретения
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Не правильный вид движения для документа ""Корректировка приобретения"".'"));
	
	// Условия проверок соответствуют функции ТекстЗапросаВтВозвратыПоставщикам() в модуле менеджера документа "КорректировкаПриобретения".
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.Ссылка КАК Ссылка,
	|	ТаблицаРасхождения.Ссылка.Дата КАК Дата,
	|	ТаблицаРасхождения.Ссылка.Организация КАК Организация,
	|	ТаблицаРасхождения.Ссылка.ДокументОснование КАК ДокументОснование,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры,
	|	(ВЫБОР
	|		КОГДА НЕ &УчитыватьСебестоимостьТоваровПоНазначениям ТОГДА АналитикаБезНазначения.КлючАналитики
	|		ИНАЧЕ ТаблицаРасхождения.АналитикаУчетаНоменклатуры КОНЕЦ) КАК АналитикаУчетаСебестоимости,
	|	ТаблицаРасхождения.ВидЗапасов,
	|	(ВЫБОР
	|		КОГДА ТаблицаРасхождения.Ссылка.Организация В (&ОрганизацииСФИФОСкользящая)
	|			ТОГДА Приобретение.Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ) КАК Партия,
	|	СУММА(ТаблицаРасхождения.Количество) КАК Количество
	|ПОМЕСТИТЬ ВтКорректировкиПриобретения
	|ИЗ
	|	Документ.КорректировкаПриобретения.Расхождения КАК ТаблицаРасхождения
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК Приобретение
	|		ПО Приобретение.Ссылка = ТаблицаРасхождения.Ссылка.ДокументОснование
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО Аналитика.Ссылка = ТаблицаРасхождения.АналитикаУчетаНоменклатуры
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|		ПО АналитикаБезНазначения.Номенклатура = Аналитика.Номенклатура
	|		И АналитикаБезНазначения.Характеристика = Аналитика.Характеристика
	|		И АналитикаБезНазначения.Серия = Аналитика.Серия
	|		И АналитикаБезНазначения.МестоХранения = Аналитика.МестоХранения
	|		И АналитикаБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка.Проведен
	|	И ТаблицаРасхождения.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ТаблицаРасхождения.Ссылка.Организация В (&МассивОрганизаций)
	|	И (Приобретение.Дата < &НачалоПериода
	|		ИЛИ Приобретение.Дата > &КонецПериода
	|		ИЛИ НЕ Приобретение.Проведен)
	|	И ТаблицаРасхождения.ВариантОтражения В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУменьшитьСкладскиеОстатки),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУчестьПриИнвентаризации))
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРасхождения.Ссылка,
	|	ТаблицаРасхождения.Ссылка.Организация,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры,
	|	(ВЫБОР
	|		КОГДА НЕ &УчитыватьСебестоимостьТоваровПоНазначениям ТОГДА АналитикаБезНазначения.КлючАналитики
	|		ИНАЧЕ ТаблицаРасхождения.АналитикаУчетаНоменклатуры КОНЕЦ),
	|	(ВЫБОР
	|		КОГДА ТаблицаРасхождения.Ссылка.Организация В (&ОрганизацииСФИФОСкользящая)
	|			ТОГДА Приобретение.Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ),
	|	ТаблицаРасхождения.ВидЗапасов
	|;
	|ВЫБРАТЬ
	|	ОстаткиТоваров.АналитикаУчетаНоменклатуры,
	|	ОстаткиТоваров.ВидЗапасов,
	|	ОстаткиТоваров.КоличествоОстаток КАК Количество
	|ПОМЕСТИТЬ ВтНеНулевыеОстаткиТоваров
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.Остатки(&ГраницаНачалоПериода,
	|		НЕ Организация В (&ОрганизацииСФИФОСкользящая)
	|		И (АналитикаУчетаНоменклатуры, Организация, ВидЗапасов) В (
	|			ВЫБРАТЬ
	|				Корректировки.АналитикаУчетаНоменклатуры,
	|				Корректировки.Организация,
	|               Корректировки.ВидЗапасов
	|			ИЗ
	|				ВтКорректировкиПриобретения КАК Корректировки
	|		)
	|	) КАК ОстаткиТоваров
	|;
	|ВЫБРАТЬ
	|	ОстаткиТоваров.АналитикаУчетаНоменклатуры,
	|	ОстаткиТоваров.ВидЗапасов,
	|	ОстаткиТоваров.Партия,
	|	ОстаткиТоваров.КоличествоОстаток КАК Количество
	|ПОМЕСТИТЬ ВтНеНулевыеОстаткиСебестоимости
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров.Остатки(&ГраницаНачалоПериода,
	|		Организация В (&ОрганизацииСФИФОСкользящая)
	|		И (АналитикаУчетаНоменклатуры, ВидЗапасов, Организация, Партия) В (
	|			ВЫБРАТЬ
	|				Корректировки.АналитикаУчетаСебестоимости,
	|               Корректировки.ВидЗапасов,
	|				Корректировки.Организация,
	|				Корректировки.Партия
	|			ИЗ
	|				ВтКорректировкиПриобретения КАК Корректировки
	|		)
	|	) КАК ОстаткиТоваров
	|;
	|ВЫБРАТЬ
	|	Движения.АналитикаУчетаНоменклатуры,
	|	Движения.ВидЗапасов,
	|	Корректировки.Ссылка КАК Ссылка,
	|	СУММА(Движения.Количество) КАК Количество
	|ПОМЕСТИТЬ ВтПрошлыеКорректировки
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Движения
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения КАК КорректировкаПриобретения
	|		ПО КорректировкаПриобретения.Ссылка = Движения.Регистратор
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтКорректировкиПриобретения КАК Корректировки
	|		ПО Корректировки.Организация = Движения.Организация
	|		И Корректировки.ВидЗапасов = Движения.ВидЗапасов
	|		И Корректировки.АналитикаУчетаСебестоимости = Движения.АналитикаУчетаНоменклатуры
	|		И Корректировки.ДокументОснование = КорректировкаПриобретения.ДокументОснование
	|ГДЕ
	|	Движения.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Движения.Период <= Корректировки.Дата
	|	И Движения.Регистратор <> Корректировки.Ссылка
	|	И Движения.Активность
	|	И Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|СГРУППИРОВАТЬ ПО
	|	Движения.АналитикаУчетаНоменклатуры,
	|	Движения.ВидЗапасов,
	|	Корректировки.Ссылка
	|;
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Себестоимость.Регистратор,
	|	Корректировки.АналитикаУчетаНоменклатуры,
	|	Себестоимость.ВидЗапасов
	|ПОМЕСТИТЬ ВтВозвратыПоставщикам
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Себестоимость
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтКорректировкиПриобретения КАК Корректировки
	|		ПО Корректировки.Ссылка = Себестоимость.Регистратор
	|		И Корректировки.ВидЗапасов = Себестоимость.ВидЗапасов
	|		И Корректировки.АналитикаУчетаСебестоимости = Себестоимость.АналитикаУчетаНоменклатуры
	|ГДЕ
	|	Себестоимость.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Себестоимость.Активность
	|	И Себестоимость.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И Себестоимость.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровПоставщику)
	|;
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров""  КАК ИмяРегистра,
	|	Корректировки.Ссылка 	  КАК Ссылка,
	|	Корректировки.Организация КАК Организация,
	|	1 						  КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	ВтКорректировкиПриобретения КАК Корректировки
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтВозвратыПоставщикам КАК Возвраты
	|		ПО Возвраты.Регистратор = Корректировки.Ссылка
	|		И Возвраты.АналитикаУчетаНоменклатуры = Корректировки.АналитикаУчетаНоменклатуры
	|		И Возвраты.ВидЗапасов = Корректировки.ВидЗапасов
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтНеНулевыеОстаткиТоваров КАК ОстаткиТоваров
	|		ПО ОстаткиТоваров.АналитикаУчетаНоменклатуры = Корректировки.АналитикаУчетаНоменклатуры
	|		И ОстаткиТоваров.ВидЗапасов = Корректировки.ВидЗапасов
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтНеНулевыеОстаткиСебестоимости КАК ОстаткиСебестоимости
	|		ПО ОстаткиСебестоимости.АналитикаУчетаНоменклатуры = Корректировки.АналитикаУчетаСебестоимости
	|		И ОстаткиСебестоимости.ВидЗапасов = Корректировки.ВидЗапасов
	|		И ОстаткиСебестоимости.Партия = Корректировки.Партия
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтПрошлыеКорректировки КАК ПрошлыеКорректировки
	|		ПО ПрошлыеКорректировки.АналитикаУчетаНоменклатуры = Корректировки.АналитикаУчетаСебестоимости
	|		И ПрошлыеКорректировки.ВидЗапасов = Корректировки.ВидЗапасов
	|		И ПрошлыеКорректировки.Ссылка = Корректировки.Ссылка
	|ГДЕ
	|	Возвраты.Регистратор ЕСТЬ NULL
	|	И (НЕ Корректировки.Организация В (&ОрганизацииСФИФОСкользящая) И ЕСТЬNULL(ОстаткиТоваров.Количество, 0)
	|		<= (-Корректировки.Количество - ЕСТЬNULL(ПрошлыеКорректировки.Количество, 0))
	|	ИЛИ Корректировки.Организация В (&ОрганизацииСФИФОСкользящая) И ЕСТЬNULL(ОстаткиСебестоимости.Количество, 0)
	|		<= (-Корректировки.Количество - ЕСТЬNULL(ПрошлыеКорректировки.Количество, 0)))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ ПЕРВЫЕ 0
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	Движения.Регистратор	 КАК Ссылка,
	|	Движения.Организация 	 КАК Организация,
	|	1 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Движения
	|";

	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='исправление движений документа ""Корректировка приобретения""'"));
	
	#КонецОбласти
	
	РасчетСебестоимостиПодготовкаДанныхЛокализация.ИсправитьПроблемыВДвиженияхИДокументах(ПараметрыРасчета);
	
КонецПроцедуры

// Возвращает текст запроса, позволяющего получить список документов
// с некорректными (задублированными) движениями по выручке и себестоимости продаж,
// появившимися в результате ошибки 00-00071689.
//
Функция ТекстЗапросаДокументыСДублямиДвиженийПоВыручкеИСебестоимостиПродаж()
	Возврат
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ВыручкаИСебестоимостьПродаж"" КАК ИмяРегистра,
		|	Т.Регистратор 					КАК Ссылка,
		|	Аналитики.Организация			КАК Организация,
		|	1 								КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитики
		|		ПО Т.АналитикаУчетаПоПартнерам = Аналитики.Ссылка
		|ГДЕ
		|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Аналитики.Организация В(&МассивОрганизаций)
		|	И Т.Количество = 0
		|	И Т.Стоимость <> 0
		|	И НЕ Т.РасчетСебестоимости
		|	И НЕ Т.Регистратор ССЫЛКА Документ.КорректировкаРегистров
		|	И НЕ Т.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
		|	И НЕ Т.Регистратор ССЫЛКА Документ.КорректировкаРеализации";
КонецФункции

// Возвращает текст запроса, позволяющего получить список документов
// с пропавшими движениями по корректировке выручки и себестоимости продаж,
// появившимися в результате ошибки 00-00302463.
//
Функция ТекстЗапросаДокументыСПропавшимиДвижениямиПоВыручкеИСебестоимостиПродаж()
	Возврат
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	""ВыручкаИСебестоимостьПродаж"" 		КАК ИмяРегистра,
		|	Т.Регистратор							КАК Ссылка,
		|	Т.АналитикаУчетаПоПартнерам.Организация	КАК Организация,
		|	1 										КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ВыручкаИСебестоимостьПродаж"" КАК ИмяРегистра,
		|	Т.Ссылка 						КАК Ссылка,
		|	Т.Ссылка.Организация			КАК Организация,
		|	1 								КАК КодОшибки
		|ИЗ
		|	Документ.КорректировкаРеализации.ВидыЗапасовКорректировкаВыручки КАК Т
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Выручка
		|		ПО Т.Ссылка = Выручка.Регистратор
		|		И Т.АналитикаУчетаНоменклатуры.Номенклатура = Выручка.АналитикаУчетаНоменклатуры.Номенклатура
		|		И Т.АналитикаУчетаНоменклатуры.Характеристика = Выручка.АналитикаУчетаНоменклатуры.Характеристика
		|		И Т.АналитикаУчетаНоменклатуры.Серия = Выручка.АналитикаУчетаНоменклатуры.Серия
		|		И Выручка.Количество = 0
		|		И НЕ Выручка.РасчетСебестоимости
		|ГДЕ
		|	Т.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Т.Ссылка.Организация В(&МассивОрганизаций)
		|	И (Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|		ИЛИ (Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|			И ВЫБОР КОГДА Т.Ссылка.ДокументОснование ССЫЛКА Документ.РеализацияТоваровУслуг
		|				ТОГДА ВЫРАЗИТЬ(Т.Ссылка.ДокументОснование КАК Документ.РеализацияТоваровУслуг).ВернутьМногооборотнуюТару
		|				ИНАЧЕ ЛОЖЬ
		|			  КОНЕЦ = ЛОЖЬ)
		|		ИЛИ Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры ЕСТЬ NULL)
		|	И Выручка.Регистратор ЕСТЬ NULL";
КонецФункции


Процедура ОчиститьДанныеУпрРегистров(ПараметрыРасчета)
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "ОчиститьДанныеУправленческихРегистров");
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Регистратор
		|ИЗ
		|	РегистрНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы КАК Т
		|ГДЕ
		|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Т.Организация В(&МассивОрганизаций)
		|	И Т.Регистратор ССЫЛКА Документ.РаспределениеПрочихЗатрат";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		
		ПараметрыЗаписи = РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьПараметрыЗаписи(
			ПараметрыРасчета.РасчетныйПериод.НачалоПериода,
			ПараметрыРасчета.ОграниченияВыборки.КоличествоЗаписейВНЗ,
			ПараметрыРасчета.НомерЗаданияДоРасчета);
	
		Попытка
			РасчетСебестоимостиПрикладныеАлгоритмы.ЗаписатьДвиженияПоРегистру(
				Выборка,
				РегистрыНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы,
				ПараметрыЗаписи);
		Исключение
			
			// Информацию об ошибке добавим в протокол расчета, в механизме закрытия месяца ошибки уже должны быть зарегистрированы.
			ТекстДляПротокола = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			РасчетСебестоимостиПротоколРасчета.ЗафиксироватьОшибкуРасчета(
				ПараметрыРасчета,
				Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаЗаписиДвиженийПоРегистрам,
				ТекстДляПротокола);
				
			ВызватьИсключение ТекстДляПротокола;
			
		КонецПопытки;
		
		РасчетСебестоимостиПротоколРасчета.ДополнительнаяИнформация(
			ПараметрыРасчета,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Очищено движений по регистру %1: %2'", ОбщегоНазначения.КодОсновногоЯзыка()),
				Метаданные.РегистрыНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы.ПолноеИмя(),
				РасчетСебестоимостиПротоколРасчета.ПредставлениеЗначения(Выборка.Количество())));
		
		
	КонецЕсли;
	
КонецПроцедуры

#Область ВключениеУчетаСебестоимостиПоВидамЗапасов

// Выбираем документы, в движениях которых не заполнен вид запасов, кроме исключений.
// Дополнительно выбираем данные из документов "Исправление развернутого сальдо товаров организаций",
// если у них не было движений по регистру "Себестоимость товаров" и отличаются виды запасов списания и оприходования.
//
// Движения, для которых не заполняется вид запасов в учете себестоимости товаров:
// - в аналитике учета номенклатуры указана номенклатура с типом номенклатуры "Работа"
// - товары, выкупленные у комитентов
//		раздел учета "Товары на складах", в аналитике учета номенклатуры тип места хранения "Партнер" или "Организация"
// - продукция, отраженная отчетами переработчика
//		раздел учета "Производственные затраты", в аналитике учета номенклатуры тип места хранения "Партнер"
// - работы к оформлению отчетов давальцу
//		движения "Приход" по разделу учета "Производственные затраты" с заполненным реквизитом "Кор. аналитика учета номенклатуры", 
//		хозяйственной операцией "Выпуск продукции", и типом записи "Партия"
// - продукция, реализованная документом "Отчет давальцу"
//		движения с хозяйственной операцией "Отчет давальцу"
//
// Дополнительные исключения для выручки и себестоимости продаж:
// - в типе запасов указано значение "Агентская услуга"
//	(в аналитике учета номенклатуры указана номенклатура с типом номенклатуры "Услуга. Выполняется партнером")
//
// Исключения для документов производства 2.1:	
// - вид запасов в движениях заполняется оффлайн: раздел учета "Производственные затраты" и тип записи "Потребление (производство)"
// - для документа "Перемещение материалов в производстве" вид запасов в движениях заполняется оффлайн
//
// Необходимо исправить движения в регистрах:
//	- Себестоимость товаров
//	- Выручка и себестоимость продаж
//
Функция ТекстЗапросаВключениеУчетаСебестоимостиПоВидамЗапасов() Экспорт
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""СебестоимостьТоваров""			КАК ИмяРегистра,
		|	СебестоимостьТоваров.Регистратор	КАК Ссылка,
		|	СебестоимостьТоваров.Организация	КАК Организация,
		|	1									КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|ГДЕ
		|	СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СебестоимостьТоваров.Организация В(&МассивОрганизаций)
		|	И СебестоимостьТоваров.ВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
		|	И СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		// Товары, выкупленные у комитентов, или отраженные отчетами переработчика
		|	И НЕ (
		|		СебестоимостьТоваров.РазделУчета В (
		|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
		|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты))
		|		И СебестоимостьТоваров.АналитикаУчетаНоменклатуры.ТипМестаХранения В (
		|			ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Организация)))
		|	И НЕ СебестоимостьТоваров.РасчетСебестоимости
		|	И НЕ СебестоимостьТоваров.РасчетПартий
		|	И НЕ СебестоимостьТоваров.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// Дополнительно выбираем данные из документов "Исправление развернутого сальдо товаров организаций",
		// если у них не было движений по регистру "Себестоимость товаров" и отличаются виды запасов списания и оприходования.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""СебестоимостьТоваров""		КАК ИмяРегистра,
		|	Исправления.Ссылка				КАК Ссылка,
		|	Исправления.Ссылка.Организация	КАК Организация,
		|	1								КАК КодОшибки
		|ИЗ
		|	Документ.ИсправлениеРазвернутогоСальдоТоваровОрганизаций.Исправления КАК Исправления
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|		ПО СебестоимостьТоваров.Период = Исправления.Ссылка.Дата
		|		И СебестоимостьТоваров.Регистратор = Исправления.Ссылка
		|ГДЕ
		|	Исправления.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Исправления.Ссылка.Организация В(&МассивОрганизаций)
		|	И Исправления.ВидЗапасовСписания <> Исправления.ВидЗапасовОприходования
		|	И СебестоимостьТоваров.Регистратор ЕСТЬ NULL
		|";
КонецФункции

Функция ТекстЗапросаВключениеУчетаВыручкиИСебестоимостиПродажПоВидамЗапасов() Экспорт
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ВыручкаИСебестоимостьПродаж""			КАК ИмяРегистра,
		|	ВыручкаИСебестоимостьПродаж.Регистратор	КАК Ссылка,
		|	Аналитики.Организация					КАК Организация,
		|	1										КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитики
		|		ПО ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам = Аналитики.Ссылка
		|ГДЕ
		|	ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Аналитики.Организация В(&МассивОрганизаций)
		|	И ВыручкаИСебестоимостьПродаж.ВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
		|	И ВыручкаИСебестоимостьПродаж.Количество <> 0
		|	И НЕ ВыручкаИСебестоимостьПродаж.ТипЗапасов В (
		|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга),
		|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.АгентскаяУслуга))
		|	И ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|	И НЕ ВыручкаИСебестоимостьПродаж.РасчетСебестоимости
		|	И НЕ ВыручкаИСебестоимостьПродаж.РасчетПартий
		|	И НЕ ВыручкаИСебестоимостьПродаж.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
		|";
КонецФункции

#КонецОбласти

#Область ВыключениеУчетаСебестоимостиПоНазначениям

// Выбираем документы, у которых в движениях в аналитике учета номенклатуры указано назначение.
// Необходимо исправить движения в регистрах:
//	- Себестоимость товаров
//	- Выручка и себестоимость продаж
//
Функция ТекстЗапросаВыключениеУчетаСебестоимостиПоНазначениям() Экспорт
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""СебестоимостьТоваров""			КАК ИмяРегистра,
		|	СебестоимостьТоваров.Регистратор	КАК Ссылка,
		|	СебестоимостьТоваров.Организация	КАК Организация,
		|	1									КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|ГДЕ
		|	НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
		|	И СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СебестоимостьТоваров.Организация В(&МассивОрганизаций)
		|	И СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	И НЕ СебестоимостьТоваров.РасчетСебестоимости
		|	И НЕ СебестоимостьТоваров.РасчетПартий
		|	И НЕ СебестоимостьТоваров.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
		|";
КонецФункции

Функция ТекстЗапросаВыключениеУчетаВыручкиИСебестоимостиПродажПоНазначениям() Экспорт
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ВыручкаИСебестоимостьПродаж""			КАК ИмяРегистра,
		|	ВыручкаИСебестоимостьПродаж.Регистратор	КАК Ссылка,
		|	Аналитики.Организация					КАК Организация,
		|	1										КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитики
		|		ПО ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам = Аналитики.Ссылка
		|ГДЕ
		|	НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
		|	И ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Аналитики.Организация В(&МассивОрганизаций)
		|	И ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	И НЕ ВыручкаИСебестоимостьПродаж.РасчетСебестоимости
		|	И НЕ ВыручкаИСебестоимостьПродаж.РасчетПартий
		|	И НЕ ВыручкаИСебестоимостьПродаж.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
		|";
КонецФункции

#КонецОбласти

#Область ВключениеУчетаСебестоимостиПоНазначениям

// Выбираем документы, у которых в движениях по оперативным регистрам указано назначение,
// а в движении учета себестоимости не указано назначение.
// Используются данные следующих оперативных регистров:
//	- Товары организаций
//		кроме таможенных деклараций с нулевыми суммами пошлины и НДС
//		кроме перемещений товаров в статусе "Отгружено"
//		кроме сборок товаров в статусе "В работе"
//	- Товары организаций к передаче
//	- Товары переданные переработчику
//	- Товары полученные от переработчика
//	- Товары к оформлению таможенных деклараций (движения "Приход)
//	- Материалы и работы в производстве (учитываются работы при партионном учете версии 2.2,
//		при партионном учете 2.1 не используется одновременно с онлайн движениями по регистру "Себестоимость товаров")
//		исключаются движения по товарам у документов "Маршрутный лист производства", "Списание затрат на выпуск", "Распределение материалов и работ",
//		используемых для производства 2.1, т.к. эти документы не делают онлайн движений по регистру "Себестоимость товаров" 
//		одновременно с регистром "Материалы и работы в производстве".
// Дополнительно к оперативным регистрам выбираем данные непосредственно из документов (у этих документов нет оперативных регистров):
//	- Отчет давальцу
//	- Заявление о ввозе товаров из ЕАЭС
//	- Ввод остатков с выключенным флажком "ОУ"
// Исключения:
//	- По товарам, переданным на комиссию, учет по назначениям не ведется
//	- По товаром к оформлению отчетов комитенту учет по назначениям не ведется
//	- По товарам, отгруженным без перехода права собственности, учет по назначениям не ведется
//	- Для движений себестоимости по разделу учета "Незавершенное производство" нет соответствующих оперативных регистров.
//		Но этот раздел учета всегда корреспондирует с другими разделами, которые имеют соответствующие оперативные регистры.
//		Поэтому движения по разделу "Незавершенное производстве" не проверяем. Они будут переформированы одновременно с другими движениями.
//	- Движения документов "Заявление о ввозе товаров из ЕАЭС" по себестоимости не имеют соответствующих оперативных регистров. 
//		Проверяем данные в табличной части документов
//	- Движения документов "Распределение материалов и работ" при распределении материалов по правилу
//	- Движения документов "Исправление развернутого сальдо товаров организаций" если в движениях не меняется вид запасов.
// 		У таких документов не будет движений по регистру "Себестоимость товаров".
//	- Документы корректировки регистров. У таких документов могут быть движения только по оперативным регистрам.
//	- Документы ввода начальных остатков, у которых снят флажок "Себестоимость" (у таких документов нет движений по учету себестоимости)
//
Функция ТекстЗапросаВключениеУчетаСебестоимостиПоНазначениям() Экспорт
	Возврат "
		|ВЫБРАТЬ
		|	Ключи.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ АналитикаСНазначениями
		|ИЗ
		|	Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И &ИспользоватьУчетСебестоимости
		|	И Ключи.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		// Выбираем таможенные декларации на импорт, в которых указаны нулевые суммы таможенной пошлины и НДС.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Ссылка,
		|	Строки.АналитикаУчетаНоменклатуры
		|ПОМЕСТИТЬ ДекларацииСНулевымиСуммами
		|ИЗ
		|	Документ.ТаможеннаяДекларацияИмпорт КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТаможеннаяДекларацияИмпорт.Товары КАК Строки
		|		ПО Строки.Ссылка = ДД.Ссылка
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|		ПО АналитикаСНазначениями.Ссылка = Строки.АналитикаУчетаНоменклатуры
		|ГДЕ
		|	ДД.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Проведен
		|	И Строки.СуммаПошлины = 0
		|	И Строки.СуммаНДС = 0
		|;
		// Выбираем документы, у которых в движениях по оперативным регистрам указано назначение.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Товары.Регистратор КАК Регистратор,
		|	Товары.Организация КАК Организация
		|ПОМЕСТИТЬ ДокументыСНазначениями
		|ИЗ (
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыОрганизаций.Регистратор КАК Регистратор,
		|		ТоварыОрганизаций.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ТоварыОрганизаций.АналитикаУчетаНоменклатуры
		// Исключаем таможенные декларации с нулевыми суммами пошлины и НДС
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДекларацииСНулевымиСуммами КАК Декларации
		|			ПО Декларации.Ссылка = ТоварыОрганизаций.Регистратор
		|			И Декларации.АналитикаУчетаНоменклатуры = ТоварыОрганизаций.АналитикаУчетаНоменклатуры
		|			И ТИПЗНАЧЕНИЯ(ТоварыОрганизаций.Регистратор) = ТИП(Документ.ТаможеннаяДекларацияИмпорт)
		// Исключаем перемещения товаров в статусе "Отгружено"
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеТоваров КАК Перемещение
		|			ПО Перемещение.Ссылка = ТоварыОрганизаций.Регистратор
		|			И Перемещение.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПеремещенийТоваров.Отгружено)
		// Исключаем сборки товаров в статусе "В работе"
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СборкаТоваров КАК Сборка
		|			ПО Сборка.Ссылка = ТоварыОрганизаций.Регистратор
		|			И Сборка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.ВРаботе)
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ТоварыОрганизаций.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыОрганизаций.Организация В(&МассивОрганизаций)
		|		И ТИПЗНАЧЕНИЯ(ТоварыОрганизаций.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
		// Исключаем движения документов производства 2.2, для которых нет онлайн движений по регистру "Себестоимость товаров"
		|		И ТоварыОрганизаций.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовНаПартииПроизводства)
		// Исключаем движения документов "Исправление развернутого сальдо товаров организаций" если в движениях не меняется вид запасов.
		// У таких документов не будет движений по регистру "Себестоимость товаров".
		|		И НЕ (
		|			ТоварыОрганизаций.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаОбособленногоУчета)
		|			И ТоварыОрганизаций.ВидЗапасов = ТоварыОрганизаций.КорВидЗапасов
		|			И ТИПЗНАЧЕНИЯ(ТоварыОрганизаций.Регистратор) <> ТИП(Документ.КорректировкаНазначенияТоваров))
		// Исключаем таможенные декларации с нулевыми суммами пошлины и НДС
		|		И Декларации.Ссылка ЕСТЬ NULL
		// Исключаем вводы остатков с выключенным учетом себестоимости
		|		И ЕСТЬNULL(ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ВводОстатков).ОтражатьСебестоимость, ИСТИНА)
		// Исключаем перемещения товаров в статусе "Отгружено"
		|		И Перемещение.Ссылка ЕСТЬ NULL
		// Исключаем сборки товаров в статусе "В работе"
		|		И Сборка.Ссылка ЕСТЬ NULL
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыОрганизацийКПередаче.Регистратор КАК Регистратор,
		|		ТоварыОрганизацийКПередаче.ВидЗапасовПродавца.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыОрганизацийКПередаче КАК ТоварыОрганизацийКПередаче
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ТоварыОрганизацийКПередаче.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ТоварыОрганизацийКПередаче.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыОрганизацийКПередаче.ВидЗапасовПродавца.Организация В(&МассивОрганизаций)
		|		И ТИПЗНАЧЕНИЯ(ТоварыОрганизацийКПередаче.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
		|
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыКОформлениюТаможенныхДеклараций.Регистратор КАК Регистратор,
		|		ТоварыКОформлениюТаможенныхДеклараций.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыКОформлениюТаможенныхДеклараций КАК ТоварыКОформлениюТаможенныхДеклараций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ТоварыКОформлениюТаможенныхДеклараций.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ТоварыКОформлениюТаможенныхДеклараций.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыКОформлениюТаможенныхДеклараций.Организация В(&МассивОрганизаций)
		|		И ТоварыКОформлениюТаможенныхДеклараций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		И ТИПЗНАЧЕНИЯ(ТоварыКОформлениюТаможенныхДеклараций.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		МатериалыИРаботыВПроизводстве.Регистратор КАК Регистратор,
		|		МатериалыИРаботыВПроизводстве.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.МатериалыИРаботыВПроизводстве КАК МатериалыИРаботыВПроизводстве
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И МатериалыИРаботыВПроизводстве.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И МатериалыИРаботыВПроизводстве.Организация В(&МассивОрганизаций)
		|		И МатериалыИРаботыВПроизводстве.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		И МатериалыИРаботыВПроизводстве.Количество <> 0
		|		И НЕ ТИПЗНАЧЕНИЯ(МатериалыИРаботыВПроизводстве.Регистратор) В (
		|			ТИП(Документ.КорректировкаРегистров),
		|			ТИП(Документ.РасчетСебестоимостиТоваров))
		// Исключаем вводы остатков с выключенным учетом себестоимости
		|		И ЕСТЬNULL(ВЫРАЗИТЬ(МатериалыИРаботыВПроизводстве.Регистратор КАК Документ.ВводОстатков).ОтражатьСебестоимость, ИСТИНА)
		|
		|
		//++ Локализация
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ЗаявлениеОВвозеТовары.Ссылка КАК Регистратор,
		|		ЗаявлениеОВвозеТовары.Ссылка.Организация КАК Организация
		|	ИЗ
		|		Документ.ЗаявлениеОВвозеТоваров.Товары КАК ЗаявлениеОВвозеТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ЗаявлениеОВвозеТовары.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ЗаявлениеОВвозеТовары.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ЗаявлениеОВвозеТовары.Ссылка.Организация В(&МассивОрганизаций)
		|		И ЗаявлениеОВвозеТовары.Ссылка.Проведен
		|		И ЗаявлениеОВвозеТовары.СуммаНДС <> 0
		//-- Локализация
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ВводОстатков.Ссылка КАК Регистратор,
		|		ВводОстатков.Ссылка.Организация КАК Организация
		|	ИЗ
		|		Документ.ВводОстатков.Товары КАК ВводОстатков
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ВводОстатков.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И ВводОстатков.Ссылка.ОтражатьСебестоимость
		|		И НЕ ВводОстатков.Ссылка.ОтражатьВОперативномУчете
		|		И ВводОстатков.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ВводОстатков.Ссылка.Организация В(&МассивОрганизаций)
		|		И ВводОстатков.Ссылка.Проведен
		|	) КАК Товары
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	Организация
		|;
		// Выбираем документы, у которых в движениях по себестоимости указано назначение.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СебестоимостьТоваров.Регистратор КАК Регистратор,
		|	СебестоимостьТоваров.Организация КАК Организация
		|
		|ПОМЕСТИТЬ ДвиженияСебестоимостиСНазначениями
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыСНазначениями КАК ДокументыСНазначениями
		|		ПО ДокументыСНазначениями.Регистратор = СебестоимостьТоваров.Регистратор
		|		И ДокументыСНазначениями.Организация = СебестоимостьТоваров.Организация
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|		ПО АналитикаСНазначениями.Ссылка = СебестоимостьТоваров.АналитикаУчетаНоменклатуры
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СебестоимостьТоваров.Организация В(&МассивОрганизаций)
		|	И НЕ СебестоимостьТоваров.РасчетСебестоимости
		|	И НЕ СебестоимостьТоваров.РасчетПартий
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	Организация
		|;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СебестоимостьТоваров.Регистратор КАК Регистратор,
		|	СебестоимостьТоваров.Организация КАК Организация
		|
		|ПОМЕСТИТЬ ЕстьДвиженияСебестоимости
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыСНазначениями КАК ДокументыСНазначениями
		|		ПО ДокументыСНазначениями.Регистратор = СебестоимостьТоваров.Регистратор
		|		И ДокументыСНазначениями.Организация = СебестоимостьТоваров.Организация
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СебестоимостьТоваров.Организация В(&МассивОрганизаций)
		|	И НЕ СебестоимостьТоваров.РасчетСебестоимости
		|	И НЕ СебестоимостьТоваров.РасчетПартий
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	Организация
		|;
		// Выбираем документы, у которых в движениях по оперативным регистрам указано назначение,
		// а в движениях учета себестоимости не указано назначение (нет движений с указанным назначением).
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""СебестоимостьТоваров""			КАК ИмяРегистра,
		|	ДокументыСНазначениями.Регистратор	КАК Ссылка,
		|	ДокументыСНазначениями.Организация	КАК Организация,
		|	1									КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	ДокументыСНазначениями КАК ДокументыСНазначениями
		// Выполняем проверерку только при наличии движений по себестоимости товаров. 
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЕстьДвиженияСебестоимости КАК ЕстьДвиженияСебестоимости
		|		ПО ЕстьДвиженияСебестоимости.Регистратор = ДокументыСНазначениями.Регистратор
		|		И ЕстьДвиженияСебестоимости.Организация = ДокументыСНазначениями.Организация
		|	ЛЕВОЕ СОЕДИНЕНИЕ ДвиженияСебестоимостиСНазначениями КАК СебестоимостьТоваров
		|		ПО СебестоимостьТоваров.Регистратор = ДокументыСНазначениями.Регистратор
		|		И СебестоимостьТоваров.Организация = ДокументыСНазначениями.Организация
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И СебестоимостьТоваров.Регистратор ЕСТЬ NULL
		|;
		|УНИЧТОЖИТЬ АналитикаСНазначениями;
		|УНИЧТОЖИТЬ ДокументыСНазначениями;
		|УНИЧТОЖИТЬ ДвиженияСебестоимостиСНазначениями
		|";
КонецФункции

Функция ТекстЗапросаВключениеУчетаВыручкиИСебестоимостиПродажПоНазначениям() Экспорт
	Возврат "
		|ВЫБРАТЬ
		|	Ключи.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ АналитикаСНазначениями
		|ИЗ
		|	Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И &ИспользоватьУчетСебестоимости
		|	И Ключи.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВыручкаИСебестоимостьПродаж.Регистратор КАК Регистратор,
		|	Аналитики.Организация КАК Организация
		|ПОМЕСТИТЬ ДокументыПродаж
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитики
		|		ПО ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам = Аналитики.Ссылка
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК ДокументРеализации
		|		ПО ДокументРеализации.Ссылка = ВыручкаИСебестоимостьПродаж.Регистратор
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И &ИспользоватьУчетСебестоимости
		|	И ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Аналитики.Организация В (&МассивОрганизаций)
		|	И ЕСТЬNULL(ДокументРеализации.ХозяйственнаяОперация, НЕОПРЕДЕЛЕНО)
		|		<> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
		|;
		// Выбираем документы продаж, у которых в движениях по оперативным регистрам указано назначение.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Товары.Регистратор КАК Регистратор,
		|	Товары.Организация КАК Организация
		|ПОМЕСТИТЬ ДокументыСНазначениями
		|ИЗ (
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыОрганизаций.Регистратор КАК Регистратор,
		|		ТоварыОрганизаций.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыПродаж КАК ДокументыПродаж
		|			ПО ДокументыПродаж.Регистратор = ТоварыОрганизаций.Регистратор
		|			И ДокументыПродаж.Организация = ТоварыОрганизаций.Организация
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ТоварыОрганизаций.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ТоварыОрганизаций.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыОрганизаций.Организация В(&МассивОрганизаций)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыОрганизацийКПередаче.Регистратор КАК Регистратор,
		|		ТоварыОрганизацийКПередаче.ВидЗапасовПродавца.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыОрганизацийКПередаче КАК ТоварыОрганизацийКПередаче
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыПродаж КАК ДокументыПродаж
		|			ПО ДокументыПродаж.Регистратор = ТоварыОрганизацийКПередаче.Регистратор
		|			И ДокументыПродаж.Организация = ТоварыОрганизацийКПередаче.ВидЗапасовПродавца.Организация
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ТоварыОрганизацийКПередаче.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ТоварыОрганизацийКПередаче.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыОрганизацийКПередаче.ВидЗапасовПродавца.Организация В(&МассивОрганизаций)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		МатериалыИРаботыВПроизводстве.Регистратор КАК Регистратор,
		|		МатериалыИРаботыВПроизводстве.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.МатериалыИРаботыВПроизводстве КАК МатериалыИРаботыВПроизводстве
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыПродаж КАК ДокументыПродаж
		|			ПО ДокументыПродаж.Регистратор = МатериалыИРаботыВПроизводстве.Регистратор
		|			И ДокументыПродаж.Организация = МатериалыИРаботыВПроизводстве.Организация
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И МатериалыИРаботыВПроизводстве.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И МатериалыИРаботыВПроизводстве.Организация В(&МассивОрганизаций)
		|		И МатериалыИРаботыВПроизводстве.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|
		|	) КАК Товары
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	Организация
		|;
		// Выбираем документы, у которых в движениях по выручке и себестоимости продаж указано назначение.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВыручкаИСебестоимостьПродаж.Регистратор КАК Регистратор,
		|	Аналитики.Организация КАК Организация
		|
		|ПОМЕСТИТЬ ДвиженияСебестоимостиСНазначениями
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитики
		|		ПО ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам = Аналитики.Ссылка
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыСНазначениями КАК ДокументыСНазначениями
		|		ПО ДокументыСНазначениями.Регистратор = ВыручкаИСебестоимостьПродаж.Регистратор
		|		И ДокументыСНазначениями.Организация = Аналитики.Организация
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|		ПО АналитикаСНазначениями.Ссылка = ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Аналитики.Организация В(&МассивОрганизаций)
		|	И ВыручкаИСебестоимостьПродаж.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	Организация
		|;
		// Выбираем документы, у которых в движениях по оперативным регистрам указано назначение,
		// а в движениях выручки и себестоимости продаж не указано назначение (нет движений с указанным назначением).
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ВыручкаИСебестоимостьПродаж""		КАК ИмяРегистра,
		|	ДокументыСНазначениями.Регистратор	КАК Ссылка,
		|	ДокументыСНазначениями.Организация	КАК Организация,
		|	1									КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	ДокументыСНазначениями КАК ДокументыСНазначениями
		|	ЛЕВОЕ СОЕДИНЕНИЕ ДвиженияСебестоимостиСНазначениями КАК СебестоимостьТоваров
		|		ПО СебестоимостьТоваров.Регистратор = ДокументыСНазначениями.Регистратор
		|		И СебестоимостьТоваров.Организация = ДокументыСНазначениями.Организация
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И СебестоимостьТоваров.Регистратор ЕСТЬ NULL
		|;
		|УНИЧТОЖИТЬ АналитикаСНазначениями;
		|УНИЧТОЖИТЬ ДокументыПродаж;
		|УНИЧТОЖИТЬ ДокументыСНазначениями;
		|УНИЧТОЖИТЬ ДвиженияСебестоимостиСНазначениями
		|";
КонецФункции

#КонецОбласти

#КонецОбласти